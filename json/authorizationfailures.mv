<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-JSN-ATF-
| Next Error Code: 5
|
</MvCOMMENT>

<MvFUNCTION NAME = "JSON_AuthorizationFailureList_Joins" PARAMETERS = "filter var, sort var, ondemandcolumns var, joins var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.sort_field"	VALUE = "{ [ g.Library_DB ].SQL_Sort_Field( l.sort, l.null ) }">

	<MvIF EXPR = "{ l.ondemandcolumns:cust_login }">												<MvASSIGN NAME = "l.joins:customers"		VALUE = 1>
	<MvELSEIF EXPR = "{ l.ondemandcolumns:cust_pw_email }">											<MvASSIGN NAME = "l.joins:customers"		VALUE = 1>
	<MvELSEIF EXPR = "{ l.sort_field EQ 'cust_login' }">											<MvASSIGN NAME = "l.joins:customers"		VALUE = 1>
	<MvELSEIF EXPR = "{ l.sort_field EQ 'cust_pw_email' }">											<MvASSIGN NAME = "l.joins:customers"		VALUE = 1>
	<MvELSEIF EXPR = "{ JSON_Filter_Contains_Search_Field( l.filter, 'cust_login', l.null ) }">		<MvASSIGN NAME = "l.joins:customers"		VALUE = 1>
	<MvELSEIF EXPR = "{ JSON_Filter_Contains_Search_Field( l.filter, 'cust_pw_email', l.null ) }">	<MvASSIGN NAME = "l.joins:customers"		VALUE = 1>
	<MvELSE>																						<MvASSIGN NAME = "l.joins:customers"		VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.ondemandcolumns:business_title }">											<MvASSIGN NAME = "l.joins:businessaccounts"	VALUE = 1>
	<MvELSEIF EXPR = "{ l.sort_field EQ 'business_title' }">										<MvASSIGN NAME = "l.joins:businessaccounts"	VALUE = 1>
	<MvELSEIF EXPR = "{ JSON_Filter_Contains_Search_Field( l.filter, 'business_title', l.null ) }">	<MvASSIGN NAME = "l.joins:businessaccounts"	VALUE = 1>
	<MvELSE>																						<MvASSIGN NAME = "l.joins:businessaccounts"	VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.joins:businessaccounts }">													<MvASSIGN NAME = "l.joins:customers"		VALUE = 1>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AuthorizationFailureList_Load_Query_Filter" PARAMETERS = "query var, field_count var, filter_name, filter_value, data var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.filter_name EQ 'ondemandcolumns' }">
		<MvFOREACH ITERATOR = "l.column_code" ARRAY = "l.filter_value">
			<MvIF EXPR = "{ ISNULL l.column_code }">
				<MvFOREACHCONTINUE>
			</MvIF>

			<MvASSIGN NAME = "l.data" MEMBER = "{ l.column_code }" VALUE = 1>
		</MvFOREACH>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AuthorizationFailureList_Load_Query" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT JSON_Store_Open() }">								<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Filename_Admin ].CanI( 'ORDR', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvASSIGN NAME = "l.available_filters"	VALUE = "">
	<MvASSIGN NAME = "l.filter"				VALUE = "">
	<MvASSIGN NAME = "l.sort"				VALUE = "">
	<MvASSIGN NAME = "l.offset"				VALUE = 0>
	<MvASSIGN NAME = "l.count"				VALUE = 0>
	<MvASSIGN NAME = "l.search_query"		VALUE = "">

	<MvEVAL EXPR = "{ JSON_AvailableFilter_INTEGER(		l.available_filters, 'af.order_id',		'order_id' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_BOOL(		l.available_filters, 'af.ship_res',		'ship_res' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.ship_fname',	'ship_fname' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.ship_lname',	'ship_lname' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.ship_email',	'ship_email' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.ship_comp',	'ship_comp' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.ship_phone',	'ship_phone' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.ship_fax',		'ship_fax' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.ship_addr',	'ship_addr1' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.ship_addr2',	'ship_addr2' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.ship_city',	'ship_city' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.ship_state',	'ship_state' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.ship_zip',		'ship_zip' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.ship_cntry',	'ship_cntry' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.bill_fname',	'bill_fname' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.bill_lname',	'bill_lname' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.bill_email',	'bill_email' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.bill_comp',	'bill_comp' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.bill_phone',	'bill_phone' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.bill_fax',		'bill_fax' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.bill_addr',	'bill_addr1' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.bill_addr2',	'bill_addr2' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.bill_city',	'bill_city' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.bill_state',	'bill_state' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.bill_zip',		'bill_zip' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.bill_cntry',	'bill_cntry' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_INTEGER(		l.available_filters, 'af.dtstamp',		'dtstamp' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.ip',			'ip' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.mod_name',		'mod_name' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.method',		'method' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_NUMBER(		l.available_filters, 'af.amount',		'amount' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(		l.available_filters, 'af.error_text',	'error_text' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_NULL_CHAR(	l.available_filters, 'cust.login',		'cust_login' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_NULL_CHAR(	l.available_filters, 'cust.pw_email',	'cust_pw_email' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_NULL_CHAR(	l.available_filters, 'ba.title',		'business_title' ) }">

	<MvIF EXPR = "{ NOT JSON_Input_Filter(	'o', 'Filter',	l.filter )	OR
					NOT JSON_Input_Text(	'o', 'Sort',	l.sort )	OR
					NOT JSON_Input_Integer(	'o', 'Offset',	l.offset )	OR
					NOT JSON_Input_Integer(	'o', 'Count',	l.count ) }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Response_InputErrors() }">
	</MvIF>

	<MvEVAL EXPR = "{ JSON_Filter_Callback( l.search_query, l.filter, l.available_filters, g.Module_JSON, 'JSON_AuthorizationFailureList_Load_Query_Filter','', l.ondemandcolumns ) }">
	<MvEVAL EXPR = "{ JSON_OrderBy( l.search_query, l.sort, 'af.order_id,af.ship_res,af.dtstamp,af.ip,af.mod_name,af.method,af.amount,af.error_text,
															 ship_fname:af.ship_fname,ship_lname:af.ship_lname,ship_email:af.ship_email,ship_comp:af.ship_comp,ship_phone:af.ship_phone,ship_fax:af.ship_fax,
															 ship_addr1:af.ship_addr,ship_addr2:af.ship_addr2,ship_city:af.ship_city,ship_state:af.ship_state,ship_zip:af.ship_zip,ship_cntry:af.ship_cntry,
															 bill_fname:af.bill_fname,bill_lname:af.bill_lname,bill_email:af.bill_email,bill_comp:af.bill_comp,bill_phone:af.bill_phone,bill_fax:af.bill_fax,
															 bill_addr1:af.bill_addr,bill_addr2:af.bill_addr2,bill_city:af.bill_city,bill_state:af.bill_state,bill_zip:af.bill_zip,bill_cntry:af.bill_cntry,
															 cust_login:cust.login:null_char,cust_pw_email:cust.pw_email:null_char,business_title:ba.title:null_char', '-dtstamp;-id' ) }">

	<MvEVAL EXPR = "{ JSON_AuthorizationFailureList_Joins( l.filter, l.sort, l.ondemandcolumns, l.joins ) }">

	<MvEVAL EXPR = "{ [ g.Library_DB ].SQL_Query_SELECT( l.search_query, 'af.*' ) }">
	<MvEVAL EXPR = "{ [ g.Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'AuthorizationFailures', 'af' ) }">

	<MvIF EXPR = "{ l.joins:customers }">
		<MvEVAL EXPR = "{ [ g.Library_DB ].SQL_Query_SELECT_NULL_CHAR( l.search_query, 'cust.login',	'cust_login' ) }">
		<MvEVAL EXPR = "{ [ g.Library_DB ].SQL_Query_SELECT_NULL_CHAR( l.search_query, 'cust.pw_email',	'cust_pw_email' ) }">
		<MvEVAL EXPR = "{ [ g.Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'af', g.Store_Table_Prefix $ 'Customers', 'cust', 'cust.id = af.cust_id', '' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.joins:businessaccounts }">
		<MvEVAL EXPR = "{ [ g.Library_DB ].SQL_Query_SELECT_NULL_CHAR( l.search_query, 'ba.title',		'business_title' ) }">
		<MvEVAL EXPR = "{ [ g.Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'af', g.Store_Table_Prefix $ 'BusinessAccounts', 'ba', 'cust.account_id = ba.id', '' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.search_sql"	VALUE = "{ [ g.Library_DB ].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Library_DB ].SQL_Query_Count( l.search_query, l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'AuthorizationFailures', l.search_sql, l.search_fields, l.offset, l.count ) }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Response_Error( 'MER-JSN-ATF-00001', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.failure_count" VALUE = 0>

	<MvEVAL EXPR = "{ JSON_Response_Start() }">
	{
		"total_count":	<MvEVAL EXPR = "{ int( l.total_count ) }">,
		"start_offset":	<MvEVAL EXPR = "{ int( l.offset ) }">,
		"data":
		[
		<MvWHILE EXPR = "{ ( NOT AuthorizationFailures.d.EOF ) AND ( ( l.count EQ 0 ) OR ( l.failure_count LT l.count ) ) }">
			<MvEVAL EXPR = "{ [ g.Library_DB ].AuthorizationFailure_Read( l.authorizationfailure ) }">

			<MvEVAL EXPR = "{ JSON_ArrayElement_Start( l.failure_count ) }">
			"id":			<MvEVAL EXPR = "{ int( l.authorizationfailure:id ) }">,
			"order_id":		<MvEVAL EXPR = "{ int( l.authorizationfailure:order_id ) }">,
			"cust_id":		<MvEVAL EXPR = "{ int( l.authorizationfailure:cust_id ) }">,
			"ship_res":		<MvEVAL EXPR = "{ JSON_Boolean( l.authorizationfailure:ship_res ) }">,
			"ship_fname":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:ship_fname ) }">",
			"ship_lname":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:ship_lname ) }">",
			"ship_email":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:ship_email ) }">",
			"ship_comp":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:ship_comp ) }">",
			"ship_phone":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:ship_phone ) }">",
			"ship_fax":		"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:ship_fax ) }">",
			"ship_addr1":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:ship_addr1 ) }">",
			"ship_addr2":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:ship_addr2 ) }">",
			"ship_city":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:ship_city ) }">",
			"ship_state":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:ship_state ) }">",
			"ship_zip":		"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:ship_zip ) }">",
			"ship_cntry":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:ship_cntry ) }">",
			"bill_fname":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:bill_fname ) }">",
			"bill_lname":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:bill_lname ) }">",
			"bill_email":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:bill_email ) }">",
			"bill_comp":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:bill_comp ) }">",
			"bill_phone":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:bill_phone ) }">",
			"bill_fax":		"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:bill_fax ) }">",
			"bill_addr1":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:bill_addr1 ) }">",
			"bill_addr2":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:bill_addr2 ) }">",
			"bill_city":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:bill_city ) }">",
			"bill_state":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:bill_state ) }">",
			"bill_zip":		"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:bill_zip ) }">",
			"bill_cntry":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:bill_cntry ) }">",
			"dtstamp":		<MvEVAL EXPR = "{ int( l.authorizationfailure:dtstamp ) }">,
			"ip":			"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:ip ) }">",
			"mod_name":		"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:mod_name ) }">",
			"method":		"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:method ) }">",
			"amount":		<MvEVAL EXPR = "{ l.authorizationfailure:amount ROUND 2 }">,
			"error_text":	"<MvEVAL EXPR = "{ JSON_Encode( l.authorizationfailure:error_text ) }">"

			<MvIF EXPR = "{ l.ondemandcolumns:cust_login }">
				, "cust_login":		"<MvEVAL EXPR = "{ JSON_Encode( AuthorizationFailures.d.cust_login ) }">"
			</MvIF>

			<MvIF EXPR = "{ l.ondemandcolumns:cust_pw_email }">
				, "cust_pw_email":	"<MvEVAL EXPR = "{ JSON_Encode( AuthorizationFailures.d.cust_pw_email ) }">"
			</MvIF>

			<MvIF EXPR = "{ l.ondemandcolumns:business_title }">
				, "business_title":	"<MvEVAL EXPR = "{ JSON_Encode( AuthorizationFailures.d.business_title ) }">"
			</MvIF>
			<MvEVAL EXPR = "{ JSON_ArrayElement_End() }">

			<MvSKIP NAME = "Merchant" VIEW = "AuthorizationFailures" ROWS = 1>
		</MvWHILE>
		]
	}
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AuthorizationFailures">
	<MvFUNCTIONRETURN VALUE = "{ JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AuthorizationFailureList_Delete" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT JSON_Store_Open() }">								<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Filename_Admin ].CanI( 'ORDR', 0, 0, 0, 1 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvASSIGN NAME = "l.processed" 		VALUE = 0>
	<MvASSIGN NAME = "l.error_count" 	VALUE = 0>
	<MvASSIGN NAME = "l.errors" 		VALUE = "">
	<MvASSIGN NAME = "l.start_time" 	VALUE = "{ s.dyn_time_t }">

	<MvFOREACH ITERATOR = "l.failure_id" ARRAY = "l.failure_ids" COUNT = "{ JSON_Array_Integer( g.AuthorizationFailure_IDs, l.failure_ids ) }">
		<MvASSIGN NAME = "l.processed" 	VALUE = "{ l.processed + 1 }">

		<MvIF EXPR = "{ NOT [ g.Library_DB ].AuthorizationFailure_Delete_ID( l.failure_id ) }">
			<MvASSIGN NAME = "l.error_count" VALUE = "{ JSON_Response_ListProcessed_Error( l.failure_id, g.Error_Code, g.Error_Message, l.errors ) }">
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-JSN-ATF-00002', 'Authorization failure \'' $ l.failure_id $ '\' deleted' ) }">
		</MvIF>

		<MvIF EXPR = "{ ( s.dyn_time_t - l.start_time ) GE 60 }">								<MvFOREACHSTOP>
		<MvELSEIF EXPR = "{ ( s.dyn_time_remaining GE 0 ) AND ( s.dyn_time_remaining LE 3 ) }">	<MvFOREACHSTOP>
		</MvIF>
	</MvFOREACH>

	<MvIF EXPR = "{ l.error_count }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Response_ListProcessed_Errors( l.processed, l.errors ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_Response_ListProcessed_Success( l.processed ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AuthorizationFailure_Blacklist_IPs" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT JSON_Store_Open() }">								<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Filename_Admin ].CanI( 'PMNT', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvASSIGN NAME = "g.AuthorizationFailure_Blacklist_Expires_Mode"	VALUE = "{ tolower( JSON_Decode( g.AuthorizationFailure_Blacklist_Expires_Mode ) ) }">
	<MvASSIGN NAME = "g.AuthorizationFailure_Blacklist_Expires_In"		VALUE = "{ int( g.AuthorizationFailure_Blacklist_Expires_In ) }">
	<MvASSIGN NAME = "g.AuthorizationFailure_Blacklist_Expires_In_Unit"	VALUE = "{ tolower( JSON_Decode( g.AuthorizationFailure_Blacklist_Expires_In_Unit ) ) }">
	<MvASSIGN NAME = "g.AuthorizationFailure_Blacklist_Expires_On"		VALUE = "{ int( g.AuthorizationFailure_Blacklist_Expires_On ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].Value_In_List( g.AuthorizationFailure_Blacklist_Expires_Mode, 'never,in,on' ) }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Response_Error( 'MER-JSN-ATF-00003', 'Please select a valid expiration mode' ) }">
	</MvIF>

	<MvIF EXPR = "{ g.AuthorizationFailure_Blacklist_Expires_Mode EQ 'on' }">
		<MvIF EXPR = "{ g.AuthorizationFailure_Blacklist_Expires_On LT s.dyn_time_t }">
			<MvFUNCTIONRETURN VALUE = "{ JSON_Response_FieldError( '', 'Please select a future expiration' ) }">
		</MvIF>
	<MvELSEIF EXPR = "{ g.AuthorizationFailure_Blacklist_Expires_Mode EQ 'in' }">
		<MvIF EXPR = "{ NOT [ g.Filename_Admin ].Validate_WholeNumber_Positive_Required( g.AuthorizationFailure_Blacklist_Expires_In ) }">
			<MvFUNCTIONRETURN VALUE = "{ JSON_Response_FieldError( 'AuthorizationFailure_Blacklist_Expires_In', g.Validation_Message ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].Value_In_List( g.AuthorizationFailure_Blacklist_Expires_In_Unit, 'm,h,d' ) }">
			<MvFUNCTIONRETURN VALUE = "{ JSON_Response_FieldError( '', 'Please select a valid expiration unit' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.AuthorizationFailure_Blacklist_Expires_Mode EQ 'never' }">	<MvASSIGN NAME = "l.expires" VALUE = 0>
	<MvELSEIF EXPR = "{ g.AuthorizationFailure_Blacklist_Expires_Mode EQ 'on' }">	<MvASSIGN NAME = "l.expires" VALUE = "{ g.AuthorizationFailure_Blacklist_Expires_On }">
	<MvELSEIF EXPR = "{ g.AuthorizationFailure_Blacklist_Expires_Mode EQ 'in' }">	<MvASSIGN NAME = "l.expires" VALUE = "{ s.dyn_time_t + [ g.Module_Feature_PAY_UT ].Payment_BuildSeconds( g.AuthorizationFailure_Blacklist_Expires_In, g.AuthorizationFailure_Blacklist_Expires_In_Unit ) }">
	</MvIF>

	<MvFOREACH ITERATOR = "l.ip" ARRAY = "l.ips" COUNT = "{ JSON_Array_String( g.AuthorizationFailure_Blacklist_IPs, l.ips ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PAY_UT ].Payment_AuthorizationBlacklist_Blacklist_IP( l.ip, l.expires ) }">
			<MvFUNCTIONRETURN VALUE = "{ JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-JSN-ATF-00004', 'IP \'' $ l.ip $ '\' was manually blacklisted' ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = "{ JSON_Response_Success() }">
</MvFUNCTION>

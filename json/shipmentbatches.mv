<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-JSN-SBCH-
| Next Error Code: 7    
|
</MvCOMMENT>

<MvFUNCTION NAME = "JSON_ShipmentBatch" PARAMETERS = "shipmentbatch var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	"id":			<MvEVAL EXPR = "{ int( l.shipmentbatch:id ) }">,
	"batchdate":	<MvEVAL EXPR = "{ int( l.shipmentbatch:batchdate ) }">,
	"name":			"<MvEVAL EXPR = "{ JSON_Encode( l.shipmentbatch:name ) }">"
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ShipmentBatchList_Load_Query" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT JSON_Store_Open() }">								<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Filename_Admin ].CanI( 'ORDR', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvASSIGN NAME = "l.available_filters"		VALUE = "">
	<MvASSIGN NAME = "l.filter"					VALUE = "">
	<MvASSIGN NAME = "l.sort"					VALUE = "">
	<MvASSIGN NAME = "l.offset"					VALUE = 0>
	<MvASSIGN NAME = "l.count"					VALUE = 0>

	<MvEVAL EXPR = "{ JSON_AvailableFilter_INTEGER(	l.available_filters, 'id',			'' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_INTEGER(	l.available_filters, 'batchdate',	'' ) }">
	<MvEVAL EXPR = "{ JSON_AvailableFilter_CHAR(	l.available_filters, 'name',		'' ) }">

	<MvIF EXPR = "{ NOT JSON_Input_Filter(	'o', 'Filter',	l.filter )	OR
					NOT JSON_Input_Text(	'o', 'Sort',	l.sort )	OR
					NOT JSON_Input_Integer(	'o', 'Offset',	l.offset )	OR
					NOT JSON_Input_Integer(	'o', 'Count',	l.count ) }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Response_InputErrors() }">
	</MvIF>
	
	<MvEVAL EXPR = "{ [ g.Library_DB ].SQL_Query_SELECT( l.search_query, '*' ) }">
	<MvEVAL EXPR = "{ [ g.Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'ShipmentBatches', '' ) }">
	
	<MvEVAL EXPR = "{ JSON_Filter( l.search_query, l.filter, l.available_filters ) }">
	<MvEVAL EXPR = "{ JSON_OrderBy( l.search_query, l.sort, 'id,batchdate,name', 'id' ) }">

	<MvASSIGN NAME = "l.search_sql" VALUE = "{ [ g.Library_DB ].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Library_DB ].SQL_Query_Count( l.search_query, l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'ShipmentBatches', l.search_sql, l.search_fields, l.offset, l.count ) }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Response_Error( 'MER-JSN-SBCH-00005', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.shipmentbatch_count" VALUE = 0>

	<MvEVAL EXPR = "{ JSON_Response_Start() }">
	{
		"total_count": 	<MvEVAL EXPR = "{ int( l.total_count ) }">,
		"start_offset": <MvEVAL EXPR = "{ int( l.offset ) }">,
		"data":
		[
		<MvWHILE EXPR = "{ ( NOT ShipmentBatches.d.EOF ) AND ( ( l.count EQ 0 ) OR ( l.shipmentbatch_count LT l.count ) ) }">
			<MvEVAL EXPR = "{ [ g.Library_DB ].ShipmentBatch_Read( l.shipmentbatch ) }">

			<MvEVAL EXPR = "{ JSON_ArrayElement_Start( l.shipmentbatch_count ) }">
				<MvEVAL EXPR = "{ JSON_ShipmentBatch( l.shipmentbatch ) }">
			<MvEVAL EXPR = "{ JSON_ArrayElement_End() }">

			<MvSKIP NAME = "Merchant" VIEW = "ShipmentBatches" ROWS = 1>
		</MvWHILE>
		]
	}
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ShipmentBatches">
	<MvFUNCTIONRETURN VALUE = "{ JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ShipmentBatchReportList_Load" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT JSON_Store_Open() }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Filename_Admin ].CanI( 'ORDR', 1, 0, 0, 0 ) }">		<MvFUNCTIONRETURN>	</MvIF>

	<MvASSIGN NAME = "l.module_pos"				VALUE = 1>
	<MvASSIGN NAME = "l.module_count"			VALUE = "{ [ g.Library_DB ].ModuleList_Load_Features( 'batchreport', l.modules ) }">

	<MvEVAL EXPR = "{ JSON_Response_Start() }">
	[
	<MvWHILE EXPR = "{ l.module_pos LE l.module_count }">
		<MvIF EXPR = "{ l.modules[ l.module_pos ]:api_ver GE 5.70 }">
			<MvASSIGN NAME = "l.report_pos"		VALUE = 1>
			<MvASSIGN NAME = "l.report_count"	VALUE = "{ [ g.Module_Root $ l.modules[ l.module_pos ]:module ].BatchReportModule_Shipment_Reports( l.modules[ l.module_pos ], l.reports ) }">

			<MvWHILE EXPR = "{ l.report_pos LE l.report_count }">
				<MvEVAL EXPR = "{ JSON_ArrayElement_Start( l.output_report_count ) }">
				"code":		"<MvEVAL EXPR = "{ JSON_Encode( l.reports[ l.report_pos ]:code ) }">",
				"name":		"<MvEVAL EXPR = "{ JSON_Encode( l.reports[ l.report_pos ]:name ) }">",
				"module":	{ <MvEVAL EXPR = "{ JSON_Module( l.modules[ l.module_pos ] ) }"> }
				<MvEVAL EXPR = "{ JSON_ArrayElement_End() }">

				<MvASSIGN NAME = "l.report_pos"	VALUE = "{ l.report_pos + 1 }">
			</MvWHILE>
		</MvIF>

		<MvASSIGN NAME = "l.module_pos"			VALUE = "{ l.module_pos + 1 }">
	</MvWHILE>
	]
	<MvFUNCTIONRETURN VALUE = "{ JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ShipmentBatchReport_Run" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT JSON_Store_Open() }">								<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Filename_Admin ].CanI( 'ORDR', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Library_DB ].ModuleAndFeatures_Load_Code( g.Module_Code, l.module ) }">
		<MvIF EXPR = "{ NOT [ g.Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ JSON_HTML_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ JSON_HTML_Error( 'MER-JSN-SBCH-00006', 'Module not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ ( NOT l.module:feature_hash:batchreport ) OR ( l.module:api_ver LT 5.70 ) }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_HTML_Error( 'MER-JSN-SBCH-00003', 'Module \'' $ l.module:name $ '\' does not support the 5.70 or newer Batch Reporting API' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.shipment_count"				VALUE = "{ JSON_Array_Integer( g.OrderShipment_IDs, l.shipment_ids ) }">

	<MvIF EXPR = "{ NOT ISNULL g.ShipmentBatch_ID }">
		<MvASSIGN NAME = "l.loaded_shipment_count"	VALUE = "{ [ g.Library_DB ].OrderShipmentList_Load_ShipmentBatch( g.ShipmentBatch_ID, l.shipments ) }">
	<MvELSE>
		<MvFOREACH ITERATOR = "l.shipment_id" ARRAY = "l.shipment_ids" COUNT = "{ l.shipment_count }">
			<MvIF EXPR = "{ NOT [ g.Library_DB ].OrderShipment_Load_ID( l.shipment_id, l.shipment ) }">
				<MvFOREACHCONTINUE>
			</MvIF>

			<MvASSIGN NAME = "l.loaded_shipment_count"											VALUE = "{ l.loaded_shipment_count + 1 }">
			<MvASSIGN NAME = "l.shipments"				INDEX = "{ l.loaded_shipment_count }"	VALUE = "{ l.shipment }">
		</MvFOREACH>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Root $ l.module:module ].BatchReportModule_Run_ShipmentList( l.module, g.Report_Code, l.shipments, l.loaded_shipment_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_HTML_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ShipmentBatch_Insert" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT JSON_Store_Open() }">								<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Filename_Admin ].CanI( 'ORDR', 0, 1, 0, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvIF EXPR = "{ NOT JSON_Input_Text( 'o:100', 'ShipmentBatch_Name', l.shipmentbatch_name ) }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Response_InputErrors() }">
	</MvIF>

	<MvASSIGN NAME = "l.shipmentbatch"				VALUE = "">
	<MvASSIGN NAME = "l.shipmentbatch:batchdate"	VALUE = "{ s.dyn_time_t }">
	<MvASSIGN NAME = "l.shipmentbatch:name"			VALUE = "{ l.shipmentbatch_name }">

	<MvIF EXPR = "{ NOT [ g.Library_DB ].ShipmentBatch_Insert( l.shipmentbatch ) }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Response_Error( g.Error_code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.shipmentbatch_name }">	<MvASSIGN NAME = "l.log_message" VALUE = "{ 'Shipment Batch \'' $ l.shipmentbatch:name $ '\' created' }">
	<MvELSE>											<MvASSIGN NAME = "l.log_message" VALUE = "{ 'Shipment Batch ' $ l.shipmentbatch:id $ ' created' }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Filename_Admin ].Admin_Log_Action( 'MER-JSN-SBCH-00004', l.log_message ) }">

	<MvEVAL EXPR = "{ JSON_Response_Start() }">
	{
		<MvEVAL EXPR = "{ JSON_ShipmentBatch( l.shipmentbatch ) }">
	}
	<MvFUNCTIONRETURN VALUE = "{ JSON_Response_End() }">
</MvFUNCTION>

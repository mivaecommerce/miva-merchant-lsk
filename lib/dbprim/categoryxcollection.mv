<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-CXC-
| Next Error Code: 12
|
</MvCOMMENT>

<MvFUNCTION NAME = "CategoryXCollection_Read" PARAMETERS = "categoryxcollection var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.categoryxcollection:cat_id" VALUE = "{ CategoryXCollection.d.cat_id }">
	<MvASSIGN NAME = "l.categoryxcollection:col_id" VALUE = "{ CategoryXCollection.d.col_id }">
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXCollection_Load" PARAMETERS = "cat_id, col_id, categoryxcollection var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CategoryXCollection"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CategoryXCollection WHERE cat_id = ? AND col_id = ?' }"
				FIELDS	= "l.cat_id, l.col_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXC-00001', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ CategoryXCollection.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryXCollection">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CXC-00002' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ CategoryXCollection_Read( l.categoryxcollection ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryXCollection">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXCollection_Load_Category" PARAMETERS = "cat_id, categoryxcollections var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CategoryXCollection"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CategoryXCollection WHERE cat_id = ?' }"
				FIELDS	= "l.cat_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXC-00003', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT CategoryXCollection.d.EOF }">
		<MvEVAL EXPR = "{ CategoryXCollection_Read( l.categoryxcollections[ ++l.count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "CategoryXCollection" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryXCollection">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-CXC-00004', l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXCollection_Load_Collection" PARAMETERS = "col_id, categoryxcollections var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CategoryXCollection"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CategoryXCollection WHERE col_id = ?' }"
				FIELDS	= "l.col_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXC-00005', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT CategoryXCollection.d.EOF }">
		<MvEVAL EXPR = "{ CategoryXCollection_Read( l.categoryxcollections[ ++l.count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "CategoryXCollection" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryXCollection">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-CXC-00006', l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXCollection_Count_Collection" PARAMETERS = "col_id, count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CategoryXCollection"
				QUERY	= "{ 'SELECT COUNT( * ) AS total_count FROM ' $ g.Store_Table_Prefix $ 'CategoryXCollection WHERE col_id = ?' }"
				FIELDS	= "l.col_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXC-00011', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ CategoryXCollection.d.EOF }">	<MvASSIGN NAME = "l.count" VALUE = 0>
	<MvELSE>										<MvASSIGN NAME = "l.count" VALUE = "{ CategoryXCollection.d.total_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryXCollection">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXCollection_Insert" PARAMETERS = "categoryxcollection var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'CategoryXCollection
					      ( cat_id, col_id )
						  VALUES
						  ( ?, ? )' }"
			 FIELDS	= "l.categoryxcollection:cat_id, l.categoryxcollection:col_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXC-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXCollection_Delete" PARAMETERS = "cat_id, col_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CategoryXCollection
						  WHERE
							cat_id = ? AND
							col_id = ?' }"
			 FIELDS	= "l.cat_id, l.col_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXC-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXCollection_Delete_All_Category" PARAMETERS = "cat_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CategoryXCollection WHERE cat_id = ?' }"
			 FIELDS	= "l.cat_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXC-00009', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXCollection_Delete_All_Collection" PARAMETERS = "col_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CategoryXCollection WHERE col_id = ?' }"
			 FIELDS	= "l.col_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXC-00010', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-CTG-
| Next Error Code: 26
|
</MvCOMMENT>

<MvFUNCTION NAME = "Category_Read" PARAMETERS = "category var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.category:id"					VALUE = "{ Categories.d.id }">
	<MvASSIGN NAME = "l.category:parent_id"				VALUE = "{ Categories.d.parent_id }">
	<MvASSIGN NAME = "l.category:validated_parent_id"	VALUE = "{ Categories.d.parent_id }">
	<MvASSIGN NAME = "l.category:original_parent_id"	VALUE = "{ Categories.d.parent_id }">
	<MvASSIGN NAME = "l.category:agrpcount"				VALUE = "{ Categories.d.agrpcount }">
	<MvASSIGN NAME = "l.category:depth"					VALUE = "{ Categories.d.depth }">
	<MvASSIGN NAME = "l.category:disp_order"			VALUE = "{ Categories.d.disp_order }">
	<MvASSIGN NAME = "l.category:page_code"				VALUE = "{ Categories.d.page_code }">
	<MvASSIGN NAME = "l.category:code"					VALUE = "{ Categories.d.code }">
	<MvASSIGN NAME = "l.category:name"					VALUE = "{ Categories.d.name }">
	<MvASSIGN NAME = "l.category:page_title"			VALUE = "{ Categories.d.page_title }">
	<MvASSIGN NAME = "l.category:active"				VALUE = "{ Categories.d.active }">
	<MvASSIGN NAME = "l.category:dt_created"			VALUE = "{ Categories.d.dt_created }">
	<MvASSIGN NAME = "l.category:dt_updated"			VALUE = "{ Categories.d.dt_updated }">
</MvFUNCTION>

<MvFUNCTION NAME = "Category_Insert_LowLevel" PARAMETERS = "category var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.category:id"					VALUE = "{ StoreKey_Generate( 'Categories' ) }">
	<MvASSIGN NAME = "l.category:agrpcount"				VALUE = 0>
	<MvASSIGN NAME = "l.category:disp_order"			VALUE = "{ StoreKey_Generate( 'CategoryDisplayOrder' ) }">
	<MvASSIGN NAME = "l.category:validated_parent_id"	VALUE = "{ l.category:parent_id }">
	<MvASSIGN NAME = "l.category:original_parent_id"	VALUE = "{ l.category:parent_id }">
	<MvASSIGN NAME = "l.category:dt_created"			VALUE = "{ s.dyn_time_t }">
	<MvASSIGN NAME = "l.category:dt_updated"			VALUE = "{ l.category:dt_created }">

	<MvIF EXPR = "{ ISNULL l.category:depth }">
		<MvASSIGN NAME = "l.category:depth"				VALUE = 0>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'Categories
					      ( id, parent_id, agrpcount, depth, disp_order, page_code, code, name, page_title, active, dt_created, dt_updated )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.category:id, l.category:parent_id, l.category:agrpcount, l.category:depth, l.category:disp_order,
					   l.category:page_code, l.category:code, l.category:name, l.category:page_title,
					   l.category:active, l.category:dt_created, l.category:dt_updated">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CTG-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Category_Update_LowLevel" PARAMETERS = "category var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.category:parent_id AND ( l.category:parent_id NE l.category:validated_parent_id ) }">
		<MvIF EXPR = "{ NOT Category_Validate_Parent( l.category ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.category:depth }">
		<MvASSIGN NAME = "l.category:depth"		VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.category:dt_updated"	VALUE = "{ s.dyn_time_t }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Categories
					      SET
							parent_id	= ?,
							page_code	= ?,
							depth		= ?,
							code		= ?,
							name		= ?,
							page_title	= ?,
							active		= ?,
							dt_updated	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.category:parent_id, l.category:page_code, l.category:depth, l.category:code, l.category:name, l.category:page_title, l.category:active, l.category:dt_updated,
					   l.category:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CTG-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Category_Update_Parent_All" PARAMETERS = "old_parent_id, new_parent_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Categories
					      SET
							parent_id	= ?
					      WHERE
						    parent_id	= ?' }"
			 FIELDS	= "l.new_parent_id, l.old_parent_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CTG-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Category_Update_Page_All" PARAMETERS = "old_page_id, new_page_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CTG-00019', 'Category_Update_Page_All has been deprecated' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Category_Update_DisplayOrder" PARAMETERS = "category var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Categories
					      SET
							disp_order	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.category:disp_order,
					   l.category:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CTG-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Category_Update_LastUpdated" PARAMETERS = "category_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.dt_updated" VALUE = "{ s.dyn_time_t }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Categories
					      SET
							dt_updated	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.dt_updated,
					   l.category_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CTG-00020', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Category_Increment_AvailabilityGroupCount" PARAMETERS = "cat_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Categories
					      SET
							agrpcount	= agrpcount + 1
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.cat_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CTG-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Category_Decrement_AvailabilityGroupCount" PARAMETERS = "cat_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Categories
					      SET
							agrpcount	= agrpcount - 1
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.cat_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CTG-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Category_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'Categories WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CTG-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Category_Load_ID" PARAMETERS = "id, category var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Categories"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Categories WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CTG-00008', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Categories.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Categories">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CTG-00014' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Category_Read( l.category ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Categories">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Category_Load_ID_Cached" PARAMETERS = "id, category var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.id LE 0 }">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CTG-00022' ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| Probe the cache for Runtime_Category_Load_ID_Cached.  We do not populate this value, however,
	| because this function may return results that would be excluded in runtime.
	|
	</MvCOMMENT>
	
	<MvREFERENCEARRAY NAME = "l.cache"					VARIABLE = "g.Session:cache:category">
		<MvDIMENSION INDEX = "{ l.id }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ l.cache:result }">
		<MvASSIGN NAME = "l.category"					VALUE = "{ l.cache:category }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvCOMMENT>
	|
	| Perform our own loading
	|
	</MvCOMMENT>

	<MvREFERENCEARRAY NAME = "l.cache"					VARIABLE = "g.Session:cache:category_load_id_cached">
		<MvDIMENSION INDEX = "{ l.id }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ Category_Load_ID( l.id, l.cache:category ) }">

		<MvIF EXPR = "{ l.cache:result }">
			<MvREFERENCE NAME = "g.Session:cache:category_load_code_cached" MEMBER = "{ l.cache:category:code }" VARIABLE = "l.cache">
		<MvELSE>
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.category"						VALUE = "{ l.cache:category }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "Category_Load_Code" PARAMETERS = "code, category var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Categories"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Categories WHERE ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CTG-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Categories.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Categories">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CTG-00015' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Category_Read( l.category ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Categories">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Category_Load_Code_Cached" PARAMETERS = "category_code, category var" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCEARRAY NAME = "l.cache" 					VARIABLE = "g.Session:cache:category_load_code_cached">
		<MvMEMBER NAME = "{ l.category_code }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ Category_Load_Code( l.category_code, l.cache:category ) }">

		<MvIF EXPR = "{ l.cache:result }">
			<MvREFERENCE NAME = "g.Session:cache:category_load_id_cached" INDEX = "{ l.cache:category:id }" VARIABLE = "l.cache">
		<MvELSE>
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.category"						VALUE = "{ l.cache:category }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "Category_Load_DisplayOrder" PARAMETERS = "disp_order, category var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Categories"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Categories WHERE disp_order = ?' }"
				FIELDS	= "l.disp_order">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CTG-00013', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Categories.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Categories">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CTG-00016' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Category_Read( l.category ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Categories">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryList_Load_All" PARAMETERS = "categories var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Categories"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Categories ORDER BY disp_order' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CTG-00010', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.category_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Categories.d.EOF }">
		<MvASSIGN NAME = "l.category_count" VALUE = "{ l.category_count + 1 }">
		<MvEVAL EXPR = "{ Category_Read( l.categories[ l.category_count ] ) }">
		
		<MvSKIP NAME = "Merchant" VIEW = "Categories" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Categories">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-CTG-00017', l.category_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryList_Load_Parent" PARAMETERS = "parent_id, categories var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Categories"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Categories WHERE parent_id = ? ORDER BY disp_order' }"
				FIELDS	= "l.parent_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CTG-00011', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.category_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Categories.d.EOF }">
		<MvASSIGN NAME = "l.category_count" VALUE = "{ l.category_count + 1 }">
		<MvEVAL EXPR = "{ Category_Read( l.categories[ l.category_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Categories" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Categories">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-CTG-00018', l.category_count ) }">
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-BOD-
| Next Error Code: 10    
|
</MvCOMMENT>

<MvFUNCTION NAME = "BasketOptionDiscount_Read" PARAMETERS = "basketoptiondiscount var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.basketoptiondiscount:basket_id"		VALUE = "{ BasketOptionDiscounts.d.basket_id }">
	<MvASSIGN NAME = "l.basketoptiondiscount:line_id"		VALUE = "{ BasketOptionDiscounts.d.line_id }">
	<MvASSIGN NAME = "l.basketoptiondiscount:lineopt_id"	VALUE = "{ BasketOptionDiscounts.d.lineopt_id }">
	<MvASSIGN NAME = "l.basketoptiondiscount:attr_id"		VALUE = "{ BasketOptionDiscounts.d.attr_id }">
	<MvASSIGN NAME = "l.basketoptiondiscount:attmpat_id"	VALUE = "{ BasketOptionDiscounts.d.attmpat_id }">
	<MvASSIGN NAME = "l.basketoptiondiscount:pgrp_id"		VALUE = "{ BasketOptionDiscounts.d.pgrp_id }">
	<MvASSIGN NAME = "l.basketoptiondiscount:priority"		VALUE = "{ BasketOptionDiscounts.d.priority }">
	<MvASSIGN NAME = "l.basketoptiondiscount:display"		VALUE = "{ BasketOptionDiscounts.d.display }">
	<MvASSIGN NAME = "l.basketoptiondiscount:descrip"		VALUE = "{ BasketOptionDiscounts.d.descrip }">
	<MvASSIGN NAME = "l.basketoptiondiscount:discount"		VALUE = "{ BasketOptionDiscounts.d.discount }">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketOptionDiscount_Insert" PARAMETERS = "basketoptiondiscount var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basketoptiondiscount:basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BOD-00006', 'Basket is provisional' ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'BasketOptionDiscounts
					      ( basket_id, line_id, lineopt_id, attr_id, attmpat_id, pgrp_id, priority, display, descrip, discount )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.basketoptiondiscount:basket_id, l.basketoptiondiscount:line_id, l.basketoptiondiscount:lineopt_id,
					   l.basketoptiondiscount:attr_id, l.basketoptiondiscount:attmpat_id, l.basketoptiondiscount:pgrp_id,
					   l.basketoptiondiscount:priority, l.basketoptiondiscount:display, l.basketoptiondiscount:descrip,
					   l.basketoptiondiscount:discount">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BOD-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketOptionDiscountList_Load_Option" PARAMETERS = "line_id, attr_id, attmpat_id, basketoptiondiscounts var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "BasketOptionDiscounts"
				QUERY	= "{ 'SELECT
								*
							  FROM
								' $ g.Store_Table_Prefix $ 'BasketOptionDiscounts
							  WHERE
								line_id		= ?		AND
								attr_id		= ?		AND
								attmpat_id	= ?
							  ORDER BY
								priority DESC, pgrp_id ASC' }"
				FIELDS	= "l.line_id, l.attr_id, l.attmpat_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BOD-00002', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basketoptiondiscount_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT BasketOptionDiscounts.d.EOF }">
		<MvEVAL EXPR = "{ BasketOptionDiscount_Read( l.basketoptiondiscounts[ ++l.basketoptiondiscount_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "BasketOptionDiscounts" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "BasketOptionDiscounts">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BOD-00003', l.basketoptiondiscount_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketOptionDiscountList_Load_LineOption_ID" PARAMETERS = "lineopt_id, basketoptiondiscounts var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "BasketOptionDiscounts"
				QUERY	= "{ 'SELECT
								*
							  FROM
								' $ g.Store_Table_Prefix $ 'BasketOptionDiscounts
							  WHERE
								lineopt_id = ?
							  ORDER BY
								priority DESC, pgrp_id ASC' }"
				FIELDS	= "l.lineopt_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BOD-00007', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basketoptiondiscount_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT BasketOptionDiscounts.d.EOF }">
		<MvEVAL EXPR = "{ BasketOptionDiscount_Read( l.basketoptiondiscounts[ ++l.basketoptiondiscount_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "BasketOptionDiscounts" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "BasketOptionDiscounts">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BOD-00008', l.basketoptiondiscount_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketOptionDiscount_Delete_All_Basket" PARAMETERS = "basket_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'BasketOptionDiscounts WHERE basket_id = ?' }"
			 FIELDS	= "l.basket_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BOD-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketOptionDiscount_Delete_All_Line" PARAMETERS = "line_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'BasketOptionDiscounts WHERE line_id = ?' }"
			 FIELDS	= "l.line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BOD-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketOptionDiscount_DeleteAll_LineOption_ID" PARAMETERS = "lineopt_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'BasketOptionDiscounts WHERE lineopt_id = ?' }"
			 FIELDS	= "l.lineopt_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BOD-00009', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

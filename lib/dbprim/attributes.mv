<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-ATT-
| Next Error Code: 16   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Attribute_Read" PARAMETERS = "attribute var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.attribute:id"			VALUE = "{ Attributes.d.id }">
	<MvASSIGN NAME = "l.attribute:product_id"	VALUE = "{ Attributes.d.product_id }">
	<MvASSIGN NAME = "l.attribute:default_id"	VALUE = "{ Attributes.d.default_id }">
	<MvASSIGN NAME = "l.attribute:disp_order"	VALUE = "{ Attributes.d.disp_order }">
	<MvASSIGN NAME = "l.attribute:code"			VALUE = "{ Attributes.d.code }">
	<MvASSIGN NAME = "l.attribute:type"			VALUE = "{ Attributes.d.type }">
	<MvASSIGN NAME = "l.attribute:attemp_id"	VALUE = "{ Attributes.d.attemp_id }">
	<MvASSIGN NAME = "l.attribute:prompt"		VALUE = "{ Attributes.d.prompt }">
	<MvASSIGN NAME = "l.attribute:price"		VALUE = "{ Attributes.d.price }">
	<MvASSIGN NAME = "l.attribute:cost"			VALUE = "{ Attributes.d.cost }">
	<MvASSIGN NAME = "l.attribute:weight"		VALUE = "{ Attributes.d.weight }">
	<MvASSIGN NAME = "l.attribute:required"		VALUE = "{ Attributes.d.required }">
	<MvASSIGN NAME = "l.attribute:inventory"	VALUE = "{ Attributes.d.inventory }">
	<MvASSIGN NAME = "l.attribute:image"		VALUE = "{ Attributes.d.image }">
</MvFUNCTION>

<MvFUNCTION NAME = "Attribute_Insert_LowLevel" PARAMETERS = "attribute var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.attribute:id"			VALUE = "{ StoreKey_Generate( 'Attributes' ) }">
	<MvASSIGN NAME = "l.attribute:disp_order"	VALUE = "{ StoreKey_Generate( 'ProductAttributeDisplayOrder' ) }">

	<MvIF EXPR = "{ ISNULL l.attribute:inventory }">	<MvASSIGN NAME = "l.attribute:inventory"	VALUE = 0>	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'Attributes
					      ( id, product_id, default_id, disp_order, code, type, attemp_id, prompt, price, cost, weight, required,
						    inventory, image )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.attribute:id, l.attribute:product_id, l.attribute:default_id, l.attribute:disp_order,
					   l.attribute:code, l.attribute:type, l.attribute:attemp_id, l.attribute:prompt,
					   l.attribute:price, l.attribute:cost, l.attribute:weight, l.attribute:required,
					   l.attribute:inventory, l.attribute:image">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATT-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Attribute_Update_LowLevel" PARAMETERS = "attribute var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Attributes
					      SET
							code		= ?,
							type		= ?,
							attemp_id	= ?,
							prompt		= ?,
							price		= ?,
							cost		= ?,
							weight		= ?,
							required	= ?,
							image		= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.attribute:code, l.attribute:type, l.attribute:attemp_id, l.attribute:prompt,
					   l.attribute:price, l.attribute:cost, l.attribute:weight, l.attribute:required,
					   l.attribute:image,
					   l.attribute:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATT-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Attribute_Update_Default_LowLevel" PARAMETERS = "attribute var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Attributes
					      SET
							default_id	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.attribute:default_id,
					   l.attribute:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATT-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Attribute_Update_DisplayOrder" PARAMETERS = "attribute var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Attributes
					      SET
							disp_order	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.attribute:disp_order,
					   l.attribute:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATT-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Attribute_Update_Inventory_LowLevel" PARAMETERS = "attribute var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Attributes
					      SET
							inventory	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.attribute:inventory,
					   l.attribute:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATT-00013', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Attribute_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'Attributes WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATT-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Attribute_Delete_All_Product" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'Attributes WHERE product_id = ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATT-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Attribute_Load_ID" PARAMETERS = "id, attribute var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Attributes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Attributes WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATT-00007', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Attributes.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Attributes">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-ATT-00010' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Attribute_Read( l.attribute ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Attributes">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Attribute_Load_Code" PARAMETERS = "product_id, code, attribute var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Attributes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Attributes WHERE product_id = ? AND ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.product_id, l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATT-00008', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Attributes.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Attributes">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-ATT-00011' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Attribute_Read( l.attribute ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Attributes">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeList_Load_Product" PARAMETERS = "product_id, attributes var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Attributes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Attributes WHERE product_id = ? ORDER BY disp_order' }"
				FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATT-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.attribute_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Attributes.d.EOF }">
		<MvASSIGN NAME = "l.attribute_count" VALUE = "{ l.attribute_count + 1 }">
		<MvEVAL EXPR = "{ Attribute_Read( l.attributes[ l.attribute_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Attributes" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Attributes">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-ATT-00012', l.attribute_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeList_Load_Product_Inventory" PARAMETERS = "product_id, attributes var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Attributes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Attributes WHERE product_id = ? AND inventory = 1 ORDER BY disp_order' }"
				FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATT-00014', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.attribute_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Attributes.d.EOF }">
		<MvASSIGN NAME = "l.attribute_count" VALUE = "{ l.attribute_count + 1 }">
		<MvEVAL EXPR = "{ Attribute_Read( l.attributes[ l.attribute_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Attributes" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Attributes">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-ATT-00015', l.attribute_count ) }">
</MvFUNCTION>

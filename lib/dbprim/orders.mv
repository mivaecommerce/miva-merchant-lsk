<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-ORD-
| Next Error Code: 36   
|
</MvCOMMENT>

<MvFUNCTION NAME = "v56_Order_Read" PARAMETERS = "order var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.order:id"				VALUE = "{ Orders.d.id }">
	<MvASSIGN NAME = "l.order:batch_id"			VALUE = "{ Orders.d.batch_id }">
	<MvASSIGN NAME = "l.order:pay_id"			VALUE = "{ Orders.d.pay_id }">
	<MvASSIGN NAME = "l.order:status"			VALUE = "{ Orders.d.status }">
	<MvASSIGN NAME = "l.order:pay_status"		VALUE = "{ Orders.d.pay_status }">
	<MvASSIGN NAME = "l.order:stk_status"		VALUE = "{ Orders.d.stk_status }">
	<MvASSIGN NAME = "l.order:dt_instock"		VALUE = "{ Orders.d.dt_instock }">
	<MvASSIGN NAME = "l.order:orderdate"		VALUE = "{ Orders.d.orderdate }">
	<MvASSIGN NAME = "l.order:cust_id"			VALUE = "{ Orders.d.cust_id }">
	<MvASSIGN NAME = "l.order:original_cust_id"	VALUE = "{ Orders.d.cust_id }">
	<MvASSIGN NAME = "l.order:ship_res"			VALUE = "{ Orders.d.ship_res }">
	<MvASSIGN NAME = "l.order:ship_fname"		VALUE = "{ Orders.d.ship_fname }">
	<MvASSIGN NAME = "l.order:ship_lname"		VALUE = "{ Orders.d.ship_lname }">
	<MvASSIGN NAME = "l.order:ship_email"		VALUE = "{ Orders.d.ship_email }">
	<MvASSIGN NAME = "l.order:ship_comp"		VALUE = "{ Orders.d.ship_comp }">
	<MvASSIGN NAME = "l.order:ship_phone"		VALUE = "{ Orders.d.ship_phone }">
	<MvASSIGN NAME = "l.order:ship_fax"			VALUE = "{ Orders.d.ship_fax }">

	<MvIF EXPR = "{ len( Orders.d.ship_addr2 ) EQ 0 }">
		<MvASSIGN NAME = "l.order:ship_addr"	VALUE = "{ Orders.d.ship_addr }">
	<MvELSE>
		<MvASSIGN NAME = "l.order:ship_addr"	VALUE = "{ Orders.d.ship_addr $ ' ' $ Orders.d.ship_addr2 }">
	</MvIF>

	<MvASSIGN NAME = "l.order:ship_addr1"		VALUE = "{ Orders.d.ship_addr }">
	<MvASSIGN NAME = "l.order:ship_addr2"		VALUE = "{ Orders.d.ship_addr2 }">
	<MvASSIGN NAME = "l.order:ship_city"		VALUE = "{ Orders.d.ship_city }">
	<MvASSIGN NAME = "l.order:ship_state"		VALUE = "{ Orders.d.ship_state }">
	<MvASSIGN NAME = "l.order:ship_zip"			VALUE = "{ Orders.d.ship_zip }">
	<MvASSIGN NAME = "l.order:ship_cntry"		VALUE = "{ Orders.d.ship_cntry }">
	<MvASSIGN NAME = "l.order:bill_fname"		VALUE = "{ Orders.d.bill_fname }">
	<MvASSIGN NAME = "l.order:bill_lname"		VALUE = "{ Orders.d.bill_lname }">
	<MvASSIGN NAME = "l.order:bill_email"		VALUE = "{ Orders.d.bill_email }">
	<MvASSIGN NAME = "l.order:bill_comp"		VALUE = "{ Orders.d.bill_comp }">
	<MvASSIGN NAME = "l.order:bill_phone"		VALUE = "{ Orders.d.bill_phone }">
	<MvASSIGN NAME = "l.order:bill_fax"			VALUE = "{ Orders.d.bill_fax }">

	<MvIF EXPR = "{ len( Orders.d.bill_addr2 ) EQ 0 }">
		<MvASSIGN NAME = "l.order:bill_addr"	VALUE = "{ Orders.d.bill_addr }">
	<MvELSE>
		<MvASSIGN NAME = "l.order:bill_addr"	VALUE = "{ Orders.d.bill_addr $ ' ' $ Orders.d.bill_addr2 }">
	</MvIF>

	<MvASSIGN NAME = "l.order:bill_addr1"		VALUE = "{ Orders.d.bill_addr }">
	<MvASSIGN NAME = "l.order:bill_addr2"		VALUE = "{ Orders.d.bill_addr2 }">
	<MvASSIGN NAME = "l.order:bill_city"		VALUE = "{ Orders.d.bill_city }">
	<MvASSIGN NAME = "l.order:bill_state"		VALUE = "{ Orders.d.bill_state }">
	<MvASSIGN NAME = "l.order:bill_zip"			VALUE = "{ Orders.d.bill_zip }">
	<MvASSIGN NAME = "l.order:bill_cntry"		VALUE = "{ Orders.d.bill_cntry }">
	<MvASSIGN NAME = "l.order:ship_id"			VALUE = "{ Orders.d.ship_id }">
	<MvASSIGN NAME = "l.order:ship_data"		VALUE = "{ Orders.d.ship_data }">
	<MvASSIGN NAME = "l.order:source"			VALUE = "{ Orders.d.source }">
	<MvASSIGN NAME = "l.order:source_id"		VALUE = "{ Orders.d.source_id }">
	<MvASSIGN NAME = "l.order:total"			VALUE = "{ Orders.d.total }">
	<MvASSIGN NAME = "l.order:total_ship"		VALUE = "{ Orders.d.total_ship }">
	<MvASSIGN NAME = "l.order:total_tax"		VALUE = "{ Orders.d.total_tax }">
	<MvASSIGN NAME = "l.order:total_auth"		VALUE = "{ Orders.d.total_auth }">
	<MvASSIGN NAME = "l.order:total_capt"		VALUE = "{ Orders.d.total_capt }">
	<MvASSIGN NAME = "l.order:total_rfnd"		VALUE = "{ Orders.d.total_rfnd }">
	<MvASSIGN NAME = "l.order:net_capt"			VALUE = "{ Orders.d.net_capt }">
	<MvASSIGN NAME = "l.order:pend_count"		VALUE = "{ Orders.d.pend_count }">
	<MvASSIGN NAME = "l.order:bord_count"		VALUE = "{ Orders.d.bord_count }">
	<MvASSIGN NAME = "l.order:note_count"		VALUE = "{ Orders.d.note_count }">
	<MvASSIGN NAME = "l.order:dt_updated"		VALUE = "{ Orders.d.dt_updated }">
</MvFUNCTION>

<MvFUNCTION NAME = "Order_Insert_LowLevel" PARAMETERS = "order var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.order:pay_id }">
		<MvASSIGN NAME = "l.order:pay_id"		VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.order:orderdate }">
		<MvASSIGN NAME = "l.order:orderdate"	VALUE = "{ s.dyn_time_t }">
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.order:dt_instock }">
		<MvASSIGN NAME = "l.order:dt_instock"	VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ len( l.order:ship_addr ) AND ( NOT len( l.order:ship_addr1 ) ) AND ( NOT len( l.order:ship_addr2 ) ) }">
		<MvASSIGN NAME = "l.order:ship_addr1"	VALUE = "{ l.order:ship_addr }">
	</MvIF>

	<MvIF EXPR = "{ len( l.order:bill_addr ) AND ( NOT len( l.order:bill_addr1 ) ) AND ( NOT len( l.order:bill_addr2 ) ) }">
		<MvASSIGN NAME = "l.order:bill_addr1"	VALUE = "{ l.order:bill_addr }">
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.order:ship_res }">
		<MvASSIGN NAME = "l.order:ship_res" 	VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.order:note_count }">
		<MvASSIGN NAME = "l.order:note_count"	VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.order:dt_updated }">
		<MvASSIGN NAME = "l.order:dt_updated"	VALUE = "{ s.dyn_time_t }">
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.order:source_id }">
		<MvASSIGN NAME = "l.order:source_id"	VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.order:total_ship }">
		<MvASSIGN NAME = "l.order:total_ship"	VALUE = 0.00>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.order:total_tax }">
		<MvASSIGN NAME = "l.order:total_tax"	VALUE = 0.00>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'Orders
						  ( id, batch_id, pay_id, status, pay_status, stk_status, dt_instock, orderdate, cust_id,
						  	ship_res, ship_fname, ship_lname, ship_email, ship_comp, ship_phone, ship_fax, ship_addr, ship_addr2, ship_city, ship_state, ship_zip, ship_cntry,
						  	bill_fname, bill_lname, bill_email, bill_comp, bill_phone, bill_fax, bill_addr, bill_addr2, bill_city, bill_state, bill_zip, bill_cntry,
						  	ship_id, ship_data, source, source_id, total, total_ship, total_tax, total_auth, total_capt, total_rfnd, net_capt, pend_count, bord_count, note_count, dt_updated )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.order:id, l.order:batch_id, l.order:pay_id, l.order:status, l.order:pay_status, l.order:stk_status,
					   l.order:dt_instock, l.order:orderdate, l.order:cust_id, l.order:ship_res,
					   l.order:ship_fname, l.order:ship_lname, l.order:ship_email, l.order:ship_comp,
					   l.order:ship_phone, l.order:ship_fax, l.order:ship_addr1, l.order:ship_addr2, l.order:ship_city,
					   l.order:ship_state, l.order:ship_zip, l.order:ship_cntry, l.order:bill_fname,
					   l.order:bill_lname, l.order:bill_email, l.order:bill_comp, l.order:bill_phone,
					   l.order:bill_fax, l.order:bill_addr1, l.order:bill_addr2, l.order:bill_city, l.order:bill_state,
					   l.order:bill_zip, l.order:bill_cntry, l.order:ship_id, l.order:ship_data, l.order:source, l.order:source_id,
					   l.order:total, l.order:total_ship, l.order:total_tax, l.order:total_auth, l.order:total_capt, l.order:total_rfnd, l.order:net_capt,
					   l.order:pend_count, l.order:bord_count, l.order:note_count, l.order:dt_updated">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ len( l.order:ship_addr2 ) EQ 0 }">
		<MvASSIGN NAME = "l.order:ship_addr"	VALUE = "{ l.order:ship_addr1 }">
	<MvELSE>
		<MvASSIGN NAME = "l.order:ship_addr"	VALUE = "{ l.order:ship_addr1 $ ' ' $ l.order:ship_addr2 }">
	</MvIF>

	<MvIF EXPR = "{ len( l.order:bill_addr2 ) EQ 0 }">
		<MvASSIGN NAME = "l.order:bill_addr"	VALUE = "{ l.order:bill_addr1 }">
	<MvELSE>
		<MvASSIGN NAME = "l.order:bill_addr"	VALUE = "{ l.order:bill_addr1 $ ' ' $ l.order:bill_addr2 }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Order_Update_Customer_Information" PARAMETERS = "order var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ len( l.order:ship_addr ) AND
					l.order:ship_addr NE ( l.order:ship_addr1 $ ' ' $ l.order:ship_addr2 ) }">
		<MvASSIGN NAME = "l.order:ship_addr1"	VALUE = "{ l.order:ship_addr }">
		<MvASSIGN NAME = "l.order:ship_addr2"	VALUE = "">
	</MvIF>

	<MvIF EXPR = "{ len( l.order:bill_addr ) AND
					l.order:bill_addr NE ( l.order:bill_addr1 $ ' ' $ l.order:bill_addr2 ) }">
		<MvASSIGN NAME = "l.order:bill_addr1"	VALUE = "{ l.order:bill_addr }">
		<MvASSIGN NAME = "l.order:bill_addr2"	VALUE = "">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Orders
					      SET
							cust_id		= ?,
							ship_res	= ?,
							ship_fname	= ?,
							ship_lname	= ?,
							ship_email	= ?,
							ship_comp	= ?,
							ship_phone	= ?,
							ship_fax	= ?,
							ship_addr	= ?,
							ship_addr2	= ?,
							ship_city	= ?,
							ship_state	= ?,
							ship_zip	= ?,
							ship_cntry	= ?,
							bill_fname	= ?,
							bill_lname	= ?,
							bill_email	= ?,
							bill_comp	= ?,
							bill_phone	= ?,
							bill_fax	= ?,
							bill_addr	= ?,
							bill_addr2	= ?,
							bill_city	= ?,
							bill_state	= ?,
							bill_zip	= ?,
							bill_cntry	= ?
					      WHERE
						    id	= ?' }"
			 FIELDS	= "l.order:cust_id, l.order:ship_res, l.order:ship_fname, l.order:ship_lname, l.order:ship_email, l.order:ship_comp,
					   l.order:ship_phone, l.order:ship_fax, l.order:ship_addr1, l.order:ship_addr2, l.order:ship_city,
					   l.order:ship_state, l.order:ship_zip, l.order:ship_cntry, l.order:bill_fname,
					   l.order:bill_lname, l.order:bill_email, l.order:bill_comp, l.order:bill_phone,
					   l.order:bill_fax, l.order:bill_addr1, l.order:bill_addr2, l.order:bill_city, l.order:bill_state,
					   l.order:bill_zip, l.order:bill_cntry,
					   l.order:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ len( l.order:ship_addr2 ) EQ 0 }">
		<MvASSIGN NAME = "l.order:ship_addr"	VALUE = "{ l.order:ship_addr1 }">
	<MvELSE>
		<MvASSIGN NAME = "l.order:ship_addr"	VALUE = "{ l.order:ship_addr1 $ ' ' $ l.order:ship_addr2 }">
	</MvIF>

	<MvIF EXPR = "{ len( l.order:bill_addr2 ) EQ 0 }">
		<MvASSIGN NAME = "l.order:bill_addr"	VALUE = "{ l.order:bill_addr1 }">
	<MvELSE>
		<MvASSIGN NAME = "l.order:bill_addr"	VALUE = "{ l.order:bill_addr1 $ ' ' $ l.order:bill_addr2 }">
	</MvIF>

	<MvIF EXPR = "{ l.order:original_cust_id NE l.order:cust_id }">
		<MvIF EXPR = "{ l.order:original_cust_id }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_CUS_DB ].Customer_Update_OrderMetrics( l.order:original_cust_id ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.order:cust_id }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_CUS_DB ].Customer_Update_OrderMetrics( l.order:cust_id ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Order_Update_Batch" PARAMETERS = "order var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Orders
					      SET
							batch_id	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.order:batch_id,
					   l.order:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00018', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Order_Update_Shipping" PARAMETERS = "order var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Orders
					      SET
							ship_id		= ?,
							ship_data	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.order:ship_id, l.order:ship_data,
					   l.order:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Order_Update_Payment_Module" PARAMETERS = "order var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Orders
					      SET
							pay_id	= ?
					      WHERE
						    id		= ?' }"
			 FIELDS	= "l.order:pay_id,
			 		   l.order:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00032', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Order_Update_Total_LowLevel" PARAMETERS = "order var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Orders
						  SET
							total	= ?
						  WHERE
							id		= ?' }"
			 FIELDS	= "l.order:total,
					   l.order:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00024', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "v56_Order_Update_Total_LowLevel" PARAMETERS = "order var, total" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Orders
					      SET
							total	= ?
					      WHERE
						    id		= ?' }"
			 FIELDS	= "l.total,
					   l.order:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.null"			VALUE = "{ [ g.Module_Feature_STA_DB ].Stats_Inc_Revenue( l.total - l.order:total ) }">
	<MvASSIGN NAME = "l.order:total"	VALUE = "{ l.total }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Order_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'Orders WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "v56_Order_Load_ID" PARAMETERS = "order_id, order var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Orders"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Orders WHERE id = ?' }"
				FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00022', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Orders.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Orders">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-ORD-00023' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ v56_Order_Read( l.order ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Orders">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Order_Count_Batch" PARAMETERS = "batch_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Orders"
				QUERY	= "{ 'SELECT COUNT( * ) AS order_count FROM ' $ g.Store_Table_Prefix $ 'Orders WHERE batch_id = ?' }"
				FIELDS	= "l.batch_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00011', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Orders.d.EOF }">
		<MvASSIGN NAME = "l.order_count"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.order_count"	VALUE = "{ Orders.d.order_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Orders">
	<MvFUNCTIONRETURN VALUE = "{ l.order_count }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderList_Load_Customer" PARAMETERS = "cust_id, orders var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Orders"
				QUERY	= "{ 'SELECT 
							*
						FROM ' $ 
							g.Store_Table_Prefix $ 'Orders
						WHERE 
							cust_id = ? 
						ORDER BY 
							id' }"
				FIELDS	= "l.cust_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00019', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.order_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Orders.d.EOF }">
		<MvASSIGN NAME = "l.order_count" VALUE = "{ l.order_count + 1 }">
		<MvEVAL EXPR = "{ Order_Read( l.orders[ l.order_count ] ) }">
		<MvASSIGN NAME = "l.orders" INDEX = "{ l.order_count }" MEMBER = "tracknum" VALUE = "{ Orders.d.tracknum }">

		<MvSKIP NAME = "Merchant" VIEW = "Orders" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Orders">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-ORD-00028', l.order_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Order_Load_Customer" PARAMETERS = "order_id, cust_id, order var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Orders"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Orders WHERE id = ? AND cust_id = ?' }"
				FIELDS	= "l.order_id, l.cust_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00020', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Orders.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Orders">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-ORD-00021' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ v56_Order_Read( l.order ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Orders">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItem_Total" PARAMETERS = "order_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW	NAME	= "Merchant"
				VIEW	= "OrderItems"
				QUERY	= "{ 'SELECT
								SUM( total ) AS total
							  FROM
								' $ g.Store_Table_Prefix $ 'OrderItems
							  WHERE
								order_id	= ?		AND
								status		<> 300' }"
				FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00025', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.total" VALUE = "{ OrderItems.d.total }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].IEEE754_Normalize( 2, l.total ) ROUND 2 }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderOption_Total" PARAMETERS = "order_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-ORD-00035', 'Function OrderOption_Total has been deprecated' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Order_Update_Date" PARAMETERS = "order var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Orders SET orderdate = ? WHERE id = ?' }"
			 FIELDS = "l.order:orderdate, l.order:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00027', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Order_Update_All_Customer" PARAMETERS = "old_cust_id, new_cust_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Orders SET cust_id = ? WHERE cust_id = ?' }"
			 FIELDS = "l.new_cust_id, l.old_cust_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00029', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Order_Update_All_Shipping" PARAMETERS = "old_ship_id, old_ship_data, new_ship_id, new_ship_data" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Orders
						  SET
							ship_id		= ?,
						  	ship_data	= ?
						  WHERE
						  	ship_id		= ? AND
						  	ship_data	= ?' }"
			 FIELDS = "l.new_ship_id, l.new_ship_data,
			 		   l.old_ship_id, l.old_ship_data">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00033', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Order_Increment_NoteCount" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Orders
			 			  SET
			 			  	note_count 	= note_count + 1
			 			  WHERE
			 			  	id			= ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00030', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Order_Decrement_NoteCount" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Orders
			 			  SET
			 			  	note_count 	= note_count - 1
			 			  WHERE
			 			  	id			= ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00031', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Order_Update_LastUpdated" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Orders
			 			  SET
			 				dt_updated	= ?
			 			  WHERE
			 				id			= ?' }"
			 FIELDS = "s.dyn_time_t,
					   l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ORD-00034', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

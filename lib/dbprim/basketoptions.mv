<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-BOP-
| Next Error Code: 11    
|
</MvCOMMENT>

<MvFUNCTION NAME = "BasketOption_Read" PARAMETERS = "basketoption var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.basketoption:id"				VALUE = "{ BasketOptions.d.id }">
	<MvASSIGN NAME = "l.basketoption:basket_id"			VALUE = "{ BasketOptions.d.basket_id }">
	<MvASSIGN NAME = "l.basketoption:line_id"			VALUE = "{ BasketOptions.d.line_id }">
	<MvASSIGN NAME = "l.basketoption:attr_id"			VALUE = "{ BasketOptions.d.attr_id }">
	<MvASSIGN NAME = "l.basketoption:attr_code"			VALUE = "{ BasketOptions.d.attr_code }">
	<MvASSIGN NAME = "l.basketoption:attmpat_id"		VALUE = "{ BasketOptions.d.attmpat_id }">
	<MvASSIGN NAME = "l.basketoption:option_id"			VALUE = "{ BasketOptions.d.option_id }">
	<MvASSIGN NAME = "l.basketoption:opt_code"			VALUE = "{ BasketOptions.d.opt_code }">
	<MvASSIGN NAME = "l.basketoption:retail"			VALUE = "{ BasketOptions.d.retail }">
	<MvASSIGN NAME = "l.basketoption:base_price"		VALUE = "{ BasketOptions.d.base_price }">
	<MvASSIGN NAME = "l.basketoption:price"				VALUE = "{ BasketOptions.d.price }">
	<MvASSIGN NAME = "l.basketoption:weight"			VALUE = "{ BasketOptions.d.weight }">
	<MvASSIGN NAME = "l.basketoption:data"				VALUE = "{ BasketOptions.d.data }">
	<MvASSIGN NAME = "l.basketoption:data_long"			VALUE = "{ BasketOptions.d.data_long }">

	<MvIF EXPR = "{ BasketOptions.d.attmpat_id }">
		<MvASSIGN NAME = "l.basketoption:attr_prompt"	VALUE = "{ BasketOptions.d.attmpat_prompt }">
		<MvASSIGN NAME = "l.basketoption:opt_prompt"	VALUE = "{ BasketOptions.d.attmpopt_prompt }">
	<MvELSE>
		<MvASSIGN NAME = "l.basketoption:attr_prompt"	VALUE = "{ BasketOptions.d.attr_prompt }">
		<MvASSIGN NAME = "l.basketoption:opt_prompt"	VALUE = "{ BasketOptions.d.opt_prompt }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketOption_Insert" PARAMETERS = "basketoption var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basketoption:basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BOP-00008', 'Basket is provisional' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basketoption:id" VALUE = "{ StoreKey_Generate( 'BasketOptions' ) }">

	<MvIF EXPR = "{ ISNULL l.basketoption:retail }">		<MvASSIGN NAME = "l.basketoption:retail"		VALUE = 0.00>							</MvIF>
	<MvIF EXPR = "{ ISNULL l.basketoption:base_price }">	<MvASSIGN NAME = "l.basketoption:base_price"	VALUE = "{ l.basketoption:price }">		</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'BasketOptions
					      ( id, basket_id, line_id, attr_id, attr_code, attmpat_id, option_id, opt_code,
						    retail, base_price, price, weight, data, data_long )
						  VALUES
					      ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.basketoption:id, l.basketoption:basket_id, l.basketoption:line_id, l.basketoption:attr_id, l.basketoption:attr_code,
					   l.basketoption:attmpat_id, l.basketoption:option_id, l.basketoption:opt_code, l.basketoption:retail,
					   l.basketoption:base_price, l.basketoption:price, l.basketoption:weight, l.basketoption:data,
					   l.basketoption:data_long">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BOP-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketOption_Update" PARAMETERS = "basketoption var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'BasketOptions
						  SET
							basket_id	= ?,
							line_id		= ?,
							attr_id		= ?,
							attr_code	= ?,
							attmpat_id	= ?,
							option_id	= ?,
							opt_code	= ?,
							retail		= ?,
							base_price	= ?,
							price		= ?,
							weight		= ?,
							data		= ?,
							data_long	= ?
						  WHERE
							id			= ?' }"
			 FIELDS	= "l.basketoption:basket_id, l.basketoption:line_id, l.basketoption:attr_id, l.basketoption:attr_code,
			 		   l.basketoption:attmpat_id, l.basketoption:option_id, l.basketoption:opt_code, l.basketoption:retail,
			 		   l.basketoption:base_price, l.basketoption:price, l.basketoption:weight, l.basketoption:data, 
			 		   l.basketoption:data_long,
					   l.basketoption:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BOP-00009', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketOption_Update_Pricing" PARAMETERS = "basketoption var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'BasketOptions
						  SET
							retail		= ?,
							base_price	= ?,
							price		= ?
						  WHERE
							id			= ?' }"
			 FIELDS	= "l.basketoption:retail, l.basketoption:base_price, l.basketoption:price,
					   l.basketoption:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BOP-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketOptionList_Load_Line" PARAMETERS = "line_id, basketoptions var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "BasketOptions"
				QUERY	= "{ 'SELECT
								bo.*,
								ats.prompt	AS attr_prompt,
								ata.prompt	AS attmpat_prompt,
								opt.prompt	AS opt_prompt,
								ato.prompt	AS attmpopt_prompt,
								' $ [ g.Library_Filename_Native_DBAPI ].DB_IFNULL( 'ats.disp_order',	'0' ) $ ' AS ats_disp_order,
								' $ [ g.Library_Filename_Native_DBAPI ].DB_IFNULL( 'ata.disporder',		'0' ) $ ' AS ata_disp_order
							  FROM ' $
								g.Store_Table_Prefix $ 'BasketOptions bo
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'Attributes ats ON ats.id = bo.attr_id
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs ata ON ata.id = bo.attmpat_id
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'Options opt ON opt.id = bo.option_id
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions ato ON ato.id = bo.option_id
							  WHERE
								bo.line_id = ?
							  ORDER BY
								ats_disp_order, ata_disp_order' }"
				FIELDS	= "l.line_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BOP-00002', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basketoption_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT BasketOptions.d.EOF }">
		<MvASSIGN NAME = "l.basketoption_count" VALUE = "{ l.basketoption_count + 1 }">
		<MvEVAL EXPR = "{ BasketOption_Read( l.basketoptions[ l.basketoption_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "BasketOptions" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "BasketOptions">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BOP-00006', l.basketoption_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketOption_Delete" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'BasketOptions WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BOP-00010', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketOption_Delete_All_Line" PARAMETERS = "line_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'BasketOptions WHERE line_id = ?' }"
			 FIELDS	= "l.line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BOP-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketOption_Delete_All_Basket" PARAMETERS = "basket_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'BasketOptions WHERE basket_id = ?' }"
			 FIELDS	= "l.basket_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BOP-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketOption_Delete_All_Attribute" PARAMETERS = "line_id, attr_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'BasketOptions WHERE line_id = ? AND attr_id = ?' }"
			 FIELDS	= "l.line_id, l.attr_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BOP-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

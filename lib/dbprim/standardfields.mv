<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-FLD-
| Next Error Code: 4    
|
</MvCOMMENT>

<MvFUNCTION NAME = "StandardFields_Read" PARAMETERS = "standardfields var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.standardfields:shipping"	VALUE = "{ StandardFields.d.shipping }">
	<MvASSIGN NAME = "l.standardfields:billing"		VALUE = "{ StandardFields.d.billing }">
	<MvASSIGN NAME = "l.standardfields:primaddr"	VALUE = "{ StandardFields.d.primaddr }">
	<MvASSIGN NAME = "l.standardfields:ship_res"	VALUE = "{ StandardFields.d.ship_res }">
	<MvASSIGN NAME = "l.standardfields:mode_fname"	VALUE = "{ StandardFields.d.mode_fname }">
	<MvASSIGN NAME = "l.standardfields:mode_lname"	VALUE = "{ StandardFields.d.mode_lname }">
	<MvASSIGN NAME = "l.standardfields:mode_email"	VALUE = "{ StandardFields.d.mode_email }">
	<MvASSIGN NAME = "l.standardfields:mode_comp"	VALUE = "{ StandardFields.d.mode_comp }">
	<MvASSIGN NAME = "l.standardfields:mode_phone"	VALUE = "{ StandardFields.d.mode_phone }">
	<MvASSIGN NAME = "l.standardfields:mode_fax"	VALUE = "{ StandardFields.d.mode_fax }">
	<MvASSIGN NAME = "l.standardfields:mode_addr"	VALUE = "{ StandardFields.d.mode_addr }">
	<MvASSIGN NAME = "l.standardfields:mode_addr2"	VALUE = "{ StandardFields.d.mode_addr2 }">
	<MvASSIGN NAME = "l.standardfields:mode_city"	VALUE = "{ StandardFields.d.mode_city }">
	<MvASSIGN NAME = "l.standardfields:mode_state"	VALUE = "{ StandardFields.d.mode_state }">
	<MvASSIGN NAME = "l.standardfields:mode_zip"	VALUE = "{ StandardFields.d.mode_zip }">
	<MvASSIGN NAME = "l.standardfields:mode_cntry"	VALUE = "{ StandardFields.d.mode_cntry }">
</MvFUNCTION>

<MvFUNCTION NAME = "StandardFields_Insert" PARAMETERS = "standardfields var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'StandardFields
					      ( shipping, billing, primaddr, ship_res, mode_fname, mode_lname, mode_email, mode_comp, mode_phone, mode_fax, mode_addr,
						    mode_addr2, mode_city, mode_state, mode_zip, mode_cntry )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.standardfields:shipping, l.standardfields:billing, l.standardfields:primaddr,
			 		   l.standardfields:ship_res, l.standardfields:mode_fname, l.standardfields:mode_lname,
			 		   l.standardfields:mode_email, l.standardfields:mode_comp, l.standardfields:mode_phone,
			 		   l.standardfields:mode_fax, l.standardfields:mode_addr, l.standardfields:mode_addr2,
			 		   l.standardfields:mode_city, l.standardfields:mode_state, l.standardfields:mode_zip,
			 		   l.standardfields:mode_cntry">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-FLD-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StandardFields_Update_LowLevel" PARAMETERS = "standardfields var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'StandardFields
					      SET
							shipping	= ?,
							billing		= ?,
							primaddr	= ?,
							ship_res	= ?,
							mode_fname	= ?,
							mode_lname	= ?,
							mode_email	= ?,
							mode_comp	= ?,
							mode_phone	= ?,
							mode_fax	= ?,
							mode_addr	= ?,
							mode_addr2	= ?,
							mode_city	= ?,
							mode_state	= ?,
							mode_zip	= ?,
							mode_cntry	= ?' }"
			 FIELDS	= "l.standardfields:shipping, l.standardfields:billing, l.standardfields:primaddr,
			 		   l.standardfields:ship_res, l.standardfields:mode_fname, l.standardfields:mode_lname,
			 		   l.standardfields:mode_email, l.standardfields:mode_comp, l.standardfields:mode_phone,
			 		   l.standardfields:mode_fax, l.standardfields:mode_addr, l.standardfields:mode_addr2,
			 		   l.standardfields:mode_city, l.standardfields:mode_state, l.standardfields:mode_zip,
			 		   l.standardfields:mode_cntry">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-FLD-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StandardFields_Load" PARAMETERS = "standardfields var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "StandardFields"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'StandardFields' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-FLD-00003', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvEVAL EXPR = "{ StandardFields_Read( l.standardfields ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "StandardFields">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StandardFields_Load_Cached" PARAMETERS = "standardfields var" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCE NAME = "l.cache"						VARIABLE = "g.Session:cache:standardfields_load_cached">

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ StandardFields_Load( l.cache:standardfields ) }">

		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.standardfields"					VALUE = "{ l.cache:standardfields }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

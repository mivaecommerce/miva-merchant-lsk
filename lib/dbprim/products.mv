<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-PRD-
| Next Error Code: 31   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Product_Read" PARAMETERS = "product var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.product:id"					VALUE = "{ Products.d.id }">
	<MvASSIGN NAME = "l.product:catcount"			VALUE = "{ Products.d.catcount }">
	<MvASSIGN NAME = "l.product:cancat_id"			VALUE = "{ Products.d.cancat_id }">
	<MvASSIGN NAME = "l.product:agrpcount"			VALUE = "{ Products.d.agrpcount }">
	<MvASSIGN NAME = "l.product:pgrpcount"			VALUE = "{ Products.d.pgrpcount }">
	<MvASSIGN NAME = "l.product:disp_order"			VALUE = "{ Products.d.disp_order }">
	<MvASSIGN NAME = "l.product:page_code"			VALUE = "{ Products.d.page_code }">
	<MvASSIGN NAME = "l.product:code"				VALUE = "{ Products.d.code }">
	<MvASSIGN NAME = "l.product:sku"				VALUE = "{ Products.d.sku }">
	<MvASSIGN NAME = "l.product:name"				VALUE = "{ Products.d.name }">
	<MvASSIGN NAME = "l.product:page_title"			VALUE = "{ Products.d.page_title }">
	<MvASSIGN NAME = "l.product:thumbnail"			VALUE = "{ Products.d.thumbnail }">
	<MvASSIGN NAME = "l.product:image"				VALUE = "{ Products.d.image }">
	<MvASSIGN NAME = "l.product:price"				VALUE = "{ Products.d.price }">
	<MvASSIGN NAME = "l.product:cost"				VALUE = "{ Products.d.cost }">
	<MvASSIGN NAME = "l.product:descrip"			VALUE = "{ Products.d.descrip }">
	<MvASSIGN NAME = "l.product:weight"				VALUE = "{ Products.d.weight }">
	<MvASSIGN NAME = "l.product:taxable"			VALUE = "{ Products.d.taxable }">
	<MvASSIGN NAME = "l.product:active"				VALUE = "{ Products.d.active }">
	<MvASSIGN NAME = "l.product:original_active"	VALUE = "{ Products.d.active }">
	<MvASSIGN NAME = "l.product:dt_created"			VALUE = "{ Products.d.dt_created }">
	<MvASSIGN NAME = "l.product:dt_updated"			VALUE = "{ Products.d.dt_updated }">
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Insert_LowLevel" PARAMETERS = "product var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.product:id"				VALUE = "{ StoreKey_Generate( 'Products' ) }">
	<MvASSIGN NAME = "l.product:catcount"		VALUE = 0>
	<MvASSIGN NAME = "l.product:agrpcount"		VALUE = 0>
	<MvASSIGN NAME = "l.product:pgrpcount"		VALUE = 0>
	<MvASSIGN NAME = "l.product:disp_order"		VALUE = "{ StoreKey_Generate( 'ProductDisplayOrder' ) }">
	<MvASSIGN NAME = "l.product:dt_created"		VALUE = "{ s.dyn_time_t }">
	<MvASSIGN NAME = "l.product:dt_updated"		VALUE = "{ l.product:dt_created }">

	<MvIF EXPR = "{ ISNULL l.product:cancat_id }">	<MvASSIGN NAME = "l.product:cancat_id"	VALUE = 0>	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'Products
					      ( id, catcount, cancat_id, agrpcount, pgrpcount, disp_order, page_code, code, sku, name, page_title,
					        thumbnail, image, price, cost, descrip, weight, taxable, active, dt_created, dt_updated )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.product:id, l.product:catcount, l.product:cancat_id, l.product:agrpcount, l.product:pgrpcount, l.product:disp_order,
			 		   l.product:page_code, l.product:code, l.product:sku, l.product:name, l.product:page_title, l.product:thumbnail,
			 		   l.product:image, l.product:price, l.product:cost, l.product:descrip, l.product:weight, l.product:taxable,
			 		   l.product:active, l.product:dt_created, l.product:dt_updated">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Update_LowLevel" PARAMETERS = "product var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.product:cancat_id }">	<MvASSIGN NAME = "l.product:cancat_id"	VALUE = 0>	</MvIF>

	<MvASSIGN NAME = "l.product:original_active"	VALUE = "{ l.product:active }">
	<MvASSIGN NAME = "l.product:dt_updated"			VALUE = "{ s.dyn_time_t }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Products
					      SET
							cancat_id	= ?,
							page_code	= ?,
							code		= ?,
							sku			= ?,
							name		= ?,
							page_title	= ?,
							thumbnail	= ?,
							image		= ?,
							price		= ?,
							cost		= ?,
							descrip		= ?,
							weight		= ?,
							taxable		= ?,
							active		= ?,
							dt_updated	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.product:cancat_id, l.product:page_code, l.product:code, l.product:sku, l.product:name,
					   l.product:page_title, l.product:thumbnail, l.product:image, l.product:price, l.product:cost,
					   l.product:descrip, l.product:weight, l.product:taxable, l.product:active, l.product:dt_updated,
					   l.product:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'Products WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Load_ID" PARAMETERS = "id, product var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Products"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Products WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00004', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Products.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Products">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-PRD-00017' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Product_Read( l.product ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Products">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Load_ID_Cached" PARAMETERS = "id, product var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.id" VALUE = "{ int( l.id ) }">

	<MvIF EXPR = "{ l.id LE 0 }">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-PRD-00025' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ DB_Cache_Data_Reference( 'product_load_id_cached', l.id, l.ref ) }">

	<MvIF EXPR = "{ DB_Cache_Data_Requires_Load( l.ref ) }">
		<MvASSIGN NAME = "l.result" VALUE = "{ Product_Load_ID( l.id, l.product ) }">

		<MvEVAL EXPR = "{ DB_Cache_Data_Store_Result( l.ref, l.result, l.product ) }">

		<MvIF EXPR = "{ l.result }">
			<MvEVAL EXPR = "{ DB_Cache_Data_Reference( 'product_load_code_cached', l.product:code, l.ref_code ) }">
			<MvEVAL EXPR = "{ DB_Cache_Data_Store_Result( l.ref_code, l.result, l.product ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ DB_Cache_Data_Result( l.ref, l.product ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Update_CanonicalCategory_All" PARAMETERS = "old_cancat_id, new_cancat_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY	NAME	 = "Merchant"
				QUERY	 = "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Products 
							   SET 
							   	  cancat_id = ? 
							   WHERE 
							   	  cancat_id = ?' }"
				FIELDS	 = "l.new_cancat_id, l.old_cancat_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00022', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Update_Page_All" PARAMETERS = "old_page_id, new_page_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00023', 'Product_Update_Page_All has been deprecated' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Update_LastUpdated" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.dt_updated" VALUE = "{ s.dyn_time_t }">

	<MvQUERY	NAME	 = "Merchant"
				QUERY	 = "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Products 
							   SET 
							   	  dt_updated	= ? 
							   WHERE 
							   	  id			= ?' }"
				FIELDS	 = "l.dt_updated, l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00026', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Update_LastUpdated_Product" PARAMETERS = "product var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.product:dt_updated" VALUE = "{ s.dyn_time_t }">

	<MvQUERY	NAME	 = "Merchant"
				QUERY	 = "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Products 
							   SET 
							   	  dt_updated	= ? 
							   WHERE 
							   	  id			= ?' }"
				FIELDS	 = "l.product:dt_updated,
							l.product:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00030', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Load_Code" PARAMETERS = "code, product var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Products"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Products WHERE ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00005', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Products.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Products">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-PRD-00018' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Product_Read( l.product ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Products">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Load_Code_Cached" PARAMETERS = "product_code, product var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ DB_Cache_Data_Reference( 'product_load_code_cached', l.product_code, l.ref ) }">

	<MvIF EXPR = "{ DB_Cache_Data_Requires_Load( l.ref ) }">
		<MvASSIGN NAME = "l.result" VALUE = "{ Product_Load_Code( l.product_code, l.product ) }">

		<MvEVAL EXPR = "{ DB_Cache_Data_Store_Result( l.ref, l.result, l.product ) }">

		<MvIF EXPR = "{ l.result }">
			<MvEVAL EXPR = "{ DB_Cache_Data_Reference( 'product_load_id_cached', l.product:id, l.ref_id ) }">
			<MvEVAL EXPR = "{ DB_Cache_Data_Store_Result( l.ref_id, l.result, l.product ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ DB_Cache_Data_Result( l.ref, l.product ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Load_DisplayOrder" PARAMETERS = "disp_order, product var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Products"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Products WHERE disp_order = ?' }"
				FIELDS	= "l.disp_order">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00016', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Products.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Products">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-PRD-00019' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Product_Read( l.product ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Products">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductList_Load_All" PARAMETERS = "products var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Products"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Products ORDER BY disp_order' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00006', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.product_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Products.d.EOF }">
		<MvASSIGN NAME = "l.product_count" VALUE = "{ l.product_count + 1 }">
		<MvEVAL EXPR = "{ Product_Read( l.products[ l.product_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Products" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Products">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-PRD-00020', l.product_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ProductList_Load_SKU" PARAMETERS = "sku, products var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Products"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Products WHERE ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( 'sku' ) $ ' = ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( '?' ) $ ' ORDER BY disp_order' }"
				FIELDS	= "l.sku">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00028', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.product_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Products.d.EOF }">
		<MvEVAL EXPR = "{ Product_Read( l.products[ ++l.product_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "Products" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Products">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-PRD-00029', l.product_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Count" PARAMETERS = "" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Products"
				QUERY	= "{ 'SELECT COUNT( * ) AS product_count FROM ' $ g.Store_Table_Prefix $ 'Products' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00007', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Products.d.EOF }">
		<MvASSIGN NAME = "l.product_count"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.product_count"	VALUE = "{ Products.d.product_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Products">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-PRD-00021', l.product_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Update_DisplayOrder" PARAMETERS = "product var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Products
					      SET
							disp_order	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.product:disp_order,
					   l.product:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Increment_CategoryCount" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Products
					      SET
							catcount	= catcount + 1
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00009', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Decrement_CategoryCount" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Products
					      SET
							catcount	= catcount - 1
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00010', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Update_CategoryCount" PARAMETERS = "product_id, count" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Products
					      SET
							catcount	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.count, l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00024', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Increment_AvailabilityGroupCount" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Products
					      SET
							agrpcount	= agrpcount + 1
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00011', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Decrement_AvailabilityGroupCount" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Products
					      SET
							agrpcount	= agrpcount - 1
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00012', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Increment_PriceGroupCount" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Products
					      SET
							pgrpcount	= pgrpcount + 1
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00013', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Decrement_PriceGroupCount" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Products
					      SET
							pgrpcount	= pgrpcount - 1
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PRD-00014', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

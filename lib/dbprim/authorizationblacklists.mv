<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2019 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-ATB-
| Next Error Code: 10
|
</MvCOMMENT>

<MvFUNCTION NAME = "AuthorizationBlacklist_Read" PARAMETERS = "authorizationblacklist var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.authorizationblacklist:id"			VALUE = "{ AuthorizationBlacklists.d.id }">
	<MvASSIGN NAME = "l.authorizationblacklist:ip"			VALUE = "{ AuthorizationBlacklists.d.ip }">
	<MvASSIGN NAME = "l.authorizationblacklist:expires"		VALUE = "{ AuthorizationBlacklists.d.expires }">
	<MvASSIGN NAME = "l.authorizationblacklist:start_ip"	VALUE = "{ AuthorizationBlacklists.d.start_ip }">
	<MvASSIGN NAME = "l.authorizationblacklist:end_ip"		VALUE = "{ AuthorizationBlacklists.d.end_ip }">
</MvFUNCTION>

<MvFUNCTION NAME = "AuthorizationBlacklist_Insert" PARAMETERS = "authorizationblacklist var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.authorizationblacklist:id"	VALUE = "{ StoreKey_Generate( 'AuthorizationBlacklists' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'AuthorizationBlacklists
						  ( id, ip, expires, start_ip, end_ip )
						  VALUES
						  ( ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.authorizationblacklist:id, l.authorizationblacklist:ip, l.authorizationblacklist:expires, l.authorizationblacklist:start_ip,
			 		   l.authorizationblacklist:end_ip">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATB-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AuthorizationBlacklist_Update" PARAMETERS = "authorizationblacklist var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'AuthorizationBlacklists
	 					  SET
	 					  	ip			= ?,
	 					  	expires		= ?,
	 					  	start_ip	= ?,
	 					  	end_ip 		= ?
	 					  WHERE
	 					  	id			= ?' }"
			 FIELDS = "l.authorizationblacklist:ip, l.authorizationblacklist:expires, l.authorizationblacklist:start_ip, l.authorizationblacklist:end_ip, 
			 		   l.authorizationblacklist:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATB-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AuthorizationBlacklist_Update_Expires" PARAMETERS = "expires, ip" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.expires EQ 0 }">
		<MvASSIGN NAME = "l.where"	VALUE = "ip = ?">
		<MvASSIGN NAME = "l.fields"	VALUE = "l.expires, l.ip">
	<MvELSE>
		<MvASSIGN NAME = "l.where"	VALUE = "ip = ? AND expires <> 0 AND expires < ?">
		<MvASSIGN NAME = "l.fields" VALUE = "l.expires, l.ip, l.expires">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'AuthorizationBlacklists
	 					  SET
	 					  	expires	= ?
	 					  WHERE
	 					  	' $ l.where }"
			 FIELDS = "{ l.fields }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATB-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AuthorizationBlacklist_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AuthorizationBlacklists WHERE id = ?' }"
			 FIELDS = "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATB-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AuthorizationBlacklist_Delete_All_Expired" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AuthorizationBlacklists WHERE expires < ? AND expires <> 0' }"
			 FIELDS = "s.time_t">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATB-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AuthorizationBlacklist_Load_ID" PARAMETERS = "id, authorizationblacklist var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "AuthorizationBlacklists"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AuthorizationBlacklists WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATB-00006', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ AuthorizationBlacklists.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "AuthorizationBlacklists">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-ATB-00007' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ AuthorizationBlacklist_Read( l.authorizationblacklist ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AuthorizationBlacklists">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

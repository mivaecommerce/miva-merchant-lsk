<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-GIM-
| Next Error Code: 15   
|
</MvCOMMENT>

<MvFUNCTION NAME = "GeneratedImage_Read" PARAMETERS = "generatedimage var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.generatedimage:id"			VALUE = "{ GeneratedImages.d.id }">
	<MvASSIGN NAME = "l.generatedimage:image_id"	VALUE = "{ GeneratedImages.d.image_id }">
	<MvASSIGN NAME = "l.generatedimage:image"		VALUE = "{ GeneratedImages.d.image }">
	<MvASSIGN NAME = "l.generatedimage:width"		VALUE = "{ GeneratedImages.d.width }">
	<MvASSIGN NAME = "l.generatedimage:height"		VALUE = "{ GeneratedImages.d.height }">
	<MvASSIGN NAME = "l.generatedimage:image_md5"	VALUE = "{ GeneratedImages.d.image_md5 }">
</MvFUNCTION>

<MvFUNCTION NAME = "GeneratedImage_Insert_LowLevel" PARAMETERS = "generatedimage var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.generatedimage:id"			VALUE = "{ StoreKey_Generate( 'GeneratedImages' ) }">
	<MvASSIGN NAME = "l.generatedimage:image_md5"	VALUE = "{ crypto_md5( l.generatedimage:image ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'GeneratedImages
					      ( id, image_id, width, height, image, image_md5 )
						  VALUES
						  ( ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.generatedimage:id, l.generatedimage:image_id, l.generatedimage:width,
					   l.generatedimage:height, l.generatedimage:image, l.generatedimage:image_md5">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-GIM-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GeneratedImage_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'GeneratedImages WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-GIM-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GeneratedImage_Delete_All_Image" PARAMETERS = "image_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'GeneratedImages WHERE image_id = ?' }"
			 FIELDS	= "l.image_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-GIM-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GeneratedImage_Load_ID" PARAMETERS = "generatedimage_id, generatedimage var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "GeneratedImages"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'GeneratedImages WHERE id = ?' }"
				FIELDS	= "l.generatedimage_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-GIM-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ GeneratedImages.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "GeneratedImages">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-GIM-00010' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ GeneratedImage_Read( l.generatedimage ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "GeneratedImages">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GeneratedImage_Load_Dimensions" PARAMETERS = "image_id, image_width, image_height, generatedimage var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.image_width"	VALUE = "{ int( l.image_width ) }">
	<MvASSIGN NAME = "l.image_height"	VALUE = "{ int( l.image_height ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "GeneratedImages"
				QUERY	= "{ 'SELECT 
								* 
							FROM ' 
								$ g.Store_Table_Prefix $ 'GeneratedImages 
							WHERE
								image_id = ?
							AND
								( ( width = ? AND height <= ? ) OR ( height = ? AND width <= ? ) )' }"
				FIELDS	= "l.image_id, l.image_width, l.image_height, l.image_height, l.image_width">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-GIM-00006', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ GeneratedImages.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "GeneratedImages">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-GIM-00007' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ GeneratedImage_Read( l.generatedimage ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "GeneratedImages">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GeneratedImageList_Load_Image" PARAMETERS = "image_id, generatedimages var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "GeneratedImages"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'GeneratedImages WHERE image_id = ? ORDER BY id' }"
				FIELDS	= "l.image_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-GIM-00011', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.generatedimage_count"		VALUE = 0>
	<MvWHILE EXPR = "{ NOT GeneratedImages.d.EOF }">
		<MvASSIGN NAME = "l.generatedimage_count"	VALUE = "{ l.generatedimage_count + 1 }">
		<MvEVAL EXPR = "{ GeneratedImage_Read( l.generatedimages[ l.generatedimage_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "GeneratedImages" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "GeneratedImages">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-GIM-00012', l.generatedimage_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "GeneratedImage_Load_File" PARAMETERS = "image, generatedimage var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.image_md5" VALUE = "{ crypto_md5( l.image ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "GeneratedImages"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'GeneratedImages WHERE image_md5 = ?' }"
				FIELDS	= "l.image_md5">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-GIM-00013', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ GeneratedImages.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "GeneratedImages">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-GIM-00014' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ GeneratedImage_Read( l.generatedimage ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "GeneratedImages">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2017 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-CPL-
| Next Error Code: 14   
|
</MvCOMMENT>

<MvFUNCTION NAME = "CachedProductList_Read" PARAMETERS = "cachedproductlist var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.cachedproductlist:id"			VALUE = "{ CachedProductLists.d.id }">
	<MvASSIGN NAME = "l.cachedproductlist:signature"	VALUE = "{ CachedProductLists.d.signature }">
	<MvASSIGN NAME = "l.cachedproductlist:populated"	VALUE = "{ CachedProductLists.d.populated }">
	<MvASSIGN NAME = "l.cachedproductlist:out_hide"		VALUE = "{ CachedProductLists.d.out_hide }">
	<MvASSIGN NAME = "l.cachedproductlist:prodcount"	VALUE = "{ CachedProductLists.d.prodcount }">
	<MvASSIGN NAME = "l.cachedproductlist:version"		VALUE = "{ CachedProductLists.d.version }">
	<MvASSIGN NAME = "l.cachedproductlist:dt_created"	VALUE = "{ CachedProductLists.d.dt_created }">
</MvFUNCTION>

<MvFUNCTION NAME = "CachedProductList_Load_ID" PARAMETERS = "id, cachedproductlist var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CachedProductLists"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CachedProductLists WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CPL-00001', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ CachedProductLists.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "CachedProductLists">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CPL-00002' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ CachedProductList_Read( l.cachedproductlist ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CachedProductLists">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CachedProductList_Load_Signature" PARAMETERS = "signature, version, cachedproductlist var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CachedProductLists"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CachedProductLists WHERE signature = ? AND version = ? ORDER BY id DESC' }"
				FIELDS	= "l.signature, l.version">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CPL-00003', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ CachedProductLists.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "CachedProductLists">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CPL-00004' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ CachedProductList_Read( l.cachedproductlist ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CachedProductLists">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CachedProductList_Load_Signature_UnExpired" PARAMETERS = "signature, version, cachedproductlist var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.dt_created"		VALUE = "{ s.dyn_time_t - ( g.Store:cache_exp * 60 ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CachedProductLists"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CachedProductLists WHERE signature = ? AND version = ? AND dt_created >= ? ORDER BY id DESC' }"
				FIELDS	= "l.signature, l.version, l.dt_created">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CPL-00012', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ CachedProductLists.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "CachedProductLists">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CPL-00013' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ CachedProductList_Read( l.cachedproductlist ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CachedProductLists">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CachedProductListList_Load_All" PARAMETERS = "cachedproductlists var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CachedProductLists"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CachedProductLists ORDER BY id' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CPL-00007', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.cachedproductlist_count"	VALUE = 0>

	<MvWHILE EXPR = "{ NOT CachedProductLists.d.EOF }">
		<MvEVAL EXPR = "{ CachedProductList_Read( l.cachedproductlists[ ++l.cachedproductlist_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "CachedProductLists" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CachedProductLists">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-CPL-00008', l.cachedproductlist_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "CachedProductListList_Load_OlderThan" PARAMETERS = "time, cachedproductlists var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CachedProductLists"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CachedProductLists WHERE dt_created < ? ORDER BY id' }"
				FIELDS	= "l.time">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CPL-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.cachedproductlist_count"	VALUE = 0>

	<MvWHILE EXPR = "{ NOT CachedProductLists.d.EOF }">
		<MvEVAL EXPR = "{ CachedProductList_Read( l.cachedproductlists[ ++l.cachedproductlist_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "CachedProductLists" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CachedProductLists">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-CPL-00010', l.cachedproductlist_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "CachedProductList_Insert" PARAMETERS = "cachedproductlist var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.cachedproductlist:id"			VALUE = "{ StoreKey_Generate( 'CachedProductLists' ) }">
	<MvASSIGN NAME = "l.cachedproductlist:dt_created"	VALUE = "{ s.dyn_time_t }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'CachedProductLists
					      ( id, signature, populated, out_hide, prodcount, version, dt_created )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.cachedproductlist:id, l.cachedproductlist:signature, l.cachedproductlist:populated,
					   l.cachedproductlist:out_hide, l.cachedproductlist:prodcount, l.cachedproductlist:version,
					   l.cachedproductlist:dt_created">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CPL-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CachedProductList_Delete_LowLevel" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CachedProductLists WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CPL-00011', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

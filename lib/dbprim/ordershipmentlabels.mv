<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-OSL-
| Next Error Code: 33   
|
</MvCOMMENT>

<MvFUNCTION NAME = "OrderShipmentLabel_Read" PARAMETERS = "ordershipmentlabel var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.ordershipmentlabel:id"			VALUE = "{ OrderShipmentLabels.d.id }">
	<MvASSIGN NAME = "l.ordershipmentlabel:order_id"	VALUE = "{ OrderShipmentLabels.d.order_id }">
	<MvASSIGN NAME = "l.ordershipmentlabel:shpmnt_id"	VALUE = "{ OrderShipmentLabels.d.shpmnt_id }">
	<MvASSIGN NAME = "l.ordershipmentlabel:module_id"	VALUE = "{ OrderShipmentLabels.d.module_id }">
	<MvASSIGN NAME = "l.ordershipmentlabel:tracknum"	VALUE = "{ OrderShipmentLabels.d.tracknum }">
	<MvASSIGN NAME = "l.ordershipmentlabel:tracktype"	VALUE = "{ OrderShipmentLabels.d.tracktype }">
	<MvASSIGN NAME = "l.ordershipmentlabel:label_type"	VALUE = "{ OrderShipmentLabels.d.label_type }">
	<MvASSIGN NAME = "l.ordershipmentlabel:label_data"	VALUE = "{ OrderShipmentLabels.d.label_data }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderShipmentLabel_Load_ID" PARAMETERS = "id, ordershipmentlabel var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderShipmentLabels"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderShipmentLabels WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OSL-00016', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ OrderShipmentLabels.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderShipmentLabels">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-OSL-00017' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ OrderShipmentLabel_Read( l.ordershipmentlabel ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderShipmentLabels">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderShipmentLabel_Insert_LowLevel" PARAMETERS = "ordershipmentlabel var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.ordershipmentlabel:id"	VALUE = "{ StoreKey_Generate( 'OrderShipmentLabels' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'OrderShipmentLabels
						  ( id, order_id, shpmnt_id, module_id, tracknum, tracktype, label_type, label_data )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.ordershipmentlabel:id, l.ordershipmentlabel:order_id, l.ordershipmentlabel:shpmnt_id,
					   l.ordershipmentlabel:module_id, l.ordershipmentlabel:tracknum, l.ordershipmentlabel:tracktype,
					   l.ordershipmentlabel:label_type, l.ordershipmentlabel:label_data">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OSL-00018', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderShipmentLabel_Update" PARAMETERS = "ordershipmentlabel var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderShipmentLabels
						  SET
							order_id	= ?,
							shpmnt_id	= ?,
							module_id	= ?,
							tracknum	= ?,
							tracktype	= ?,
							label_type	= ?,
							label_data	= ?
						  WHERE
							id			= ?' }"
			 FIELDS	= "l.ordershipmentlabel:order_id, l.ordershipmentlabel:shpmnt_id, l.ordershipmentlabel:module_id,
					   l.ordershipmentlabel:tracknum, l.ordershipmentlable:tracktype, l.ordershipmentlabel:label_type,
					   l.ordershipmentlabel:label_data,
					   l.ordershipmentlabel:id ">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OSL-00019', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderShipmentLabel_Delete_LowLevel" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderShipmentLabels WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OSL-00020', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderShipmentLabelList_Load_Order" PARAMETERS = "order_id, ordershipmentlabels var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderShipmentLabels"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderShipmentLabels WHERE order_id = ? ORDER BY id' }"
				FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OSL-00025', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.ordershipmentlabel_count"		VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderShipmentLabels.d.EOF }">
		<MvASSIGN NAME = "l.ordershipmentlabel_count"	VALUE = "{ l.ordershipmentlabel_count + 1 }">
		<MvEVAL EXPR = "{ OrderShipmentLabel_Read( l.ordershipmentlabels[ l.ordershipmentlabel_count ] ) }">
		
		<MvSKIP NAME = "Merchant" VIEW = "OrderShipmentLabels" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderShipmentLabels">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OSL-00026', l.ordershipmentlabel_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderShipmentLabel_Count_Shipment" PARAMETERS = "shpmnt_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderShipmentLabels"
				QUERY	= "{ 'SELECT COUNT(*) AS label_count FROM ' $ g.Store_Table_Prefix $ 'OrderShipmentLabels WHERE shpmnt_id = ?' }"
				FIELDS	= "l.shpmnt_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OSL-00027', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ OrderShipmentLabels.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderShipmentLabels">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-OSL-00028' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = "{ OrderShipmentLabels.d.label_count }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderShipmentLabels">

	<MvFUNCTIONRETURN VALUE = "{ EOF_Return( l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderShipmentLabelList_Load_Shipment" PARAMETERS = "shpmnt_id, ordershipmentlabels var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderShipmentLabels"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderShipmentLabels WHERE shpmnt_id = ? ORDER BY id' }"
				FIELDS	= "l.shpmnt_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OSL-00023', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.ordershipmentlabel_count"		VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderShipmentLabels.d.EOF }">
		<MvASSIGN NAME = "l.ordershipmentlabel_count"	VALUE = "{ l.ordershipmentlabel_count + 1 }">
		<MvEVAL EXPR = "{ OrderShipmentLabel_Read( l.ordershipmentlabels[ l.ordershipmentlabel_count ] ) }">
		
		<MvSKIP NAME = "Merchant" VIEW = "OrderShipmentLabels" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderShipmentLabels">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OSL-00024', l.ordershipmentlabel_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderShipmentLabel_Count_Shipment_Module" PARAMETERS = "shpmnt_id, module_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderShipmentLabels"
				QUERY	= "{ 'SELECT COUNT(*) AS label_count FROM ' $ g.Store_Table_Prefix $ 'OrderShipmentLabels WHERE shpmnt_id = ? AND module_id = ?' }"
				FIELDS	= "l.shpmnt_id, l.module_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OSL-00029', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ OrderShipmentLabels.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderShipmentLabels">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-OSL-00030' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = "{ OrderShipmentLabels.d.label_count }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderShipmentLabels">

	<MvFUNCTIONRETURN VALUE = "{ EOF_Return( l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderShipmentLabelList_Load_Shipment_Module" PARAMETERS = "shpmnt_id, module_id, ordershipmentlabels var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderShipmentLabels"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderShipmentLabels WHERE shpmnt_id = ? AND module_id = ? ORDER BY id' }"
				FIELDS	= "l.shpmnt_id, l.module_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OSL-00031', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.ordershipmentlabel_count"		VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderShipmentLabels.d.EOF }">
		<MvASSIGN NAME = "l.ordershipmentlabel_count"	VALUE = "{ l.ordershipmentlabel_count + 1 }">
		<MvEVAL EXPR = "{ OrderShipmentLabel_Read( l.ordershipmentlabels[ l.ordershipmentlabel_count ] ) }">
		
		<MvSKIP NAME = "Merchant" VIEW = "OrderShipmentLabels" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderShipmentLabels">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OSL-00032', l.ordershipmentlabel_count ) }">
</MvFUNCTION>

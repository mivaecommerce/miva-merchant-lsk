<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-DKY-
| Next Error Code: 5    
|
</MvCOMMENT>

<MvFUNCTION NAME = "DomainKey_Read" PARAMETERS = "domainkey var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.domainkey:type"		VALUE = "{ DomainKeys.d.type }">
	<MvASSIGN NAME = "l.domainkey:maxvalue"	VALUE = "{ DomainKeys.d.maxvalue }">
</MvFUNCTION>

<MvFUNCTION NAME = "DomainKey_Insert" PARAMETERS = "type" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO DomainKeys
						  ( type, ' $ [ g.Library_Filename_Native_DBAPI ].DB_Escape_Identifier( 'maxvalue' ) $ ' )
						  VALUES
						  ( ?, 0 )' }"
			 FIELDS	= "l.type">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-DKY-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DomainKey_Delete" PARAMETERS = "type" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM DomainKeys WHERE type = ?' }"
			 FIELDS	= "l.type">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-DKY-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DomainKey_Load_Type" PARAMETERS = "type, domainkey var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "DomainKeys"
				QUERY	= "SELECT * FROM DomainKeys WHERE type = ?"
				FIELDS	= "l.type">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-DKY-00002', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ DomainKeys.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "DomainKeys">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-DKY-00003' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ DomainKey_Read( l.domainkey ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "DomainKeys">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

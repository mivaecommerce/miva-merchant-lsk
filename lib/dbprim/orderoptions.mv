<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-OOP-
| Next Error Code: 12    
|
</MvCOMMENT>

<MvFUNCTION NAME = "OrderOption_Read" PARAMETERS = "orderoption var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.orderoption:id"					VALUE = "{ OrderOptions.d.id }">
	<MvASSIGN NAME = "l.orderoption:order_id"			VALUE = "{ OrderOptions.d.order_id }">
	<MvASSIGN NAME = "l.orderoption:line_id"			VALUE = "{ OrderOptions.d.line_id }">
	<MvASSIGN NAME = "l.orderoption:attr_id"			VALUE = "{ OrderOptions.d.attr_id }">
	<MvASSIGN NAME = "l.orderoption:attr_code"			VALUE = "{ OrderOptions.d.attr_code }">
	<MvASSIGN NAME = "l.orderoption:attmpat_id"			VALUE = "{ OrderOptions.d.attmpat_id }">
	<MvASSIGN NAME = "l.orderoption:option_id"			VALUE = "{ OrderOptions.d.option_id }">
	<MvASSIGN NAME = "l.orderoption:opt_code"			VALUE = "{ OrderOptions.d.opt_code }">
	<MvASSIGN NAME = "l.orderoption:retail"				VALUE = "{ OrderOptions.d.retail }">
	<MvASSIGN NAME = "l.orderoption:base_price"			VALUE = "{ OrderOptions.d.base_price }">
	<MvASSIGN NAME = "l.orderoption:price"				VALUE = "{ OrderOptions.d.price }">
	<MvASSIGN NAME = "l.orderoption:weight"				VALUE = "{ OrderOptions.d.weight }">
	<MvASSIGN NAME = "l.orderoption:data"				VALUE = "{ OrderOptions.d.data }">
	<MvASSIGN NAME = "l.orderoption:data_long"			VALUE = "{ OrderOptions.d.data_long }">

	<MvIF EXPR = "{ OrderOptions.d.attmpat_id }">
		<MvASSIGN NAME = "l.orderoption:attr_prompt"	VALUE = "{ OrderOptions.d.attmpat_prompt }">
		<MvASSIGN NAME = "l.orderoption:opt_prompt"		VALUE = "{ OrderOptions.d.attmpopt_prompt }">
	<MvELSE>
		<MvASSIGN NAME = "l.orderoption:attr_prompt"	VALUE = "{ OrderOptions.d.attr_prompt }">
		<MvASSIGN NAME = "l.orderoption:opt_prompt"		VALUE = "{ OrderOptions.d.opt_prompt }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderOption_Insert_LowLevel" PARAMETERS = "orderoption var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.orderoption:id" VALUE = "{ StoreKey_Generate( 'BasketOptions' ) }">

	<MvIF EXPR = "{ ISNULL l.orderoption:retail }">		<MvASSIGN NAME = "l.orderoption:retail"		VALUE = 0.00>						</MvIF>
	<MvIF EXPR = "{ ISNULL l.orderoption:base_price }">	<MvASSIGN NAME = "l.orderoption:base_price"	VALUE = "{ l.orderoption:price }">	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'OrderOptions
						  ( id, order_id, line_id, attr_id, attr_code, attmpat_id, option_id, opt_code,
							retail, base_price, price, weight, data, data_long )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.orderoption:id, l.orderoption:order_id, l.orderoption:line_id, l.orderoption:attr_id, l.orderoption:attr_code,
					   l.orderoption:attmpat_id, l.orderoption:option_id, l.orderoption:opt_code, l.orderoption:retail, l.orderoption:base_price,
					   l.orderoption:price, l.orderoption:weight, l.orderoption:data, l.orderoption:data_long">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OOP-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderOption_Update_LowLevel" PARAMETERS = "orderoption var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderOptions
						  SET
							order_id	= ?,
							line_id		= ?,
							attr_id		= ?,
							attr_code	= ?,
							attmpat_id	= ?,
							option_id	= ?,
							opt_code	= ?,
							retail		= ?,
							base_price	= ?,
							price		= ?,
							weight		= ?,
							data		= ?,
							data_long	= ?
						  WHERE
							id			= ?' }"
			 FIELDS	= "l.orderoption:order_id, l.orderoption:line_id, l.orderoption:attr_id, l.orderoption:attr_code, l.orderoption:attmpat_id,
			 		   l.orderoption:option_id, l.orderoption:opt_code, l.orderoption:retail, l.orderoption:base_price, l.orderoption:price,
			 		   l.orderoption:weight, l.orderoption:data, l.orderoption:data_long,
					   l.orderoption:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OOP-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderOption_Update_AttributeCode" PARAMETERS = "order_id, line_id, attr_code, new_attr_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderOptions
			 			  SET
			 			  	attr_code 	= ?
			 			  WHERE
			 			  	order_id	= ? AND
			 			  	line_id		= ? AND
			 			  	attr_code	= ?' }"
			 FIELDS = "l.new_attr_code,
			 		   l.order_id, l.line_id, l.attr_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OOP-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderOption_Update_Pricing_LowLevel" PARAMETERS = "orderoption var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderOptions
						  SET
							retail		= ?,
							base_price	= ?,
							price		= ?
						  WHERE
							id			= ?' }"
			 FIELDS	= "l.orderoption:retail, l.orderoption:base_price, l.orderoption:price,
					   l.orderoption:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OOP-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderOptionList_Load_Line" PARAMETERS = "line_id, orderoptions var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderOptions"
				QUERY	= "{ 'SELECT
								oo.*,
								ats.prompt	AS attr_prompt,
								ata.prompt	AS attmpat_prompt,
								opt.prompt	AS opt_prompt,
								ato.prompt	AS attmpopt_prompt,
								' $ [ g.Library_Filename_Native_DBAPI ].DB_IFNULL( 'ats.disp_order',	'0' ) $ ' AS ats_disp_order,
								' $ [ g.Library_Filename_Native_DBAPI ].DB_IFNULL( 'ata.disporder',		'0' ) $ ' AS ata_disp_order,
								' $ [ g.Library_Filename_Native_DBAPI ].DB_IFNULL( 'opt.disp_order',	'0' ) $ ' AS opt_disp_order,
								' $ [ g.Library_Filename_Native_DBAPI ].DB_IFNULL( 'ato.disporder',		'0' ) $ ' AS ato_disp_order
							  FROM ' $
								g.Store_Table_Prefix $ 'OrderOptions oo
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'Attributes ats ON ats.id = oo.attr_id
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs ata ON ata.id = oo.attmpat_id
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'Options opt ON opt.id = oo.option_id
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions ato ON ato.id = oo.option_id
							  WHERE
								oo.line_id = ?
							  ORDER BY
								ats_disp_order, ata_disp_order, oo.attr_code, opt_disp_order, ato_disp_order, oo.opt_code, oo.id' }"
				FIELDS	= "l.line_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OOP-00001', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderoption_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderOptions.d.EOF }">
		<MvASSIGN NAME = "l.orderoption_count" VALUE = "{ l.orderoption_count + 1 }">
		<MvEVAL EXPR = "{ OrderOption_Read( l.orderoptions[ l.orderoption_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "OrderOptions" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderOptions">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OOP-00004', l.orderoption_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderOption_Load_ID" PARAMETERS = "id, orderoption var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderOptions"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderOptions WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OOP-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ OrderOptions.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderOptions">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-OOP-00010' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ OrderOption_Read( l.orderoption ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderOptions">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderOption_Delete_LowLevel" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderOptions WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OOP-00011', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderOption_Delete_All_Order" PARAMETERS = "order_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderOptions WHERE order_id = ?' }"
			 FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OOP-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderOption_Delete_All_Line" PARAMETERS = "line_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderOptions WHERE line_id = ?' }"
			 FIELDS	= "l.line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OOP-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-BSK-
| Next Error Code: 20   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Basket_Read" PARAMETERS = "basket var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.basket:session_id"		VALUE = "{ Baskets.d.session_id }">
	<MvASSIGN NAME = "l.basket:cussess_id"		VALUE = "{ Baskets.d.cussess_id }">
	<MvASSIGN NAME = "l.basket:affsess_id"		VALUE = "{ Baskets.d.affsess_id }">
	<MvASSIGN NAME = "l.basket:chksess_id"		VALUE = "{ Baskets.d.chksess_id }">
	<MvASSIGN NAME = "l.basket:cust_id"			VALUE = "{ Baskets.d.cust_id }">
	<MvASSIGN NAME = "l.basket:basket_id"		VALUE = "{ Baskets.d.basket_id }">
	<MvASSIGN NAME = "l.basket:order_id"		VALUE = "{ Baskets.d.order_id }">
	<MvASSIGN NAME = "l.basket:order_proc"		VALUE = "{ Baskets.d.order_proc }">
	<MvASSIGN NAME = "l.basket:sub_count"		VALUE = "{ Baskets.d.sub_count }">
	<MvASSIGN NAME = "l.basket:auth_token"		VALUE = "{ Baskets.d.auth_token }">
	<MvASSIGN NAME = "l.basket:auth_fails"		VALUE = "{ Baskets.d.auth_fails }">
	<MvASSIGN NAME = "l.basket:lastupdate"		VALUE = "{ Baskets.d.lastupdate }">
	<MvASSIGN NAME = "l.basket:csrf_token"		VALUE = "{ Baskets.d.csrf_token }">
	<MvASSIGN NAME = "l.basket:ship_res"		VALUE = "{ Baskets.d.ship_res }">
	<MvASSIGN NAME = "l.basket:ship_fname"		VALUE = "{ Baskets.d.ship_fname }">
	<MvASSIGN NAME = "l.basket:ship_lname"		VALUE = "{ Baskets.d.ship_lname }">
	<MvASSIGN NAME = "l.basket:ship_email"		VALUE = "{ Baskets.d.ship_email }">
	<MvASSIGN NAME = "l.basket:ship_comp"		VALUE = "{ Baskets.d.ship_comp }">
	<MvASSIGN NAME = "l.basket:ship_phone"		VALUE = "{ Baskets.d.ship_phone }">
	<MvASSIGN NAME = "l.basket:ship_fax"		VALUE = "{ Baskets.d.ship_fax }">

	<MvIF EXPR = "{ len( Baskets.d.ship_addr2 ) EQ 0 }">
		<MvASSIGN NAME = "l.basket:ship_addr"	VALUE = "{ Baskets.d.ship_addr }">
	<MvELSE>
		<MvASSIGN NAME = "l.basket:ship_addr"	VALUE = "{ Baskets.d.ship_addr $ ' ' $ Baskets.d.ship_addr2 }">
	</MvIF>

	<MvASSIGN NAME = "l.basket:ship_addr1"		VALUE = "{ Baskets.d.ship_addr }">
	<MvASSIGN NAME = "l.basket:ship_addr2"		VALUE = "{ Baskets.d.ship_addr2 }">
	<MvASSIGN NAME = "l.basket:ship_city"		VALUE = "{ Baskets.d.ship_city }">
	<MvASSIGN NAME = "l.basket:ship_state"		VALUE = "{ Baskets.d.ship_state }">
	<MvASSIGN NAME = "l.basket:ship_zip"		VALUE = "{ Baskets.d.ship_zip }">
	<MvASSIGN NAME = "l.basket:ship_cntry"		VALUE = "{ Baskets.d.ship_cntry }">
	<MvASSIGN NAME = "l.basket:bill_fname"		VALUE = "{ Baskets.d.bill_fname }">
	<MvASSIGN NAME = "l.basket:bill_lname"		VALUE = "{ Baskets.d.bill_lname }">
	<MvASSIGN NAME = "l.basket:bill_email"		VALUE = "{ Baskets.d.bill_email }">
	<MvASSIGN NAME = "l.basket:bill_comp"		VALUE = "{ Baskets.d.bill_comp }">
	<MvASSIGN NAME = "l.basket:bill_phone"		VALUE = "{ Baskets.d.bill_phone }">
	<MvASSIGN NAME = "l.basket:bill_fax"		VALUE = "{ Baskets.d.bill_fax }">
	
	<MvIF EXPR = "{ len( Baskets.d.bill_addr2 ) EQ 0 }">
		<MvASSIGN NAME = "l.basket:bill_addr"	VALUE = "{ Baskets.d.bill_addr }">
	<MvELSE>
		<MvASSIGN NAME = "l.basket:bill_addr"	VALUE = "{ Baskets.d.bill_addr $ ' ' $ Baskets.d.bill_addr2 }">
	</MvIF>

	<MvASSIGN NAME = "l.basket:bill_addr1"		VALUE = "{ Baskets.d.bill_addr }">
	<MvASSIGN NAME = "l.basket:bill_addr2"		VALUE = "{ Baskets.d.bill_addr2 }">
	<MvASSIGN NAME = "l.basket:bill_city"		VALUE = "{ Baskets.d.bill_city }">
	<MvASSIGN NAME = "l.basket:bill_state"		VALUE = "{ Baskets.d.bill_state }">
	<MvASSIGN NAME = "l.basket:bill_zip"		VALUE = "{ Baskets.d.bill_zip }">
	<MvASSIGN NAME = "l.basket:bill_cntry"		VALUE = "{ Baskets.d.bill_cntry }">
	<MvASSIGN NAME = "l.basket:ship_id"			VALUE = "{ Baskets.d.ship_id }">
	<MvASSIGN NAME = "l.basket:ship_data"		VALUE = "{ Baskets.d.ship_data }">
	<MvASSIGN NAME = "l.basket:tax_exempt" 		VALUE = "{ Baskets.d.tax_exempt }">
	<MvASSIGN NAME = "l.basket:addr_val_s" 		VALUE = "{ Baskets.d.addr_val_s }">
	<MvASSIGN NAME = "l.basket:addr_val_b" 		VALUE = "{ Baskets.d.addr_val_b }">
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Insert" PARAMETERS = "basket var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.basket:basket_id"		VALUE = "{ StoreKey_Generate( 'Baskets' ) }">

	<MvIF EXPR = "{ ISNULL l.basket:sub_count }">
		<MvASSIGN NAME = "l.basket:sub_count"	VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.basket:auth_fails }">
		<MvASSIGN NAME = "l.basket:auth_fails"	VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.basket:lastupdate }">
		<MvASSIGN NAME = "l.basket:lastupdate"	VALUE = "{ s.dyn_time_t }">
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.basket:csrf_token }">
		<MvASSIGN NAME = "l.basket:csrf_token"	VALUE = "{ MakeSessionID() }">
	</MvIF>

	<MvIF EXPR = "{ len( l.basket:ship_addr ) AND ( NOT len( l.basket:ship_addr1 ) ) AND ( NOT len( l.basket:ship_addr2 ) ) }">
		<MvASSIGN NAME = "l.basket:ship_addr1"	VALUE = "{ l.basket:ship_addr }">
	</MvIF>

	<MvIF EXPR = "{ len( l.basket:bill_addr ) AND ( NOT len( l.basket:bill_addr1 ) ) AND ( NOT len( l.basket:bill_addr2 ) ) }">
		<MvASSIGN NAME = "l.basket:bill_addr1"	VALUE = "{ l.basket:bill_addr }">
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.basket:ship_res }">
		<MvIF EXPR = "{ NOT StandardFields_Load_Cached( l.std_fields ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ l.std_fields:ship_res EQ 'A' }">	<MvASSIGN NAME = "l.basket:ship_res" VALUE = "{ ISNULL l.basket:ship_comp }">
		<MvELSE>											<MvASSIGN NAME = "l.basket:ship_res" VALUE = 1>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.basket:tax_exempt }">
		<MvASSIGN NAME = "l.basket:tax_exempt" 	VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.basket:addr_val_s }">
		<MvASSIGN NAME = "l.basket:addr_val_s" 	VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.basket:addr_val_b }">
		<MvASSIGN NAME = "l.basket:addr_val_b" 	VALUE = 0>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'Baskets
					      ( session_id, cussess_id, affsess_id, chksess_id, cust_id, basket_id, order_id, order_proc, sub_count, auth_token, auth_fails, lastupdate, csrf_token, ship_res,
					      	ship_fname, ship_lname, ship_email, ship_comp, ship_phone, ship_fax, ship_addr, ship_addr2, ship_city, ship_state, ship_zip, ship_cntry,
					      	bill_fname, bill_lname, bill_email, bill_comp, bill_phone, bill_fax, bill_addr, bill_addr2, bill_city, bill_state, bill_zip, bill_cntry,
					      	ship_id, ship_data, tax_exempt, addr_val_s, addr_val_b )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.basket:session_id, l.basket:cussess_id, l.basket:affsess_id, l.basket:chksess_id, l.basket:cust_id, l.basket:basket_id, l.basket:order_id,
					   l.basket:order_proc, l.basket:sub_count, l.basket:auth_token, l.basket:auth_fails, l.basket:lastupdate, l.basket:csrf_token, l.basket:ship_res,
					   l.basket:ship_fname, l.basket:ship_lname, l.basket:ship_email, l.basket:ship_comp, l.basket:ship_phone, l.basket:ship_fax, l.basket:ship_addr1, l.basket:ship_addr2, l.basket:ship_city, l.basket:ship_state, l.basket:ship_zip, l.basket:ship_cntry,
					   l.basket:bill_fname, l.basket:bill_lname, l.basket:bill_email, l.basket:bill_comp, l.basket:bill_phone, l.basket:bill_fax, l.basket:bill_addr1, l.basket:bill_addr2, l.basket:bill_city, l.basket:bill_state, l.basket:bill_zip, l.basket:bill_cntry,
					   l.basket:ship_id, l.basket:ship_data, l.basket:tax_exempt, l.basket:addr_val_s, l.basket:addr_val_b">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BSK-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ len( l.basket:ship_addr2 ) EQ 0 }">
		<MvASSIGN NAME = "l.basket:ship_addr"	VALUE = "{ l.basket:ship_addr1 }">
	<MvELSE>
		<MvASSIGN NAME = "l.basket:ship_addr"	VALUE = "{ l.basket:ship_addr1 $ ' ' $ l.basket:ship_addr2 }">
	</MvIF>

	<MvIF EXPR = "{ len( l.basket:bill_addr2 ) EQ 0 }">
		<MvASSIGN NAME = "l.basket:bill_addr"	VALUE = "{ l.basket:bill_addr1 }">
	<MvELSE>
		<MvASSIGN NAME = "l.basket:bill_addr"	VALUE = "{ l.basket:bill_addr1 $ ' ' $ l.basket:bill_addr2 }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Update_Customer_ID" PARAMETERS = "basket var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_Is_Provisional( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ Basket_Register( l.basket ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Baskets
					      SET
							cussess_id	= ?,
							cust_id		= ?,
							tax_exempt	= ?
					      WHERE
						    session_id	= ?' }"
			 FIELDS	= "l.basket:cussess_id,
					   l.basket:cust_id,
					   l.basket:tax_exempt,
					   l.basket:session_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BSK-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Clear_Customer_ID" PARAMETERS = "basket var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.basket:cussess_id"	VALUE = "">
	<MvASSIGN NAME = "l.basket:cust_id"		VALUE = 0>

	<MvIF EXPR = "{ Basket_Is_Provisional( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ Basket_Update_Customer_ID( l.basket ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Update_Affiliate_Session" PARAMETERS = "basket var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_Is_Provisional( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ Basket_Register( l.basket ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Baskets
					      SET
							affsess_id	= ?
					      WHERE
						    session_id	= ?' }"
			 FIELDS	= "l.basket:affsess_id,
					   l.basket:session_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BSK-00010', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Clear_Affiliate_Session" PARAMETERS = "basket var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.basket:affsess_id"	VALUE = "">

	<MvIF EXPR = "{ Basket_Is_Provisional( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ Basket_Update_Affiliate_Session( l.basket ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Update_Order" PARAMETERS = "basket var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_Is_Provisional( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ Basket_Register( l.basket ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Baskets
					      SET
							order_id	= ?,
							order_proc	= ?
					      WHERE
						    session_id	= ?' }"
			 FIELDS	= "l.basket:order_id, l.basket:order_proc,
					   l.basket:session_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BSK-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Update_LastUpdate" PARAMETERS = "basket var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_Is_Provisional( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Baskets
					      SET
							lastupdate	= ?
					      WHERE
						    session_id	= ?' }"
			 FIELDS	= "l.basket:lastupdate,
					   l.basket:session_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BSK-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Update_SubCount" PARAMETERS = "basket_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_Is_Provisional( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Baskets
			 			  SET
			 			  	sub_count = ( SELECT COUNT( * ) FROM ' $ g.Store_Table_Prefix $ 'BasketItems WHERE basket_id = ? AND subterm_id <> 0 )
			 			  WHERE
			 			  	basket_id = ?' }"
			 FIELDS	= "l.basket_id, l.basket_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BSK-00017', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Update_Customer_Information" PARAMETERS = "basket var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ len( l.basket:ship_addr ) AND
					l.basket:ship_addr NE ( l.basket:ship_addr1 $ ' ' $ l.basket:ship_addr2 ) }">
		<MvASSIGN NAME = "l.basket:ship_addr1"	VALUE = "{ l.basket:ship_addr }">
		<MvASSIGN NAME = "l.basket:ship_addr2"	VALUE = "">
	</MvIF>

	<MvIF EXPR = "{ len( l.basket:bill_addr ) AND
					l.basket:bill_addr NE ( l.basket:bill_addr1 $ ' ' $ l.basket:bill_addr2 ) }">
		<MvASSIGN NAME = "l.basket:bill_addr1"	VALUE = "{ l.basket:bill_addr }">
		<MvASSIGN NAME = "l.basket:bill_addr2"	VALUE = "">
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.basket:addr_val_s }">
		<MvASSIGN NAME = "l.basket:addr_val_s" 	VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.basket:addr_val_b }">
		<MvASSIGN NAME = "l.basket:addr_val_b" 	VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ Basket_Is_Provisional( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ Basket_Register( l.basket ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Baskets
					      SET
							chksess_id	= ?,
							auth_token	= ?,
							auth_fails	= ?,
							ship_res	= ?,
							ship_fname	= ?,
							ship_lname	= ?,
							ship_email	= ?,
							ship_comp	= ?,
							ship_phone	= ?,
							ship_fax	= ?,
							ship_addr	= ?,
							ship_addr2	= ?,
							ship_city	= ?,
							ship_state	= ?,
							ship_zip	= ?,
							ship_cntry	= ?,
							bill_fname	= ?,
							bill_lname	= ?,
							bill_email	= ?,
							bill_comp	= ?,
							bill_phone	= ?,
							bill_fax	= ?,
							bill_addr	= ?,
							bill_addr2	= ?,
							bill_city	= ?,
							bill_state	= ?,
							bill_zip	= ?,
							bill_cntry	= ?,
							addr_val_s	= ?,
							addr_val_b	= ?
					      WHERE
						    session_id	= ?' }"
			 FIELDS	= "l.basket:chksess_id, l.basket:auth_token, l.basket:auth_fails, l.basket:ship_res, l.basket:ship_fname,
					   l.basket:ship_lname, l.basket:ship_email, l.basket:ship_comp,
					   l.basket:ship_phone, l.basket:ship_fax, l.basket:ship_addr1, l.basket:ship_addr2, l.basket:ship_city,
					   l.basket:ship_state, l.basket:ship_zip, l.basket:ship_cntry, l.basket:bill_fname,
					   l.basket:bill_lname, l.basket:bill_email, l.basket:bill_comp, l.basket:bill_phone,
					   l.basket:bill_fax, l.basket:bill_addr1, l.basket:bill_addr2, l.basket:bill_city, l.basket:bill_state,
					   l.basket:bill_zip, l.basket:bill_cntry, l.basket:addr_val_s, l.basket:addr_val_b,
					   l.basket:session_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BSK-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ len( l.basket:ship_addr2 ) EQ 0 }">
		<MvASSIGN NAME = "l.basket:ship_addr"	VALUE = "{ l.basket:ship_addr1 }">
	<MvELSE>
		<MvASSIGN NAME = "l.basket:ship_addr"	VALUE = "{ l.basket:ship_addr1 $ ' ' $ l.basket:ship_addr2 }">
	</MvIF>

	<MvIF EXPR = "{ len( l.basket:bill_addr2 ) EQ 0 }">
		<MvASSIGN NAME = "l.basket:bill_addr"	VALUE = "{ l.basket:bill_addr1 }">
	<MvELSE>
		<MvASSIGN NAME = "l.basket:bill_addr"	VALUE = "{ l.basket:bill_addr1 $ ' ' $ l.basket:bill_addr2 }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Clear_Customer_Information" PARAMETERS = "basket var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.basket:chksess_id"			VALUE = "">
	<MvASSIGN NAME = "l.basket:auth_token"			VALUE = "">
	<MvASSIGN NAME = "l.basket:auth_fails"			VALUE = 0>
	<MvASSIGN NAME = "l.basket:ship_fname"			VALUE = "">
	<MvASSIGN NAME = "l.basket:ship_lname"			VALUE = "">
	<MvASSIGN NAME = "l.basket:ship_email"			VALUE = "">
	<MvASSIGN NAME = "l.basket:ship_comp"			VALUE = "">
	<MvASSIGN NAME = "l.basket:ship_phone"			VALUE = "">
	<MvASSIGN NAME = "l.basket:ship_fax"			VALUE = "">
	<MvASSIGN NAME = "l.basket:ship_addr"			VALUE = "">
	<MvASSIGN NAME = "l.basket:ship_addr1"			VALUE = "">
	<MvASSIGN NAME = "l.basket:ship_addr2"			VALUE = "">
	<MvASSIGN NAME = "l.basket:ship_city"			VALUE = "">
	<MvASSIGN NAME = "l.basket:ship_state"			VALUE = "">
	<MvASSIGN NAME = "l.basket:ship_zip"			VALUE = "">
	<MvASSIGN NAME = "l.basket:ship_cntry"			VALUE = "">
	<MvASSIGN NAME = "l.basket:bill_fname"			VALUE = "">
	<MvASSIGN NAME = "l.basket:bill_lname"			VALUE = "">
	<MvASSIGN NAME = "l.basket:bill_email"			VALUE = "">
	<MvASSIGN NAME = "l.basket:bill_comp"			VALUE = "">
	<MvASSIGN NAME = "l.basket:bill_phone"			VALUE = "">
	<MvASSIGN NAME = "l.basket:bill_fax"			VALUE = "">
	<MvASSIGN NAME = "l.basket:bill_addr"			VALUE = "">
	<MvASSIGN NAME = "l.basket:bill_addr1"			VALUE = "">
	<MvASSIGN NAME = "l.basket:bill_addr2"			VALUE = "">
	<MvASSIGN NAME = "l.basket:bill_city"			VALUE = "">
	<MvASSIGN NAME = "l.basket:bill_state"			VALUE = "">
	<MvASSIGN NAME = "l.basket:bill_zip"			VALUE = "">
	<MvASSIGN NAME = "l.basket:bill_cntry"			VALUE = "">
	<MvASSIGN NAME = "l.basket:addr_val_s"			VALUE = 0>
	<MvASSIGN NAME = "l.basket:addr_val_b"			VALUE = 0>

	<MvIF EXPR = "{ Basket_Is_Provisional( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ Basket_Update_Customer_Information( l.basket ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Update_AuthorizationToken" PARAMETERS = "basket var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_Is_Provisional( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ Basket_Register( l.basket ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Baskets
					      SET
							auth_token	= ?
					      WHERE
						    session_id	= ?' }"
			 FIELDS	= "l.basket:auth_token,
					   l.basket:session_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BSK-00015', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Increment_AuthorizationFailures" PARAMETERS = "basket_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Baskets
					      SET
							auth_fails	= auth_fails + 1
					      WHERE
						    basket_id	= ?' }"
			 FIELDS	= "l.basket_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BSK-00016', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Update_Shipping" PARAMETERS = "basket var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_Is_Provisional( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ Basket_Register( l.basket ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Baskets
					      SET
							ship_id		= ?,
							ship_data	= ?
					      WHERE
						    session_id	= ?' }"
			 FIELDS	= "l.basket:ship_id, l.basket:ship_data,
					   l.basket:session_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BSK-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Delete_ID" PARAMETERS = "basket_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'Baskets WHERE basket_id = ?' }"
			 FIELDS	= "l.basket_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BSK-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Load_ID" PARAMETERS = "basket_id, basket var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-BSK-00018' ) }">
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Baskets"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Baskets WHERE basket_id = ?' }"
				FIELDS	= "l.basket_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BSK-00011', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Baskets.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Baskets">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-BSK-00012' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Basket_Read( l.basket ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Baskets">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Load_Session" PARAMETERS = "session_id, basket var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Baskets"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Baskets WHERE session_id = ?' }"
				FIELDS	= "l.session_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BSK-00008', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Baskets.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Baskets">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-BSK-00009' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Basket_Read( l.basket ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Baskets">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Expire_All_Customer" PARAMETERS = "cust_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE
							' $ g.Store_Table_Prefix $ 'Baskets
						  SET
							lastupdate	= 0
						  WHERE
							cust_id		= ?' }"
			 FIELDS	= "l.cust_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BSK-00013', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Expire_All_Customer_OtherBasket" PARAMETERS = "cust_id, basket_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE
							' $ g.Store_Table_Prefix $ 'Baskets
						  SET
							lastupdate	= 0
						  WHERE
							cust_id		= ?	AND
							basket_id	<> ?' }"
			 FIELDS	= "l.cust_id, l.basket_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BSK-00014', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>
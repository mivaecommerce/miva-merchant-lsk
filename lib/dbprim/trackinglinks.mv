<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-TKL-
| Next Error Code: 8    
|
</MvCOMMENT>

<MvFUNCTION NAME = "TrackingLink_Read" PARAMETERS = "tracklink var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.tracklink:type"	VALUE = "{ TrackingLinks.d.type }">
	<MvASSIGN NAME = "l.tracklink:url"	VALUE = "{ TrackingLinks.d.url }">
</MvFUNCTION>

<MvFUNCTION NAME = "TrackingLink_Insert" PARAMETERS = "tracklink var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "INSERT INTO TrackingLinks
					   ( type, url )
					   VALUES
					   ( ?, ? )"
			 FIELDS	= "l.tracklink:type, l.tracklink:url">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-TKL-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TrackingLink_Update" PARAMETERS = "tracklink var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "UPDATE TrackingLinks SET url = ? WHERE type = ?"
			 FIELDS	= "l.tracklink:url, l.tracklink:type">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-TKL-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TrackingLink_Delete" PARAMETERS = "type" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "DELETE FROM TrackingLinks WHERE type = ?"
			 FIELDS	= "l.type">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-TKL-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TrackingLink_Load_Type_Cached" PARAMETERS = "type, tracklink var" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCEARRAY NAME = "l.cache" 					VARIABLE = "g.Session:cache:trackinglink_load_type_cached">
		<MvMEMBER NAME = "{ l.type }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ TrackingLink_Load_Type( l.type, l.cache:tracklink ) }">

		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.tracklink"						VALUE = "{ l.cache:tracklink }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "TrackingLink_Load_Type" PARAMETERS = "type, tracklink var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TrackingLinks"
				QUERY	= "{ 'SELECT * FROM TrackingLinks WHERE ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( 'type' ) $ ' = ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.type">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-TKL-00004', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TrackingLinks.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TrackingLinks">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-TKL-00005' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ TrackingLink_Read( l.tracklink ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TrackingLinks">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TrackingLinkList_Load_All" PARAMETERS = "tracklinks var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TrackingLinks"
				QUERY	= "SELECT * FROM TrackingLinks ORDER BY type">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-TKL-00006', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.tracklink_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT TrackingLinks.d.EOF }">
		<MvASSIGN NAME = "l.tracklink_count" VALUE = "{ l.tracklink_count + 1 }">
		<MvEVAL EXPR = "{ TrackingLink_Read( l.tracklinks[ l.tracklink_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "TrackingLinks" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TrackingLinks">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-TKL-00007', l.tracklink_count ) }">
</MvFUNCTION>

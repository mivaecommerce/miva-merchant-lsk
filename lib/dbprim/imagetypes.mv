<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-ITY-
| Next Error Code: 10   
|
</MvCOMMENT>

<MvFUNCTION NAME = "ImageType_Read" PARAMETERS = "imagetype var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.imagetype:id"			VALUE = "{ ImageTypes.d.id }">
	<MvASSIGN NAME = "l.imagetype:code"			VALUE = "{ ImageTypes.d.code }">
	<MvASSIGN NAME = "l.imagetype:descrip"		VALUE = "{ ImageTypes.d.descrip }">
</MvFUNCTION>

<MvFUNCTION NAME = "ImageType_Insert" PARAMETERS = "imagetype var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.imagetype:id"			VALUE = "{ StoreKey_Generate( 'ImageTypes' ) }">
	
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'ImageTypes
					      ( id, code, descrip )
						  VALUES
						  ( ?, ?, ? )' }"
			 FIELDS	= "l.imagetype:id, l.imagetype:code, l.imagetype:descrip">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ITY-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImageType_Update" PARAMETERS = "imagetype" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'ImageTypes
					      SET
							code	= ?,
							descrip	= ?
					      WHERE
						    id		= ?' }"
			 FIELDS	= "l.imagetype:code, l.imagetype:descrip, l.imagetype:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ITY-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImageType_Delete_LowLevel" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ImageTypes WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ITY-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImageType_Load_ID" PARAMETERS = "id, imagetype var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ImageTypes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'ImageTypes WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ITY-00004', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ ImageTypes.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "ImageTypes">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-ITY-00005' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ ImageType_Read( l.imagetype ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ImageTypes">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImageType_Load_Code" PARAMETERS = "code, imagetype var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ImageTypes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'ImageTypes WHERE ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ITY-00006', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ ImageTypes.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "ImageTypes">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-ITY-00007' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ ImageType_Read( l.imagetype ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ImageTypes">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImageTypeList_Load_All" PARAMETERS = "imagetypes var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ImageTypes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'ImageTypes' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ITY-00008', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.imagetype_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT ImageTypes.d.EOF }">
		<MvASSIGN NAME = "l.imagetype_count" VALUE = "{ l.imagetype_count + 1 }">
		<MvEVAL EXPR = "{ ImageType_Read( l.imagetypes[ l.imagetype_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "ImageTypes" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ImageTypes">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-ITY-00009', l.imagetype_count ) }">
</MvFUNCTION>

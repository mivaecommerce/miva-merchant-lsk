<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-PVPT-
| Next Error Code: 10   
|
</MvCOMMENT>

<MvFUNCTION NAME = "ProductVariantPart_Read" PARAMETERS = "productvariantpart var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.productvariantpart:product_id"	VALUE = "{ ProductVariantParts.d.product_id }">
	<MvASSIGN NAME = "l.productvariantpart:variant_id"	VALUE = "{ ProductVariantParts.d.variant_id }">
	<MvASSIGN NAME = "l.productvariantpart:quantity"	VALUE = "{ ProductVariantParts.d.quantity }">
	<MvASSIGN NAME = "l.productvariantpart:part_id"		VALUE = "{ ProductVariantParts.d.part_id }">
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantPartList_Load_Variant" PARAMETERS = "product_id, variant_id, productvariantparts var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ProductVariantParts"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'ProductVariantParts WHERE product_id = ? AND variant_id = ?' }"
				FIELDS	= "l.product_id, l.variant_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVPT-00001', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.productvariantpart_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT ProductVariantParts.d.EOF }">
		<MvASSIGN NAME = "l.productvariantpart_count" VALUE = "{ l.productvariantpart_count + 1 }">
		<MvEVAL EXPR = "{ ProductVariantPart_Read( l.productvariantparts[ l.productvariantpart_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "ProductVariantParts" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ProductVariantParts">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-PVPT-00002', l.productvariantpart_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantPartList_Load_Part" PARAMETERS = "part_id, productvariantparts var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ProductVariantParts"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'ProductVariantParts WHERE part_id = ?' }"
				FIELDS	= "l.part_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVPT-00003', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.productvariantpart_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT ProductVariantParts.d.EOF }">
		<MvASSIGN NAME = "l.productvariantpart_count" VALUE = "{ l.productvariantpart_count + 1 }">
		<MvEVAL EXPR = "{ ProductVariantPart_Read( l.productvariantparts[ l.productvariantpart_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "ProductVariantParts" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ProductVariantParts">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-PVPT-00004', l.productvariantpart_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantPart_Insert" PARAMETERS = "productvariantpart var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'ProductVariantParts
						  ( product_id, variant_id, quantity, part_id )
						  VALUES
						  ( ?, ?, ?, ? )' }"
			 FIELDS	= "l.productvariantpart:product_id, l.productvariantpart:variant_id,
					   l.productvariantpart:quantity, l.productvariantpart:part_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVPT-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantPart_Delete_All_Product" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ProductVariantParts WHERE product_id = ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVPT-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantPart_Delete_All_Product_NotDefault" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ProductVariantParts WHERE product_id = ? AND variant_id <> 0' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVPT-00009', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantPart_Delete_All_Variant" PARAMETERS = "product_id, variant_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ProductVariantParts WHERE product_id = ? AND variant_id = ?' }"
			 FIELDS	= "l.product_id, l.variant_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVPT-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantPart_Delete_All_Part_LowLevel" PARAMETERS = "part_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ProductVariantParts WHERE part_id = ?' }"
			 FIELDS	= "l.part_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVPT-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

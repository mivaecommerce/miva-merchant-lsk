<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2022 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-OOD-
| Next Error Code: 8    
|
</MvCOMMENT>

<MvFUNCTION NAME = "OrderOptionDiscount_Read" PARAMETERS = "orderoptiondiscount var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.orderoptiondiscount:order_id"	VALUE = "{ OrderOptionDiscounts.d.order_id }">
	<MvASSIGN NAME = "l.orderoptiondiscount:line_id"	VALUE = "{ OrderOptionDiscounts.d.line_id }">
	<MvASSIGN NAME = "l.orderoptiondiscount:lineopt_id"	VALUE = "{ OrderOptionDiscounts.d.lineopt_id }">
	<MvASSIGN NAME = "l.orderoptiondiscount:attr_id"	VALUE = "{ OrderOptionDiscounts.d.attr_id }">
	<MvASSIGN NAME = "l.orderoptiondiscount:attmpat_id"	VALUE = "{ OrderOptionDiscounts.d.attmpat_id }">
	<MvASSIGN NAME = "l.orderoptiondiscount:pgrp_id"	VALUE = "{ OrderOptionDiscounts.d.pgrp_id }">
	<MvASSIGN NAME = "l.orderoptiondiscount:priority"	VALUE = "{ OrderOptionDiscounts.d.priority }">
	<MvASSIGN NAME = "l.orderoptiondiscount:display"	VALUE = "{ OrderOptionDiscounts.d.display }">
	<MvASSIGN NAME = "l.orderoptiondiscount:descrip"	VALUE = "{ OrderOptionDiscounts.d.descrip }">
	<MvASSIGN NAME = "l.orderoptiondiscount:discount"	VALUE = "{ OrderOptionDiscounts.d.discount }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderOptionDiscount_Insert" PARAMETERS = "orderoptiondiscount var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'OrderOptionDiscounts
					      ( order_id, line_id, lineopt_id, attr_id, attmpat_id, pgrp_id, priority, display, descrip, discount )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.orderoptiondiscount:order_id, l.orderoptiondiscount:line_id, l.orderoptiondiscount:lineopt_id,
					   l.orderoptiondiscount:attr_id, l.orderoptiondiscount:attmpat_id, l.orderoptiondiscount:pgrp_id,
					   l.orderoptiondiscount:priority, l.orderoptiondiscount:display, l.orderoptiondiscount:descrip,
					   l.orderoptiondiscount:discount">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OOD-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderOptionDiscountList_Load_Option" PARAMETERS = "line_id, attr_id, attmpat_id, orderoptiondiscounts var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderOptionDiscounts"
				QUERY	= "{ 'SELECT
								*
							  FROM
								' $ g.Store_Table_Prefix $ 'OrderOptionDiscounts
							  WHERE
								line_id		= ?		AND
								attr_id		= ?		AND
								attmpat_id	= ?
							  ORDER BY
								priority DESC, pgrp_id ASC' }"
				FIELDS	= "l.line_id, l.attr_id, l.attmpat_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OOD-00002', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderoptiondiscount_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderOptionDiscounts.d.EOF }">
		<MvEVAL EXPR = "{ OrderOptionDiscount_Read( l.orderoptiondiscounts[ ++l.orderoptiondiscount_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "OrderOptionDiscounts" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderOptionDiscounts">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OOD-00003', l.orderoptiondiscount_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderOptionDiscountList_Load_LineOption_ID" PARAMETERS = "lineopt_id, orderoptiondiscounts var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderOptionDiscounts"
				QUERY	= "{ 'SELECT
								*
							  FROM
								' $ g.Store_Table_Prefix $ 'OrderOptionDiscounts
							  WHERE
								lineopt_id = ?	
							  ORDER BY
								priority DESC, pgrp_id ASC' }"
				FIELDS	= "l.lineopt_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OOD-00006', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderoptiondiscount_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderOptionDiscounts.d.EOF }">
		<MvEVAL EXPR = "{ OrderOptionDiscount_Read( l.orderoptiondiscounts[ ++l.orderoptiondiscount_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "OrderOptionDiscounts" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderOptionDiscounts">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OOD-00007', l.orderoptiondiscount_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderOptionDiscount_Delete_All_Order" PARAMETERS = "order_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderOptionDiscounts WHERE order_id = ?' }"
			 FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OOD-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderOptionDiscount_Delete_All_Line" PARAMETERS = "line_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderOptionDiscounts WHERE line_id = ?' }"
			 FIELDS	= "l.line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OOD-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

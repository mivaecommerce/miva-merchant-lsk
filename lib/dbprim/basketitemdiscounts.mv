<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2019 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-BID-
| Next Error Code: 7    
|
</MvCOMMENT>

<MvFUNCTION NAME = "BasketItemDiscount_Read" PARAMETERS = "basketitemdiscount var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.basketitemdiscount:basket_id"	VALUE = "{ BasketItemDiscounts.d.basket_id }">
	<MvASSIGN NAME = "l.basketitemdiscount:line_id"		VALUE = "{ BasketItemDiscounts.d.line_id }">
	<MvASSIGN NAME = "l.basketitemdiscount:pgrp_id"		VALUE = "{ BasketItemDiscounts.d.pgrp_id }">
	<MvASSIGN NAME = "l.basketitemdiscount:priority"	VALUE = "{ BasketItemDiscounts.d.priority }">
	<MvASSIGN NAME = "l.basketitemdiscount:display"		VALUE = "{ BasketItemDiscounts.d.display }">
	<MvASSIGN NAME = "l.basketitemdiscount:descrip"		VALUE = "{ BasketItemDiscounts.d.descrip }">
	<MvASSIGN NAME = "l.basketitemdiscount:discount"	VALUE = "{ BasketItemDiscounts.d.discount }">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItemDiscount_Insert" PARAMETERS = "basketitemdiscount var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basketitemdiscount:basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BID-00006', 'Basket is provisional' ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'BasketItemDiscounts
					      ( basket_id, line_id, pgrp_id, priority, display, descrip, discount )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.basketitemdiscount:basket_id, l.basketitemdiscount:line_id, l.basketitemdiscount:pgrp_id,
					   l.basketitemdiscount:priority, l.basketitemdiscount:display, l.basketitemdiscount:descrip,
					   l.basketitemdiscount:discount">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BID-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItemDiscountList_Load_Line" PARAMETERS = "line_id, basketitemdiscounts var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "BasketItemDiscounts"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'BasketItemDiscounts WHERE line_id = ? ORDER BY priority DESC, pgrp_id ASC' }"
				FIELDS	= "l.line_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BID-00002', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basketitemdiscount_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT BasketItemDiscounts.d.EOF }">
		<MvEVAL EXPR = "{ BasketItemDiscount_Read( l.basketitemdiscounts[ ++l.basketitemdiscount_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "BasketItemDiscounts" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "BasketItemDiscounts">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BID-00003', l.basketitemdiscount_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItemDiscount_Delete_All_Basket" PARAMETERS = "basket_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'BasketItemDiscounts WHERE basket_id = ?' }"
			 FIELDS	= "l.basket_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BID-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItemDiscount_Delete_All_Line" PARAMETERS = "line_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'BasketItemDiscounts WHERE line_id = ?' }"
			 FIELDS	= "l.line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BID-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2025 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-BIT-
| Next Error Code: 42   
|
</MvCOMMENT>

<MvFUNCTION NAME = "BasketItem_Read" PARAMETERS = "basketitem var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.basketitem:basket_id"	VALUE = "{ BasketItems.d.basket_id }">
	<MvASSIGN NAME = "l.basketitem:line_id"		VALUE = "{ BasketItems.d.line_id }">
	<MvASSIGN NAME = "l.basketitem:group_id"	VALUE = "{ BasketItems.d.group_id }">
	<MvASSIGN NAME = "l.basketitem:parent_id"	VALUE = "{ BasketItems.d.parent_id }">
	<MvASSIGN NAME = "l.basketitem:product_id"	VALUE = "{ BasketItems.d.product_id }">
	<MvASSIGN NAME = "l.basketitem:variant_id"	VALUE = "{ BasketItems.d.variant_id }">
	<MvASSIGN NAME = "l.basketitem:wish_id"		VALUE = "{ BasketItems.d.wish_id }">
	<MvASSIGN NAME = "l.basketitem:subscrp_id"	VALUE = "{ BasketItems.d.subscrp_id }">
	<MvASSIGN NAME = "l.basketitem:subterm_id"	VALUE = "{ BasketItems.d.subterm_id }">
	<MvASSIGN NAME = "l.basketitem:code"		VALUE = "{ BasketItems.d.code }">
	<MvASSIGN NAME = "l.basketitem:name"		VALUE = "{ BasketItems.d.name }">
	<MvASSIGN NAME = "l.basketitem:sku"			VALUE = "{ BasketItems.d.sku }">
	<MvASSIGN NAME = "l.basketitem:type"		VALUE = "{ BasketItems.d.type }">
	<MvASSIGN NAME = "l.basketitem:retail"		VALUE = "{ BasketItems.d.retail }">
	<MvASSIGN NAME = "l.basketitem:base_price"	VALUE = "{ BasketItems.d.base_price }">
	<MvASSIGN NAME = "l.basketitem:price"		VALUE = "{ BasketItems.d.price }">
	<MvASSIGN NAME = "l.basketitem:total"		VALUE = "{ BasketItems.d.total }">
	<MvASSIGN NAME = "l.basketitem:weight"		VALUE = "{ BasketItems.d.weight }">
	<MvASSIGN NAME = "l.basketitem:taxable"		VALUE = "{ BasketItems.d.taxable }">
	<MvASSIGN NAME = "l.basketitem:upsold"		VALUE = "{ BasketItems.d.upsold }">
	<MvASSIGN NAME = "l.basketitem:quantity"	VALUE = "{ BasketItems.d.quantity }">
	<MvASSIGN NAME = "l.basketitem:tax"			VALUE = "{ BasketItems.d.tax }">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItem_Insert_LowLevel" PARAMETERS = "basketitem var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.basketitem:group_id }">	<MvASSIGN NAME = "l.basketitem:group_id"	VALUE = "{ l.basketitem:line_id }">	</MvIF>
	<MvIF EXPR = "{ ISNULL l.basketitem:parent_id }">	<MvASSIGN NAME = "l.basketitem:parent_id"	VALUE = 0>							</MvIF>
	<MvIF EXPR = "{ ISNULL l.basketitem:variant_id }">	<MvASSIGN NAME = "l.basketitem:variant_id"	VALUE = 0>							</MvIF>
	<MvIF EXPR = "{ ISNULL l.basketitem:wish_id }">		<MvASSIGN NAME = "l.basketitem:wish_id"		VALUE = 0>							</MvIF>
	<MvIF EXPR = "{ ISNULL l.basketitem:subscrp_id }">	<MvASSIGN NAME = "l.basketitem:subscrp_id"	VALUE = 0>							</MvIF>
	<MvIF EXPR = "{ ISNULL l.basketitem:subterm_id }">	<MvASSIGN NAME = "l.basketitem:subterm_id"	VALUE = 0>							</MvIF>
	<MvIF EXPR = "{ ISNULL l.basketitem:retail }">		<MvASSIGN NAME = "l.basketitem:retail"		VALUE = 0.00>						</MvIF>
	<MvIF EXPR = "{ ISNULL l.basketitem:base_price }">	<MvASSIGN NAME = "l.basketitem:base_price"	VALUE = "{ l.basketitem:price }">	</MvIF>
	<MvIF EXPR = "{ ISNULL l.basketitem:total }">		<MvASSIGN NAME = "l.basketitem:total"		VALUE = 0.00>						</MvIF>
	<MvIF EXPR = "{ ISNULL l.basketitem:tax }">			<MvASSIGN NAME = "l.basketitem:tax"			VALUE = 0.00>						</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'BasketItems
					      ( basket_id, line_id, group_id, parent_id, product_id, variant_id, wish_id, subscrp_id, subterm_id,
					        code, name, sku, type, retail, base_price, price, total, weight, taxable, upsold, quantity, tax )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.basketitem:basket_id, l.basketitem:line_id, l.basketitem:group_id, l.basketitem:parent_id, l.basketitem:product_id, l.basketitem:variant_id,
			 		   l.basketitem:wish_id, l.basketitem:subscrp_id, l.basketitem:subterm_id, l.basketitem:code, l.basketitem:name, l.basketitem:sku, l.basketitem:type,
			 		   l.basketitem:retail, l.basketitem:base_price, l.basketitem:price, l.basketitem:total, l.basketitem:weight, l.basketitem:taxable, l.basketitem:upsold,
			 		   l.basketitem:quantity, l.basketitem:tax">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItem_Update_LowLevel" PARAMETERS = "basketitem var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'BasketItems
						  SET
							product_id	= ?,
							variant_id	= ?,
							subscrp_id	= ?,
							subterm_id	= ?,
							code		= ?,
							name		= ?,
							sku			= ?,
							type		= ?,
							retail	 	= ?,
							base_price 	= ?,
							price		= ?,
							total		= ?,
							weight 		= ?,
							taxable 	= ?,
							quantity 	= ?,
							tax 		= ?
						  WHERE
							line_id		= ?' }"
			 FIELDS	= "l.basketitem:product_id, l.basketitem:variant_id, l.basketitem:subscrp_id, l.basketitem:subterm_id, l.basketitem:code, l.basketitem:name,
			 		   l.basketitem:sku, l.basketitem:type, l.basketitem:retail, l.basketitem:base_price, l.basketitem:price, l.basketitem:total, l.basketitem:weight,
			 		   l.basketitem:taxable, l.basketitem:quantity, l.basketitem:tax,
			 		   l.basketitem:line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00031', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItem_Update_Pricing_LowLevel" PARAMETERS = "basketitem var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'BasketItems
						  SET
							group_id	= ?,
							parent_id	= ?,
							quantity	= ?,
							retail		= ?,
							base_price	= ?,
							price		= ?,
							total		= ?
						  WHERE
							line_id		= ?' }"
			 FIELDS	= "l.basketitem:group_id, l.basketitem:parent_id, l.basketitem:quantity, l.basketitem:retail, l.basketitem:base_price, l.basketitem:price, l.basketitem:total,
					   l.basketitem:line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00013', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItem_Update_Tax_LowLevel" PARAMETERS = "basketitem var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'BasketItems
			 			  SET
			 				tax		= ?
			 			  WHERE
			 				line_id	= ?' }"
			 FIELDS = "l.basketitem:tax,
			 		   l.basketitem:line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-BIT-00032', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItem_Increment_Quantity_LowLevel" PARAMETERS = "line_id, quantity" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'BasketItems
						  SET
							quantity	= quantity + ?
						  WHERE
							line_id		= ?' }"
			 FIELDS	= "l.quantity,
					   l.line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItem_Load_Line" PARAMETERS = "basket_id, line_id, basketitem var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-BIT-00024' ) }">
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "BasketItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'BasketItems WHERE basket_id = ? AND line_id = ?' }"
				FIELDS	= "l.basket_id, l.line_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00003', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ BasketItems.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "BasketItems">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-BIT-00009' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ BasketItem_Read( l.basketitem ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "BasketItems">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItem_Load_Line_ID" PARAMETERS = "line_id, basketitem var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "BasketItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'BasketItems WHERE line_id = ?' }"
				FIELDS	= "l.line_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00036', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ BasketItems.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "BasketItems">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-BIT-00037' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ BasketItem_Read( l.basketitem ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "BasketItems">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItemList_Load_Basket" PARAMETERS = "basket_id, basketitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BIT-00025', 0 ) }">
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "BasketItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'BasketItems WHERE basket_id = ? ORDER BY parent_id, line_id' }"
				FIELDS	= "l.basket_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00004', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basketitem_count"	VALUE = 0>
	<MvASSIGN NAME = "l.parent_indexes"		VALUE = "">

	<MvCOMMENT>
	|
	| Process top-level BasketItems
	|
	</MvCOMMENT>

	<MvWHILE EXPR = "{ NOT BasketItems.d.EOF }">
		<MvIF EXPR = "{ BasketItems.d.parent_id }">
			<MvWHILESTOP>
		</MvIF>

		<MvREFERENCEARRAY NAME = "l.basketitem" VARIABLE = "l.basketitems">
			<MvDIMENSION INDEX = "{ ++l.basketitem_count }">
		</MvREFERENCEARRAY>

		<MvEVAL EXPR = "{ BasketItem_Read( l.basketitem ) }">

		<MvASSIGN NAME = "l.basketitem:child_count"								VALUE = 0>
		<MvASSIGN NAME = "l.parent_indexes"	INDEX = "{ l.basketitem:line_id }"	VALUE = "{ l.basketitem_count }">

		<MvSKIP NAME = "Merchant" VIEW = "BasketItems" ROWS = 1>
	</MvWHILE>

	<MvCOMMENT>
	|
	| Process children, end result is that the child ends up in both:
	|
	|	parent:children[]
	|	basketitems[ following parent ]
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.last_parent_id"							VALUE = 0>
	<MvASSIGN NAME = "l.last_parent_index_correction"			VALUE = 0>
	<MvASSIGN NAME = "l.parent_index_correction"				VALUE = 0>

	<MvWHILE EXPR = "{ NOT BasketItems.d.EOF }">
		<MvIF EXPR = "{ BasketItems.d.parent_id NE l.last_parent_id }">
			<MvASSIGN NAME = "l.last_parent_index_correction"	VALUE = "{ l.last_parent_index_correction + l.parent_index_correction }">
			<MvASSIGN NAME = "l.last_parent_id"					VALUE = "{ BasketItems.d.parent_id }">
			<MvASSIGN NAME = "l.parent_index_correction"		VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT miva_element_exists( l.parent_indexes, BasketItems.d.parent_id ) }">
			<MvEVAL EXPR = "{ BasketItem_Read( l.basketitems[ ++l.basketitem_count ] ) }">
		<MvELSE>
			<MvREFERENCEARRAY NAME = "l.parent_basketitem" VARIABLE = "l.basketitems">
				<MvDIMENSION INDEX = "{ l.parent_indexes[ BasketItems.d.parent_id ] + l.last_parent_index_correction }">
			</MvREFERENCEARRAY>

			<MvEVAL EXPR = "{ BasketItem_Read( l.parent_basketitem:children[ ++l.parent_basketitem:child_count ] ) }">

			<MvASSIGN NAME = "l.basketitem_count"				VALUE = "{ miva_array_insert_ref( l.basketitems, l.parent_basketitem:children[ l.parent_basketitem:child_count ], l.parent_indexes[ BasketItems.d.parent_id ] + l.last_parent_index_correction + l.parent_index_correction + 1 ) }">
			<MvASSIGN NAME = "l.parent_index_correction"		VALUE = "{ l.parent_index_correction + 1 }">
		</MvIF> 

		<MvSKIP NAME = "Merchant" VIEW = "BasketItems" ROWS = 1>
	 </MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "BasketItems">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BIT-00010', l.basketitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItemList_Load_Group" PARAMETERS = "group_id, basketitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "BasketItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'BasketItems WHERE group_id = ? ORDER BY line_id' }"
				FIELDS	= "l.group_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00014', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basketitem_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT BasketItems.d.EOF }">
		<MvEVAL EXPR = "{ BasketItem_Read( l.basketitems[ ++l.basketitem_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "BasketItems" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "BasketItems">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BIT-00015', l.basketitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItemList_Load_Basket_Group" PARAMETERS = "basket_id, group_id, basketitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BIT-00033', 0 ) }">
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "BasketItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'BasketItems WHERE basket_id = ? AND group_id = ? ORDER BY line_id' }"
				FIELDS	= "l.basket_id, l.group_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00034', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basketitem_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT BasketItems.d.EOF }">
		<MvEVAL EXPR = "{ BasketItem_Read( l.basketitems[ ++l.basketitem_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "BasketItems" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "BasketItems">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BIT-00035', l.basketitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItemList_Load_Basket_Product" PARAMETERS = "basket_id, product_id, basketitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BIT-00026', 0 ) }">
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "BasketItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'BasketItems WHERE basket_id = ? AND product_id = ? ORDER BY line_id' }"
				FIELDS	= "l.basket_id, l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00005', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basketitem_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT BasketItems.d.EOF }">
		<MvASSIGN NAME = "l.basketitem_count" VALUE = "{ l.basketitem_count + 1 }">
		<MvEVAL EXPR = "{ BasketItem_Read( l.basketitems[ l.basketitem_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "BasketItems" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "BasketItems">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BIT-00011', l.basketitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItemList_Load_Basket_WithSubscriptionTerm" PARAMETERS = "basket_id, basketitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BIT-00027', 0 ) }">
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "BasketItems"
				QUERY	= "{ 'SELECT
								bi.*,
								pst.id			AS productsubscriptionterm_id,
								pst.product_id	AS productsubscriptionterm_product_id,
								pst.frequency	AS productsubscriptionterm_frequency,
								pst.term		AS productsubscriptionterm_term,
								pst.descrip		AS productsubscriptionterm_descrip,
								pst.n			AS productsubscriptionterm_n,
								pst.fixed_dow	AS productsubscriptionterm_fixed_dow,
								pst.fixed_dom	AS productsubscriptionterm_fixed_dom,
								pst.sub_count	AS productsubscriptionterm_sub_count
							  FROM 
							  	' $ g.Store_Table_Prefix $ 'BasketItems bi,
							  	' $ g.Store_Table_Prefix $ 'ProductSubscriptionTerms pst
							  WHERE
							  	bi.basket_id	= ? AND
							  	pst.id			= bi.subterm_id
							  ORDER BY
							  	bi.line_id' }"
				FIELDS	= "l.basket_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00021', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basketitem_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT BasketItems.d.EOF }">
		<MvEVAL EXPR = "{ BasketItem_Read( l.basketitem ) }">

		<MvASSIGN NAME = "l.basketitem:productsubscriptionterm:id"			VALUE = "{ BasketItems.d.productsubscriptionterm_id }">
		<MvASSIGN NAME = "l.basketitem:productsubscriptionterm:product_id"	VALUE = "{ BasketItems.d.productsubscriptionterm_product_id }">
		<MvASSIGN NAME = "l.basketitem:productsubscriptionterm:frequency"	VALUE = "{ BasketItems.d.productsubscriptionterm_frequency }">
		<MvASSIGN NAME = "l.basketitem:productsubscriptionterm:term"		VALUE = "{ BasketItems.d.productsubscriptionterm_term }">
		<MvASSIGN NAME = "l.basketitem:productsubscriptionterm:descrip"		VALUE = "{ BasketItems.d.productsubscriptionterm_descrip }">
		<MvASSIGN NAME = "l.basketitem:productsubscriptionterm:n"			VALUE = "{ BasketItems.d.productsubscriptionterm_n }">
		<MvASSIGN NAME = "l.basketitem:productsubscriptionterm:fixed_dow"	VALUE = "{ BasketItems.d.productsubscriptionterm_fixed_dow }">
		<MvASSIGN NAME = "l.basketitem:productsubscriptionterm:fixed_dom"	VALUE = "{ BasketItems.d.productsubscriptionterm_fixed_dom }">
		<MvASSIGN NAME = "l.basketitem:productsubscriptionterm:sub_count"	VALUE = "{ BasketItems.d.productsubscriptionterm_sub_count }">

		<MvASSIGN NAME = "l.basketitem_count" VALUE = "{ miva_array_insert( l.basketitems, l.basketitem, -1 ) }">
		<MvSKIP NAME = "Merchant" VIEW = "BasketItems" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "BasketItems">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BIT-00022', l.basketitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItemList_Load_Basket_WishListItem" PARAMETERS = "basket_id, wish_id, basketitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BIT-00028', 0 ) }">
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "BasketItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'BasketItems WHERE basket_id = ? AND wish_id = ? ORDER BY line_id' }"
				FIELDS	= "l.basket_id, l.wish_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00017', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basketitem_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT BasketItems.d.EOF }">
		<MvASSIGN NAME = "l.basketitem_count" VALUE = "{ l.basketitem_count + 1 }">
		<MvEVAL EXPR = "{ BasketItem_Read( l.basketitems[ l.basketitem_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "BasketItems" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "BasketItems">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BIT-00018', l.basketitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItemList_Load_Basket_WithWishListItem" PARAMETERS = "basket_id, basketitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BIT-00029', 0 ) }">
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "BasketItems"
				QUERY	= "{ 'SELECT
								bi.*,
								wli.id 			AS wishlistitem_id,
								wli.wshlst_id 	AS wishlistitem_wshlst_id,
								wli.parent_id 	AS wishlistitem_parent_id,
								wli.product_id 	AS wishlistitem_product_id,
								wli.dtadded 	AS wishlistitem_dtadded,
								wli.quantity 	AS wishlistitem_quantity,
								wli.notes 		AS wishlistitem_notes
							  FROM ' $
							  	g.Store_Table_Prefix $ 'BasketItems bi, ' $
							  	g.Store_Table_Prefix $ 'WishListItems wli
							  WHERE
							  	bi.basket_id 	= ? AND
							  	bi.wish_id		<> 0 AND
							  	bi.wish_id 		= wli.id
							  ORDER BY
							  	line_id' }"
				FIELDS	= "l.basket_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00019', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basketitem_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT BasketItems.d.EOF }">
		<MvEVAL EXPR = "{ BasketItem_Read( l.basketitems[ ++l.basketitem_count ] ) }">

		<MvREFERENCEARRAY NAME = "l.wishlistitem" VARIABLE = "l.basketitems">
			<MvDIMENSION INDEX = "{ l.basketitem_count }">
			<MvMEMBER NAME = "wishlistitem">
		</MvREFERENCEARRAY>

		<MvASSIGN NAME = "l.wishlistitem:id"			VALUE = "{ BasketItems.d.wishlistitem_id }">
		<MvASSIGN NAME = "l.wishlistitem:wshlst_id"		VALUE = "{ BasketItems.d.wishlistitem_wshlst_id }">
		<MvASSIGN NAME = "l.wishlistitem:parent_id"		VALUE = "{ BasketItems.d.wishlistitem_parent_id }">
		<MvASSIGN NAME = "l.wishlistitem:product_id"	VALUE = "{ BasketItems.d.wishlistitem_product_id }">
		<MvASSIGN NAME = "l.wishlistitem:dtadded"		VALUE = "{ BasketItems.d.wishlistitem_dtadded }">
		<MvASSIGN NAME = "l.wishlistitem:quantity"		VALUE = "{ BasketItems.d.wishlistitem_quantity }">
		<MvASSIGN NAME = "l.wishlistitem:notes"			VALUE = "{ BasketItems.d.wishlistitem_notes }">

		<MvSKIP NAME = "Merchant" VIEW = "BasketItems" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "BasketItems">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BIT-00020', l.basketitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItemList_Load_Upsold" PARAMETERS = "basket_id, basketitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BIT-00030', 0 ) }">
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "BasketItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'BasketItems WHERE basket_id = ? AND upsold = 1 ORDER BY line_id' }"
				FIELDS	= "l.basket_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00006', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basketitem_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT BasketItems.d.EOF }">
		<MvASSIGN NAME = "l.basketitem_count" VALUE = "{ l.basketitem_count + 1 }">
		<MvEVAL EXPR = "{ BasketItem_Read( l.basketitems[ l.basketitem_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "BasketItems" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "BasketItems">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BIT-00012', l.basketitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItemList_Load_Parent" PARAMETERS = "basket_id, parent_id, basketitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "BasketItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'BasketItems WHERE basket_id = ? AND parent_id = ? ORDER BY line_id' }"
				FIELDS	= "l.basket_id, l.parent_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00039', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basketitem_count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT BasketItems.d.EOF }">
		<MvEVAL EXPR = "{ BasketItem_Read( l.basketitems[ ++l.basketitem_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "BasketItems" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "BasketItems">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BIT-00040', l.basketitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItem_Delete_Line" PARAMETERS = "line_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'BasketItems WHERE line_id = ?' }"
			 FIELDS	= "l.line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItem_Delete_All_Basket" PARAMETERS = "basket_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Basket_ID_Is_Provisional( l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'BasketItems WHERE basket_id = ?' }"
			 FIELDS	= "l.basket_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketItem_Delete_All_Group" PARAMETERS = "group_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'BasketItems WHERE group_id = ?' }"
			 FIELDS	= "l.group_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BIT-00016', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

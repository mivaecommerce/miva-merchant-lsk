<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2020 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-ATF-
| Next Error Code: 9
|
</MvCOMMENT>

<MvFUNCTION NAME = "AuthorizationFailure_Read" PARAMETERS = "authorizationfailure var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.authorizationfailure"				VALUE = "">
	<MvASSIGN NAME = "l.authorizationfailure:id"			VALUE = "{ AuthorizationFailures.d.id }">
	<MvASSIGN NAME = "l.authorizationfailure:order_id"		VALUE = "{ AuthorizationFailures.d.order_id }">
	<MvASSIGN NAME = "l.authorizationfailure:ship_res"		VALUE = "{ AuthorizationFailures.d.ship_res }">
	<MvASSIGN NAME = "l.authorizationfailure:ship_fname"	VALUE = "{ AuthorizationFailures.d.ship_fname }">
	<MvASSIGN NAME = "l.authorizationfailure:ship_lname"	VALUE = "{ AuthorizationFailures.d.ship_lname }">
	<MvASSIGN NAME = "l.authorizationfailure:ship_email"	VALUE = "{ AuthorizationFailures.d.ship_email }">
	<MvASSIGN NAME = "l.authorizationfailure:ship_comp"		VALUE = "{ AuthorizationFailures.d.ship_comp }">
	<MvASSIGN NAME = "l.authorizationfailure:ship_phone"	VALUE = "{ AuthorizationFailures.d.ship_phone }">
	<MvASSIGN NAME = "l.authorizationfailure:ship_fax"		VALUE = "{ AuthorizationFailures.d.ship_fax }">
	<MvASSIGN NAME = "l.authorizationfailure:ship_addr1"	VALUE = "{ AuthorizationFailures.d.ship_addr }">
	<MvASSIGN NAME = "l.authorizationfailure:ship_addr2"	VALUE = "{ AuthorizationFailures.d.ship_addr2 }">
	<MvASSIGN NAME = "l.authorizationfailure:ship_city"		VALUE = "{ AuthorizationFailures.d.ship_city }">
	<MvASSIGN NAME = "l.authorizationfailure:ship_state"	VALUE = "{ AuthorizationFailures.d.ship_state }">
	<MvASSIGN NAME = "l.authorizationfailure:ship_zip"		VALUE = "{ AuthorizationFailures.d.ship_zip }">
	<MvASSIGN NAME = "l.authorizationfailure:ship_cntry"	VALUE = "{ AuthorizationFailures.d.ship_cntry }">
	<MvASSIGN NAME = "l.authorizationfailure:bill_fname"	VALUE = "{ AuthorizationFailures.d.bill_fname }">
	<MvASSIGN NAME = "l.authorizationfailure:bill_lname"	VALUE = "{ AuthorizationFailures.d.bill_lname }">
	<MvASSIGN NAME = "l.authorizationfailure:bill_email"	VALUE = "{ AuthorizationFailures.d.bill_email }">
	<MvASSIGN NAME = "l.authorizationfailure:bill_comp"		VALUE = "{ AuthorizationFailures.d.bill_comp }">
	<MvASSIGN NAME = "l.authorizationfailure:bill_phone"	VALUE = "{ AuthorizationFailures.d.bill_phone }">
	<MvASSIGN NAME = "l.authorizationfailure:bill_fax"		VALUE = "{ AuthorizationFailures.d.bill_fax }">
	<MvASSIGN NAME = "l.authorizationfailure:bill_addr1"	VALUE = "{ AuthorizationFailures.d.bill_addr }">
	<MvASSIGN NAME = "l.authorizationfailure:bill_addr2"	VALUE = "{ AuthorizationFailures.d.bill_addr2 }">
	<MvASSIGN NAME = "l.authorizationfailure:bill_city"		VALUE = "{ AuthorizationFailures.d.bill_city }">
	<MvASSIGN NAME = "l.authorizationfailure:bill_state"	VALUE = "{ AuthorizationFailures.d.bill_state }">
	<MvASSIGN NAME = "l.authorizationfailure:bill_zip"		VALUE = "{ AuthorizationFailures.d.bill_zip }">
	<MvASSIGN NAME = "l.authorizationfailure:bill_cntry"	VALUE = "{ AuthorizationFailures.d.bill_cntry }">
	<MvASSIGN NAME = "l.authorizationfailure:dtstamp"		VALUE = "{ AuthorizationFailures.d.dtstamp }">
	<MvASSIGN NAME = "l.authorizationfailure:ip"			VALUE = "{ AuthorizationFailures.d.ip }">
	<MvASSIGN NAME = "l.authorizationfailure:mod_name"		VALUE = "{ AuthorizationFailures.d.mod_name }">
	<MvASSIGN NAME = "l.authorizationfailure:method"		VALUE = "{ AuthorizationFailures.d.method }">
	<MvASSIGN NAME = "l.authorizationfailure:amount"		VALUE = "{ AuthorizationFailures.d.amount }">
	<MvASSIGN NAME = "l.authorizationfailure:error_text"	VALUE = "{ AuthorizationFailures.d.error_text }">
	<MvASSIGN NAME = "l.authorizationfailure:cust_id"		VALUE = "{ AuthorizationFailures.d.cust_id }">
</MvFUNCTION>

<MvFUNCTION NAME = "AuthorizationFailure_Insert" PARAMETERS = "authorizationfailure var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.authorizationfailure:id"			VALUE = "{ StoreKey_Generate( 'AuthorizationFailures' ) }">

	<MvIF EXPR = "{ ISNULL l.authorizationfailure:cust_id }">
		<MvASSIGN NAME = "l.authorizationfailure:cust_id"	VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.authorizationfailure:ship_res }">
		<MvASSIGN NAME = "l.authorizationfailure:ship_res"	VALUE = 0>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'AuthorizationFailures
						  ( id, order_id, cust_id, ship_res,
						    ship_fname, ship_lname, ship_email, ship_comp, ship_phone, ship_fax, ship_addr, ship_addr2, ship_city, ship_state, ship_zip, ship_cntry,
						    bill_fname, bill_lname, bill_email, bill_comp, bill_phone, bill_fax, bill_addr, bill_addr2, bill_city, bill_state, bill_zip, bill_cntry,
						    dtstamp, ip, mod_name, method, amount, error_text )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.authorizationfailure:id, l.authorizationfailure:order_id, l.authorizationfailure:cust_id, l.authorizationfailure:ship_res,
					   l.authorizationfailure:ship_fname, l.authorizationfailure:ship_lname, l.authorizationfailure:ship_email, l.authorizationfailure:ship_comp, l.authorizationfailure:ship_phone, l.authorizationfailure:ship_fax,
					   l.authorizationfailure:ship_addr1, l.authorizationfailure:ship_addr2, l.authorizationfailure:ship_city, l.authorizationfailure:ship_state, l.authorizationfailure:ship_zip, l.authorizationfailure:ship_cntry,
					   l.authorizationfailure:bill_fname, l.authorizationfailure:bill_lname, l.authorizationfailure:bill_email, l.authorizationfailure:bill_comp, l.authorizationfailure:bill_phone, l.authorizationfailure:bill_fax,
					   l.authorizationfailure:bill_addr1, l.authorizationfailure:bill_addr2, l.authorizationfailure:bill_city, l.authorizationfailure:bill_state, l.authorizationfailure:bill_zip, l.authorizationfailure:bill_cntry,
					   l.authorizationfailure:dtstamp, l.authorizationfailure:ip, l.authorizationfailure:mod_name, l.authorizationfailure:method, l.authorizationfailure:amount, l.authorizationfailure:error_text">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATF-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AuthorizationFailure_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AuthorizationFailures WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATF-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AuthorizationFailure_Count_IPNewerThan" PARAMETERS = "ip, dtstamp, count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "AuthorizationFailures"
				QUERY 	= "{ 'SELECT COUNT( * ) AS authorizationfailure_count FROM ' $ g.Store_Table_Prefix $ 'AuthorizationFailures WHERE ip = ? AND dtstamp > ?' }"
				FIELDS	= "l.ip, l.dtstamp">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATF-00003', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = "{ AuthorizationFailures.d.authorizationfailure_count }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AuthorizationFailures">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AuthorizationFailure_Count_NewerThan" PARAMETERS = "dtstamp, count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "AuthorizationFailures"
				QUERY 	= "{ 'SELECT COUNT( * ) AS authorizationfailure_count FROM ' $ g.Store_Table_Prefix $ 'AuthorizationFailures WHERE dtstamp > ?' }"
				FIELDS	= "l.dtstamp">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATF-00004', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = "{ AuthorizationFailures.d.authorizationfailure_count }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AuthorizationFailures">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AuthorizationFailure_Load_Newest_Offset" PARAMETERS = "offset, authorizationfailure var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant',
																			 'AuthorizationFailures',
																			 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AuthorizationFailures ORDER BY dtstamp DESC',
																			 '',
																			 l.offset, 1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-ATF-00007', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ AuthorizationFailures.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "AuthorizationFailures">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-ATF-00008' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ AuthorizationFailure_Read( l.authorizationfailure ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AuthorizationFailures">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-SKY-
| Next Error Code: 5    
|
</MvCOMMENT>

<MvFUNCTION NAME = "StoreKey_Read" PARAMETERS = "storekey var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.storekey:type"		VALUE = "{ StoreKeys.d.type }">
	<MvASSIGN NAME = "l.storekey:maxvalue"	VALUE = "{ StoreKeys.d.maxvalue }">
</MvFUNCTION>

<MvFUNCTION NAME = "StoreKey_Insert" PARAMETERS = "type, firstvalue" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.maxvalue"	VALUE = "{ l.firstvalue - 1 }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'StoreKeys
					      ( type, ' $ [ g.Library_Filename_Native_DBAPI ].DB_Escape_Identifier( 'maxvalue' ) $ ' )
						  VALUES
						  ( ?, ? )' }"
			 FIELDS	= "l.type, l.maxvalue">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-SKY-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreKey_Delete" PARAMETERS = "type" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'StoreKeys WHERE type = ?' }"
			 FIELDS	= "l.type">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-SKY-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreKey_Load" PARAMETERS = "type, storekey var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW	NAME	= "Merchant"
				VIEW	= "StoreKeys"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'StoreKeys WHERE type = ?' }"
				FIELDS	= "l.type">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-SKY-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ StoreKeys.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "StoreKeys">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-SKY-00004' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ StoreKey_Read( l.storekey ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "StoreKeys">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

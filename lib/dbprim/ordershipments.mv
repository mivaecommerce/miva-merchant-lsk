<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-OSP-
| Next Error Code: 19   
|
</MvCOMMENT>

<MvFUNCTION NAME = "v57_OrderShipment_Read" PARAMETERS = "ordershipment var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.ordershipment:id"			VALUE = "{ OrderShipments.d.id }">
	<MvASSIGN NAME = "l.ordershipment:batch_id"		VALUE = "{ OrderShipments.d.batch_id }">
	<MvASSIGN NAME = "l.ordershipment:code"			VALUE = "{ OrderShipments.d.code }">
	<MvASSIGN NAME = "l.ordershipment:order_id"		VALUE = "{ OrderShipments.d.order_id }">
	<MvASSIGN NAME = "l.ordershipment:status"		VALUE = "{ OrderShipments.d.status }">
	<MvASSIGN NAME = "l.ordershipment:labelcount"	VALUE = "{ OrderShipments.d.labelcount }">
	<MvASSIGN NAME = "l.ordershipment:ship_date"	VALUE = "{ OrderShipments.d.ship_date }">
	<MvASSIGN NAME = "l.ordershipment:tracknum"		VALUE = "{ OrderShipments.d.tracknum }">
	<MvASSIGN NAME = "l.ordershipment:tracktype"	VALUE = "{ OrderShipments.d.tracktype }">
	<MvASSIGN NAME = "l.ordershipment:weight"		VALUE = "{ OrderShipments.d.weight }">
	<MvASSIGN NAME = "l.ordershipment:cost"			VALUE = "{ OrderShipments.d.cost }">
</MvFUNCTION>

<MvFUNCTION NAME = "v57_OrderShipment_Load_ID" PARAMETERS = "shpmnt_id, ordershipment var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderShipments"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderShipments WHERE id = ?' }"
				FIELDS	= "l.shpmnt_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OSP-00001', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ OrderShipments.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderShipments">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-OSP-00002' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ v57_OrderShipment_Read( l.ordershipment ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderShipments">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "v57_OrderShipment_Load_Code" PARAMETERS = "shpmnt_code, ordershipment var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderShipments"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderShipments WHERE code = ?' }"
				FIELDS	= "l.shpmnt_code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OSP-00018', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ OrderShipments.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderShipments">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-OSP-00009' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ v57_OrderShipment_Read( l.ordershipment ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderShipments">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderShipment_Insert_LowLevel" PARAMETERS = "ordershipment var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.ordershipment:batch_id }">		<MvASSIGN NAME = "l.ordershipment:batch_id"		VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ ISNULL l.ordershipment:ship_date }">	<MvASSIGN NAME = "l.ordershipment:ship_date"	VALUE = 0>	</MvIF>
	
	<MvASSIGN NAME = "l.ordershipment:labelcount"	VALUE = 0>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'OrderShipments
						  ( id, batch_id, code, order_id, status, labelcount, ship_date, tracknum, tracktype, weight, cost )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.ordershipment:id, l.ordershipment:batch_id, l.ordershipment:code, l.ordershipment:order_id, l.ordershipment:status,
					   l.ordershipment:labelcount, l.ordershipment:ship_date, l.ordershipment:tracknum, l.ordershipment:tracktype, l.ordershipment:weight,
					   l.ordershipment:cost">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OSP-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderShipment_Update_Status_LowLevel" PARAMETERS = "ordershipment var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderShipments
						  SET
							status		= ?,
							ship_date	= ?,
							tracknum	= ?,
							tracktype	= ?,
							cost		= ?,
							weight		= ?
						  WHERE
							id			= ?' }"
			 FIELDS	= "l.ordershipment:status, l.ordershipment:ship_date, l.ordershipment:tracknum, 
					   l.ordershipment:tracktype, l.ordershipment:cost, l.ordershipment:weight,
					   l.ordershipment:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OSP-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderShipment_Update_Batch" PARAMETERS = "ordershipment var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderShipments
					      SET
							batch_id	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.ordershipment:batch_id,
					   l.ordershipment:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OSP-00010', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderShipment_Increment_LabelCount" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderShipments SET labelcount = labelcount + 1 WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OSP-00016', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderShipment_Decrement_LabelCount" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderShipments SET labelcount = labelcount - 1 WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OSP-00017', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderShipment_Delete_LowLevel" PARAMETERS = "shpmnt_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderShipments WHERE id = ?' }"
			 FIELDS	= "l.shpmnt_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OSP-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderShipment_Delete_All_Order" PARAMETERS = "order_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderShipments WHERE order_id = ?' }"
			 FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OSP-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "v57_OrderShipmentList_Load_Order" PARAMETERS = "order_id, ordershipments var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderShipments"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderShipments WHERE order_id = ?' }"
				FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OSP-00008', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.ordershipment_count"		VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderShipments.d.EOF }">
		<MvASSIGN NAME = "l.ordershipment_count"	VALUE = "{ l.ordershipment_count + 1 }">
		<MvEVAL EXPR = "{ v57_OrderShipment_Read( l.ordershipments[ l.ordershipment_count ] ) }">
		
		<MvSKIP NAME = "Merchant" VIEW = "OrderShipments" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderShipments">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OSP-00011', l.ordershipment_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "v57_OrderShipmentList_Load_Status" PARAMETERS = "order_id, status, ordershipments var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderShipments"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderShipments WHERE order_id = ? AND status = ?' }"
				FIELDS	= "l.order_id, l.status">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OSP-00014', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.ordershipment_count"		VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderShipments.d.EOF }">
		<MvASSIGN NAME = "l.ordershipment_count"	VALUE = "{ l.ordershipment_count + 1 }">
		<MvEVAL EXPR = "{ v57_OrderShipment_Read( l.ordershipments[ l.ordershipment_count ] ) }">
		
		<MvSKIP NAME = "Merchant" VIEW = "OrderShipments" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderShipments">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OSP-00015', l.ordershipment_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "v57_OrderShipmentList_Load_ShipmentBatch" PARAMETERS = "shipmentbatch_id, ordershipments var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderShipments"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderShipments WHERE batch_id = ?' }"
				FIELDS	= "l.shipmentbatch_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OSP-00012', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.ordershipment_count"		VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderShipments.d.EOF }">
		<MvASSIGN NAME = "l.ordershipment_count"	VALUE = "{ l.ordershipment_count + 1 }">
		<MvEVAL EXPR = "{ v57_OrderShipment_Read( l.ordershipments[ l.ordershipment_count ] ) }">
		
		<MvSKIP NAME = "Merchant" VIEW = "OrderShipments" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderShipments">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OSP-00013', l.ordershipment_count ) }">
</MvFUNCTION>

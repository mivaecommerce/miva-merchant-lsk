<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-CXPCE-
| Next Error Code: 11
|
</MvCOMMENT>

<MvFUNCTION NAME = "CategoryXProductCollectionExclusion_Read" PARAMETERS = "categoryxproductcollectionexclusion var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.categoryxproductcollectionexclusion:cat_id"		VALUE = "{ CategoryXProductCollectionExclusions.d.cat_id }">
	<MvASSIGN NAME = "l.categoryxproductcollectionexclusion:product_id"	VALUE = "{ CategoryXProductCollectionExclusions.d.product_id }">
	<MvASSIGN NAME = "l.categoryxproductcollectionexclusion:colcount"	VALUE = "{ CategoryXProductCollectionExclusions.d.colcount }">
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXProductCollectionExclusion_Load" PARAMETERS = "cat_id, product_id, categoryxproductcollectionexclusion var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CategoryXProductCollectionExclusions"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CategoryXProductCollectionExclusions WHERE cat_id = ? AND product_id = ?' }"
				FIELDS	= "l.cat_id, l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXPCE-00001', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ CategoryXProductCollectionExclusions.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryXProductCollectionExclusions">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CXPCE-00002' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ CategoryXProductCollectionExclusion_Read( l.categoryxproductcollectionexclusion ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryXProductCollectionExclusions">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXProductCollectionExclusion_Load_Category" PARAMETERS = "cat_id, categoryxproductcollectionexclusions var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CategoryXProductCollectionExclusions"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CategoryXProductCollectionExclusions WHERE cat_id = ?' }"
				FIELDS	= "l.cat_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXPCE-00003', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT CategoryXProductCollectionExclusions.d.EOF }">
		<MvEVAL EXPR = "{ CategoryXProductCollectionExclusion_Read( l.categoryxproductcollectionexclusions[ ++l.count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "CategoryXProductCollectionExclusions" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryXProductCollectionExclusions">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-CXPCE-00004', l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXProductCollectionExclusion_Load_Product" PARAMETERS = "product_id, categoryxproductcollectionexclusions var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CategoryXProductCollectionExclusions"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CategoryXProductCollectionExclusions WHERE product_id = ?' }"
				FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXPCE-00005', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT CategoryXProductCollectionExclusions.d.EOF }">
		<MvEVAL EXPR = "{ CategoryXProductCollectionExclusion_Read( l.categoryxproductcollectionexclusions[ ++l.count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "CategoryXProductCollectionExclusions" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryXProductCollectionExclusions">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-CXPCE-00006', l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXProductCollectionExclusion_Insert" PARAMETERS = "categoryxproductcollectionexclusion var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'CategoryXProductCollectionExclusions
					      ( cat_id, product_id, colcount )
						  VALUES
						  ( ?, ?, ? )' }"
			 FIELDS	= "l.categoryxproductcollectionexclusion:cat_id, l.categoryxproductcollectionexclusion:product_id, l.categoryxproductcollectionexclusion:colcount">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXPCE-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXProductCollectionExclusion_Delete" PARAMETERS = "cat_id, product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CategoryXProductCollectionExclusions
						  WHERE
							cat_id		= ? AND
							product_id	= ?' }"
			 FIELDS	= "l.cat_id, l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXPCE-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXProductCollectionExclusion_Delete_All_Category" PARAMETERS = "cat_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CategoryXProductCollectionExclusions WHERE cat_id = ?' }"
			 FIELDS	= "l.cat_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXPCE-00009', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXProductCollectionExclusion_Delete_All_Product" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CategoryXProductCollectionExclusions WHERE product_id = ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXPCE-00010', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>
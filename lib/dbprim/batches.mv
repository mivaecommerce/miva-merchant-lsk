<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-BCH-
| Next Error Code: 11   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Batch_Read" PARAMETERS = "batch var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.batch:id"			VALUE = "{ Batches.d.id }">
	<MvASSIGN NAME = "l.batch:batchdate"	VALUE = "{ Batches.d.batchdate }">
	<MvASSIGN NAME = "l.batch:name"			VALUE = "{ Batches.d.name }">
</MvFUNCTION>

<MvFUNCTION NAME = "Batch_Insert" PARAMETERS = "batch var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.batch:id"	VALUE = "{ StoreKey_Generate( 'Batches' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'Batches
					      ( id, batchdate, name )
						  VALUES
						  ( ?, ?, ? )' }"
			 FIELDS	= "l.batch:id, l.batch:batchdate, l.batch:name">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BCH-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Batch_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'Batches WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BCH-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Batch_Load_ID" PARAMETERS = "id, batch var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Batches"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Batches WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BCH-00003', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Batches.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Batches">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-BCH-00006' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Batch_Read( l.batch ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Batches">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BatchList_Load_All" PARAMETERS = "batches var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Batches"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Batches ORDER BY id DESC' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BCH-00004', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.batch_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Batches.d.EOF }">
		<MvASSIGN NAME = "l.batch_count" VALUE = "{ l.batch_count + 1 }">
		<MvEVAL EXPR = "{ Batch_Read( l.batches[ l.batch_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Batches" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Batches">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BCH-00007', l.batch_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "BatchList_Load_Closed" PARAMETERS = "closed, batches var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.closed }">	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BCH-00008', 0 ) }">
	<MvELSE>						<MvFUNCTIONRETURN VALUE = "{ BatchList_Load_All( l.batches ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "BatchList_Load_Offset" PARAMETERS = "offset, search, searchable_fields, max, nextoffset var, batches var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.query"		VALUE = "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Batches' }">

	<MvIF EXPR = "{ len( l.search ) }">
		<MvASSIGN NAME = "l.query"	VALUE = "{ l.query $ ' WHERE ' $ SQL_Search_Clause( l.search, g.Store_Table_Prefix $ 'Batches', l.searchable_fields, l.fields ) }">
	</MvIF>

	<MvASSIGN NAME = "l.query"		VALUE = "{ l.query $ ' ORDER BY id desc' }">

	<MvIF EXPR = "{ l.max EQ 0 }">	<MvASSIGN NAME = "l.limit" VALUE = 0>
	<MvELSE>						<MvASSIGN NAME = "l.limit" VALUE = "{ l.max + 1 }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Library_Filename_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'Batches',
																			   l.query, l.fields,
																			   l.offset, l.limit ) }">
		<MvFUNCTIONRETURN VALUE = "{  [ g.Library_Filename_Utilities ].Error( 'MER-DBP-BCH-00005', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.batch_count" VALUE = 0>
	<MvWHILE EXPR = "{ ( NOT Batches.d.EOF ) AND ( ( l.max EQ 0 ) OR ( l.batch_count LT l.max ) ) }">
		<MvASSIGN NAME = "l.batch_count" VALUE = "{ l.batch_count + 1 }">
		<MvEVAL EXPR = "{ Batch_Read( l.batches[ l.batch_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Batches" ROWS = 1>
	</MvWHILE>

	<MvIF EXPR = "{ Batches.d.EOF }">
		<MvASSIGN NAME = "l.nextoffset"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.nextoffset"	VALUE = "{ l.offset + l.batch_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Batches">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-BCH-00009', l.batch_count ) }">
</MvFUNCTION>

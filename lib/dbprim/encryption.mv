<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-CRY-
| Next Error Code: 18   
|
</MvCOMMENT>

<MvCOMMENT>
|
| Encryption
|
</MvCOMMENT>

<MvFUNCTION NAME = "Encryption_Read" PARAMETERS = "encryption var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.encryption:id"			VALUE = "{ Encryption.d.id }">
	<MvASSIGN NAME = "l.encryption:dt_created"	VALUE = "{ Encryption.d.dt_created }">
	<MvASSIGN NAME = "l.encryption:prompt"		VALUE = "{ Encryption.d.prompt }">
	<MvASSIGN NAME = "l.encryption:refcount"	VALUE = "{ Encryption.d.refcount }">
	<MvASSIGN NAME = "l.encryption:pub_key" 	VALUE = "{ Encryption.d.pub_key }">
</MvFUNCTION>

<MvFUNCTION NAME = "Encryption_Insert" PARAMETERS = "encryption var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.encryption:id"			VALUE = "{ StoreKey_Generate( 'Encryption' ) }">
	<MvASSIGN NAME = "l.encryption:dt_created"	VALUE = "{ s.dyn_time_t }">
	<MvASSIGN NAME = "l.encryption:refcount"	VALUE = 0>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'Encryption
					      ( id, dt_created, prompt, refcount, pub_key )
						  VALUES
						  ( ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.encryption:id, l.encryption:dt_created, l.encryption:prompt, l.encryption:refcount, l.encryption:pub_key">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CRY-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Encryption_Update" PARAMETERS = "encryption var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Encryption
					      SET
							prompt		= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.encryption:prompt,
					   l.encryption:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CRY-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Encryption_Increment_ReferenceCount" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Encryption
					      SET
							refcount	= refcount + 1
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CRY-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Encryption_Decrement_ReferenceCount" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Encryption
					      SET
							refcount	= refcount - 1
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CRY-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Encryption_Delete_ID_LowLevel" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'Encryption WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CRY-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Encryption_Load_ID" PARAMETERS = "id, encryption var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Encryption"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Encryption WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CRY-00006', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Encryption.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Encryption">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CRY-00010' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Encryption_Read( l.encryption ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Encryption">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Encryption_Load_Prompt" PARAMETERS = "prompt, encryption var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Encryption"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Encryption WHERE ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( 'prompt' ) $ ' = ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.prompt">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CRY-00007', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Encryption.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Encryption">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CRY-00011' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Encryption_Read( l.encryption ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Encryption">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "EncryptionList_Load_All" PARAMETERS = "crypt_keys var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Encryption"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Encryption ORDER BY id' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CRY-00008', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.crypt_key_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Encryption.d.EOF }">
		<MvASSIGN NAME = "l.crypt_key_count" VALUE = "{ l.crypt_key_count + 1 }">
		<MvEVAL EXPR = "{ Encryption_Read( l.crypt_keys[ l.crypt_key_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Encryption" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Encryption">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-CRY-00012', l.crypt_key_count ) }">
</MvFUNCTION>

<MvCOMMENT>
|
| PrivateKeys
|
</MvCOMMENT>

<MvFUNCTION NAME = "PrivateKey_Read" PARAMETERS = "privatekey var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.privatekey:id"			VALUE = "{ PrivateKeys.d.id }">
	<MvASSIGN NAME = "l.privatekey:type"		VALUE = "{ PrivateKeys.d.type }">
	<MvASSIGN NAME = "l.privatekey:prv_key" 	VALUE = "{ PrivateKeys.d.prv_key }">
</MvFUNCTION>

<MvFUNCTION NAME = "PrivateKey_Insert" PARAMETERS = "privatekey var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Library_Filename_DBAPI ].DB_Open_PrivateKey( l.privatekey_db ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvQUERY NAME	= "{ l.privatekey_db }"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'PrivateKeys
					      ( id, type, prv_key )
						  VALUES
						  ( ?, ?, ? )' }"
			 FIELDS	= "l.privatekey:id, l.privatekey:type, l.privatekey:prv_key">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvEVAL EXPR = "{ [ g.Library_Filename_DBAPI ].DB_Close_PrivateKey( l.privatekey_db ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CRY-00013', g.MvQUERY_Error ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Library_Filename_DBAPI ].DB_Close_PrivateKey( l.privatekey_db ) }">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "PrivateKey_Update" PARAMETERS = "privatekey var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Library_Filename_DBAPI ].DB_Open_PrivateKey( l.privatekey_db ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvQUERY NAME	= "{ l.privatekey_db }"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'PrivateKeys
						  SET
							type	= ?,
							prv_key	= ?
						  WHERE
							id		= ?' }"
			 FIELDS	= "l.privatekey:type, l.privatekey:prv_key,
					   l.privatekey:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvEVAL EXPR = "{ [ g.Library_Filename_DBAPI ].DB_Close_PrivateKey( l.privatekey_db ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CRY-00017', g.MvQUERY_Error ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Library_Filename_DBAPI ].DB_Close_PrivateKey( l.privatekey_db ) }">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "PrivateKey_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Library_Filename_DBAPI ].DB_Open_PrivateKey( l.privatekey_db ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvQUERY NAME	= "{ l.privatekey_db }"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'PrivateKeys WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvEVAL EXPR = "{ [ g.Library_Filename_DBAPI ].DB_Close_PrivateKey( l.privatekey_db ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CRY-00014', g.MvQUERY_Error ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Library_Filename_DBAPI ].DB_Close_PrivateKey( l.privatekey_db ) }">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "PrivateKey_Load_ID" PARAMETERS = "id, privatekey var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Library_Filename_DBAPI ].DB_Open_PrivateKey( l.privatekey_db ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvOPENVIEW NAME	= "{ l.privatekey_db }"
				VIEW	= "PrivateKeys"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'PrivateKeys WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvEVAL EXPR = "{ [ g.Library_Filename_DBAPI ].DB_Close_PrivateKey( l.privatekey_db ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CRY-00015', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ PrivateKeys.d.EOF }">
		<MvCLOSEVIEW NAME = "{ l.privatekey_db }" VIEW = "PrivateKeys">
		<MvEVAL EXPR = "{ [ g.Library_Filename_DBAPI ].DB_Close_PrivateKey( l.privatekey_db ) }">

		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CRY-00016' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ PrivateKey_Read( l.privatekey ) }">

	<MvCLOSEVIEW NAME = "{ l.privatekey_db }" VIEW = "PrivateKeys">
	<MvEVAL EXPR = "{ [ g.Library_Filename_DBAPI ].DB_Close_PrivateKey( l.privatekey_db ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-OCH-
| Next Error Code: 15   
|
</MvCOMMENT>

<MvFUNCTION NAME = "OrderCharge_Read" PARAMETERS = "ordercharge var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.ordercharge:order_id"		VALUE = "{ OrderCharges.d.order_id }">
	<MvASSIGN NAME = "l.ordercharge:charge_id"		VALUE = "{ OrderCharges.d.charge_id }">
	<MvASSIGN NAME = "l.ordercharge:module_id"		VALUE = "{ OrderCharges.d.module_id }">
	<MvASSIGN NAME = "l.ordercharge:type"			VALUE = "{ OrderCharges.d.type }">
	<MvASSIGN NAME = "l.ordercharge:descrip"		VALUE = "{ OrderCharges.d.descrip }">
	<MvASSIGN NAME = "l.ordercharge:amount"			VALUE = "{ OrderCharges.d.amount }">
	<MvASSIGN NAME = "l.ordercharge:disp_amt"		VALUE = "{ OrderCharges.d.disp_amt }">
	<MvASSIGN NAME = "l.ordercharge:tax_exempt"		VALUE = "{ OrderCharges.d.tax_exempt }">
	<MvASSIGN NAME = "l.ordercharge:tax"			VALUE = "{ OrderCharges.d.tax }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderCharge_Insert" PARAMETERS = "ordercharge var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.ordercharge:charge_id"	VALUE = "{ StoreKey_Generate( 'BasketCharges' ) }">
	<MvASSIGN NAME = "l.ordercharge:descrip"	VALUE = "{ substring_var( l.ordercharge:descrip, 1, 100 ) }">

	<MvIF EXPR = "{ ISNULL l.ordercharge:tax }">
		<MvASSIGN NAME = "l.ordercharge:tax"	VALUE = 0.00>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'OrderCharges
					      ( order_id, charge_id, module_id, type, descrip, amount, disp_amt, tax_exempt, tax )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.ordercharge:order_id, l.ordercharge:charge_id, l.ordercharge:module_id,
					   l.ordercharge:type, l.ordercharge:descrip, l.ordercharge:amount,
					   l.ordercharge:disp_amt, l.ordercharge:tax_exempt, l.ordercharge:tax">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OCH-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderCharge_Update" PARAMETERS = "ordercharge var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.ordercharge:descrip" VALUE = "{ substring_var( l.ordercharge:descrip, 1, 100 ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderCharges
						  SET
							order_id	= ?,
							module_id	= ?,
							type		= ?,
							descrip		= ?,
							amount		= ?,
							disp_amt	= ?,
							tax_exempt	= ?,
							tax			= ?
						  WHERE
							charge_id	= ?' }"
			 FIELDS	= "l.ordercharge:order_id, l.ordercharge:module_id, l.ordercharge:type, l.ordercharge:descrip,
					   l.ordercharge:amount, l.ordercharge:disp_amt, l.ordercharge:tax_exempt, l.ordercharge:tax,
					   l.ordercharge:charge_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OCH-00010', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderCharge_Update_Amount" PARAMETERS = "ordercharge var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderCharges
					      SET
							amount		= ?,
					    	disp_amt	= ?
					      WHERE
						    charge_id	= ?' }"
			 FIELDS	= "l.ordercharge:amount,
					   l.ordercharge:disp_amt,
					   l.ordercharge:charge_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OCH-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderCharge_Update_Tax" PARAMETERS = "ordercharge var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderCharges
			 			  SET
			 				tax			= ?
			 			  WHERE
			 				charge_id	= ?' }"
			 FIELDS = "l.ordercharge:tax,
			 		   l.ordercharge:charge_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OCH-00014', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderChargeList_Load_Order" PARAMETERS = "order_id, ordercharges var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderCharges"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderCharges WHERE order_id = ? ORDER BY charge_id' }"
				FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OCH-00002', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.ordercharge_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderCharges.d.EOF }">
		<MvASSIGN NAME = "l.ordercharge_count" VALUE = "{ l.ordercharge_count + 1 }">
		<MvEVAL EXPR = "{ OrderCharge_Read( l.ordercharges[ l.ordercharge_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "OrderCharges" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderCharges">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OCH-00006', l.ordercharge_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderCharge_Load" PARAMETERS = "order_id, module_id, type, ordercharge var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderCharges"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderCharges WHERE order_id = ? AND module_id = ? AND type = ?' }"
				FIELDS	= "l.order_id, l.module_id, l.type">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OCH-00012', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ OrderCharges.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderCharges">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-OCH-00013' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ OrderCharge_Read( l.ordercharge ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderCharges">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderCharge_Total_Type" PARAMETERS = "order_id, type" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderCharges"
				QUERY	= "{ 'SELECT SUM( amount ) AS total FROM ' $ g.Store_Table_Prefix $ 'OrderCharges WHERE order_id = ? AND type = ?' }"
				FIELDS	= "l.order_id, l.type">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OCH-00007', g.MvOPENVIEW_Error ) }">
	</MvIF>
	
	<MvIF EXPR = "{ OrderCharges.d.EOF }">
		<MvASSIGN NAME = "l.total"	VALUE = 0.00>
	<MvELSE>
		<MvASSIGN NAME = "l.total"	VALUE = "{ OrderCharges.d.total ROUND 2 }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderCharges">
	<MvFUNCTIONRETURN VALUE = "{ l.total }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderChargeList_Load_Type" PARAMETERS = "order_id, type, ordercharges var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderCharges"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderCharges WHERE order_id = ? AND type = ? ORDER BY charge_id' }"
				FIELDS	= "l.order_id, l.type">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OCH-00003', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.ordercharge_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderCharges.d.EOF }">
		<MvASSIGN NAME = "l.ordercharge_count" VALUE = "{ l.ordercharge_count + 1 }">
		<MvEVAL EXPR = "{ OrderCharge_Read( l.ordercharges[ l.ordercharge_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "OrderCharges" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderCharges">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OCH-00008', l.ordercharge_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderCharge_Delete_All_Order" PARAMETERS = "order_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderCharges WHERE order_id = ?' }"
			 FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OCH-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderCharge_Delete_All_Type" PARAMETERS = "order_id, type" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderCharges WHERE order_id = ? AND type = ?' }"
			 FIELDS	= "l.order_id, l.type">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OCH-00009', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderCharge_Delete_Charge" PARAMETERS = "charge_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderCharges WHERE charge_id = ?' }"
			 FIELDS	= "l.charge_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OCH-00011', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

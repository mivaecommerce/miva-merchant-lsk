<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-SCY-
| Next Error Code: 12   
|
</MvCOMMENT>

<MvFUNCTION NAME = "StoreCountry_Read" PARAMETERS = "storecountry var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.storecountry:id"			VALUE = "{ StoreCountries.d.id }">
	<MvASSIGN NAME = "l.storecountry:name"			VALUE = "{ StoreCountries.d.name }">
	<MvASSIGN NAME = "l.storecountry:alpha"			VALUE = "{ StoreCountries.d.alpha }">
	<MvASSIGN NAME = "l.storecountry:iso_code"		VALUE = "{ StoreCountries.d.iso_code }">
	<MvASSIGN NAME = "l.storecountry:disp_order"	VALUE = "{ StoreCountries.d.disp_order }">
</MvFUNCTION>

<MvFUNCTION NAME = "StoreCountry_Insert" PARAMETERS = "storecountry var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.storecountry:disp_order"		VALUE = "{ StoreKey_Generate( 'StoreCountryDisplayOrder' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'StoreCountries
					      ( id, name, alpha, iso_code, disp_order )
						  VALUES
						  ( ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.storecountry:id, l.storecountry:name, l.storecountry:alpha, l.storecountry:iso_code,
					   l.storecountry:disp_order">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-SCY-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreCountry_Delete_All" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'StoreCountries' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-SCY-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreCountry_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'StoreCountries WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-SCY-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreCountryList_Load_All" PARAMETERS = "storecountries var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "StoreCountries"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'StoreCountries ORDER BY disp_order' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-SCY-00004', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.storecountry_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT StoreCountries.d.EOF }">
		<MvASSIGN NAME = "l.storecountry_count" VALUE = "{ l.storecountry_count + 1 }">
		<MvEVAL EXPR = "{ StoreCountry_Read( l.storecountries[ l.storecountry_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "StoreCountries" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "StoreCountries">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-SCY-00008', l.storecountry_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "StoreCountry_Load_ID" PARAMETERS = "id, storecountry var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "StoreCountries"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'StoreCountries WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-SCY-00005', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ StoreCountries.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "StoreCountries">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-SCY-00009' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ StoreCountry_Read( l.storecountry ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "StoreCountries">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreCountry_Load_Alpha" PARAMETERS = "alpha, storecountry var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "StoreCountries"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'StoreCountries WHERE alpha = ?' }"
				FIELDS	= "l.alpha">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-SCY-00006', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ StoreCountries.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "StoreCountries">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-SCY-00010' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ StoreCountry_Read( l.storecountry ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "StoreCountries">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreCountry_Load_ISO_Code" PARAMETERS = "iso_code, storecountry var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "StoreCountries"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'StoreCountries WHERE iso_code = ?' }"
				FIELDS	= "l.iso_code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-SCY-00007', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ StoreCountries.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "StoreCountries">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-SCY-00011' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ StoreCountry_Read( l.storecountry ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "StoreCountries">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

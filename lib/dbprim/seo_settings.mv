<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix		 : MER-DBP-SEO-
| Next Error Code: 6	
|
</MvCOMMENT>

<MvFUNCTION NAME = "SEOSettings_Create" PARAMETERS = "" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE SEOSettings
							(
								sf_active	' $ [ g.Library_Filename_Native_DBAPI ].DB_Type_BOOL() 		$ ',
								sl_active	' $ [ g.Library_Filename_Native_DBAPI ].DB_Type_BOOL() 		$ ',
								plit_in_bc  ' $ [ g.Library_Filename_Native_DBAPI ].DB_Type_BOOL() 		$ ',
								sc_always   ' $ [ g.Library_Filename_Native_DBAPI ].DB_Type_BOOL() 		$ ',
								url_delim	' $ [ g.Library_Filename_Native_DBAPI ].DB_Type_CHAR( 1 ) 	$ ',
								prod_lit	' $ [ g.Library_Filename_Native_DBAPI ].DB_Type_CHAR( 100 ) $ ',
								cat_lit		' $ [ g.Library_Filename_Native_DBAPI ].DB_Type_CHAR( 100 ) $ ',
								prefix		' $ [ g.Library_Filename_Native_DBAPI ].DB_Type_CHAR( 100 ) $ ',
								suffix		' $ [ g.Library_Filename_Native_DBAPI ].DB_Type_CHAR( 100 ) $ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-SEO-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.seo_settings" VALUE = "{ SEOSettings_Default_Values() }">
	<MvFUNCTIONRETURN VALUE = "{ SEOSettings_Insert( l.seo_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "SEOSettings_Default_Values" PARAMETERS = "" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.seo_settings:sf_active"			VALUE = 0>
	<MvASSIGN NAME = "l.seo_settings:sl_active"			VALUE = 0>
	<MvASSIGN NAME = "l.seo_settings:sm_active"			VALUE = 1>
	<MvASSIGN NAME = "l.seo_settings:plit_in_bc"		VALUE = 0>
	<MvASSIGN NAME = "l.seo_settings:sc_always"			VALUE = 0>
	<MvASSIGN NAME = "l.seo_settings:url_delim"			VALUE = "/">
	<MvASSIGN NAME = "l.seo_settings:prod_lit"			VALUE = "product">
	<MvASSIGN NAME = "l.seo_settings:cat_lit"			VALUE = "category">
	<MvASSIGN NAME = "l.seo_settings:prefix"			VALUE = "">
	<MvASSIGN NAME = "l.seo_settings:suffix"			VALUE = "html">
	<MvASSIGN NAME = "l.seo_settings:urlprefix"			VALUE = "">
	<MvASSIGN NAME = "l.seo_settings:surlprefix"		VALUE = "">

	<MvCOMMENT>The following are derived values</MvCOMMENT>
	<MvASSIGN NAME = "l.seo_settings:prod_lit_choice"   VALUE = "product">
	<MvASSIGN NAME = "l.seo_settings:cat_lit_choice"	VALUE = "category">
	<MvASSIGN NAME = "l.seo_settings:prefix_choice"		VALUE = "">
	<MvASSIGN NAME = "l.seo_settings:suffix_choice"		VALUE = "html">

	<MvFUNCTIONRETURN VALUE = "{ l.seo_settings }">
</MvFUNCTION>

<MvFUNCTION NAME = "SEOSettings_Read" PARAMETERS = "seo_settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.seo_settings:sf_active"				VALUE = "{ SEOSettings.d.sf_active }">
	<MvASSIGN NAME = "l.seo_settings:sl_active"				VALUE = "{ SEOSettings.d.sl_active }">
	<MvASSIGN NAME = "l.seo_settings:sm_active"				VALUE = 1>
	<MvASSIGN NAME = "l.seo_settings:plit_in_bc"			VALUE = "{ SEOSettings.d.plit_in_bc }">
	<MvASSIGN NAME = "l.seo_settings:sc_always"				VALUE = "{ SEOSettings.d.sc_always }">
	<MvASSIGN NAME = "l.seo_settings:url_delim"				VALUE = "{ SEOSettings.d.url_delim }">
	<MvASSIGN NAME = "l.seo_settings:prod_lit"				VALUE = "{ SEOSettings.d.prod_lit }">
	<MvASSIGN NAME = "l.seo_settings:cat_lit"				VALUE = "{ SEOSettings.d.cat_lit }">
	<MvASSIGN NAME = "l.seo_settings:prefix"				VALUE = "{ SEOSettings.d.prefix }">
	<MvASSIGN NAME = "l.seo_settings:suffix"				VALUE = "{ SEOSettings.d.suffix }">

	<MvCOMMENT>
	|
	| The following are derived values
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.seo_settings:prod_lit EQ 'product' }">
		<MvASSIGN NAME = "l.seo_settings:prod_lit_choice"	VALUE = "product">
	<MvELSEIF EXPR = "{ l.seo_settings:prod_lit EQ 'PROD' }">
		<MvASSIGN NAME = "l.seo_settings:prod_lit_choice"	VALUE = "PROD">
	<MvELSE>
		<MvASSIGN NAME = "l.seo_settings:prod_lit_choice"	VALUE = "Custom">
	</MvIF>
	<MvIF EXPR = "{ l.seo_settings:cat_lit EQ 'category' }">
		<MvASSIGN NAME = "l.seo_settings:cat_lit_choice"	VALUE = "category">
	<MvELSEIF EXPR = "{ l.seo_settings:cat_lit EQ 'CTGY' }">
		<MvASSIGN NAME = "l.seo_settings:cat_lit_choice"	VALUE = "CTGY">
	<MvELSE>
		<MvASSIGN NAME = "l.seo_settings:cat_lit_choice"	VALUE = "Custom">
	</MvIF>
	<MvIF EXPR = "{ l.seo_settings:prefix EQ '' }">
		<MvASSIGN NAME = "l.seo_settings:prefix_choice"		VALUE = "">
	<MvELSEIF EXPR = "{ l.seo_settings:prefix EQ 'page' }">
		<MvASSIGN NAME = "l.seo_settings:prefix_choice"		VALUE = "page">
	<MvELSE>
		<MvASSIGN NAME = "l.seo_settings:prefix_choice"		VALUE = "Custom">
	</MvIF>
	<MvIF EXPR = "{ l.seo_settings:suffix EQ 'html' }">
		<MvASSIGN NAME = "l.seo_settings:suffix_choice"		VALUE = "html">
	<MvELSEIF EXPR = "{ l.seo_settings:suffix EQ '' }">
		<MvASSIGN NAME = "l.seo_settings:suffix_choice"		VALUE = "">
	<MvELSE>
		<MvASSIGN NAME = "l.seo_settings:suffix_choice"		VALUE = "Custom">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "SEOSettings_Load_Cached" PARAMETERS = "seo_settings var" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCE NAME = "l.cache"						VARIABLE = "g.Session:cache:seo_settings_load_cached">

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ SEOSettings_Load( l.cache:seo_settings ) }">
		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.seo_settings"					VALUE = "{ l.cache:seo_settings }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "SEOSettings_Load" PARAMETERS = "seo_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT SEOSettings_Load_LowLevel( l.seo_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| As of 9.4, many of the Legacy SEO settings are emulated/pulled from URI settings
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_URI_DB ].URISettings_Load_Cached( l.urisettings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.urisettings:source NE 'legacy_seo' }">
		<MvASSIGN NAME = "l.seo_settings:sf_active"	VALUE = 0>
		<MvASSIGN NAME = "l.seo_settings:sl_active"	VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.seo_settings:urlprefix"		VALUE = "{ l.urisettings:urlprefix }">
	<MvASSIGN NAME = "l.seo_settings:surlprefix"	VALUE = "{ l.urisettings:surlprefix }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SEOSettings_Load_LowLevel" PARAMETERS = "seo_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "SEOSettings"
				QUERY	= "SELECT * FROM SEOSettings">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-SEO-00001', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvEVAL EXPR = "{ SEOSettings_Read( l.seo_settings ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SEOSettings">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SEOSettings_Insert" PARAMETERS = "seo_settings" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ SEOSettings_Normalize( l.seo_settings ) }">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "INSERT INTO SEOSettings
					   ( sf_active, sl_active, plit_in_bc, sc_always, url_delim, prod_lit, cat_lit, prefix, suffix )
					   VALUES
					   ( ?, ?, ?, ?, ?, ?, ?, ?, ? )"
			 FIELDS	= "l.seo_settings:sf_active, l.seo_settings:sl_active, l.seo_settings:plit_in_bc,
					   l.seo_settings:sc_always, l.seo_settings:url_delim, l.seo_settings:prod_lit,
					   l.seo_settings:cat_lit, l.seo_settings:prefix, l.seo_settings:suffix">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-SEO-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SEOSettings_Update" PARAMETERS = "seo_settings" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ SEOSettings_Normalize( l.seo_settings ) }">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "UPDATE SEOSettings
					   SET
						sf_active   = ?,
						sl_active   = ?,
						plit_in_bc  = ?,
						sc_always   = ?,
						url_delim   = ?,
						prod_lit	= ?,
						cat_lit		= ?,
						prefix		= ?,
						suffix		= ?"
			 FIELDS	= "l.seo_settings:sf_active, l.seo_settings:sl_active, l.seo_settings:plit_in_bc,
					   l.seo_settings:sc_always, l.seo_settings:url_delim, l.seo_settings:prod_lit,
					   l.seo_settings:cat_lit, l.seo_settings:prefix, l.seo_settings:suffix">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-SEO-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SEOSettings_Normalize" PARAMETERS = "seo_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.seo_settings:prod_lit_choice EQ 'product' }">
		<MvASSIGN NAME = "l.seo_settings:prod_lit"	VALUE = "product">
	<MvELSEIF EXPR = "{ l.seo_settings:prod_lit_choice EQ 'PROD' }">
		<MvASSIGN NAME = "l.seo_settings:prod_lit"	VALUE = "PROD">
	</MvIF>
	<MvIF EXPR = "{ l.seo_settings:cat_lit_choice EQ 'category' }">
		<MvASSIGN NAME = "l.seo_settings:cat_lit"	VALUE = "category">
	<MvELSEIF EXPR = "{ l.seo_settings:cat_lit_choice EQ 'CTGY' }">
		<MvASSIGN NAME = "l.seo_settings:cat_lit"	VALUE = "CTGY">
	</MvIF>
	<MvIF EXPR = "{ l.seo_settings:prefix_choice EQ '' }">
		<MvASSIGN NAME = "l.seo_settings:prefix"	VALUE = "">
	<MvELSEIF EXPR = "{ l.seo_settings:prefix_choice EQ 'page' }">
		<MvASSIGN NAME = "l.seo_settings:prefix"	VALUE = "page">
	</MvIF>
	<MvIF EXPR = "{ l.seo_settings:suffix_choice EQ 'html' }">
		<MvASSIGN NAME = "l.seo_settings:suffix"	VALUE = "html">
	<MvELSEIF EXPR = "{ l.seo_settings:suffix_choice EQ '' }">
		<MvASSIGN NAME = "l.seo_settings:suffix"	VALUE = "">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SEOSettings_Sample_Product" PARAMETERS = "store_code, product_code var, category_code var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Admin_Open_Store( l.store_code, g.Store ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "SEOSample"
				QUERY	= "{ 'SELECT 
								prod.code AS pcode,
								cat.code AS ccode
							  FROM
								' $ g.Store_Table_Prefix $ 'Products prod,
								' $ g.Store_Table_Prefix $ 'Categories cat,
								' $ g.Store_Table_Prefix $ 'CategoryXProduct
							  WHERE prod.id = product_id and cat.id = cat_id' }"
				FIELDS  = "">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-SEO-00004', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.product_code"		VALUE = "{ SEOSample.d.pcode }">
	<MvASSIGN NAME = "l.category_code"		VALUE = "{ SEOSample.d.ccode }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SEOSample">
	<MvASSIGN NAME = "l.null"				VALUE = "{ [ g.Module_Admin ].Admin_Close_Store() }">

	<MvIF EXPR = "{ l.product_code EQ '' }">
		<MvASSIGN NAME = "l.product_code"	VALUE = "PRODX">
	</MvIF>
	<MvIF EXPR = "{ l.category_code EQ '' }">
		<MvASSIGN NAME = "l.category_code"	VALUE = "CATY">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

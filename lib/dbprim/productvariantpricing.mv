<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-PVPR-
| Next Error Code: 8    
|
</MvCOMMENT>

<MvFUNCTION NAME = "ProductVariantPricing_Read" PARAMETERS = "productvariantpricing var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.productvariantpricing:product_id"	VALUE = "{ ProductVariantPricing.d.product_id }">
	<MvASSIGN NAME = "l.productvariantpricing:variant_id"	VALUE = "{ ProductVariantPricing.d.variant_id }">
	<MvASSIGN NAME = "l.productvariantpricing:method"		VALUE = "{ ProductVariantPricing.d.method }">
	<MvASSIGN NAME = "l.productvariantpricing:price"		VALUE = "{ ProductVariantPricing.d.price }">
	<MvASSIGN NAME = "l.productvariantpricing:cost"			VALUE = "{ ProductVariantPricing.d.cost }">
	<MvASSIGN NAME = "l.productvariantpricing:weight"		VALUE = "{ ProductVariantPricing.d.weight }">
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantPricing_Load_Variant" PARAMETERS = "product_id, variant_id, productvariantpricing var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ProductVariantPricing"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'ProductVariantPricing WHERE product_id = ? AND variant_id = ?' }"
				FIELDS	= "l.product_id, l.variant_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVPR-00001', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ ProductVariantPricing.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "ProductVariantPricing">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-PVPR-00002' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ ProductVariantPricing_Read( l.productvariantpricing ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ProductVariantPricing">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantPricing_Insert" PARAMETERS = "productvariantpricing var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'ProductVariantPricing
						  ( product_id, variant_id, method, price, cost, weight )
						  VALUES
						  ( ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.productvariantpricing:product_id, l.productvariantpricing:variant_id,
					   l.productvariantpricing:method, l.productvariantpricing:price,
					   l.productvariantpricing:cost, l.productvariantpricing:weight">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVPR-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantPricing_Update" PARAMETERS = "productvariantpricing var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'ProductVariantPricing
						  SET
							method		= ?,
							price		= ?,
							cost		= ?,
							weight		= ?
						  WHERE
							product_id	= ? AND
							variant_id	= ?' }"
			 FIELDS	= "l.productvariantpricing:method, l.productvariantpricing:price,
					   l.productvariantpricing:cost, l.productvariantpricing:weight,
					   l.productvariantpricing:product_id, l.productvariantpricing:variant_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVPR-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantPricing_Delete_All_Product" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ProductVariantPricing WHERE product_id = ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVPR-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantPricing_Delete_All_Product_NotDefault" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ProductVariantPricing WHERE product_id = ? AND variant_id <> 0' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVPR-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantPricing_Delete_Variant" PARAMETERS = "product_id, variant_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ProductVariantPricing WHERE product_id = ? AND variant_id = ?' }"
			 FIELDS	= "l.product_id, l.variant_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVPR-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-OPT-
| Next Error Code: 13   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Option_Read" PARAMETERS = "option var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.option:id"			VALUE = "{ Options.d.id }">
	<MvASSIGN NAME = "l.option:product_id"	VALUE = "{ Options.d.product_id }">
	<MvASSIGN NAME = "l.option:attr_id"		VALUE = "{ Options.d.attr_id }">
	<MvASSIGN NAME = "l.option:disp_order"	VALUE = "{ Options.d.disp_order }">
	<MvASSIGN NAME = "l.option:code"		VALUE = "{ Options.d.code }">
	<MvASSIGN NAME = "l.option:prompt"		VALUE = "{ Options.d.prompt }">
	<MvASSIGN NAME = "l.option:price"		VALUE = "{ Options.d.price }">
	<MvASSIGN NAME = "l.option:cost"		VALUE = "{ Options.d.cost }">
	<MvASSIGN NAME = "l.option:weight"		VALUE = "{ Options.d.weight }">
	<MvASSIGN NAME = "l.option:image"		VALUE = "{ Options.d.image }">
</MvFUNCTION>

<MvFUNCTION NAME = "Option_Insert_LowLevel" PARAMETERS = "option var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.option:id"			VALUE = "{ StoreKey_Generate( 'Options' ) }">
	<MvASSIGN NAME = "l.option:disp_order"	VALUE = "{ StoreKey_Generate( 'ProductAttributeOptionDisplayOrder' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'Options
					      ( id, product_id, attr_id, disp_order, code, prompt, price, cost, weight, image )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.option:id, l.option:product_id, l.option:attr_id, l.option:disp_order, l.option:code, l.option:prompt,
					   l.option:price, l.option:cost, l.option:weight, l.option:image">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OPT-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Option_Update_LowLevel" PARAMETERS = "option var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Options
					      SET
							code		= ?,
							prompt		= ?,
							price		= ?,
							cost		= ?,
							weight		= ?,
							image		= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.option:code, l.option:prompt, l.option:price,
					   l.option:cost, l.option:weight, l.option:image,
					   l.option:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OPT-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Option_Update_DisplayOrder" PARAMETERS = "option var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Options
					      SET
							disp_order	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.option:disp_order,
					   l.option:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OPT-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Option_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'Options WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OPT-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Option_Delete_All_Attribute" PARAMETERS = "attr_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'Options WHERE attr_id = ?' }"
			 FIELDS	= "l.attr_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OPT-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Option_Delete_All_Product" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'Options WHERE product_id = ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OPT-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Option_Load_ID" PARAMETERS = "id, option var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Options"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Options WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OPT-00007', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Options.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Options">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-OPT-00010' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Option_Read( l.option ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Options">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Option_Load_Code" PARAMETERS = "attr_id, code, option var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Options"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Options WHERE attr_id = ? AND ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.attr_id, l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OPT-00008', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Options.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Options">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-OPT-00011' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Option_Read( l.option ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Options">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OptionList_Load_Attribute" PARAMETERS = "attr_id, options var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Options"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Options WHERE attr_id = ? ORDER BY disp_order' }"
				FIELDS	= "l.attr_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OPT-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.option_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Options.d.EOF }">
		<MvASSIGN NAME = "l.option_count" VALUE = "{ l.option_count + 1 }">
		<MvEVAL EXPR = "{ Option_Read( l.options[ l.option_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Options" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Options">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OPT-00012', l.option_count ) }">
</MvFUNCTION>

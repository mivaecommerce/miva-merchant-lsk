<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-PVN-
| Next Error Code: 19   
|
</MvCOMMENT>

<MvFUNCTION NAME = "ProductVariant_Read" PARAMETERS = "productvariant var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.productvariant:product_id"	VALUE = "{ ProductVariants.d.product_id }">
	<MvASSIGN NAME = "l.productvariant:variant_id"	VALUE = "{ ProductVariants.d.variant_id }">
	<MvASSIGN NAME = "l.productvariant:dimensions"	VALUE = "{ ProductVariants.d.dimensions }">
	<MvASSIGN NAME = "l.productvariant:part_count"	VALUE = "{ ProductVariants.d.part_count }">
	<MvASSIGN NAME = "l.productvariant:attr_id"		VALUE = "{ ProductVariants.d.attr_id }">
	<MvASSIGN NAME = "l.productvariant:attmpat_id"	VALUE = "{ ProductVariants.d.attmpat_id }">
	<MvASSIGN NAME = "l.productvariant:option_id"	VALUE = "{ ProductVariants.d.option_id }">
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantList_Load_Part" PARAMETERS = "part_id, productvariants var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ProductVariants"
				QUERY	= "{ 'SELECT DISTINCT
								pv.product_id, pv.variant_id, pv.dimensions, pv.part_count
							  FROM
								' $ g.Store_Table_Prefix $ 'ProductVariantParts pvp,
								' $ g.Store_Table_Prefix $ 'ProductVariants pv
							  WHERE
								pvp.part_id		= ?					AND
								pv.product_id	= pvp.product_id	AND
								pv.variant_id	= pvp.variant_id' }"
				FIELDS	= "l.part_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVN-00001', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.productvariant_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT ProductVariants.d.EOF }">
		<MvASSIGN NAME = "l.productvariant_count" VALUE = "{ l.productvariant_count + 1 }">
		<MvEVAL EXPR = "{ ProductVariant_Read( l.productvariants[ l.productvariant_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "ProductVariants" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ProductVariants">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-PVN-00002', l.productvariant_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantList_Load_Product_Attribute" PARAMETERS = "product_id, attr_id, productvariants var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ProductVariants"
				QUERY	= "{ 'SELECT DISTINCT
								product_id, variant_id, dimensions, part_count
							  FROM
								' $ g.Store_Table_Prefix $ 'ProductVariants
							  WHERE
								product_id		= ?	AND
								attr_id			= ?' }"
				FIELDS	= "l.product_id, l.attr_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVN-00003', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.productvariant_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT ProductVariants.d.EOF }">
		<MvASSIGN NAME = "l.productvariant_count" VALUE = "{ l.productvariant_count + 1 }">
		<MvEVAL EXPR = "{ ProductVariant_Read( l.productvariants[ l.productvariant_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "ProductVariants" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ProductVariants">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-PVN-00004', l.productvariant_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantList_Load_Product_Option" PARAMETERS = "product_id, attr_id, option_id, productvariants var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ProductVariants"
				QUERY	= "{ 'SELECT DISTINCT
								product_id, variant_id, dimensions, part_count
							  FROM
								' $ g.Store_Table_Prefix $ 'ProductVariants
							  WHERE
								product_id		= ?	AND
								attr_id			= ?	AND
								option_id		= ?' }"
				FIELDS	= "l.product_id, l.attr_id, l.option_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVN-00005', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.productvariant_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT ProductVariants.d.EOF }">
		<MvASSIGN NAME = "l.productvariant_count" VALUE = "{ l.productvariant_count + 1 }">
		<MvEVAL EXPR = "{ ProductVariant_Read( l.productvariants[ l.productvariant_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "ProductVariants" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ProductVariants">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-PVN-00006', l.productvariant_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantList_Load_AttributeTemplateAttribute" PARAMETERS = "attmpat_id, productvariants var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ProductVariants"
				QUERY	= "{ 'SELECT DISTINCT
								product_id, variant_id, dimensions, part_count
							  FROM
								' $ g.Store_Table_Prefix $ 'ProductVariants
							  WHERE
								attmpat_id	= ?' }"
				FIELDS	= "l.attmpat_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVN-00007', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.productvariant_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT ProductVariants.d.EOF }">
		<MvASSIGN NAME = "l.productvariant_count" VALUE = "{ l.productvariant_count + 1 }">
		<MvEVAL EXPR = "{ ProductVariant_Read( l.productvariants[ l.productvariant_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "ProductVariants" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ProductVariants">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-PVN-00008', l.productvariant_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantList_Load_AttributeTemplateOption" PARAMETERS = "attmpat_id, option_id, productvariants var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ProductVariants"
				QUERY	= "{ 'SELECT DISTINCT
								product_id, variant_id, dimensions, part_count
							  FROM
								' $ g.Store_Table_Prefix $ 'ProductVariants
							  WHERE
								attmpat_id	= ?	AND
								option_id	= ?' }"
				FIELDS	= "l.attmpat_id, l.option_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVN-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.productvariant_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT ProductVariants.d.EOF }">
		<MvASSIGN NAME = "l.productvariant_count" VALUE = "{ l.productvariant_count + 1 }">
		<MvEVAL EXPR = "{ ProductVariant_Read( l.productvariants[ l.productvariant_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "ProductVariants" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ProductVariants">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-PVN-00010', l.productvariant_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariantList_Load_Variant" PARAMETERS = "product_id, variant_id, productvariants var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ProductVariants"
				QUERY	= "{ 'SELECT
								*
							  FROM
								' $ g.Store_Table_Prefix $ 'ProductVariants
							  WHERE
								product_id	= ? AND
								variant_id	= ?' }"
				FIELDS	= "l.product_id, l.variant_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVN-00017', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.productvariant_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT ProductVariants.d.EOF }">
		<MvEVAL EXPR = "{ ProductVariant_Read( l.productvariants[ ++l.productvariant_count ] ) }">

		<MvASSIGN NAME = "l.productvariants" INDEX = "{ l.productvariant_count }" MEMBER = "attr_id"	VALUE = "{ ProductVariants.d.attr_id }">
		<MvASSIGN NAME = "l.productvariants" INDEX = "{ l.productvariant_count }" MEMBER = "attmpat_id"	VALUE = "{ ProductVariants.d.attmpat_id }">
		<MvASSIGN NAME = "l.productvariants" INDEX = "{ l.productvariant_count }" MEMBER = "option_id"	VALUE = "{ ProductVariants.d.option_id }">

		<MvSKIP NAME = "Merchant" VIEW = "ProductVariants" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ProductVariants">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-PVN-00018', l.productvariant_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariant_Insert_LowLevel" PARAMETERS = "productvariant var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'ProductVariants
						  ( product_id, variant_id, dimensions, part_count, attr_id, attmpat_id, option_id )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.productvariant:product_id, l.productvariant:variant_id, l.productvariant:dimensions,
					   l.productvariant:part_count, l.productvariant:attr_id, l.productvariant:attmpat_id,
					   l.productvariant:option_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVN-00011', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariant_Update_Part_Count" PARAMETERS = "product_id, variant_id, part_count" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'ProductVariants
						  SET
							part_count	= ?
						  WHERE
							product_id	= ?	AND
							variant_id	= ?' }"
			 FIELDS	= "l.part_count,
					   l.product_id, l.variant_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVN-00016', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariant_Decrement_Part_Count" PARAMETERS = "product_id, variant_id, decrement" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'ProductVariants
						  SET
							part_count	= part_count - ?
						  WHERE
							product_id	= ?	AND
							variant_id	= ?' }"
			 FIELDS	= "l.decrement,
					   l.product_id, l.variant_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVN-00012', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariant_Delete_All_Product_LowLevel" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ProductVariants WHERE product_id = ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVN-00013', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariant_Delete_All_Product_NotDefault_LowLevel" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ProductVariants WHERE product_id = ? AND variant_id <> 0' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVN-00015', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductVariant_Delete_LowLevel" PARAMETERS = "product_id, variant_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ProductVariants WHERE product_id = ? AND variant_id = ?' }"
			 FIELDS	= "l.product_id, l.variant_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-PVN-00014', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

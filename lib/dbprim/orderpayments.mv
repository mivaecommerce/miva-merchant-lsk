<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-OPY-
| Next Error Code: 19   
|
</MvCOMMENT>

<MvFUNCTION NAME = "OrderPayment_Read" PARAMETERS = "orderpayment var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.orderpayment:id"				VALUE = "{ OrderPayments.d.id }">
	<MvASSIGN NAME = "l.orderpayment:order_id"			VALUE = "{ OrderPayments.d.order_id }">
	<MvASSIGN NAME = "l.orderpayment:parent_id"			VALUE = "{ OrderPayments.d.parent_id }">
	<MvASSIGN NAME = "l.orderpayment:type"				VALUE = "{ OrderPayments.d.type }">
	<MvASSIGN NAME = "l.orderpayment:original_type"		VALUE = "{ l.orderpayment:type }">
	<MvASSIGN NAME = "l.orderpayment:refnum"			VALUE = "{ OrderPayments.d.refnum }">
	<MvASSIGN NAME = "l.orderpayment:amount"			VALUE = "{ OrderPayments.d.amount }">
	<MvASSIGN NAME = "l.orderpayment:original_amount"	VALUE = "{ l.orderpayment:amount }">
	<MvASSIGN NAME = "l.orderpayment:available"			VALUE = "{ OrderPayments.d.available }">
	<MvASSIGN NAME = "l.orderpayment:dtstamp"			VALUE = "{ OrderPayments.d.dtstamp }">
	<MvASSIGN NAME = "l.orderpayment:expires"			VALUE = "{ OrderPayments.d.expires }">
	<MvASSIGN NAME = "l.orderpayment:ip"				VALUE = "{ OrderPayments.d.ip }">
	<MvASSIGN NAME = "l.orderpayment:pay_id"			VALUE = "{ OrderPayments.d.pay_id }">
	<MvASSIGN NAME = "l.orderpayment:pay_data"			VALUE = "{ OrderPayments.d.pay_data }">
	<MvASSIGN NAME = "l.orderpayment:pay_secid"			VALUE = "{ OrderPayments.d.pay_secid }">
	<MvASSIGN NAME = "l.orderpayment:pay_seckey"		VALUE = "{ OrderPayments.d.pay_seckey }">
	<MvASSIGN NAME = "l.orderpayment:pay_secdat"		VALUE = "{ OrderPayments.d.pay_secdat }">
	<MvASSIGN NAME = "l.orderpayment:currency"			VALUE = "{ OrderPayments.d.currency }">
	<MvASSIGN NAME = "l.orderpayment:environ"			VALUE = "{ OrderPayments.d.environ }">
	<MvASSIGN NAME = "l.orderpayment:level"				VALUE = "{ OrderPayments.d.level }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderPayment_Update_LowLevel" PARAMETERS = "orderpayment var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderPayments
						  SET
							order_id	= ?,
							parent_id	= ?,
							type		= ?,
							refnum		= ?,
							amount		= ?,
							available	= ?,
							dtstamp		= ?,
							expires		= ?,
							pay_id		= ?,
							pay_data	= ?,
							pay_secid	= ?,
							pay_seckey	= ?,
							pay_secdat	= ?,
							currency	= ?,
							environ		= ?,
							level		= ?
						  WHERE 
							id			= ?' }"
			 FIELDS	= "l.orderpayment:order_id, l.orderpayment:parent_id, l.orderpayment:type, l.orderpayment:refnum, l.orderpayment:amount,
					   l.orderpayment:available, l.orderpayment:dtstamp, l.orderpayment:expires, l.orderpayment:pay_id, l.orderpayment:pay_data,
					   l.orderpayment:pay_secid, l.orderpayment:pay_seckey, l.orderpayment:pay_secdat, l.orderpayment:currency, l.orderpayment:environ,
					   l.orderpayment:level,
					   l.orderpayment:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OPY-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderPayment_Load_ID" PARAMETERS = "id, orderpayment var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderPayments"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderPayments WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OPY-00003', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ OrderPayments.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderPayments">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-OPY-00004' ) }">
	</MvIF>
	
	<MvEVAL EXPR = "{ OrderPayment_Read( l.orderpayment ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderPayments">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderPaymentList_Load_Order" PARAMETERS = "order_id, orderpayments var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderPayments"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderPayments WHERE order_id = ? ORDER BY id' }"
				FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OPY-00005', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderpayment_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderPayments.d.EOF }">
		<MvASSIGN NAME = "l.orderpayment_count" VALUE = "{ l.orderpayment_count + 1 }">
		<MvEVAL EXPR = "{ OrderPayment_Read( l.orderpayments[ l.orderpayment_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "OrderPayments" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderPayments">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OPY-00006', l.orderpayment_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderPaymentList_Load_Order_Authorization" PARAMETERS = "order_id, orderpayments var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderPayments"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderPayments WHERE order_id = ? AND type IN ( 1, 3, 5 ) ORDER BY id' }"
				FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OPY-00007', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderpayment_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderPayments.d.EOF }">
		<MvASSIGN NAME = "l.orderpayment_count" VALUE = "{ l.orderpayment_count + 1 }">
		<MvEVAL EXPR = "{ OrderPayment_Read( l.orderpayments[ l.orderpayment_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "OrderPayments" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderPayments">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OPY-00008', l.orderpayment_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderPaymentList_Load_Order_Module_Authorization" PARAMETERS = "order_id, module_id, orderpayments var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderPayments"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderPayments WHERE order_id = ? AND pay_id = ? AND type IN ( 1, 3, 5 ) ORDER BY id' }"
				FIELDS	= "l.order_id, l.module_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OPY-00012', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderpayment_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderPayments.d.EOF }">
		<MvASSIGN NAME = "l.orderpayment_count" VALUE = "{ l.orderpayment_count + 1 }">
		<MvEVAL EXPR = "{ OrderPayment_Read( l.orderpayments[ l.orderpayment_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "OrderPayments" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderPayments">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OPY-00013', l.orderpayment_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderPaymentList_Load_Order_Capture" PARAMETERS = "order_id, orderpayments var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderPayments"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderPayments WHERE order_id = ? AND type IN ( 2, 4, 5 ) ORDER BY id' }"
				FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OPY-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderpayment_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderPayments.d.EOF }">
		<MvASSIGN NAME = "l.orderpayment_count" VALUE = "{ l.orderpayment_count + 1 }">
		<MvEVAL EXPR = "{ OrderPayment_Read( l.orderpayments[ l.orderpayment_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "OrderPayments" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderPayments">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OPY-00010', l.orderpayment_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderPaymentList_Load_Order_Module_Capture" PARAMETERS = "order_id, module_id, orderpayments var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderPayments"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderPayments WHERE order_id = ? AND pay_id = ? AND type IN ( 2, 4, 5 ) ORDER BY id' }"
				FIELDS	= "l.order_id, l.module_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OPY-00017', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderpayment_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderPayments.d.EOF }">
		<MvEVAL EXPR = "{ OrderPayment_Read( l.orderpayments[ ++l.orderpayment_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "OrderPayments" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderPayments">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OPY-00018', l.orderpayment_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderPayment_Update_Amounts" PARAMETERS = "orderpayment var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderPayments
						  SET
							amount		= ?,
							available	= ?
						  WHERE
							id			= ?' }"
			 FIELDS	= "l.orderpayment:amount, l.orderpayment:available,
					   l.orderpayment:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OPY-00011', g.MvQUERY_Error ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_OrderPayment( 'MER-DBP-OPY-00016', 'OrderPayment_Update', l.orderpayment ) }">

	<MvASSIGN NAME = "l.orderpayment:original_amount" VALUE = "{ l.orderpayment:amount }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderPayment_Load_RefNum_Module" PARAMETERS = "refnum, module_id, orderpayments var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "OrderPayments"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderPayments WHERE refnum = ? AND pay_id = ?' }"
				FIELDS	= "l.refnum, l.module_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OPY-00014', g.MvOPENVIEW_Error ) }">
	</MvIF>

	 <MvASSIGN NAME = "l.orderpayment_count" VALUE = 0>

	 <MvWHILE EXPR = "{ NOT OrderPayments.d.EOF }">
		 <MvEVAL EXPR = "{ OrderPayment_Read( l.orderpayments[ ++l.orderpayment_count ] ) }">
		 
		 <MvSKIP NAME = "Merchant" VIEW = "OrderPayments" ROWS = 1>
	 </MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderPayments">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OPY-00015', l.orderpayment_count ) }">
</MvFUNCTION>

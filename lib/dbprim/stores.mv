<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2025 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-STR-
| Next Error Code: 27   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Store_Read" PARAMETERS = "store var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.store:id"			VALUE = "{ Stores.d.id }">
	<MvASSIGN NAME = "l.store:manager_id"	VALUE = "{ Stores.d.manager_id }">
	<MvASSIGN NAME = "l.store:code"			VALUE = "{ Stores.d.code }">
	<MvASSIGN NAME = "l.store:license"		VALUE = "{ Stores.d.license }">
	<MvASSIGN NAME = "l.store:name"			VALUE = "{ Stores.d.name }">
	<MvASSIGN NAME = "l.store:icon"			VALUE = "{ Stores.d.icon }">
	<MvASSIGN NAME = "l.store:owner"		VALUE = "{ Stores.d.owner }">
	<MvASSIGN NAME = "l.store:email"		VALUE = "{ Stores.d.email }">
	<MvASSIGN NAME = "l.store:company"		VALUE = "{ Stores.d.company }">
	<MvASSIGN NAME = "l.store:address"		VALUE = "{ Stores.d.address }">
	<MvASSIGN NAME = "l.store:city"			VALUE = "{ Stores.d.city }">
	<MvASSIGN NAME = "l.store:state"		VALUE = "{ Stores.d.state }">
	<MvASSIGN NAME = "l.store:zip"			VALUE = "{ Stores.d.zip }">
	<MvASSIGN NAME = "l.store:phone"		VALUE = "{ Stores.d.phone }">
	<MvASSIGN NAME = "l.store:fax"			VALUE = "{ Stores.d.fax }">
	<MvASSIGN NAME = "l.store:country"		VALUE = "{ Stores.d.country }">
	<MvASSIGN NAME = "l.store:wtunits"		VALUE = "{ Stores.d.wtunits }">
	<MvASSIGN NAME = "l.store:wtunitcode"	VALUE = "{ Stores.d.wtunitcode }">
	<MvASSIGN NAME = "l.store:wtdispmix"	VALUE = "{ Stores.d.wtdispmix }">
	<MvASSIGN NAME = "l.store:wtdisplow"	VALUE = "{ Stores.d.wtdisplow }">
	<MvASSIGN NAME = "l.store:wtdispdig"	VALUE = "{ Stores.d.wtdispdig }">
	<MvASSIGN NAME = "l.store:dmunitcode"	VALUE = "{ Stores.d.dmunitcode }">
	<MvASSIGN NAME = "l.store:baskexp"		VALUE = "{ Stores.d.baskexp }">
	<MvASSIGN NAME = "l.store:deferbask"	VALUE = "{ Stores.d.deferbask }">
	<MvASSIGN NAME = "l.store:trackhits"	VALUE = "{ Stores.d.trackhits }">
	<MvASSIGN NAME = "l.store:pgrp_ovlp"	VALUE = "{ Stores.d.pgrp_ovlp }">
	<MvASSIGN NAME = "l.store:ui_id"		VALUE = "{ Stores.d.ui_id }">
	<MvASSIGN NAME = "l.store:tax_id"		VALUE = "{ Stores.d.tax_id }">
	<MvASSIGN NAME = "l.store:currncy_id"	VALUE = "{ Stores.d.currncy_id }">
	<MvASSIGN NAME = "l.store:boxpack_id"	VALUE = "{ Stores.d.boxpack_id }">
	<MvASSIGN NAME = "l.store:addrval_id"	VALUE = "{ Stores.d.addrval_id }">
	<MvASSIGN NAME = "l.store:mnt_warn"		VALUE = "{ Stores.d.mnt_warn }">
	<MvASSIGN NAME = "l.store:mnt_close"	VALUE = "{ Stores.d.mnt_close }">
	<MvASSIGN NAME = "l.store:mnt_time"		VALUE = "{ Stores.d.mnt_time }">
	<MvASSIGN NAME = "l.store:mnt_no_new"	VALUE = "{ Stores.d.mnt_no_new }">
	<MvASSIGN NAME = "l.store:mnt_ips"		VALUE = "{ Stores.d.mnt_ips }">
	<MvASSIGN NAME = "l.store:omin_quant"	VALUE = "{ Stores.d.omin_quant }">
	<MvASSIGN NAME = "l.store:omin_price"	VALUE = "{ Stores.d.omin_price }">
	<MvASSIGN NAME = "l.store:omin_all"		VALUE = "{ Stores.d.omin_all }">
	<MvASSIGN NAME = "l.store:omin_msg"		VALUE = "{ Stores.d.omin_msg }">
	<MvASSIGN NAME = "l.store:crypt_id"		VALUE = "{ Stores.d.crypt_id }">
	<MvASSIGN NAME = "l.store:req_ship"		VALUE = "{ Stores.d.req_ship }">
	<MvASSIGN NAME = "l.store:req_tax"		VALUE = "{ Stores.d.req_tax }">
	<MvASSIGN NAME = "l.store:req_frship"	VALUE = "{ Stores.d.req_frship }">
	<MvASSIGN NAME = "l.store:branch_id"	VALUE = "{ Stores.d.branch_id }">
	<MvASSIGN NAME = "l.store:charset"		VALUE = "{ Stores.d.charset }">
	<MvASSIGN NAME = "l.store:schtsk_adv"	VALUE = "{ Stores.d.schtsk_adv }">
	<MvASSIGN NAME = "l.store:schtsk_min"	VALUE = "{ Stores.d.schtsk_min }">
	<MvASSIGN NAME = "l.store:schtsk_que"	VALUE = "{ Stores.d.schtsk_que }">
	<MvASSIGN NAME = "l.store:cache_exp"	VALUE = "{ Stores.d.cache_exp }">
	<MvASSIGN NAME = "l.store:cache_ver"	VALUE = "{ Stores.d.cache_ver }">
	<MvASSIGN NAME = "l.store:cache_type"	VALUE = "{ Stores.d.cache_type }">
	<MvASSIGN NAME = "l.store:cache_comp"	VALUE = "{ Stores.d.cache_comp }">
	<MvASSIGN NAME = "l.store:cache_ridx"	VALUE = "{ Stores.d.cache_ridx }">
	<MvASSIGN NAME = "l.store:redishost"	VALUE = "{ Stores.d.redishost }">
	<MvASSIGN NAME = "l.store:redisport"	VALUE = "{ Stores.d.redisport }">
	<MvASSIGN NAME = "l.store:redisto"		VALUE = "{ Stores.d.redisto }">
	<MvASSIGN NAME = "l.store:redispwd"		VALUE = "{ Stores.d.redispwd }">
	<MvASSIGN NAME = "l.store:redisex"		VALUE = "{ Stores.d.redisex }">
	<MvASSIGN NAME = "l.store:cacheset"		VALUE = "{ miva_array_deserialize( Stores.d.cacheset ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Store_Insert" PARAMETERS = "store var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.store:id"				VALUE = "{ DomainKey_Generate( 'Stores' ) }">
	<MvASSIGN NAME = "l.cacheset"				VALUE = "{ miva_array_serialize( l.store:cacheset ) }">

	<MvIF EXPR = "{ ISNULL l.store:branch_id }">
		<MvASSIGN NAME = "l.store:branch_id"	VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.store:schtsk_que }">
		<MvASSIGN NAME = "l.store:schtsk_que"	VALUE = 2>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.store:cache_type }">
		<MvASSIGN NAME = "l.store:cache_type"	VALUE = "none">
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.store:cache_comp }">
		<MvASSIGN NAME = "l.store:cache_comp"	VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.store:cache_ridx }">
		<MvASSIGN NAME = "l.store:cache_ridx"	VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.store:redisport }">
		<MvASSIGN NAME = "l.store:redisport"	VALUE = 6379>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.store:redisto }">
		<MvASSIGN NAME = "l.store:redisto"		VALUE = 1000>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.store:redisex }">
		<MvASSIGN NAME = "l.store:redisex"		VALUE = 300>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "INSERT INTO Stores
					   ( id, manager_id, code, license, name, icon, owner, email, company, address, city, state, zip, phone, fax, country,
						 wtunits, wtunitcode, wtdispmix, wtdisplow, wtdispdig, dmunitcode, baskexp, deferbask, trackhits, pgrp_ovlp, ui_id, tax_id, 
						 currncy_id, boxpack_id, addrval_id, mnt_warn, mnt_close, mnt_time, mnt_no_new, mnt_ips, omin_quant, omin_price, omin_all, 
						 omin_msg, crypt_id, req_ship, req_tax, req_frship, branch_id, charset, schtsk_adv, schtsk_min, schtsk_que, cache_exp, cache_ver, 
						 cache_type, cache_comp, cache_ridx, redishost, redisport, redisto, redispwd, redisex, cacheset )
					   VALUES
					   ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?,
					     ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )"
			 FIELDS	= "l.store:id, l.store:manager_id, l.store:code, l.store:license, l.store:name, l.store:icon, l.store:owner,
					   l.store:email, l.store:company, l.store:address, l.store:city, l.store:state, l.store:zip,
					   l.store:phone, l.store:fax, l.store:country, l.store:wtunits, l.store:wtunitcode, l.store:wtdispmix, 
					   l.store:wtdisplow, l.store:wtdispdig, l.store:dmunitcode, l.store:baskexp, l.store:deferbask, l.store:trackhits, 
					   l.store:pgrp_ovlp, l.store:ui_id, l.store:tax_id, l.store:currncy_id, l.store:boxpack_id, l.store:addrval_id, 
					   l.store:mnt_warn, l.store:mnt_close, l.store:mnt_time, l.store:mnt_no_new, l.store:mnt_ips, l.store:omin_quant, 
					   l.store:omin_price, l.store:omin_all, l.store:omin_msg, l.store:crypt_id, l.store:req_ship, l.store:req_tax, 
					   l.store:req_frship, l.store:branch_id, l.store:charset, l.store:schtsk_adv, l.store:schtsk_min, l.store:schtsk_que,
					   l.store:cache_exp, l.store:cache_ver, l.store:cache_type, l.store:cache_comp, l.store:cache_ridx, l.store:redishost,
					   l.store:redisport, l.store:redisto, l.store:redispwd, l.store:redisex, l.cacheset">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-STR-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Store_Update" PARAMETERS = "store var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "UPDATE Stores
					   SET
						manager_id	= ?,
						code		= ?,
						license		= ?,
						name		= ?,
						icon		= ?,
						owner		= ?,
						email		= ?,
						company		= ?,
						address		= ?,
						city		= ?,
						state		= ?,
						zip			= ?,
						phone		= ?,
						fax			= ?,
						country		= ?,
						wtunits		= ?,
						wtunitcode	= ?,
						wtdispmix	= ?,
						wtdisplow	= ?,
						wtdispdig	= ?,
						dmunitcode	= ?,
						baskexp		= ?,
						deferbask	= ?,
						trackhits	= ?,
						pgrp_ovlp	= ?,
						ui_id		= ?,
						tax_id		= ?,
						currncy_id	= ?,
						boxpack_id	= ?,
						addrval_id	= ?,
						mnt_warn	= ?,
						mnt_close	= ?,
						mnt_time	= ?,
						mnt_no_new	= ?,
						mnt_ips		= ?,
						omin_quant	= ?,
						omin_price	= ?,
						omin_all	= ?,
						omin_msg	= ?,
						req_ship	= ?,
						req_tax		= ?,
						req_frship	= ?,
						branch_id	= ?,
						charset		= ?,
						schtsk_adv	= ?,
						schtsk_min	= ?,
						schtsk_que	= ?,
						cache_exp	= ?,
						cache_type	= ?,
						cache_comp	= ?,
						cache_ridx	= ?,
						redishost	= ?,
						redisport	= ?,
						redisto		= ?,
						redispwd	= ?,
						redisex		= ?
					   WHERE
						id			= ?"
			 FIELDS	= "l.store:manager_id, l.store:code, l.store:license, l.store:name, l.store:icon, l.store:owner,
					   l.store:email, l.store:company, l.store:address, l.store:city, l.store:state, l.store:zip,
					   l.store:phone, l.store:fax, l.store:country, l.store:wtunits, l.store:wtunitcode, l.store:wtdispmix, 
					   l.store:wtdisplow, l.store:wtdispdig, l.store:dmunitcode, l.store:baskexp, l.store:deferbask, l.store:trackhits, 
					   l.store:pgrp_ovlp, l.store:ui_id, l.store:tax_id, l.store:currncy_id, l.store:boxpack_id, l.store:addrval_id, 
					   l.store:mnt_warn, l.store:mnt_close, l.store:mnt_time, l.store:mnt_no_new, l.store:mnt_ips, l.store:omin_quant, 
					   l.store:omin_price, l.store:omin_all, l.store:omin_msg, l.store:req_ship, l.store:req_tax, l.store:req_frship, 
					   l.store:branch_id, l.store:charset, l.store:schtsk_adv, l.store:schtsk_min, l.store:schtsk_que, l.store:cache_exp, l.store:cache_type, 
					   l.store:cache_comp, l.store:cache_ridx, l.store:redishost, l.store:redisport, l.store:redisto, l.store:redispwd, l.store:redisex, 
					   l.store:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-STR-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_SCH_UT ].ScheduledTasks_Update_Semaphore( l.store ) }">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Store_Update_Encryption" PARAMETERS = "store var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "UPDATE Stores
					   SET
						crypt_id	= ?
					   WHERE
						id			= ?"
			 FIELDS	= "l.store:crypt_id,
					   l.store:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-STR-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Store_Update_Modules" PARAMETERS = "store var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "UPDATE Stores
					   SET
						ui_id		= ?,
						tax_id		= ?,
						currncy_id	= ?,
						boxpack_id	= ?
					   WHERE
						id			= ?"
			 FIELDS	= "l.store:ui_id, l.store:tax_id, l.store:currncy_id, l.store:boxpack_id,
					   l.store:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-STR-00011', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Store_Update_Branch" PARAMETERS = "store var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "UPDATE Stores
					   SET
						branch_id	= ?
					   WHERE
						id			= ?"
			 FIELDS	= "l.store:branch_id,
					   l.store:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-STR-00024', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Store_Update_ShippingRules" PARAMETERS = "store var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "UPDATE Stores
					   SET
						req_ship	= ?,
						req_frship	= ?,
						addrval_id	= ?
					   WHERE
						id			= ?"
			 FIELDS	= "l.store:req_ship, l.store:req_frship, l.store:addrval_id,
					   l.store:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-STR-00017', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Store_Update_CacheSettings" PARAMETERS = "store var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.cacheset" VALUE = "{ miva_array_serialize( l.store:cacheset ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "UPDATE Stores
					   SET
						cacheset	= ?
					   WHERE
						id			= ?"
			 FIELDS	= "l.cacheset,
					   l.store:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-STR-00026', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Store_Update_MaintenanceMode_Time" PARAMETERS = "store var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "UPDATE Stores
					   SET
						mnt_time	= ?
					   WHERE
						id			= ?"
			 FIELDS	= "l.store:mnt_time,
					   l.store:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-STR-00018', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Store_Update_CacheVersion" PARAMETERS = "store var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.version"			VALUE = "{ s.dyn_time_t }">
	<MvIF EXPR = "{ l.store:cache_ver GE l.version }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.store:cache_ver"	VALUE = "{ l.version }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "UPDATE Stores
					   SET
						cache_ver	= ?
					   WHERE
						id			= ?"
			 FIELDS	= "l.store:cache_ver,
					   l.store:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-STR-00020', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Store_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "DELETE FROM Stores WHERE id = ?"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-STR-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Store_Load_ID" PARAMETERS = "id, store var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Stores"
				QUERY	= "SELECT * FROM Stores WHERE id = ?"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-STR-00005', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Stores.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Stores">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-STR-00012' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Store_Read( l.store ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Stores">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Store_Load_Code_Cached" PARAMETERS = "code, store var" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCEARRAY NAME = "l.cache" 					VARIABLE = "g.Session:cache:store_load_code_cached">
		<MvMEMBER NAME = "{ l.code }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ Store_Load_Code( l.code, l.cache:store ) }">

		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.store"							VALUE = "{ l.cache:store }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "Store_Load_Code" PARAMETERS = "code, store var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Stores"
				QUERY	= "{ 'SELECT * FROM Stores WHERE ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-STR-00006', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Stores.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Stores">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-STR-00013' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Store_Read( l.store ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Stores">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreList_Load_All" PARAMETERS = "stores var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Stores"
				QUERY	= "SELECT * FROM Stores ORDER BY id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-STR-00007', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.store_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Stores.d.EOF }">
		<MvASSIGN NAME = "l.store_count" VALUE = "{ l.store_count + 1 }">
		<MvEVAL EXPR = "{ Store_Read( l.stores[ l.store_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Stores" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Stores">

	<MvCOMMENT>
	|
	| This function shares its results with Store_Count_Cached
	|
	</MvCOMMENT>

	<MvREFERENCE NAME = "l.cache" VARIABLE = "g.Session:cache:store_count">

	<MvASSIGN NAME = "l.cache:result"				VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-STR-00014', l.store_count ) }">

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
		<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
		<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "StoreList_Load_User" PARAMETERS = "user_id, stores var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW	NAME	= "Merchant"
				VIEW	= "Stores"
				QUERY	= "SELECT DISTINCT
							  s.*
						   FROM 
							  Users u
							  LEFT OUTER JOIN UserXGroup uxg ON u.id = uxg.user_id
							  LEFT OUTER JOIN Stores s ON u.admin = 1 OR u.id = s.manager_id OR uxg.store_id = s.id
						   WHERE
							  u.id = ? AND
							  s.id IS NOT NULL
						   ORDER BY
							  s.id"
				FIELDS	= "l.user_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{  [ g.Library_Filename_Utilities ].Error( 'MER-DBP-STR-00008', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.store_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Stores.d.EOF }">
		<MvASSIGN NAME = "l.store_count" VALUE = "{ l.store_count + 1 }">
		<MvEVAL EXPR = "{ Store_Read( l.stores[ l.store_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Stores" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Stores">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-STR-00015', l.store_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "StoreList_Load_Manager_Cached" PARAMETERS = "manager_id, stores var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.manager_id"								VALUE = "{ int( l.manager_id ) }">

	<MvIF EXPR = "{ l.manager_id LE 0 }">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-STR-00021' ) }">
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.cache" VARIABLE = "g.Session:cache:storelist_load_manager">
		<MvDIMENSION INDEX = "{ l.manager_id }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ ISNULL l.cache }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ StoreList_Load_Manager( l.manager_id, l.stores ) }">

		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ NOT l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( l.cache:error_code ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "StoreList_Load_Manager" PARAMETERS = "manager_id, stores var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW	NAME	= "Merchant"
				VIEW	= "Stores"
				QUERY	= "SELECT * FROM Stores WHERE manager_id = ? ORDER BY id"
				FIELDS	= "l.manager_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{  [ g.Library_Filename_Utilities ].Error( 'MER-DBP-STR-00022', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.store_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Stores.d.EOF }">
		<MvASSIGN NAME = "l.store_count" VALUE = "{ l.store_count + 1 }">
		<MvEVAL EXPR = "{ Store_Read( l.stores[ l.store_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Stores" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Stores">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-STR-00023', l.store_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Store_Count_Cached" PARAMETERS = "" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCE NAME = "l.cache" VARIABLE = "g.Session:cache:store_count">

	<MvIF EXPR = "{ ISNULL l.cache }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ Store_Count() }">

		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ NOT l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( l.cache:error_code ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "Store_Count" PARAMETERS = "" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Stores"
				QUERY	= "SELECT COUNT( * ) AS store_count FROM Stores">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-STR-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Stores.d.EOF }">
		<MvASSIGN NAME = "l.store_count"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.store_count"	VALUE = "{ Stores.d.store_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Stores">
	<MvFUNCTIONRETURN VALUE = "{ l.store_count }">
</MvFUNCTION>

<MvFUNCTION NAME = "Store_Count_License" PARAMETERS = "license" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Stores"
				QUERY	= "SELECT COUNT( * ) AS store_count FROM Stores WHERE license = ?"
				FIELDS	= "l.license">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-STR-00010', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Stores.d.EOF }">
		<MvASSIGN NAME = "l.store_count"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.store_count"	VALUE = "{ Stores.d.store_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Stores">
	<MvFUNCTIONRETURN VALUE = "{ l.store_count }">
</MvFUNCTION>

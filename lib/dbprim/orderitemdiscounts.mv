<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2014 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-OID-
| Next Error Code: 6    
|
</MvCOMMENT>

<MvFUNCTION NAME = "OrderItemDiscount_Read" PARAMETERS = "orderitemdiscount var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.orderitemdiscount:order_id"		VALUE = "{ OrderItemDiscounts.d.order_id }">
	<MvASSIGN NAME = "l.orderitemdiscount:line_id"		VALUE = "{ OrderItemDiscounts.d.line_id }">
	<MvASSIGN NAME = "l.orderitemdiscount:pgrp_id"		VALUE = "{ OrderItemDiscounts.d.pgrp_id }">
	<MvASSIGN NAME = "l.orderitemdiscount:priority"		VALUE = "{ OrderItemDiscounts.d.priority }">
	<MvASSIGN NAME = "l.orderitemdiscount:display"		VALUE = "{ OrderItemDiscounts.d.display }">
	<MvASSIGN NAME = "l.orderitemdiscount:descrip"		VALUE = "{ OrderItemDiscounts.d.descrip }">
	<MvASSIGN NAME = "l.orderitemdiscount:discount"		VALUE = "{ OrderItemDiscounts.d.discount }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItemDiscount_Insert" PARAMETERS = "orderitemdiscount var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'OrderItemDiscounts
					      ( order_id, line_id, pgrp_id, priority, display, descrip, discount )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.orderitemdiscount:order_id, l.orderitemdiscount:line_id, l.orderitemdiscount:pgrp_id,
					   l.orderitemdiscount:priority, l.orderitemdiscount:display, l.orderitemdiscount:descrip,
					   l.orderitemdiscount:discount">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OID-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItemDiscountList_Load_Line" PARAMETERS = "line_id, orderitemdiscounts var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderItemDiscounts"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderItemDiscounts WHERE line_id = ? ORDER BY priority DESC, pgrp_id ASC' }"
				FIELDS	= "l.line_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OID-00002', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderitemdiscount_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderItemDiscounts.d.EOF }">
		<MvEVAL EXPR = "{ OrderItemDiscount_Read( l.orderitemdiscounts[ ++l.orderitemdiscount_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "OrderItemDiscounts" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItemDiscounts">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OID-00003', l.orderitemdiscount_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItemDiscount_Delete_All_Order" PARAMETERS = "order_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderItemDiscounts WHERE order_id = ?' }"
			 FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OID-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItemDiscount_Delete_All_Line" PARAMETERS = "line_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderItemDiscounts WHERE line_id = ?' }"
			 FIELDS	= "l.line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OID-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2021 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-OIN-
| Next Error Code: 9
|
</MvCOMMENT>

<MvFUNCTION NAME = "OrderInfo_Read" PARAMETERS = "orderinfo var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.orderinfo"				VALUE = "">
	<MvASSIGN NAME = "l.orderinfo:order_id"		VALUE = "{ OrderInfo.d.order_id }">
	<MvASSIGN NAME = "l.orderinfo:module_id"	VALUE = "{ OrderInfo.d.module_id }">
	<MvASSIGN NAME = "l.orderinfo:type"			VALUE = "{ OrderInfo.d.type }">
	<MvASSIGN NAME = "l.orderinfo:info"			VALUE = "{ miva_array_deserialize( OrderInfo.d.info ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderInfo_Insert" PARAMETERS = "orderinfo var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.info" VALUE = "{ miva_array_serialize( l.orderinfo:info ) }">

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'OrderInfo
			 			  ( order_id, module_id, type, info )
			 			  VALUES
			 			  ( ?, ?, ?, ? )' }"
			 FIELDS = "l.orderinfo:order_id , l.orderinfo:module_id , l.orderinfo:type , l.info">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OIN-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderInfo_Update" PARAMETERS = "orderinfo var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.info" VALUE = "{ miva_array_serialize( l.orderinfo:info ) }">

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderInfo
			 			  SET
			 				info		= ?
			 			  WHERE
			 				order_id	= ? AND
			 				module_id	= ? AND
			 				type		= ?' }"
			 FIELDS = "l.info,
			 		   l.orderinfo:order_id, l.orderinfo:module_id, l.orderinfo:type">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OIN-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderInfo_Load" PARAMETERS = "order_id, module_id, type, orderinfo var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "OrderInfo"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderInfo WHERE order_id = ? AND module_id = ? AND type = ?' }"
				FIELDS	= "l.order_id, l.module_id, l.type">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OIN-00003', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ OrderInfo.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderInfo">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-OIN-00004' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ OrderInfo_Read( l.orderinfo ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderInfo">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderInfoList_Load_Order" PARAMETERS = "order_id, orderinfos var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "OrderInfo"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderInfo WHERE order_id = ? ORDER BY order_id, module_id, type' }"
				FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OIN-00007', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderinfo_count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT OrderInfo.d.EOF }">
		<MvEVAL EXPR = "{ OrderInfo_Read( l.orderinfos[ ++l.orderinfo_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "OrderInfo" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderInfo">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OIN-00008', l.orderinfo_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderInfo_Delete" PARAMETERS = "order_id, module_id, type" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderInfo WHERE order_id = ? AND module_id = ? AND type = ?' }"
			 FIELDS = "l.order_id, l.module_id, l.type">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OIN-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderInfo_Delete_All_Order" PARAMETERS = "order_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderInfo WHERE order_id = ?' }"
			 FIELDS = "l.order_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OIN-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

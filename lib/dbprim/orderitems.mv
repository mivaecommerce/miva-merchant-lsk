<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-OIT-
| Next Error Code: 30   
|
</MvCOMMENT>

<MvFUNCTION NAME = "OrderItem_Read" PARAMETERS = "orderitem var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.orderitem:order_id"		VALUE = "{ OrderItems.d.order_id }">
	<MvASSIGN NAME = "l.orderitem:line_id"		VALUE = "{ OrderItems.d.line_id }">
	<MvASSIGN NAME = "l.orderitem:group_id"		VALUE = "{ OrderItems.d.group_id }">
	<MvASSIGN NAME = "l.orderitem:parent_id"	VALUE = "{ OrderItems.d.parent_id }">
	<MvASSIGN NAME = "l.orderitem:status"		VALUE = "{ OrderItems.d.status }">
	<MvASSIGN NAME = "l.orderitem:subscrp_id"	VALUE = "{ OrderItems.d.subscrp_id }">
	<MvASSIGN NAME = "l.orderitem:subterm_id"	VALUE = "{ OrderItems.d.subterm_id }">
	<MvASSIGN NAME = "l.orderitem:shpmnt_id"	VALUE = "{ OrderItems.d.shpmnt_id }">
	<MvASSIGN NAME = "l.orderitem:rma_id"		VALUE = "{ OrderItems.d.rma_id }">
	<MvASSIGN NAME = "l.orderitem:dt_instock"	VALUE = "{ OrderItems.d.dt_instock }">
	<MvASSIGN NAME = "l.orderitem:product_id"	VALUE = "{ OrderItems.d.product_id }">
	<MvASSIGN NAME = "l.orderitem:code"			VALUE = "{ OrderItems.d.code }">
	<MvASSIGN NAME = "l.orderitem:name"			VALUE = "{ OrderItems.d.name }">
	<MvASSIGN NAME = "l.orderitem:sku"			VALUE = "{ OrderItems.d.sku }">
	<MvASSIGN NAME = "l.orderitem:type"			VALUE = "{ OrderItems.d.type }">
	<MvASSIGN NAME = "l.orderitem:retail"		VALUE = "{ OrderItems.d.retail }">
	<MvASSIGN NAME = "l.orderitem:base_price"	VALUE = "{ OrderItems.d.base_price }">
	<MvASSIGN NAME = "l.orderitem:price"		VALUE = "{ OrderItems.d.price }">
	<MvASSIGN NAME = "l.orderitem:total"		VALUE = "{ OrderItems.d.total }">
	<MvASSIGN NAME = "l.orderitem:weight"		VALUE = "{ OrderItems.d.weight }">
	<MvASSIGN NAME = "l.orderitem:taxable"		VALUE = "{ OrderItems.d.taxable }">
	<MvASSIGN NAME = "l.orderitem:upsold"		VALUE = "{ OrderItems.d.upsold }">
	<MvASSIGN NAME = "l.orderitem:quantity"		VALUE = "{ OrderItems.d.quantity }">
	<MvASSIGN NAME = "l.orderitem:tax"			VALUE = "{ OrderItems.d.tax }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItem_Insert_LowLevel" PARAMETERS = "orderitem var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.orderitem:group_id }">		<MvASSIGN NAME = "l.orderitem:group_id"		VALUE = "{ l.orderitem:line_id }">	</MvIF>
	<MvIF EXPR = "{ ISNULL l.orderitem:parent_id }">	<MvASSIGN NAME = "l.orderitem:parent_id"	VALUE = 0>							</MvIF>
	<MvIF EXPR = "{ ISNULL l.orderitem:retail }">		<MvASSIGN NAME = "l.orderitem:retail"		VALUE = 0.00>						</MvIF>
	<MvIF EXPR = "{ ISNULL l.orderitem:base_price }">	<MvASSIGN NAME = "l.orderitem:base_price"	VALUE = "{ l.orderitem:price }">	</MvIF>
	<MvIF EXPR = "{ ISNULL l.orderitem:total }">		<MvASSIGN NAME = "l.orderitem:total"		VALUE = 0.00>						</MvIF>
	<MvIF EXPR = "{ ISNULL l.orderitem:subscrp_id }">	<MvASSIGN NAME = "l.orderitem:subscrp_id"	VALUE = 0>							</MvIF>
	<MvIF EXPR = "{ ISNULL l.orderitem:subterm_id }">	<MvASSIGN NAME = "l.orderitem:subterm_id"	VALUE = 0>							</MvIF>
	<MvIF EXPR = "{ ISNULL l.orderitem:dt_instock }">	<MvASSIGN NAME = "l.orderitem:dt_instock"	VALUE = 0>							</MvIF>
	<MvIF EXPR = "{ ISNULL l.orderitem:tax }">			<MvASSIGN NAME = "l.orderitem:tax"			VALUE = 0.00>						</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'OrderItems
						  ( order_id, line_id, group_id, parent_id, status, subscrp_id, subterm_id, shpmnt_id, rma_id, dt_instock, product_id,
						    code, name, sku, type, retail, base_price, price, total, weight, taxable, upsold, quantity, tax )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.orderitem:order_id, l.orderitem:line_id, l.orderitem:group_id, l.orderitem:parent_id, l.orderitem:status, l.orderitem:subscrp_id, l.orderitem:subterm_id,
			           l.orderitem:shpmnt_id, l.orderitem:rma_id, l.orderitem:dt_instock, l.orderitem:product_id, l.orderitem:code, l.orderitem:name, l.orderitem:sku,
					   l.orderitem:type,  l.orderitem:retail, l.orderitem:base_price, l.orderitem:price, l.orderitem:total, l.orderitem:weight, l.orderitem:taxable,
					   l.orderitem:upsold, l.orderitem:quantity, l.orderitem:tax">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OIT-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItem_Update_LowLevel" PARAMETERS = "orderitem var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderItems
						  SET
							group_id	= ?,
							parent_id	= ?,
							status		= ?,
							subscrp_id	= ?,
							subterm_id	= ?,
							shpmnt_id	= ?,
							rma_id		= ?,
							dt_instock	= ?,
							product_id	= ?,
							code		= ?,
							name		= ?,
							sku			= ?,
							type		= ?,
							retail		= ?,
							base_price	= ?,
							price		= ?,
							total		= ?,
							weight		= ?,
							taxable		= ?,
							upsold		= ?,
							quantity	= ?,
							tax			= ?
						  WHERE
							order_id	= ? AND
							line_id		= ?' }"
			 FIELDS	= "l.orderitem:group_id, l.orderitem:parent_id, l.orderitem:status, l.orderitem:subscrp_id, l.orderitem:subterm_id, l.orderitem:shpmnt_id, l.orderitem:rma_id,
					   l.orderitem:dt_instock, l.orderitem:product_id, l.orderitem:code, l.orderitem:name, l.orderitem:sku, l.orderitem:type, l.orderitem:retail, l.orderitem:base_price,
					   l.orderitem:price, l.orderitem:total, l.orderitem:weight, l.orderitem:taxable, l.orderitem:upsold, l.orderitem:quantity, l.orderitem:tax,
					   l.orderitem:order_id, l.orderitem:line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OIT-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItem_Increment_Quantity_LowLevel" PARAMETERS = "line_id, quantity" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderItems
						  SET
							quantity	= quantity + ?
						  WHERE
							line_id		= ?' }"
			 FIELDS	= "l.quantity,
					   l.line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OIT-00027', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItem_Update_Pricing_LowLevel" PARAMETERS = "orderitem var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderItems
						  SET
							group_id	= ?,
							parent_id	= ?,
							quantity	= ?,
							retail		= ?,
							base_price	= ?,
							price		= ?,
							total		= ?
						  WHERE
							order_id	= ? AND
							line_id		= ?' }"
			 FIELDS	= "l.orderitem:group_id, l.orderitem:parent_id, l.orderitem:quantity, l.orderitem:retail, l.orderitem:base_price, l.orderitem:price, l.orderitem:total,
					   l.orderitem:order_id, l.orderitem:line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OIT-00020', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItem_Update_Tax" PARAMETERS = "orderitem var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderItems
			 			  SET
			 				tax		= ?
			 			  WHERE
			 				line_id	= ?' }"
			 FIELDS = "l.orderitem:tax,
			 		   l.orderitem:line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OIT-00023', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItem_Update_Status_LowLevel" PARAMETERS = "orderitem var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderItems
						  SET
							status		= ?,
							shpmnt_id	= ?,
							rma_id		= ?,
							dt_instock	= ?
						  WHERE
							order_id	= ? AND
							line_id		= ?' }"
			 FIELDS	= "l.orderitem:status, l.orderitem:shpmnt_id, l.orderitem:rma_id, l.orderitem:dt_instock,
					   l.orderitem:order_id, l.orderitem:line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OIT-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItem_Update_Parent_LowLevel" PARAMETERS = "orderitem var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderItems
						  SET
							parent_id	= ?
						  WHERE
							order_id	= ? AND
							line_id		= ?' }"
			 FIELDS	= "l.orderitem:parent_id,
					   l.orderitem:order_id, l.orderitem:line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OIT-00024', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItemList_Load_ProductID" PARAMETERS = "order_id, product_id, orderitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderItems WHERE order_id = ? AND product_id = ? ORDER BY quantity' }"
				FIELDS	= "l.order_id, l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OIT-00018', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderitem_count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT OrderItems.d.EOF }">
		<MvASSIGN NAME = "l.orderitem_count" VALUE = "{ l.orderitem_count + 1 }">
		<MvEVAL EXPR = "{ OrderItem_Read( l.orderitems[ l.orderitem_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "OrderItems" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OIT-00019', l.orderitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItem_Load_Line" PARAMETERS = "order_id, line_id, orderitem var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderItems WHERE order_id = ? AND line_id = ?' }"
				FIELDS	= "l.order_id, l.line_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OIT-00001', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ OrderItems.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-OIT-00007' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ OrderItem_Read( l.orderitem ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItem_Load_Line_ID" PARAMETERS = "line_id, orderitem var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderItems WHERE line_id = ?' }"
				FIELDS	= "l.line_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OIT-00028', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ OrderItems.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-OIT-00029' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ OrderItem_Read( l.orderitem ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItemList_Load_Order" PARAMETERS = "order_id, orderitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderItems WHERE order_id = ? ORDER BY parent_id, line_id' }"
				FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OIT-00002', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderitem_count"	VALUE = 0>
	<MvASSIGN NAME = "l.parent_indexes"		VALUE = "">

	<MvCOMMENT>
	|
	| Process top-level OrderItems
	|
	</MvCOMMENT>

	<MvWHILE EXPR = "{ NOT OrderItems.d.EOF }">
		<MvIF EXPR = "{ OrderItems.d.parent_id }">
			<MvWHILESTOP>
		</MvIF>

		<MvEVAL EXPR = "{ OrderItem_Read( l.orderitems[ ++l.orderitem_count ] ) }">

		<MvASSIGN NAME = "l.parent_indexes" INDEX = "{ l.orderitems[ l.orderitem_count ]:line_id }" VALUE = "{ l.orderitem_count }">

		<MvSKIP NAME = "Merchant" VIEW = "OrderItems" ROWS = 1>
	</MvWHILE>

	<MvCOMMENT>
	|
	| Process children, end result is that the child ends up in both:
	|
	|	parent:children[]
	|	orderitems[ following parent ]
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.last_parent_id"							VALUE = 0>
	<MvASSIGN NAME = "l.last_parent_index_correction"			VALUE = 0>
	<MvASSIGN NAME = "l.parent_index_correction"				VALUE = 0>

	<MvWHILE EXPR = "{ NOT OrderItems.d.EOF }">
		<MvIF EXPR = "{ OrderItems.d.parent_id NE l.last_parent_id }">
			<MvASSIGN NAME = "l.last_parent_index_correction"   VALUE = "{ l.last_parent_index_correction + l.parent_index_correction }">
			<MvASSIGN NAME = "l.last_parent_id"                 VALUE = "{ OrderItems.d.parent_id }">
			<MvASSIGN NAME = "l.parent_index_correction"		VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT miva_element_exists( l.parent_indexes, OrderItems.d.parent_id ) }">
			<MvEVAL EXPR = "{ OrderItem_Read( l.orderitems[ ++l.orderitem_count ] ) }">
		<MvELSE>
			<MvREFERENCEARRAY NAME = "l.parent_orderitem" VARIABLE = "l.orderitems">
				<MvDIMENSION INDEX = "{ l.parent_indexes[ OrderItems.d.parent_id ] + l.last_parent_index_correction }">
			</MvREFERENCEARRAY>
	
			<MvEVAL EXPR = "{ OrderItem_Read( l.parent_orderitem:children[ ++l.parent_orderitem:child_count ] ) }">

			<MvASSIGN NAME = "l.orderitem_count"				VALUE = "{ miva_array_insert_ref( l.orderitems, l.parent_orderitem:children[ l.parent_orderitem:child_count ], l.parent_indexes[ OrderItems.d.parent_id ] + l.last_parent_index_correction + l.parent_index_correction + 1 ) }">
			<MvASSIGN NAME = "l.parent_index_correction"		VALUE = "{ l.parent_index_correction + 1 }">
		</MvIF> 

		<MvSKIP NAME = "Merchant" VIEW = "OrderItems" ROWS = 1>
	 </MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OIT-00008', l.orderitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItemList_Load_Order_Subscription" PARAMETERS = "order_id, orderitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderItems"
				QUERY	= "{ 'SELECT
							  	*
							  FROM ' $
							  	g.Store_Table_Prefix $ 'OrderItems
							  WHERE
							  	order_id = ? AND
							  	subscrp_id <> 0
							  ORDER BY line_id' }"
				FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OIT-00021', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderitem_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderItems.d.EOF }">
		<MvASSIGN NAME = "l.orderitem_count" VALUE = "{ l.orderitem_count + 1 }">
		<MvEVAL EXPR = "{ OrderItem_Read( l.orderitems[ l.orderitem_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "OrderItems" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OIT-00022', l.orderitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItemList_Load_Shipment" PARAMETERS = "shpmnt_id, orderitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderItems WHERE shpmnt_id = ? ORDER BY line_id' }"
				FIELDS	= "l.shpmnt_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OIT-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderitem_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderItems.d.EOF }">
		<MvASSIGN NAME = "l.orderitem_count" VALUE = "{ l.orderitem_count + 1 }">
		<MvEVAL EXPR = "{ OrderItem_Read( l.orderitems[ l.orderitem_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "OrderItems" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OIT-00010', l.orderitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItemList_Load_Return" PARAMETERS = "rma_id, orderitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderItems WHERE rma_id = ? ORDER BY line_id' }"
				FIELDS	= "l.rma_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OIT-00011', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderitem_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderItems.d.EOF }">
		<MvASSIGN NAME = "l.orderitem_count" VALUE = "{ l.orderitem_count + 1 }">
		<MvEVAL EXPR = "{ OrderItem_Read( l.orderitems[ l.orderitem_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "OrderItems" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OIT-00012', l.orderitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItemList_Load_Parent" PARAMETERS = "order_id, parent_id, orderitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderItems WHERE order_id = ? AND parent_id = ? ORDER BY line_id' }"
				FIELDS	= "l.order_id, l.parent_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OIT-00025', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderitem_count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT OrderItems.d.EOF }">
		<MvEVAL EXPR = "{ OrderItem_Read( l.orderitems[ ++l.orderitem_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "OrderItems" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OIT-00026', l.orderitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItemList_Load_Status" PARAMETERS = "order_id, status, orderitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderItems WHERE order_id = ? AND status = ? ORDER BY line_id' }"
				FIELDS	= "l.order_id, l.status">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OIT-00013', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderitem_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT OrderItems.d.EOF }">
		<MvASSIGN NAME = "l.orderitem_count" VALUE = "{ l.orderitem_count + 1 }">
		<MvEVAL EXPR = "{ OrderItem_Read( l.orderitems[ l.orderitem_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "OrderItems" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-OIT-00014', l.orderitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItem_Count_Shipment" PARAMETERS = "shpmnt_id, count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderItems"
				QUERY	= "{ 'SELECT COUNT( * ) AS item_count FROM ' $ g.Store_Table_Prefix $ 'OrderItems WHERE shpmnt_id = ?' }"
				FIELDS	= "l.shpmnt_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OIT-00015', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ OrderItems.d.EOF }">	<MvASSIGN NAME = "l.count" VALUE = 0>
	<MvELSE>								<MvASSIGN NAME = "l.count" VALUE = "{ OrderItems.d.item_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItem_Count_Return" PARAMETERS = "rma_id, count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderItems"
				QUERY	= "{ 'SELECT COUNT( * ) AS item_count FROM ' $ g.Store_Table_Prefix $ 'OrderItems WHERE rma_id = ?' }"
				FIELDS	= "l.rma_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OIT-00016', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ OrderItems.d.EOF }">	<MvASSIGN NAME = "l.count" VALUE = 0>
	<MvELSE>								<MvASSIGN NAME = "l.count" VALUE = "{ OrderItems.d.item_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItem_Delete_All_Order" PARAMETERS = "order_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderItems WHERE order_id = ?' }"
			 FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-OIT-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItem_Delete" PARAMETERS = "order_id, line_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderItems WHERE order_id = ? AND line_id = ?' }"
			 FIELDS	= "l.order_id, l.line_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-OIT-00017', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

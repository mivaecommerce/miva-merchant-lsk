<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-CXP-
| Next Error Code: 13   
|
</MvCOMMENT>

<MvFUNCTION NAME = "CategoryXProduct_Read" PARAMETERS = "categoryxproduct var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.categoryxproduct:cat_id"		VALUE = "{ CategoryXProduct.d.cat_id }">
	<MvASSIGN NAME = "l.categoryxproduct:product_id"	VALUE = "{ CategoryXProduct.d.product_id }">
	<MvASSIGN NAME = "l.categoryxproduct:disp_order"	VALUE = "{ CategoryXProduct.d.disp_order }">
	<MvASSIGN NAME = "l.categoryxproduct:automatic"		VALUE = "{ CategoryXProduct.d.automatic }">
	<MvASSIGN NAME = "l.categoryxproduct:colcount"		VALUE = "{ CategoryXProduct.d.colcount }">
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXProduct_Insert_LowLevel" PARAMETERS = "categoryxproduct var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.disp_order" VALUE = "{ StoreKey_Generate( 'CategoryXProductDisplayOrder' ) }">

	<MvIF EXPR = "{ ISNULL l.categoryxproduct:automatic }">	<MvASSIGN NAME = "l.categoryxproduct:automatic"	VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.categoryxproduct:colcount }">	<MvASSIGN NAME = "l.categoryxproduct:colcount"	VALUE = 0> </MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'CategoryXProduct
					      ( cat_id, product_id, disp_order, automatic, colcount )
						  VALUES
						  ( ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.categoryxproduct:cat_id, l.categoryxproduct:product_id, l.disp_order, l.categoryxproduct:automatic, l.categoryxproduct:colcount">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXP-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXProduct_Update_DisplayOrder" PARAMETERS = "categoryxproduct var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'CategoryXProduct
						  SET
							disp_order	= ?
						  WHERE
							cat_id		= ? AND
							product_id	= ?' }"
			 FIELDS	= "l.categoryxproduct:disp_order,
					   l.categoryxproduct:cat_id, l.categoryxproduct:product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXP-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXProduct_Delete_LowLevel" PARAMETERS = "cat_id, product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CategoryXProduct
						  WHERE
							cat_id		= ? AND
							product_id	= ?' }"
			 FIELDS	= "l.cat_id, l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXP-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXProduct_Load" PARAMETERS = "cat_id, product_id, categoryxproduct var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CategoryXProduct"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CategoryXProduct where cat_id = ? and product_id = ?' }"
				FIELDS	= "l.cat_id, l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXP-00004', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ CategoryXProduct.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryXProduct">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CXP-00009' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ CategoryXProduct_Read( l.categoryxproduct ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryXProduct">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXProduct_Load_DisplayOrder" PARAMETERS = "cat_id, disp_order, categoryxproduct var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CategoryXProduct"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CategoryXProduct WHERE cat_id = ? AND disp_order = ?' }"
				FIELDS	= "l.cat_id, l.disp_order">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXP-00007', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ CategoryXProduct.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryXProduct">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CXP-00010' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ CategoryXProduct_Read( l.categoryxproduct ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryXProduct">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXProduct_Load_Category" PARAMETERS = "cat_id, categoryxproduct var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CategoryXProduct"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CategoryXProduct where cat_id = ?' }"
				FIELDS	= "l.cat_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXP-00008', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.cat_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT CategoryXProduct.d.EOF }">
		<MvASSIGN NAME = "l.cat_count" VALUE = "{ l.cat_count + 1 }">

		<MvEVAL EXPR = "{ CategoryXProduct_Read( l.categoryxproduct[ l.cat_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "CategoryXProduct" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryXProduct">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-CXP-00011', l.cat_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXProduct_Delete_All_Category" PARAMETERS = "cat_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CategoryXProduct WHERE cat_id = ?' }"
			 FIELDS	= "l.cat_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXP-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryXProduct_Delete_All_Product_LowLevel" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| Create sNN_CategoryXProductCollectionExclusion records
	|
	</MvCOMMENT>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'CategoryXProductCollectionExclusions ( cat_id, product_id, colcount )
			 			  SELECT cat_id, ?, colcount FROM ' $ g.Store_Table_Prefix $ 'CategoryXProduct WHERE product_id = ? AND colcount > 0' }"
			 FIELDS = "l.product_id, l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXP-00012', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| Delete CategoryXProduct records
	|
	</MvCOMMENT>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CategoryXProduct WHERE product_id = ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CXP-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-ORS-
| Next Error Code: 11
|
</MvCOMMENT>

<MvFUNCTION NAME = "OrderSource_Read" PARAMETERS = "ordersource var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.ordersource:order_id"	VALUE = "{ OrderSources.d.order_id }">
	<MvASSIGN NAME = "l.ordersource:source"		VALUE = "{ OrderSources.d.source }">
	<MvASSIGN NAME = "l.ordersource:source_id"	VALUE = "{ OrderSources.d.source_id }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderSource_Insert" PARAMETERS = "order_id, source, source_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'OrderSources
					      ( order_id, source, source_id )
						  VALUES
						  ( ?, ?, ? )' }"
			 FIELDS	= "l.order_id, l.source, l.source_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-ORS-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderSource_Delete" PARAMETERS = "order_id, source, source_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderSources
						  WHERE
							order_id	= ? AND
							source		= ? AND
							source_id	= ?' }"
			 FIELDS	= "l.order_id, l.source, l.source_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-ORS-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderSource_Load" PARAMETERS = "order_id, source, source_id, ordersource var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderSources"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderSources WHERE order_id = ? AND source = ? AND source_id = ?' }"
			    FIELDS	= "l.order_id, l.source, l.source_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-ORS-00003', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ OrderSources.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderSources">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-ORS-00004' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ OrderSource_Read( l.ordersource ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderSources">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderSourceList_Load_Order" PARAMETERS = "order_id, ordersources var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderSources"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'OrderSources WHERE order_id = ? ORDER BY source, source_id' }"
			    FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-ORS-00005', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.ordersource_count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT OrderSources.d.EOF }">
		<MvEVAL EXPR = "{ OrderSource_Read( l.ordersources[ ++l.ordersource_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "OrderSources" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderSources">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-ORS-00006', l.ordersource_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderSourceList_Load_Order_Source" PARAMETERS = "order_id, source, ordersources var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderSources"
				QUERY	= "{ 'SELECT
								*
							  FROM
								' $ g.Store_Table_Prefix $ 'OrderSources
							  WHERE
								order_id = ? AND
								 ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( 'source' ) $ ' = ' $ [ g.Library_Filename_Native_DBAPI ].DB_Compare_UPPER( '?' ) $ '
							  ORDER BY
								source_id' }"
			    FIELDS	= "l.order_id, l.source">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-ORS-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.ordersource_count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT OrderSources.d.EOF }">
		<MvEVAL EXPR = "{ OrderSource_Read( l.ordersources[ ++l.ordersource_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "OrderSources" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderSources">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-ORS-00010', l.ordersource_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "OrderSource_Delete_All_Order" PARAMETERS = "order_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'OrderSources WHERE order_id = ?' }"
			 FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-DBP-ORS-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

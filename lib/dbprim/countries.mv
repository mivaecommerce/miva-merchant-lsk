<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBP-CNT-
| Next Error Code: 16   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Country_Read" PARAMETERS = "country var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.country:id"			VALUE = "{ Countries.d.id }">
	<MvASSIGN NAME = "l.country:name"		VALUE = "{ Countries.d.name }">
	<MvASSIGN NAME = "l.country:alpha"		VALUE = "{ Countries.d.alpha }">
	<MvASSIGN NAME = "l.country:iso_code"	VALUE = "{ padl( Countries.d.iso_code, 3, '0' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Country_Insert" PARAMETERS = "country var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.country:id"	VALUE = "{ DomainKey_Generate( 'Countries' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "INSERT INTO Countries
					   ( id, name, alpha, iso_code )
					   VALUES
					   ( ?, ?, ?, ? )"
			 FIELDS	= "l.country:id, l.country:name, l.country:alpha, l.country:iso_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CNT-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Country_Update" PARAMETERS = "id, alpha, iso_code, name" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "UPDATE Countries
						  SET
							alpha		= ?,
							iso_code	= ?,
							name		= ?
						  WHERE
							id			= ?"
			 FIELDS	= "l.alpha, l.iso_code, l.name,
					   l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CNT-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Country_Delete" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "DELETE FROM Countries WHERE id = ?"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CNT-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CountryList_Load_All" PARAMETERS = "countries var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Countries"
				QUERY	= "SELECT * FROM Countries ORDER BY name">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CNT-00002', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.country_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Countries.d.EOF }">
		<MvASSIGN NAME = "l.country_count" VALUE = "{ l.country_count + 1 }">
		<MvEVAL EXPR = "{ Country_Read( l.countries[ l.country_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Countries" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Countries">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-CNT-00009', l.country_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "CountryList_Load_Alpha" PARAMETERS = "alpha, countries var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Countries"
				QUERY	= "SELECT * FROM Countries WHERE alpha = ?"
				FIELDS	= "l.alpha">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CNT-00003', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.country_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Countries.d.EOF }">
		<MvASSIGN NAME = "l.country_count" VALUE = "{ l.country_count + 1 }">
		<MvEVAL EXPR = "{ Country_Read( l.countries[ l.country_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Countries" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Countries">
	<MvFUNCTIONRETURN VALUE = "{ ListLoad_EOF_Return( 'MER-DBP-CNT-00010', l.country_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Country_Load_ID" PARAMETERS = "id, country var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Countries"
				QUERY	= "SELECT * FROM Countries WHERE id = ?"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CNT-00004', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Countries.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Countries">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CNT-00011' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Country_Read( l.country ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Countries">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Country_Load_Alpha" PARAMETERS = "alpha, country var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Countries"
				QUERY	= "SELECT * FROM Countries WHERE alpha = ?"
				FIELDS	= "l.alpha">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CNT-00005', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Countries.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Countries">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CNT-00012' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Country_Read( l.country ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Countries">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Country_Load_Iso_code" PARAMETERS = "iso_code, country var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Countries"
				QUERY	= "SELECT * FROM Countries WHERE iso_code = ?"
				FIELDS	= "l.iso_code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CNT-00006', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Countries.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Countries">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CNT-00013' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Country_Read( l.country ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Countries">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Country_Load_Name" PARAMETERS = "name, country var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Countries"
				QUERY	= "SELECT * FROM Countries WHERE name = ?"
				FIELDS	= "l.name">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBP-CNT-00014', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Countries.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Countries">
		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBP-CNT-00015' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Country_Read( l.country ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Countries">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

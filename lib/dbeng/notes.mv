<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2019 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBE-NTE-
| Next Error Code: 1   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Note_Insert" PARAMETERS = "note var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.note:id" 					VALUE = "{ StoreKey_Generate( 'Notes' ) }">
	<MvASSIGN NAME = "l.note:dtstamp"				VALUE = "{ s.dyn_time_t }">

	<MvIF EXPR = "{ ISNULL l.note:user_id }">
		<MvASSIGN NAME = "l.note:user_id"			VALUE = "{ g.Session:user_id }">
	</MvIF>

	<MvIF EXPR = "{ l.note:order_id AND ISNULL l.note:cust_id }">
		<MvIF EXPR = "{ Order_Load_ID( l.note:order_id, l.order ) }">
			<MvASSIGN NAME = "l.note:cust_id"		VALUE = "{ l.order:cust_id }">
		<MvELSEIF EXPR = "{ NOT Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.note:cust_id AND ISNULL l.note:account_id }">
		<MvIF EXPR = "{ [ g.Module_Feature_CUS_DB ].Customer_Load_ID( l.note:cust_id, l.customer ) }">
			<MvASSIGN NAME = "l.note:account_id"	VALUE = "{ l.customer:account_id }">
		<MvELSEIF EXPR = "{ NOT Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.note:user_id }">		<MvASSIGN NAME = "l.note:user_id"		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.note:cust_id }">		<MvASSIGN NAME = "l.note:cust_id" 		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.note:account_id }">	<MvASSIGN NAME = "l.note:account_id" 	VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.note:order_id }">		<MvASSIGN NAME = "l.note:order_id" 		VALUE = 0> </MvIF>

	<MvIF EXPR = "{ l.note:cust_id AND ( NOT l.note:order_id ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_CUS_DB ].Customer_Increment_NoteCount( l.note:cust_id ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.note:account_id AND ( NOT l.note:cust_id ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_CUS_DB ].BusinessAccount_Increment_NoteCount( l.note:account_id ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.note:order_id }">
		<MvIF EXPR = "{ NOT Order_Increment_NoteCount( l.note:order_id ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ Note_Insert_LowLevel( l.note ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Note_Delete" PARAMETERS = "note var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.note:cust_id AND ( NOT l.note:order_id ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_CUS_DB ].Customer_Decrement_NoteCount( l.note:cust_id ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.note:account_id AND ( NOT l.note:cust_id ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_CUS_DB ].BusinessAccount_Decrement_NoteCount( l.note:account_id ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.note:order_id }">
		<MvIF EXPR = "{ NOT Order_Decrement_NoteCount( l.note:order_id ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ Note_Delete_LowLevel( l.note ) }">
</MvFUNCTION>

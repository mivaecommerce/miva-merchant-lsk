<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBE-SBH-
| Next Error Code: 3    
|
</MvCOMMENT>

<MvFUNCTION NAME = "ShipmentBatch_Create" PARAMETERS = "shipmentbatch var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvTRANSACT NAME = "Merchant">

	<MvIF EXPR = "{ NOT ShipmentBatch_Insert( l.shipmentbatch ) }">
		<MvROLLBACK NAME = "Merchant">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderShipments SET batch_id = ? WHERE batch_id = 0' }"
			 FIELDS	= "l.shipmentbatch:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvROLLBACK NAME = "Merchant">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBE-SBH-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMIT NAME = "Merchant">
	<MvFUNCTIONRETURN VALUE = "{ l.shipmentbatch:id }">
</MvFUNCTION>

<MvFUNCTION NAME = "ShipmentBatch_Create_ShipmentList" PARAMETERS = "shipmentbatch var, ordershipment_count, ordershipments var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvTRANSACT NAME = "Merchant">

	<MvIF EXPR = "{ NOT ShipmentBatch_Insert( l.shipmentbatch ) }">
		<MvROLLBACK NAME = "Merchant">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.ordershipment_pos"		VALUE = 1>
	<MvWHILE EXPR = "{ l.ordershipment_pos LE l.ordershipment_count }">
		<MvASSIGN NAME = "l.ordershipment_id"	VALUE = "{ l.ordershipments[ l.ordershipment_pos ]:id }">

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'OrderShipments SET batch_id = ? WHERE id = ?' }"
				 FIELDS	= "l.shipmentbatch:id, l.ordershipment_id">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvROLLBACK NAME = "Merchant">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBE-SBH-00002', g.MvQUERY_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "l.ordershipment_pos"	VALUE = "{ l.ordershipment_pos + 1 }">
	</MvWHILE>

	<MvCOMMIT NAME = "Merchant">
	<MvFUNCTIONRETURN VALUE = "{ l.shipmentbatch:id }">
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DBE-KEY-
| Next Error Code: 11   
|
</MvCOMMENT>

<MvFUNCTION NAME = "DomainKey_Generate" PARAMETERS = "type" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Library_Filename_Native_DBAPI ].DB_Lock_Table( 'DomainKeys', '' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvTRANSACT NAME = "Merchant">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE DomainKeys SET ' $ [ g.Library_Filename_Native_DBAPI ].DB_Escape_Identifier( 'maxvalue' ) $ ' = ' $ [ g.Library_Filename_Native_DBAPI ].DB_Escape_Identifier( 'maxvalue' ) $ ' + 1 WHERE type = ?' }"
			 FIELDS	= "l.type">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvROLLBACK NAME = "Merchant">
		<MvEVAL EXPR = "{ [ g.Library_Filename_Native_DBAPI ].DB_Unlock_Table( 'DomainKeys' ) }">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBE-KEY-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvOPENVIEW	NAME	= "Merchant"
				VIEW	= "DomainKeys"
				QUERY	= "SELECT * FROM DomainKeys WHERE type = ?"
				FIELDS	= "l.type">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvROLLBACK NAME = "Merchant">
		<MvEVAL EXPR = "{ [ g.Library_Filename_Native_DBAPI ].DB_Unlock_Table( 'DomainKeys' ) }">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBE-KEY-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ DomainKeys.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "DomainKeys">

		<MvROLLBACK NAME = "Merchant">
		<MvEVAL EXPR = "{ [ g.Library_Filename_Native_DBAPI ].DB_Unlock_Table( 'DomainKeys' ) }">

		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBE-KEY-00005' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.retval" VALUE = "{ DomainKeys.d.maxvalue }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "DomainKeys">

	<MvCOMMIT NAME = "Merchant">
	<MvEVAL EXPR = "{ [ g.Library_Filename_Native_DBAPI ].DB_Unlock_Table( 'DomainKeys' ) }">

	<MvFUNCTIONRETURN VALUE = "{ EOF_Return( l.retval ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "DomainKey_Update" PARAMETERS = "type, value" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Library_Filename_Native_DBAPI ].DB_Lock_Table( 'DomainKeys', '' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvTRANSACT NAME = "Merchant">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE DomainKeys SET ' $ [ g.Library_Filename_Native_DBAPI ].DB_Escape_Identifier( 'maxvalue' ) $ ' = ? WHERE type = ?' }"
			 FIELDS	= "l.value, l.type">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvROLLBACK NAME = "Merchant">
		<MvEVAL EXPR = "{ [ g.Library_Filename_Native_DBAPI ].DB_Unlock_Table( 'DomainKeys' ) }">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBE-KEY-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvOPENVIEW	NAME	= "Merchant"
				VIEW	= "DomainKeys"
				QUERY	= "SELECT * FROM DomainKeys WHERE type = ?"
				FIELDS	= "l.type">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvROLLBACK NAME = "Merchant">
		<MvEVAL EXPR = "{ [ g.Library_Filename_Native_DBAPI ].DB_Unlock_Table( 'DomainKeys' ) }">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBE-KEY-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ DomainKeys.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "DomainKeys">

		<MvROLLBACK NAME = "Merchant">
		<MvEVAL EXPR = "{ [ g.Library_Filename_Native_DBAPI ].DB_Unlock_Table( 'DomainKeys' ) }">

		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBE-KEY-00006' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.retval" VALUE = "{ DomainKeys.d.maxvalue }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "DomainKeys">

	<MvCOMMIT NAME = "Merchant">
	<MvEVAL EXPR = "{ [ g.Library_Filename_Native_DBAPI ].DB_Unlock_Table( 'DomainKeys' ) }">

	<MvFUNCTIONRETURN VALUE = "{ EOF_Return( l.retval ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "StoreKey_Generate" PARAMETERS = "type" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Library_Filename_Native_DBAPI ].DB_Lock_Table( g.Store_Table_Prefix $ 'StoreKeys', '' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvTRANSACT NAME = "Merchant">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'StoreKeys SET ' $ [ g.Library_Filename_Native_DBAPI ].DB_Escape_Identifier( 'maxvalue' ) $ ' = ' $ [ g.Library_Filename_Native_DBAPI ].DB_Escape_Identifier( 'maxvalue' ) $ ' + 1 WHERE type = ?' }"
			 FIELDS	= "l.type">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvROLLBACK NAME = "Merchant">
		<MvEVAL EXPR = "{ [ g.Library_Filename_Native_DBAPI ].DB_Unlock_Table( g.Store_Table_Prefix $ 'StoreKeys' ) }">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBE-KEY-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvOPENVIEW	NAME	= "Merchant"
				VIEW	= "StoreKeys"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'StoreKeys WHERE type = ?' }"
				FIELDS	= "l.type">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvROLLBACK NAME = "Merchant">
		<MvEVAL EXPR = "{ [ g.Library_Filename_Native_DBAPI ].DB_Unlock_Table( g.Store_Table_Prefix $ 'StoreKeys' ) }">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBE-KEY-00009', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ StoreKeys.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "StoreKeys">

		<MvROLLBACK NAME = "Merchant">
		<MvEVAL EXPR = "{ [ g.Library_Filename_Native_DBAPI ].DB_Unlock_Table( g.Store_Table_Prefix $ 'StoreKeys' ) }">

		<MvFUNCTIONRETURN VALUE = "{ Error_Load_EOF( 'MER-DBE-KEY-00007' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.retval" VALUE = "{ StoreKeys.d.maxvalue }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "StoreKeys">

	<MvCOMMIT NAME = "Merchant">
	<MvEVAL EXPR = "{ [ g.Library_Filename_Native_DBAPI ].DB_Unlock_Table( g.Store_Table_Prefix $ 'StoreKeys' ) }">

	<MvFUNCTIONRETURN VALUE = "{ EOF_Return( l.retval ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "StoreKey_Increment" PARAMETERS = "type, increment" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvTRANSACT NAME = "Merchant">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'StoreKeys SET ' $ [ g.Library_Filename_Native_DBAPI ].DB_Escape_Identifier( 'maxvalue' ) $ ' = ' $ [ g.Library_Filename_Native_DBAPI ].DB_Escape_Identifier( 'maxvalue' ) $ ' + ? WHERE type = ?' }"
			 FIELDS	= "l.increment, l.type">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvROLLBACK NAME = "Merchant">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'MER-DBE-KEY-00010', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMIT NAME = "Merchant">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

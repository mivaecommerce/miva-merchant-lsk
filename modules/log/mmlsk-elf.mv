<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2020 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-LOG-ELF-
| Next Error Code: 11   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-elf">
	<MvASSIGN NAME = "l.module:name"		VALUE = "e-Urchin Log">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.00">
	<MvASSIGN NAME = "l.module:description"	VALUE = "Urchin was discontinued by Google and replaced with Google Analytics.  Although it is no longer supported, you can still use Urchin but you must enable the e-Urchin log.">
	<MvASSIGN NAME = "l.module:features"	VALUE = "log, vis_log, data_store, provision_store">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'ELF
						  (
							logfile		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )	$ ',
							hostnames	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							format		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 1 )		$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-LOG-ELF-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.elf:logfile"	VALUE = "elf.log">
	<MvASSIGN NAME = "l.elf:hostnames"	VALUE = 0>
	<MvASSIGN NAME = "l.elf:format"		VALUE = 2>

	<MvFUNCTIONRETURN VALUE = "{ ELF_Insert( l.elf ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'ELF' }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Logging_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Log_ELF:logfile" 	VALUE = "{ trim( g.Log_ELF:logfile ) }">
	<MvASSIGN NAME = "g.Log_ELF:hostnames" 	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Log_ELF:hostnames ) }">

	<MvIF EXPR = "{ len( g.Log_ELF:logfile ) EQ 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'ELF', 'Log_ELF:logfile', 'Please specify a log file' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Logging_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT ELF_Update( g.Log_ELF ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-LOG-ELF-00009', 'ELF Configuration updated' ) }">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Logging_Tabs" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "ELF:e-Urchin Log">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Logging_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.load_fields }">
		<MvIF EXPR = "{ NOT ELF_Load( g.Log_ELF ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.tab EQ 'ELF' }">
		<table border=0 cellpadding = 2 cellspacing = 0>
		<tr><td nowrap>
			<b>Log File:</b>
		</td><td width="100%">
			<input type="text" name="Log_ELF:logfile" size=40 value = "{ encodeentities( g.Log_ELF:logfile ) }">
		</td></tr>

		<tr><td nowrap>
			&nbsp;
		</td><td width="100%">
			<MvIF EXPR = "{ g.Log_ELF:hostnames }">
				<input type="checkbox" name="Log_ELF:hostnames" value="Yes" checked> Log Remote Hostnames
			<MvELSE>
				<input type="checkbox" name="Log_ELF:hostnames" value="Yes"> Log Remote Hostnames
			</MvIF>
		</td></tr>

		<tr><td nowrap>
			<b>Log Format:</b>
		</td><td width="100%">
			<select name="Log_ELF:format">
				<MvDO FILE = "{ g.Module_Library_Utilities }" NAME = "l.null" VALUE = "{ DrawOption( '1', g.Log_ELF:format, 'ELF' ) }">
				<MvDO FILE = "{ g.Module_Library_Utilities }" NAME = "l.null" VALUE = "{ DrawOption( '2', g.Log_ELF:format, 'ELF2' ) }">
			</select>
		</td></tr>
		</table>
	<MvELSE>
		<MvHIDE FIELDS = "g.Log_ELF">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LogModule_Action" PARAMETERS = "module var, action" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.action EQ 'AUTH' }">
		<MvIF EXPR = "{ NOT ELF_Load( l.elf ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ l.elf:format EQ '1' }">
			<MvFUNCTIONRETURN VALUE = "{ ELF_Export_ELF1( l.elf ) }">
		<MvELSEIF EXPR = "{ l.elf:format EQ '2' }">
			<MvFUNCTIONRETURN VALUE = "{ ELF_Export_ELF2( l.elf ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LogModule_Screen" PARAMETERS = "module var, screen" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LogModule_UIException" PARAMETERS = "module var, code" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ELF_Export_ELF1" PARAMETERS = "elf var" STANDARDOUTPUTLEVEL = "">
	<MvLOCKFILE FILE = "{ l.elf:logfile }">
		<MvASSIGN NAME = "l.result" VALUE = "{ ELF_Export_ELF1_LowLevel( l.elf ) }">
	</MvLOCKFILE>

	<MvFUNCTIONRETURN VALUE = "{ l.result }">
</MvFUNCTION>

<MvFUNCTION NAME = "ELF_Export_ELF1_LowLevel" PARAMETERS = "elf var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.bangorder"	VALUE = "{ '!' $ g.Order:id }">

	<MvIF EXPR = "{ l.elf:hostnames }">
		<MvASSIGN NAME = "l.remote_host" VALUE = "{ s.remote_host }">
	<MvELSE>
		<MvASSIGN NAME = "l.remote_host" VALUE = "{ s.remote_addr }">
	</MvIF>

	<MvASSIGN NAME = "l.store"		VALUE = "{ '"' $ g.Store:code $ '"' }">

	<MvASSIGN NAME = "l.session_id" VALUE = "{ g.Session_ID }">
	<MvASSIGN NAME = "l.timezone"	VALUE = "{ timezone() }">

	<MvIF EXPR = "{ l.timezone LT 0 }">	<MvASSIGN NAME = "l.timezone_prefix" VALUE = "-">
	<MvELSE>							<MvASSIGN NAME = "l.timezone_prefix" VALUE = "+">
	</MvIF>
	
	<MvASSIGN NAME = "l.time"		VALUE = "{ '[' $ padl( s.dyn_tm_mday, 2, '0' ) $ '/' $ s.dyn_stm_mon $ '/' $ s.dyn_tm_year $ ':' $
											   padl( s.dyn_tm_hour, 2, '0' ) $ ':' $ padl( s.dyn_tm_min, 2, '0' ) $ ':' $
											   padl( s.dyn_tm_sec, 2, '0' ) $ ' ' $ l.timezone_prefix $ padl( abs( l.timezone ) * 100, 4, '0' ) $ ']' }">

	<MvASSIGN NAME = "l.total" 		VALUE = "{ g.Order:total }">
	<MvASSIGN NAME = "l.tax" 		VALUE = "{ g.Order:tax }">
	<MvASSIGN NAME = "l.shipping" 	VALUE = "{ g.Order:shipping }">
	<MvASSIGN NAME = "l.bill_city" 	VALUE = "{ '"' $ g.Order:bill_city $ '"' }">
	<MvASSIGN NAME = "l.bill_state" VALUE = "{ '"' $ g.Order:bill_state $ '"' }">
	<MvASSIGN NAME = "l.bill_zip" 	VALUE = "{ '"' $ g.Order:bill_zip $ '"' }">
	<MvASSIGN NAME = "l.bill_cntry" VALUE = "{ '"' $ g.Order:bill_cntry $ '"' }">

	<MvEXPORT FILE = "{ l.elf:logfile }"
			  FIELDS = "l.bangorder, l.remote_host, l.store, l.session_id, l.time, l.total, l.tax,
						l.shipping, l.bill_city, l.bill_state, l.bill_zip, l.bill_cntry"
			  DELIMITER = "{ asciichar( 9 ) }">
	<MvIF EXPR = "{ g.MvEXPORT_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-LOG-ELF-00002', g.MvEXPORT_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderitem_pos"		VALUE = 1>
	<MvASSIGN NAME = "l.orderitem_count"	VALUE = "{ [ g.Module_Library_DB ].OrderItemList_Load_Order( g.Order:id, l.orderitems ) }">

	<MvWHILE EXPR = "{ l.orderitem_pos LE l.orderitem_count }">
		<MvASSIGN NAME = "l.code" 		VALUE = "{ '"' $ l.orderitems[ l.orderitem_pos ]:code $ '"' }">
		<MvASSIGN NAME = "l.name" 		VALUE = "{ '"' $ l.orderitems[ l.orderitem_pos ]:name $ '"' }">
		<MvASSIGN NAME = "l.price" 		VALUE = "{ l.orderitems[ l.orderitem_pos ]:price }">
		<MvASSIGN NAME = "l.quantity" 	VALUE = "{ l.orderitems[ l.orderitem_pos ]:quantity }">
		<MvASSIGN NAME = "l.upsold" 	VALUE = "{ l.orderitems[ l.orderitem_pos ]:upsold }">

		<MvASSIGN NAME = "l.variation"				VALUE = "">
		<MvASSIGN NAME = "l.orderoption_pos"		VALUE = 1>
		<MvASSIGN NAME = "l.orderoption_count"		VALUE = "{ [ g.Module_Library_DB ].OrderOptionList_Load_Line( l.orderitems[ l.orderitem_pos ]:line_id, l.orderoptions ) }">

		<MvWHILE EXPR = "{ l.orderoption_pos LE l.orderoption_count }">
			<MvIF EXPR = "{ len( l.variation ) }">
				<MvASSIGN NAME = "l.variation"		VALUE = "{ l.variation $ '&' }">
			</MvIF>

			<MvASSIGN NAME = "l.price"				VALUE = "{ l.price + l.orderoptions[ l.orderoption_pos ]:price }">

			<MvIF EXPR = "{ l.orderoptions[ l.orderoption_pos ]:option_id }">
				<MvASSIGN NAME = "l.variation"		VALUE = "{ l.variation $ encodeattribute( l.orderoptions[ l.orderoption_pos ]:attr_code ) $ '=' $
																			 encodeattribute( l.orderoptions[ l.orderoption_pos ]:opt_code ) }">
			<MvELSE>
				<MvASSIGN NAME = "l.variation"		VALUE = "{ l.variation $ encodeattribute( l.orderoptions[ l.orderoption_pos ]:attr_code ) }">
			</MvIF>

			<MvASSIGN NAME = "l.orderoption_pos"	VALUE = "{ l.orderoption_pos + 1 }">
		</MvWHILE>

		<MvIF EXPR = "{ len( l.variation ) }">
			<MvASSIGN NAME = "l.variation" VALUE = "{ '"' $ l.variation $ '"' }">
		<MvELSE>
			<MvASSIGN NAME = "l.variation" VALUE = "-">
		</MvIF>

		<MvEXPORT FILE = "{ l.elf:logfile }"
				  FIELDS = "g.Order:id, l.code, l.name, l.variation, l.price, l.quantity, l.upsold"
				  DELIMITER = "{ asciichar( 9 ) }">
		<MvIF EXPR = "{ g.MvEXPORT_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-LOG-ELF-00003', g.MvEXPORT_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "l.orderitem_pos"	VALUE = "{ l.orderitem_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ELF_Export_ELF2" PARAMETERS = "elf var" STANDARDOUTPUTLEVEL = "">
	<MvLOCKFILE FILE = "{ l.elf:logfile }">
		<MvASSIGN NAME = "l.result" VALUE = "{ ELF_Export_ELF2_LowLevel( l.elf ) }">
	</MvLOCKFILE>

	<MvFUNCTIONRETURN VALUE = "{ l.result }">
</MvFUNCTION>

<MvFUNCTION NAME = "ELF_Export_ELF2_LowLevel" PARAMETERS = "elf var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.bangorder"	VALUE = "{ '!' $ g.Order:id }">

	<MvIF EXPR = "{ l.elf:hostnames }">
		<MvASSIGN NAME = "l.remote_host" VALUE = "{ s.remote_host }">
	<MvELSE>
		<MvASSIGN NAME = "l.remote_host" VALUE = "{ s.remote_addr }">
	</MvIF>

	<MvASSIGN NAME = "l.store"		VALUE = "{ '"' $ g.Store:code $ '"' }">

	<MvASSIGN NAME = "l.session_id" VALUE = "{ g.Session_ID }">
	<MvASSIGN NAME = "l.timezone"	VALUE = "{ timezone() }">

	<MvIF EXPR = "{ l.timezone LT 0 }">	<MvASSIGN NAME = "l.timezone_prefix" VALUE = "-">
	<MvELSE>							<MvASSIGN NAME = "l.timezone_prefix" VALUE = "+">
	</MvIF>
	
	<MvASSIGN NAME = "l.time"		VALUE = "{ '[' $ padl( s.dyn_tm_mday, 2, '0' ) $ '/' $ s.dyn_stm_mon $ '/' $ s.dyn_tm_year $ ':' $
											   padl( s.dyn_tm_hour, 2, '0' ) $ ':' $ padl( s.dyn_tm_min, 2, '0' ) $ ':' $
											   padl( s.dyn_tm_sec, 2, '0' ) $ ' ' $ l.timezone_prefix $ padl( abs( l.timezone ) * 100, 4, '0' ) $ ']' }">

	<MvASSIGN NAME = "l.total" 		VALUE = "{ g.Order:total }">
	<MvASSIGN NAME = "l.tax" 		VALUE = "{ g.Order:tax }">
	<MvASSIGN NAME = "l.shipping" 	VALUE = "{ g.Order:shipping }">
	<MvASSIGN NAME = "l.bill_city" 	VALUE = "{ '"' $ g.Order:bill_city $ '"' }">
	<MvASSIGN NAME = "l.bill_state" VALUE = "{ '"' $ g.Order:bill_state $ '"' }">
	<MvASSIGN NAME = "l.bill_zip" 	VALUE = "{ '"' $ g.Order:bill_zip $ '"' }">
	<MvASSIGN NAME = "l.bill_cntry" VALUE = "{ '"' $ g.Order:bill_cntry $ '"' }">

	<MvASSIGN NAME = "l.user_agent" VALUE = "{ '"' $ s.http_user_agent $ '"' }">
	<MvASSIGN NAME = "l.cookie" 	VALUE = "{ '"' $ s.http_cookie $ '"' }">

	<MvEXPORT FILE = "{ l.elf:logfile }"
			  FIELDS = "l.bangorder, l.remote_host, l.time, l.store, l.session_id, l.total, l.tax,
						l.shipping, l.bill_city, l.bill_state, l.bill_zip, l.bill_cntry, l.user_agent, l.cookie"
			  DELIMITER = "{ asciichar( 9 ) }">
	<MvIF EXPR = "{ g.MvEXPORT_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-LOG-ELF-00004', g.MvEXPORT_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderitem_pos"		VALUE = 1>
	<MvASSIGN NAME = "l.orderitem_count"	VALUE = "{ [ g.Module_Library_DB ].OrderItemList_Load_Order( g.Order:id, l.orderitems ) }">

	<MvWHILE EXPR = "{ l.orderitem_pos LE l.orderitem_count }">
		<MvASSIGN NAME = "l.code" 		VALUE = "{ '"' $ l.orderitems[ l.orderitem_pos ]:code $ '"' }">
		<MvASSIGN NAME = "l.name" 		VALUE = "{ '"' $ l.orderitems[ l.orderitem_pos ]:name $ '"' }">
		<MvASSIGN NAME = "l.price" 		VALUE = "{ l.orderitems[ l.orderitem_pos ]:price }">
		<MvASSIGN NAME = "l.quantity" 	VALUE = "{ l.orderitems[ l.orderitem_pos ]:quantity }">
		<MvASSIGN NAME = "l.upsold" 	VALUE = "{ l.orderitems[ l.orderitem_pos ]:upsold }">

		<MvASSIGN NAME = "l.variation"				VALUE = "">
		<MvASSIGN NAME = "l.orderoption_pos"		VALUE = 1>
		<MvASSIGN NAME = "l.orderoption_count"		VALUE = "{ [ g.Module_Library_DB ].OrderOptionList_Load_Line( l.orderitems[ l.orderitem_pos ]:line_id, l.orderoptions ) }">

		<MvWHILE EXPR = "{ l.orderoption_pos LE l.orderoption_count }">
			<MvIF EXPR = "{ len( l.variation ) }">
				<MvASSIGN NAME = "l.variation"		VALUE = "{ l.variation $ '&' }">
			</MvIF>

			<MvASSIGN NAME = "l.price"				VALUE = "{ l.price + l.orderoptions[ l.orderoption_pos ]:price }">

			<MvIF EXPR = "{ l.orderoptions[ l.orderoption_pos ]:option_id }">
				<MvASSIGN NAME = "l.variation"		VALUE = "{ l.variation $ encodeattribute( l.orderoptions[ l.orderoption_pos ]:attr_code ) $ '=' $
																			 encodeattribute( l.orderoptions[ l.orderoption_pos ]:opt_code ) }">
			<MvELSE>
				<MvASSIGN NAME = "l.variation"		VALUE = "{ l.variation $ encodeattribute( l.orderoptions[ l.orderoption_pos ]:attr_code ) }">
			</MvIF>

			<MvASSIGN NAME = "l.orderoption_pos"	VALUE = "{ l.orderoption_pos + 1 }">
		</MvWHILE>

		<MvIF EXPR = "{ len( l.variation ) }">
			<MvASSIGN NAME = "l.variation"			VALUE = "{ '"' $ l.variation $ '"' }">
		<MvELSE>
			<MvASSIGN NAME = "l.variation"			VALUE = "-">
		</MvIF>

		<MvEXPORT FILE = "{ l.elf:logfile }"
				  FIELDS = "g.Order:id, l.remote_host, l.time, l.code, l.name, l.variation,
							l.price, l.quantity, l.upsold, l.user_agent, l.cookie"
				  DELIMITER = "{ asciichar( 9 ) }">
		<MvIF EXPR = "{ g.MvEXPORT_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-LOG-ELF-00005', g.MvEXPORT_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "l.orderitem_pos"	VALUE = "{ l.orderitem_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Store" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT ELF_Load( l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List( 'O', l.provide_xml,		'Format',		l.settings:format,		'ELF,ELF2', '1,2' ) OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean( 'O', l.provide_xml,	'LogHostNames',	l.settings:hostnames ) OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml,		'File',			l.settings:logfile ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT ELF_Update( l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-LOG-ELF-00010', 'ELF Configuration updated' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ELF_Load" PARAMETERS = "elf var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ELF"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'ELF' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-LOG-ELF-00006', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.elf:logfile" 	VALUE = "{ ELF.d.logfile }">
	<MvASSIGN NAME = "l.elf:hostnames" 	VALUE = "{ ELF.d.hostnames }">
	<MvASSIGN NAME = "l.elf:format" 	VALUE = "{ ELF.d.format }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ELF">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ELF_Insert" PARAMETERS = "elf var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'ELF
						  ( logfile, hostnames, format )
						  VALUES
						  ( ?, ?, ? )' }"
			 FIELDS	= "l.elf:logfile, l.elf:hostnames, l.elf:format">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-LOG-ELF-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ELF_Update" PARAMETERS = "elf var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'ELF
						  SET
							logfile		= ?,
							hostnames	= ?,
							format		= ?' }"
			 FIELDS	= "l.elf:logfile, l.elf:hostnames, l.elf:format">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-LOG-ELF-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

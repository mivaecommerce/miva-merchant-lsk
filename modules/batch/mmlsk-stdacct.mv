<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2020 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-BCH-STD-
| Next Error Code: 1    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-stdacct">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Standard Batch Report">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.00">
	<MvASSIGN NAME = "l.module:features"	VALUE = "batchreport">
</MvFUNCTION>

<MvFUNCTION NAME = "BatchReportModule_Report" PARAMETERS = "module var, batch_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<head>
	<title>Miva Merchant Administration: Standard Batch Report</title>
	<MvEVAL EXPR = "{ [ g.Module_Admin ].StyleSheet() }">
	</head>

	<body bgcolor="#ffffff">
	<b>Standard Batch Report</b></font><br><br>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Module_Load_ID( g.Store:currncy_id, l.currency_module ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.order_count" 		VALUE = "{ [ g.Module_Library_DB ].OrderList_Load_Batch( l.batch_id, l.order ) }">

	<MvASSIGN NAME = "l.pos" VALUE = 1>
	<MvWHILE EXPR = "{ l.pos LE l.order_count }">
		<table border=0 cellpadding = 0 cellspacing = 0 width = "100%">
		<tr><td>
			<table border=0 cellpadding = 2 cellspacing = 0 width = "100%">
			<tr><td align="left" valign="middle" class="be_header" colspan=4>
				<b>
				Order #<MvEVAL EXPR = "{ l.order[ l.pos ]:id }">
				</b>
			</td></tr>

			<tr><td align="left" valign="middle" class="be_row_alt" colspan=2>
				<b>
				Ship To:
				</b>
			</td><td align="left" valign="middle" class="be_row_alt" colspan=2>
				<b>
				Bill To:
				</b>
			</td></tr>

			<tr><td align="left" valign="middle">
				<b>
				Name:
				</b>
			</td><td align="left" valign="middle">
				<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:ship_fname ) }"> <MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:ship_lname ) }">
			</td><td align="left" valign="middle">
				<b>
				Name:
				</b>
			</td><td align="left" valign="middle">
				<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:bill_fname ) }"> <MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:bill_lname ) }">
			</td></tr>

			<tr><td align="left" valign="middle">
				<b>
				Email Address:
				</b>
			</td><td align="left" valign="middle">
				<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:ship_email ) }">
			</td><td align="left" valign="middle">
				<b>
				Email Address:
				</b>
			</td><td align="left" valign="middle">
				<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:bill_email ) }">
			</td></tr>

			<tr><td align="left" valign="middle">
				<b>
				Phone Number:
				</b>
			</td><td align="left" valign="middle">
				<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:ship_phone ) }">
			</td><td align="left" valign="middle">
				<b>
				Phone Number:
				</b>
			</td><td align="left" valign="middle">
				<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:bill_phone ) }">
			</td></tr>

			<tr><td align="left" valign="middle">
				<b>
				Fax Number:
				</b>
			</td><td align="left" valign="middle">
				<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:ship_fax ) }">
			</td><td align="left" valign="middle">
				<b>
				Fax Number:</b>
			</td><td align="left" valign="middle">
				<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:bill_fax ) }">
			</td></tr>

			<tr><td align="left" valign="middle">
				<b>
				Company:
				</b>
			</td><td align="left" valign="middle">
				<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:ship_comp ) }">
			</td><td align="left" valign="middle">
				<b>
				Company:
				</b>
			</td><td align="left" valign="middle">
				<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:bill_comp ) }">
			</td></tr>

			<tr><td align="left" valign="middle">
				<b>
				Address:
				</b>
			</td><td align="left" valign="middle">
				<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:ship_addr1 ) }">
			</td><td align="left" valign="middle">
				<b>
				Address:
				</b>
			</td><td align="left" valign="middle">
				<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:bill_addr1 ) }">
			</td></tr>

			<tr><td align="left" valign="middle">
				<b>
				Address2:
				</b>
			</td><td align="left" valign="middle">
				<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:ship_addr2 ) }">
			</td><td align="left" valign="middle">
				<b>
				Address2:
				</b>
			</td><td align="left" valign="middle">
				<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:bill_addr2 ) }">
			</td></tr>

			<tr><td>
				&nbsp;
			</td><td align="left" valign="middle">
				<MvIF EXPR = "{ l.order[ l.pos ]:ship_cntry EQ 'DE' }">
					<MvIF EXPR = "{ len( l.order[ l.pos ]:ship_zip ) }">
						<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:ship_zip ) }">
					</MvIF>
				</MvIF>

				<MvIF EXPR = "{ len( l.order[ l.pos ]:ship_city ) }">
					<MvIF EXPR = "{ len( l.order[ l.pos ]:ship_state ) }">
						<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:ship_city ) }">,
					<MvELSE>
						<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:ship_city ) }">
					</MvIF>
				</MvIF>

				<MvIF EXPR = "{ len( l.order[ l.pos ]:ship_state ) }">
					<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:ship_state ) }">
				</MvIF>

				<MvIF EXPR = "{ l.order[ l.pos ]:ship_cntry NE 'DE' }">
					<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:ship_zip ) }">
				</MvIF>
			</td><td>
				&nbsp;
			</td><td align="left" valign="middle">
				<MvIF EXPR = "{ l.order[ l.pos ]:bill_cntry EQ 'DE' }">
					<MvIF EXPR = "{ len( l.order[ l.pos ]:bill_zip ) }">
						<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:bill_zip ) }">
					</MvIF>
				</MvIF>

				<MvIF EXPR = "{ len( l.order[ l.pos ]:bill_city ) }">
					<MvIF EXPR = "{ len( l.order[ l.pos ]:bill_state ) }">
						<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:bill_city ) }">,
					<MvELSE>
						<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:bill_city ) }">
					</MvIF>
				</MvIF>

				<MvIF EXPR = "{ len( l.order[ l.pos ]:bill_state ) }">
					<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:bill_state ) }">
				</MvIF>

				<MvIF EXPR = "{ l.order[ l.pos ]:bill_cntry NE 'DE' }">
					<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:bill_zip ) }">
				</MvIF>
			</td></tr>

			<tr><td>
				&nbsp;
			</td><td align="left" valign="middle">
				<MvIF EXPR = "{ l.order[ l.pos ]:ship_cntry EQ 'DE' }">
					<MvASSIGN NAME = "l.country_load" VALUE = "{ [ g.Module_Library_DB ].Country_Load_Alpha( l.order[ l.pos ]:ship_cntry, l.country ) }">
					<MvIF EXPR = "{ l.country_load }">
						<MvEVAL EXPR = "{ l.country:name }">
					<MvELSE>
						<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:ship_cntry ) }">
					</MvIF>
				<MvELSE>
					<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:ship_cntry ) }">
				</MvIF>
			</td><td>
				&nbsp;
			</td><td align="left" valign="middle">
				<MvIF EXPR = "{ l.order[ l.pos ]:bill_cntry EQ 'DE' }">
					<MvASSIGN NAME = "l.country_load" VALUE = "{ [ g.Module_Library_DB ].Country_Load_Alpha( l.order[ l.pos ]:bill_cntry, l.country ) }">
					<MvIF EXPR = "{ l.country_load }">
						<MvEVAL EXPR = "{ l.country:name }">
					<MvELSE>
						<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:bill_cntry ) }">
					</MvIF>
				<MvELSE>
					<MvEVAL EXPR = "{ encodeentities( l.order[ l.pos ]:bill_cntry ) }">
				</MvIF>
			</td></tr>
			</table>

			<br>

			<table border=0 cellpadding = 2 cellspacing = 0 width = "100%">
			<tr><td align="left" valign="middle" class="be_row_alt">
				<b>
				Shipping Information:
				</b>
			</td><td align="left" valign="middle" class="be_row_alt">
				<b>
				Payment Information:
				</b>
			</td></tr>

			<tr><td align="left" valign="top">
				<MvIF EXPR = "{ l.order[ l.pos ]:ship_id }">
					<table border=0 cellpadding = 2 cellspacing = 0>
					<MvIF EXPR = "{ [ g.Module_Library_DB ].Module_Load_ID( l.order[ l.pos ]:ship_id, l.shipping_module ) }">
						<MvDO FILE = "{ g.Module_Root $ l.shipping_module:module }" NAME = "l.fields" VALUE = "{ ShippingModule_Report_Fields( l.shipping_module ) }">

						<MvASSIGN NAME = "l.fld_pos" VALUE = 1>
						<MvASSIGN NAME = "l.field" VALUE = "{ trim( gettoken( l.fields, ',', l.fld_pos ) ) }">
						<MvWHILE EXPR = "{ len( l.field ) }">
							<MvDO FILE = "{ g.Module_Root $ l.shipping_module:module }" NAME = "l.label" VALUE = "{ ShippingModule_Report_Label( l.shipping_module, l.field ) }">
							<MvDO FILE = "{ g.Module_Root $ l.shipping_module:module }" NAME = "l.value" VALUE = "{ ShippingModule_Report_Value( l.shipping_module, l.field, l.order[ l.pos ]:ship_data ) }">

							<tr><td align="left" valign="top" nowrap>
								<b><MvEVAL EXPR = "{ l.label }"></b>
							</td><td align="left" valign="top">
								<MvEVAL EXPR = "{ l.value }">
							</td></tr>

							<MvASSIGN NAME = "l.fld_pos" VALUE = "{ l.fld_pos + 1 }">
							<MvASSIGN NAME = "l.field" VALUE = "{ trim( gettoken( l.fields, ',', l.fld_pos ) ) }">
						</MvWHILE>
					</MvIF>
					</table>
				</MvIF>
			</td><td align="left" valign="top">
				<MvIF EXPR = "{ l.order[ l.pos ]:pay_id }">
					<MvIF EXPR = "{ [ g.Module_Library_DB ].Module_Load_ID( l.order[ l.pos ]:pay_id, l.payment_module ) }">
						<MvASSIGN NAME = "l.secure_data" VALUE = "{ l.order[ l.pos ]:pay_secdat }">

						<MvIF EXPR = "{ l.order[ l.pos ]:pay_secid }">
							<MvIF EXPR = "{ len( l.secure_data ) }">
								<MvIF EXPR = "{ len( g.passphrase[ l.order[ l.pos ]:pay_secid ] ) }">
									<MvASSIGN NAME = "l.RSA_key" INDEX = "{ l.order[ l.pos ]:pay_secid }" MEMBER = "valid" VALUE = "{ [ g.Module_Library_Crypto ].Decrypt_Order( l.module:name, l.order[ l.pos ], g.passphrase[ l.order[ l.pos ]:pay_secid ], l.decrypted_data ) }">
								<MvELSE>
									<MvASSIGN NAME = "l.RSA_key" INDEX = "{ l.order[ l.pos ]:pay_secid }" MEMBER = "valid" VALUE = 0>
								</MvIF>

								<MvIF EXPR = "{ NOT l.RSA_key[ l.order[ l.pos ]:pay_secid ]:valid }">
									<MvASSIGN NAME = "l.decrypted_data" VALUE = "">
								</MvIF>
							</MvIF>
						<MvELSE>
							<MvASSIGN NAME = "l.decrypted_data" VALUE = "{ miva_array_deserialize( l.secure_data ) }">
						</MvIF>

						<MvDO FILE = "{ g.Module_Root $ l.payment_module:module }" NAME = "l.desc" VALUE = "{ PaymentModule_Report_Description( l.payment_module, miva_array_deserialize( l.order[ l.pos ]:pay_data ) ) }">
						<MvDO FILE = "{ g.Module_Root $ l.payment_module:module }" NAME = "l.fields" VALUE = "{ PaymentModule_Report_Fields( l.payment_module, miva_array_deserialize( l.order[ l.pos ]:pay_data ), l.decrypted_data ) }">

						<MvIF EXPR = "{ l.desc }">
							<b><MvEVAL EXPR = "{ l.desc }"></b><br>
						</MvIF>

						<table border=0 cellpadding = 2 cellspacing = 0>

						<MvASSIGN NAME = "l.fld_pos" VALUE = 1>
						<MvASSIGN NAME = "l.field" VALUE = "{ trim( gettoken( l.fields, ',', l.fld_pos ) ) }">
						<MvWHILE EXPR = "{ len( l.field ) }">
							<MvDO FILE = "{ g.Module_Root $ l.payment_module:module }" NAME = "l.label" VALUE = "{ PaymentModule_Report_Label( l.payment_module, l.field ) }">
							<MvIF EXPR = "{ l.payment_module:api_ver LT 4.14 }">
								<MvDO FILE = "{ g.Module_Root $ l.payment_module:module }" NAME = "l.value" VALUE = "{ PaymentModule_Report_Value( l.payment_module, l.field ) }">
							<MvELSE>
								<MvDO FILE = "{ g.Module_Root $ l.payment_module:module }" NAME = "l.value" VALUE = "{ PaymentModule_Report_Value( l.payment_module, l.field, miva_array_deserialize( l.order[ l.pos ]:pay_data ), l.decrypted_data ) }">
							</MvIF>

							<tr><td align="left" valign="top" nowrap>
								<b><MvEVAL EXPR = "{ l.label }"></b>
							</td><td align="left" valign="top">
								<MvEVAL EXPR = "{ l.value }">
							</td></tr>

							<MvASSIGN NAME = "l.fld_pos" 	VALUE = "{ l.fld_pos + 1 }">
							<MvASSIGN NAME = "l.field" 		VALUE = "{ trim( gettoken( l.fields, ',', l.fld_pos ) ) }">
						</MvWHILE>

						</table>
					</MvIF>
				</MvIF>
			</td></tr>
			</table>

			<br>

			<table border=0 cellpadding = 2 cellspacing = 0 width = "100%">
			<tr>

			<MvIF EXPR = "{ l.editable }">
				<td align="left" valign="middle" class="be_row_alt">
					&nbsp;
				</td>
			</MvIF>

			<td align="left" valign="middle" class="be_row_alt">
				<b>
				Code
				</b>
			</td><td align="left" valign="middle" class="be_row_alt">
				<b>
				Product
				</b>
			</td><td align="left" valign="middle" class="be_row_alt">
				<b>
				Quantity
				</b>
			</td><td align="right" valign="middle" class="be_row_alt">
				<b>
				Price/Ea.
				</b>
			</td><td align="right" valign="middle" class="be_row_alt">
				<b>
				Total
				</b>
			</td></tr>

			<MvASSIGN NAME = "l.orderitem_count" VALUE = "{ [ g.Module_Library_DB ].OrderItemList_Load_Order( l.order[ l.pos ]:id, l.order_item ) }">

			<MvASSIGN NAME = "l.ord_itm_pos" VALUE = 1>
			<MvWHILE EXPR = "{ l.ord_itm_pos LE l.orderitem_count }">
				<tr><td align="left" valign="middle" nowrap>
					<MvEVAL EXPR = "{ encodeentities( l.order_item[ l.ord_itm_pos ]:code ) }">
				</td><td align="left" valign="middle" width="100%" nowrap>
					<MvEVAL EXPR = "{ encodeentities( l.order_item[ l.ord_itm_pos ]:name ) }">

					<MvIF EXPR = "{ l.order_item[ l.ord_itm_pos ]:upsold }">
						(Special Offer)
					</MvIF>

				</td><td align="right" valign="middle" nowrap>
					<MvEVAL EXPR = "{ encodeentities( l.order_item[ l.ord_itm_pos ]:quantity ) }">
				</td><td align="right" valign="middle" nowrap>
					<MvEVAL EXPR = "{ [ g.Module_Root $ l.currency_module:module ].CurrencyModule_AddFormatting( l.currency_module, l.order_item[ l.ord_itm_pos ]:price ) }">
				</td><td align="right" valign="middle" nowrap>
					<MvEVAL EXPR = "{ [ g.Module_Root $ l.currency_module:module ].CurrencyModule_AddFormatting( l.currency_module, l.order_item[ l.ord_itm_pos ]:price * l.order_item[ l.ord_itm_pos ]:quantity ) }">
				</td></tr>

				<MvASSIGN NAME = "l.orderoption_count" VALUE = "{ [ g.Module_Library_DB ].OrderOptionList_Load_Line( l.order_item[ l.ord_itm_pos ]:line_id, l.order_option ) }">

				<MvASSIGN NAME = "l.ord_opt_pos" VALUE = 1>
				<MvWHILE EXPR = "{ l.ord_opt_pos LE l.orderoption_count }">
					<tr><td align="left">
						&nbsp;
					</td><td align="left">
						<MvIF EXPR = "{ l.order_option[ l.ord_opt_pos ]:option_id }">
							<MvEVAL EXPR = "{ encodeentities( l.order_option[ l.ord_opt_pos ]:attr_code ) }">: <MvEVAL EXPR = "{ encodeentities( l.order_option[ l.ord_opt_pos ]:opt_code ) }">
						<MvELSE>
							<MvIF EXPR = "{ len( l.order_option[ l.ord_opt_pos ]:data ) }">
								<MvASSIGN NAME = "l.data" VALUE = "{ encodeentities( l.order_option[ l.ord_opt_pos ]:data ) }">
								<MvASSIGN NAME = "l.data" VALUE = "{ glosub( l.order_option[ l.ord_opt_pos ]:data, asciichar(10), '<BR>' ) }">
								<MvEVAL EXPR = "{ encodeentities( l.order_option[ l.ord_opt_pos ]:attr_code) }">: <MvEVAL EXPR = "{ l.data }">
							<MvELSE>
								<MvIF EXPR = "{ len( l.order_option[ l.ord_opt_pos ]:data_long ) }">
									<MvEVAL EXPR = "{ encodeentities( l.order_option[ l.ord_opt_pos ]:attr_code ) }">: <MvEVAL EXPR = "{ encodeentities( l.order_option[ l.ord_opt_pos ]:data_long ) }">
								<MvELSE>
									<MvEVAL EXPR = "{ encodeentities( l.order_option[ l.ord_opt_pos ]:attr_code ) }">
								</MvIF>
							</MvIF>
						</MvIF>
					</td><td align="left">
						&nbsp;
					</td><td align="right">
						<MvIF EXPR = "{ l.order_option[ l.ord_opt_pos ]:price NE 0 }">
							<MvEVAL EXPR = "{ [ g.Module_Root $ l.currency_module:module ].CurrencyModule_AddFormatting( l.currency_module, l.order_option[ l.ord_opt_pos ]:price ) }">
						<MvELSE>
							&nbsp;
						</MvIF>
					</td><td align="right">
						<MvIF EXPR = "{ l.order_option[ l.ord_opt_pos ]:price NE 0 }">
							<MvEVAL EXPR = "{ [ g.Module_Root $ l.currency_module:module ].CurrencyModule_AddFormatting( l.currency_module, l.order_option[ l.ord_opt_pos ]:price * l.order_item[ l.ord_itm_pos ]:quantity ) }">
						<MvELSE>
							&nbsp;
						</MvIF>
					</td></tr>

					<MvASSIGN NAME = "l.ord_opt_pos" VALUE = "{ l.ord_opt_pos + 1 }">
				</MvWHILE>

				<MvASSIGN NAME = "l.ord_itm_pos" VALUE = "{ l.ord_itm_pos + 1 }">
			</MvWHILE>

			<MvASSIGN NAME = "l.ordercharge_count" VALUE = "{ [ g.Module_Library_DB ].OrderChargeList_Load_Type( l.order[ l.pos ]:id, 'SHIPPING', l.order_charge ) }">
			<MvIF EXPR = "{ NOT l.ordercharge_count }">
				<tr><td align="right" valign="middle" colspan=3>
					&nbsp;
				</td><td align="right" valign="middle" nowrap>
					<MvIF EXPR = "{ NOT l.order[ l.pos ]:ship_id }">
						<MvASSIGN NAME = "l.description" VALUE = "">
					<MvELSE>
						<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Module_Load_ID( l.order[ l.pos ]:ship_id, l.shipping_module ) }">
							<MvFUNCTIONRETURN VALUE = 0>
						</MvIF>

						<MvDO FILE = "{ g.Module_Root $ l.shipping_module:module }" NAME = "l.description" VALUE = "{ ShippingModule_Description( l.shipping_module, l.order[ l.pos ]:ship_data ) }">
					</MvIF>

						<i>

						Shipping:

						<MvIF EXPR = "{ l.description }">
							<MvEVAL EXPR = "{ l.description }">:
						</MvIF>
						</i>
				</td><td align="right" valign="middle">
						<MvEVAL EXPR = "{ [ g.Module_Root $ l.currency_module:module ].CurrencyModule_AddFormatting( l.currency_module, l.order[ l.pos ]:shipping ) }">
				</td></tr>
			</MvIF>

			<MvASSIGN NAME = "l.ordercharge_count" VALUE = "{ [ g.Module_Library_DB ].OrderChargeList_Load_Type( l.order[ l.pos ]:id, 'TAX', l.order_charge ) }">
			<MvIF EXPR = "{ NOT l.ordercharge_count }">
				<tr><td align="right" valign="middle" colspan=3>
					&nbsp;
				</td><td align="right" valign="middle" nowrap>
					<i>
					Sales Tax:
					</i>
				</td><td align="right" valign="middle">
					<MvEVAL EXPR = "{ [ g.Module_Root $ l.currency_module:module ].CurrencyModule_AddFormatting( l.currency_module, l.order[ l.pos ]:tax ) }">
				</td></tr>
			</MvIF>

			<MvASSIGN NAME = "l.ordercharge_count" VALUE = "{ [ g.Module_Library_DB ].OrderChargeList_Load_Order( l.order[ l.pos ]:id, l.order_charge ) }">

			<MvASSIGN NAME = "l.ord_chr_pos" VALUE = 1>
			<MvWHILE EXPR = "{ l.ord_chr_pos LE l.ordercharge_count }">
				<tr><td align="right" valign="middle" colspan=3>
					&nbsp;
				</td><td align="right" valign="middle" nowrap>
					<i>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].HTML_Format_Marks( encodeentities( l.order_charge[ l.ord_chr_pos ]:descrip ) ) }">:
					</i>
				</td><td align="right" valign="middle" nowrap>
					<MvEVAL EXPR = "{ [ g.Module_Root $ l.currency_module:module ].CurrencyModule_AddFormatting( l.currency_module, l.order_charge[ l.ord_chr_pos ]:disp_amt ) }">
				</td></tr>

				<MvASSIGN NAME = "l.ord_chr_pos" VALUE = "{ l.ord_chr_pos + 1 }">
			</MvWHILE>

			<tr><td align="right" valign="middle" colspan=3>
				&nbsp;
			</td><td align="right" valign="middle" class="be_row_alt">
				<b>
				Total:
				</b>
			</td><td align="right" valign="middle" class="be_row_alt" nowrap>
				
				<b><MvEVAL EXPR = "{ [ g.Module_Root $ l.currency_module:module ].CurrencyModule_AddFormatting( l.currency_module, l.order[ l.pos ]:total ) }"></b>
				
			</td></tr>
			</table>
		</td></tr>
		</table>

		<MvASSIGN NAME = "l.pos" VALUE = "{ l.pos + 1 }">
		<MvIF EXPR = "{ l.pos LE l.order_count }">
			<br>
		</MvIF>
	</MvWHILE>

	</body>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

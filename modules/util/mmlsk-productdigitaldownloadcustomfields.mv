<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant v5.x
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2020 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-UTL-PDDI-
| Next Error Code: 11
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-productdigitaldownloadcustomfields">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Product Digital Download Custom Fields">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.72">
	<MvASSIGN NAME = "l.module:description" VALUE = "Use this module to enable custom fields for importing digital download product settings in bulk. These custom fields will appear as columns in your product spreadsheet template from Utilities > Import Data.">
	<MvASSIGN NAME = "l.module:features"	VALUE = "util, fields_prod, fields_prod_map">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature util
|
</MvCOMMENT>

<MvFUNCTION NAME = "StoreUtilityModule_Action" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_LeftNavigation" PARAMETERS = "module var, indent" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Screen" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature fields_prod
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Product_Field_Name" PARAMETERS = "module var, code" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCE NAME = "l.cache" VARIABLE = "g.Session:cache:productdigitaldownloadcustomfields:product:field_definitions">

	<MvIF EXPR = "{ ISNULL l.cache }">
		<MvASSIGN NAME = "l.null" VALUE = "{ DigitalDownload_ProductFieldList_LoadAll( l.cache ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT miva_array_search( l.cache, 1, l.element, 'l.element:code EQ l.code' ) }">
		<MvFUNCTIONRETURN VALUE = "">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.element:name }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Field_Value" PARAMETERS = "module var, product_id, code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT DigitalDownload_ProductFieldValue_LoadAll_Cache( l.product_id, l.field_values ) }">
		<MvFUNCTIONRETURN VALUE = "">
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.value" VARIABLE = "l.field_values">
		<MvMEMBER NAME = "{ l.code }">
	</MvREFERENCEARRAY>

	<MvFUNCTIONRETURN VALUE = "{ l.value }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Fields" PARAMETERS = "module var, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ DigitalDownload_ProductFieldList_LoadAll( l.fields ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Set_Field" PARAMETERS = "module var, product_id, code, value" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.product_id" VALUE = "{ int( l.product_id ) }">

	<MvIF EXPR = "{ l.product_id LE 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-UTL-PDDI-00009', 'Invalid product ID' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_DDL_DB ].DigitalDownloadProductSetting_Load_Product( l.product_id, l.dlproductsettings ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_DDL_DB ].DigitalDownloadProductSettings_Default( l.product_id, l.dlproductsettings ) }">
	</MvIF>

	<MvASSIGN NAME = "l.value" VALUE = "{ trim( l.value ) }">

	<MvIF EXPR = "{ ISNULL l.value }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.code EQ 'enabled' }">
		<MvASSIGN NAME = "l.value" VALUE = "{ tolower( l.value ) }">

		<MvIF EXPR = "{ ( l.value EQ 'yes' ) OR ( l.value EQ 'true' ) OR ( ( 'x' $ l.value ) EQ 'x1' ) }">
			<MvASSIGN NAME = "l.dlproductsettings:enabled" 			VALUE = 1>
			<MvASSIGN NAME = "l.value" 								VALUE = "Yes">
		<MvELSEIF EXPR = "{ ( l.value EQ 'no' ) OR ( l.value EQ 'false' ) OR ( ( 'x' $ l.value ) EQ 'x0' ) }">
			<MvASSIGN NAME = "l.dlproductsettings:enabled" 			VALUE = 0>
			<MvASSIGN NAME = "l.value" 								VALUE = "No">
		<MvELSE>
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-UTL-PDDI-00001', 'Value must be Yes/No, True/False, or 0/1' ) }">
		</MvIF>
	<MvELSEIF EXPR = "{ l.code EQ 'max_dl' }">
		<MvIF EXPR = "{ tolower( l.value ) EQ 'global' }">
			<MvASSIGN NAME = "l.dlproductsettings:max_dl" 			VALUE = -1>
		<MvELSEIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Optional( l.value ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-UTL-PDDI-00002', g.Validation_Message $ ' or Global' ) }">
		<MvELSE>
			<MvASSIGN NAME = "l.dlproductsettings:max_dl" 			VALUE = "{ l.value }">
		</MvIF>
	<MvELSEIF EXPR = "{ l.code EQ 'expiration' }">
		<MvIF EXPR = "{ tolower( l.value ) EQ 'global' }">
			<MvASSIGN NAME = "l.dlproductsettings:expiration" 		VALUE = -1>
		<MvELSEIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Optional( l.value ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-UTL-PDDI-00003', g.Validation_Message $ ' or Global' ) }">
		<MvELSE>
			<MvASSIGN NAME = "l.dlproductsettings:expiration" 		VALUE = "{ l.value }">
		</MvIF>
	<MvELSEIF EXPR = "{ l.code EQ 'miva_path' }">
		<MvASSIGN NAME = "l.move"									VALUE = 0>
		<MvASSIGN NAME = "l.prefix"									VALUE = "move:">
		<MvASSIGN NAME = "l.prefix_length"							VALUE = "{ len_var( l.prefix ) }">
		<MvASSIGN NAME = "l.dlproductsettings:data"					VALUE = "">

		<MvIF EXPR = "{ ( l.prefix IN l.value ) EQ 1 }">
			<MvASSIGN NAME = "l.move" 								VALUE = 1>
			<MvASSIGN NAME = "l.value"								VALUE = "{ substring_var( l.value, l.prefix_length + 1, len_var( l.value ) - l.prefix_length ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_DDL_UT ].DigitalDownload_LocalFile_IsValid( l.value ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-UTL-PDDI-00004', 'Local file \'' $ l.value $ '\' does not exist' ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT l.move }">
			<MvASSIGN NAME = "l.dlproductsettings:miva_path"		VALUE = "{ l.value }">
		<MvELSE>
			<MvASSIGN NAME = "l.upload_directory"					VALUE = "{ [ g.Module_Feature_DDL_UT ].DigitalDownload_Directory_Uploads() }">

			<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].EnsurePathExists( 'script', l.upload_directory ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFOR INDEX = "l.pos" COUNT = "{ 100 }">
				<MvASSIGN NAME = "l.path"							VALUE = "{ l.upload_directory $ MakeSessionID() }">

				<MvIF EXPR = "{ NOT sexists( l.path ) }">
					<MvFORSTOP>
				</MvIF>
			</MvFOR>

			<MvIF EXPR = "{ l.pos GT 100 }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-UTL-PDDI-00007', 'Failed to generate a unique filename' ) }">
			</MvIF>

			<MvIF EXPR = "{ NOT srename( l.value, l.path ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-UTL-PDDI-00008', 'Unable to rename \'' $ l.value $ '\' to \'' $ l.path $ '\': ' $ file_last_error() ) }">
			</MvIF>

			<MvASSIGN NAME = "l.dlproductsettings:miva_path"		VALUE = "{ l.path }">
		</MvIF>
	<MvELSEIF EXPR = "{ l.code EQ 'miva_name' }">
		<MvASSIGN NAME = "l.dlproductsettings:miva_name"			VALUE = "{ l.value }">
		<MvASSIGN NAME = "l.dlproductsettings:data"					VALUE = "">
	<MvELSEIF EXPR = "{ l.code EQ 'bucket' }">
		<MvASSIGN NAME = "l.dlproductsettings:data:bucket" 			VALUE = "{ l.value }">
		<MvASSIGN NAME = "l.dlproductsettings:miva_name"			VALUE = "">
		<MvASSIGN NAME = "l.dlproductsettings:miva_path"			VALUE = "">
	<MvELSEIF EXPR = "{ l.code EQ 'file' }">
		<MvASSIGN NAME = "l.dlproductsettings:data:file" 			VALUE = "{ l.value }">
		<MvASSIGN NAME = "l.dlproductsettings:miva_name"			VALUE = "">
		<MvASSIGN NAME = "l.dlproductsettings:miva_path"			VALUE = "">
	<MvELSEIF EXPR = "{ l.code EQ 'region' }">
		<MvASSIGN NAME = "l.valid_region"							VALUE = 0>
		<MvASSIGN NAME = "l.dlproductsettings:miva_name"			VALUE = "">
		<MvASSIGN NAME = "l.dlproductsettings:miva_path"			VALUE = "">

		<MvFOREACH ITERATOR = "l.region" ARRAY = "l.regions" COUNT = "{ [ g.Module_Feature_DDL_UT ].DigitalDownload_Amazon_Regions( l.regions ) }">
			<MvIF EXPR = "{ tolower( l.value ) EQ tolower( l.region:name )  }">
				<MvASSIGN NAME = "l.dlproductsettings:data:region"	VALUE = "{ l.region:region }">
				<MvASSIGN NAME = "l.valid_region"					VALUE = 1>
			</MvIF>
		</MvFOREACH>

		<MvIF EXPR = "{ NOT l.valid_region }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-UTL-PDDI-00005', 'Invalid region' ) }">
		</MvIF>
	<MvELSEIF EXPR = "{ l.code EQ 'cdn_name' }">
		<MvIF EXPR = "{ [ g.Module_Feature_DDL_DB ].DigitalDownloadCDN_Load_Name( l.value, l.dl_cdn ) AND ISNULL l.dlproductsettings:miva_path }">
			<MvASSIGN NAME = "l.dlproductsettings:cdn_id" 			VALUE = "{ l.dl_cdn:id }">
			<MvASSIGN NAME = "l.value" 								VALUE = "{ l.dl_cdn:name }">
		<MvELSEIF EXPR = "{ tolower( l.value ) EQ 'local' }">
			<MvASSIGN NAME = "l.dlproductsettings:cdn_id" 			VALUE = 0>
			<MvASSIGN NAME = "l.value" 								VALUE = "Local">
		<MvELSE>
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-UTL-PDDI-00006', 'Invalid delivery method' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.dlproductsettings:miva_name AND NOT ISNULL l.dlproductsettings:miva_path }">
		<MvASSIGN NAME = "l.dlproductsettings:miva_name" 			VALUE = "{ [ g.Module_Library_Utilities ].BaseName( l.dlproductsettings:miva_path ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_DDL_DB ].DigitalDownloadProductSetting_InsertOrUpdate( l.dlproductsettings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| Update in-memory cache, if neccessary
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ g.Session:cache:productdigitaldownloadcustomfields:product[ l.product_id ] }">
		<MvASSIGNARRAY NAME = "g.Session:cache:productdigitaldownloadcustomfields:product" VALUE = "{ l.value }">
			<MvDIMENSION INDEX = "{ l.product_id }">
			<MvMEMBER NAME = "field_values">
			<MvMEMBER NAME = "{ l.code }">
		</MvASSIGNARRAY>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Multiple Product Custom Fields Feature (fields_prod_map)
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Product_Fields_Mapped" PARAMETERS = "module var, product_id, field_map var, output_fields var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT DigitalDownload_ProductFieldValue_LoadAll_Cache( l.product_id, l.field_values ) }">
		<MvFUNCTIONRETURN VALUE = "">
	</MvIF>

	<MvFOREACH ITERATOR = "l.field_code" ARRAY = "l.field_codes" COUNT = "{ miva_struct_members( l.field_values, l.field_codes ) }">
		<MvREFERENCEARRAY NAME = "l.value" VARIABLE = "l.field_values">
			<MvMEMBER NAME = "{ l.field_code }">
		</MvREFERENCEARRAY>

		<MvREFERENCEARRAY NAME = "l.mapped_member" VARIABLE = "l.field_map">
			<MvMEMBER NAME = "{ l.field_code }">
		</MvREFERENCEARRAY>

		<MvIF EXPR = "{ NOT ISNULL l.mapped_member }">
			<MvASSIGN NAME = "l.output_fields" MEMBER = "{ l.mapped_member }" VALUE = "{ l.value }">
		</MvIF>
	</MvFOREACH>
</MvFUNCTION>

<MvCOMMENT>
|
| Helper Functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "DigitalDownload_ProductFieldList_LoadAll" PARAMETERS = "fields var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.fields" INDEX = 1 MEMBER = "code" VALUE = "enabled">
	<MvASSIGN NAME = "l.fields" INDEX = 1 MEMBER = "name" VALUE = "Digital Download: Creation at Purchase">
	<MvASSIGN NAME = "l.fields" INDEX = 2 MEMBER = "code" VALUE = "max_dl">
	<MvASSIGN NAME = "l.fields" INDEX = 2 MEMBER = "name" VALUE = "Digital Download: Max Downloads">
	<MvASSIGN NAME = "l.fields" INDEX = 3 MEMBER = "code" VALUE = "expiration">
	<MvASSIGN NAME = "l.fields" INDEX = 3 MEMBER = "name" VALUE = "Digital Download: Link Expiration">
	<MvASSIGN NAME = "l.fields" INDEX = 4 MEMBER = "code" VALUE = "cdn_name">
	<MvASSIGN NAME = "l.fields" INDEX = 4 MEMBER = "name" VALUE = "Digital Download: Delivery Method">
	<MvASSIGN NAME = "l.fields" INDEX = 5 MEMBER = "code" VALUE = "miva_path">
	<MvASSIGN NAME = "l.fields" INDEX = 5 MEMBER = "name" VALUE = "Digital Download: Local File Settings: Local File Path">
	<MvASSIGN NAME = "l.fields" INDEX = 6 MEMBER = "code" VALUE = "miva_name">
	<MvASSIGN NAME = "l.fields" INDEX = 6 MEMBER = "name" VALUE = "Digital Download: Local File Settings: Downloaded File Name">
	<MvASSIGN NAME = "l.fields" INDEX = 7 MEMBER = "code" VALUE = "bucket">
	<MvASSIGN NAME = "l.fields" INDEX = 7 MEMBER = "name" VALUE = "Digital Download: Amazon CDN Settings: Bucket Name">
	<MvASSIGN NAME = "l.fields" INDEX = 8 MEMBER = "code" VALUE = "file">
	<MvASSIGN NAME = "l.fields" INDEX = 8 MEMBER = "name" VALUE = "Digital Download: Amazon CDN Settings: File Path">
	<MvASSIGN NAME = "l.fields" INDEX = 9 MEMBER = "code" VALUE = "region">
	<MvASSIGN NAME = "l.fields" INDEX = 9 MEMBER = "name" VALUE = "Digital Download: Amazon CDN Settings: Region">

	<MvFUNCTIONRETURN VALUE = 9>
</MvFUNCTION>

<MvFUNCTION NAME = "DigitalDownload_ProductFieldValue_LoadAll_Cache" PARAMETERS = "product_id, field_values var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.product_id"						VALUE = "{ int( l.product_id ) }">

	<MvIF EXPR = "{ l.product_id LE 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-UTL-PDDI-00010', 'Invalid product ID' ) }">
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.cache" VARIABLE = "g.Session:cache:productdigitaldownloadcustomfields:product">
		<MvDIMENSION INDEX = "{ l.product_id }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ ISNULL l.cache }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ DigitalDownload_ProductFieldValue_LoadAll( l.product_id, l.cache:field_values ) }">

		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_is_eof"		VALUE = "{ [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.field_values"					VALUE = "{ l.cache:field_values }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DigitalDownload_ProductFieldValue_LoadAll" PARAMETERS = "product_id, field_values var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_DDL_DB ].DigitalDownloadProductSetting_Load_Product( l.product_id, l.dlproductsettings ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_DDL_DB ].DigitalDownloadProductSettings_Default( l.product_id, l.dlproductsettings ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.null" 							VALUE = "{ [ g.Module_Feature_DDL_DB ].DigitalDownloadCDN_Load_ID( l.dlproductsettings:cdn_id, l.dl_cdn ) }">
	</MvIF>

	<MvIF EXPR = "{ l.dlproductsettings:cdn_id EQ 0 }">
		<MvASSIGN NAME = "l.dl_cdn:name"					VALUE = "Local">
	</MvIF>

	<MvIF EXPR = "{ l.dlproductsettings:enabled }">
		<MvASSIGN NAME = "l.enabled" 						VALUE = "Yes">
	<MvELSE>
		<MvASSIGN NAME = "l.enabled" 						VALUE = "No">
	</MvIF>

	<MvIF EXPR = "{ l.dlproductsettings:max_dl EQ -1 }">
		<MvASSIGN NAME = "l.dlproductsettings:max_dl"		VALUE = "Global">
	</MvIF>

	<MvIF EXPR = "{ l.dlproductsettings:expiration EQ -1 }">
		<MvASSIGN NAME = "l.dlproductsettings:expiration"	VALUE = "Global">
	</MvIF>

	<MvASSIGN NAME = "l.field_values:enabled"				VALUE = "{ l.enabled }">
	<MvASSIGN NAME = "l.field_values:max_dl"				VALUE = "{ l.dlproductsettings:max_dl }">
	<MvASSIGN NAME = "l.field_values:expiration"			VALUE = "{ l.dlproductsettings:expiration }">
	<MvASSIGN NAME = "l.field_values:cdn_name"				VALUE = "{ l.dl_cdn:name }">
	<MvASSIGN NAME = "l.field_values:miva_path"				VALUE = "{ l.dlproductsettings:miva_path }">
	<MvASSIGN NAME = "l.field_values:miva_name"				VALUE = "{ l.dlproductsettings:miva_name }">
	<MvASSIGN NAME = "l.field_values:bucket"				VALUE = "{ l.dlproductsettings:data:bucket }">
	<MvASSIGN NAME = "l.field_values:file"					VALUE = "{ l.dlproductsettings:data:file }">

	<MvFOREACH ITERATOR = "l.region" ARRAY = "l.regions" COUNT = "{ [ g.Module_Feature_DDL_UT ].DigitalDownload_Amazon_Regions( l.regions ) }">
		<MvIF EXPR = "{ l.dlproductsettings:data:region EQ l.region:region }">
			<MvASSIGN NAME = "l.field_values:region"		VALUE = "{ l.region:name }">
			<MvFOREACHSTOP>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

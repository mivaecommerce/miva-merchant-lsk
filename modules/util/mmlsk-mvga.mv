<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-SYS-MVGA-
| Next Error Code: 12   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-mvga">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Google Analytics (Deprecated)">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0800">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.72">
	<MvASSIGN NAME = "l.module:description"	VALUE = "Google Analytics is a product that tracks visitor activity in your online store.  Google Analytics can tell you what pages in your store people have visited, what products have been purchased, etc.  You must have a Google Analytics account in order to use this feature.  See the Miva Merchant PR8 Reference guide for details on enabling this feature.">
	<MvASSIGN NAME = "l.module:features"	VALUE = "util, vis_util, component, data_store, provision_store, skins, clientside, clientside_sri, fulfill">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ MVGA_Settings_Default( l.settings ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Item_Load_Code( 'ga_tracking', l.null ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Item( 'ga_tracking', l.module:code ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Item_Load_Code( 'ga_transaction', l.null ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Item( 'ga_transaction', l.module:code ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Item_Load_Code( 'ga_jsencode', l.null ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Item( 'ga_jsencode', l.module:code ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ MVGA_Generate_Tracking_Template( l.settings, l.source ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'ga-tracking.mvc', l.null_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.source, l.null, 'ga-tracking.mvc' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ MVGA_Generate_Classic_Transaction_Template( l.source ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'ga-transaction.mvc', l.null_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.source, l.null, 'ga-transaction.mvc' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedProperty_Load_Code( 'mvga', 'mvga', l.managedproperty ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedProperty( 'mvga', 'mvga', '', l.null, l.settings, l.managedproperty ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_All_Runtime( l.pages ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'ga_tracking' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code( 'INVC', l.page ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'ga_transaction' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>	
	</MvIF>

	<MvIF EXPR = "{ g.Store:ui_mod:code EQ 'cssui' }">		
		<MvASSIGN NAME = "l.head_template_filename"		VALUE = "cssui-global-head.mvc">
		<MvASSIGN NAME = "l.footer_template_filename"	VALUE = "cssui-global-footer.mvc">
	<MvELSEIF EXPR = "{ g.Store:ui_mod:code EQ 'mmui' }">	
		<MvASSIGN NAME = "l.head_template_filename"		VALUE = "global-head.mvc">
		<MvASSIGN NAME = "l.footer_template_filename"	VALUE = "global-footer.mvc">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Item_Load_Code( 'hdft', l.item ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.head_template_filename, l.head_template ) }">
		<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_ID( l.head_template:current_id, l.head_managedtemplateversion ) AND
						( '<script type="text/javascript" src="&mvt:global:clientside_url;Module_Code=mvga&amp;Filename=mvga.js"></script>' IN l.head_managedtemplateversion:source ) EQ 0 }">
			<MvASSIGN NAME = "l.source" VALUE = "{ l.head_managedtemplateversion:source $ asciichar( 10 ) $ '<script type="text/javascript" src="&mvt:global:clientside_url;Module_Code=mvga&amp;Filename=mvga.js"></script>' $ asciichar( 10 ) }">
			<MvASSIGN NAME = "l.null"	VALUE = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplateVersion( l.head_template,
																																   l.module:name $ ' Template Code Insertion',
																																   l.source,
																																   l.head_managedtemplateversion:settings,
																																   l.compile_error ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.footer_template_filename, l.footer_template ) }">
		<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_ID( l.footer_template:current_id, l.footer_managedtemplateversion ) AND
						( '<mvt:item name=\"ga_tracking\" />' IN l.footer_managedtemplateversion:source ) EQ 0 }">
			<MvASSIGN NAME = "l.source" VALUE = "{ l.footer_managedtemplateversion:source $ asciichar( 10 ) $ '<mvt:item name="ga_tracking" />' $ asciichar( 10 ) }">
			<MvASSIGN NAME = "l.null"	VALUE = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplateVersion( l.footer_template,
																																   l.module:name $ ' Template Code Insertion',
																																   l.source,
																																   l.footer_managedtemplateversion:settings,
																																   l.compile_error ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'invc.mvc', l.invc_managedtemplate ) }">
		<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Template_Current( l.invc_managedtemplate:id, l.invc_managedtemplateversion ) 	AND
						( '<mvt:item name="ga_transaction" />' IN l.invc_managedtemplateversion:source ) EQ 0 													AND
						( '<mvt:item name="hdft" param="global_footer" />' IN l.invc_managedtemplateversion:source ) GT 0 }">
			<MvASSIGN NAME = "l.source" VALUE = "{ glosub( l.invc_managedtemplateversion:source, '<mvt:item name="hdft" param="global_footer" />',
														   asciichar( 10 ) $ asciichar( 9 ) $ asciichar( 9 ) $ '<mvt:item name="hdft" param="global_footer" />' $
														   asciichar( 10 ) $ asciichar( 9 ) $ asciichar( 9 ) $ '<mvt:item name="ga_transaction" />' $
														   asciichar( 10 ) $ asciichar( 9 ) ) }">
			<MvASSIGN NAME = "l.null"	VALUE = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplateVersion( l.invc_managedtemplate,
																																   l.module:name $ ' Template Code Insertion',
																																   l.source,
																																   l.invc_managedtemplateversion:settings,
																																   l.compile_error ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.module:version EQ l.version }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SYS-MVGA-00002', 'Module \'' $ l.module:name $ '\' does not support manual upgrade.  New versions may only be obtained through the streaming update system.' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_GlobalDelete_ManagedTemplate_Filename( 'ga-transaction.mvc' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_GlobalDelete_ManagedTemplate_Filename( 'ga-tracking.mvc' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_GlobalDelete_ManagedProperty_Code( 'mvga', 'mvga' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item EQ 'ga_tracking' }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'ga_jsencode' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item EQ 'ga_jsencode' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF> 

	<MvIF EXPR = "{ NOT MVGA_Load( l.settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| URI configuration
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.flags"									VALUE = "">
	<MvASSIGN NAME = "l.flags:sep"								VALUE = 1>

	<MvIF EXPR = "{ g.Secure }">
		<MvASSIGN NAME = "l.flags:force_secure"					VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ g.Screen EQ 'CTGY' }">					
		<MvASSIGN NAME = "l.settings:url_override" 				VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Category_Code_URL( g.Category_Code, l.flags ) }"> 	
	<MvELSEIF EXPR = "{	g.Screen EQ 'PROD' }"> 				
		<MvASSIGN NAME = "l.settings:url_override" 				VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Product_Code_URL( g.Product_Code, l.flags ) }"> 	
	<MvELSEIF EXPR = "{ NOT ISNULL g.Screen }">
		<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code_Cached( g.Screen, l.page ) }">
			<MvASSIGN NAME = "l.settings:url_override" 			VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Page_URL( l.page, l.flags ) }">

			<MvIF EXPR = "{ ( g.Screen EQ 'SRCH' ) AND ( NOT ISNULL g.Search ) }">
				<MvASSIGN NAME = "l.settings:url_override"		VALUE = "{ l.settings:url_override $ 'Search=' $ encodeattribute( g.Search ) }">
			<MvELSE>
				<MvASSIGN NAME = "l.settings:url_override"		VALUE = "{ substring_var( l.settings:url_override, 1, len_var( l.settings:url_override ) - 1 ) }">
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.settings:url_override }">
		<MvIF EXPR = "{ [ g.Module_Feature_URI_DB ].URISettings_Load_Cached( l.urisettings ) }">
			<MvIF EXPR = "{ g.Secure }">	<MvASSIGN NAME = "l.settings:url_override"	VALUE = "{ glosub( l.settings:url_override, [ g.Module_Feature_URI_UT ].URL_Base_Secure( l.urisettings ), '' ) }">
			<MvELSE>						<MvASSIGN NAME = "l.settings:url_override"	VALUE = "{ glosub( l.settings:url_override, [ g.Module_Feature_URI_UT ].URL_Base( l.urisettings ), '' ) }">
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ NOT ( '://' IN l.settings:url_override ) }">
			<MvASSIGN NAME = "l.settings:url_override"			VALUE = "{ '/' $ l.settings:url_override }">
		</MvIF>

		<MvASSIGN NAME = "l.settings:url_override_unencoded"	VALUE = "{ l.settings:url_override }">
		<MvASSIGN NAME = "l.settings:url_override_noquotes"		VALUE = "{ [ g.Module_Admin ].JavaScriptEncode( l.settings:url_override ) }">
		<MvASSIGN NAME = "l.settings:url_override"				VALUE = "{ ', \'' $ [ g.Module_Admin ].JavaScriptEncode( l.settings:url_override ) $ '\'' }">
	</MvIF>

	<MvASSIGN NAME = "l.pos" VALUE = 1>
	<MvASSIGN NAME = "l.parsed_domain" VALUE = "{ gettoken( l.settings:domains, ',', l.pos ) }">

	<MvWHILE EXPR = "{ len( l.parsed_domain ) }">
		<MvASSIGN NAME = "l.settings:domains_unencoded" INDEX = "{ l.pos }" VALUE = "{ l.parsed_domain }">
		<MvASSIGN NAME = "l.parsed_domain" VALUE = "{ gettoken( l.settings:domains, ',', ++l.pos ) }">
	</MvWHILE>

	<MvCOMMENT>
	|
	| Load product lists
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ g.Store:ui_mod:code EQ 'cssui' AND l.settings:mode EQ 'enhanced' }">
		<MvIF EXPR = "{ g.Screen EQ 'CTGY' }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Initialize_Item( l.all_settings, 'category_listing' ) }">
				<MvFUNCTIONRETURN VALUE = 1>
			</MvIF>

			<MvREFERENCE NAME = "l.settings:product_list" VARIABLE = "l.all_settings:category_listing:products">
		<MvELSEIF EXPR = "{ g.Screen EQ 'PLST' }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Initialize_Item( l.all_settings, 'all_products' ) }">
				<MvFUNCTIONRETURN VALUE = 1>
			</MvIF>

			<MvREFERENCE NAME = "l.settings:product_list" VARIABLE = "l.all_settings:all_products:products">
		<MvELSEIF EXPR = "{ g.Screen EQ 'PROD' }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Initialize_Item( l.all_settings, 'related_products' ) }">
				<MvFUNCTIONRETURN VALUE = 1>
			</MvIF>

			<MvREFERENCE NAME = "l.settings:product_list" VARIABLE = "l.all_settings:related_products:products">
		<MvELSEIF EXPR = "{ g.Screen EQ 'SRCH' }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Initialize_Item( l.all_settings, 'search_results' ) }">
				<MvFUNCTIONRETURN VALUE = 1>
			</MvIF>

			<MvREFERENCE NAME = "l.settings:product_list" VARIABLE = "l.all_settings:search_results:products">
		</MvIF>
	<MvELSEIF EXPR = "{ g.Store:ui_mod:code EQ 'mmui' AND
						( g.Screen EQ 'CTGY' OR
						  g.Screen EQ 'PLST' OR
						  g.Screen EQ 'PROD' OR
						  g.Screen EQ 'SRCH' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Initialize_Item( l.all_settings, 'product_list' ) }">
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>

		<MvREFERENCE NAME = "l.settings:product_list" VARIABLE = "l.all_settings:products">
	</MvIF>

	<MvIF EXPR = "{ g.Screen EQ 'CTGY' }">		<MvASSIGN NAME = "l.settings:list_label"	VALUE = "Category Listing">
	<MvELSEIF EXPR = "{ g.Screen EQ 'PLST' }">	<MvASSIGN NAME = "l.settings:list_label"	VALUE = "All Products">
	<MvELSEIF EXPR = "{ g.Screen EQ 'PROD' }">	<MvASSIGN NAME = "l.settings:list_label"	VALUE = "Related Products">
	<MvELSEIF EXPR = "{ g.Screen EQ 'SRCH' }">	<MvASSIGN NAME = "l.settings:list_label"	VALUE = "Search Results">
	</MvIF>

	<MvASSIGN NAME = "l.settings:store_name"			VALUE = "{ [ g.Module_Admin ].JavaScriptEncode( g.Store:name ) }">
	<MvASSIGN NAME = "l.settings:ship_city"				VALUE = "{ [ g.Module_Admin ].JavaScriptEncode( l.all_settings:order:ship_city ) }">
	<MvASSIGN NAME = "l.settings:ship_state"			VALUE = "{ [ g.Module_Admin ].JavaScriptEncode( l.all_settings:order:ship_state ) }">
	<MvASSIGN NAME = "l.settings:ship_cntry"			VALUE = "{ [ g.Module_Admin ].JavaScriptEncode( l.all_settings:order:ship_cntry ) }">

	<MvASSIGN NAME = "l.settings:google_id_unencoded"	VALUE = "{ l.settings:google_id }">
	<MvASSIGN NAME = "l.settings:domain_unencoded"		VALUE = "{ l.settings:domain }">

	<MvASSIGN NAME = "l.settings:google_id"				VALUE = "{ [ g.Module_Admin ].JavaScriptEncode( l.settings:google_id ) }">
	<MvASSIGN NAME = "l.settings:domain"				VALUE = "{ [ g.Module_Admin ].JavaScriptEncode( l.settings:domain ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Initialize_Item( l.all_settings, 'order' ) OR 
					NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Initialize_Item( l.all_settings, 'order_contents' ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.settings:order_id"				VALUE = "{ l.all_settings:order:id }">
	<MvASSIGN NAME = "l.settings:order_total"			VALUE = "{ l.all_settings:order:total ROUND 2 }">

	<MvASSIGN NAME = "l.settings:total_ship"			VALUE = "0.00">
	<MvASSIGN NAME = "l.settings:total_tax"				VALUE = "0.00">

	<MvFOREACH ITERATOR = "l.charge" ARRAY = "l.all_settings:order:charges">
		<MvIF EXPR = "{ l.charge:type EQ 'SHIPPING' }">	
			<MvASSIGN NAME = "l.settings:total_ship"	VALUE = "{ l.settings:total_ship + l.charge:amount }">
		<MvELSEIF EXPR = "{ l.charge:type EQ 'TAX' }">	
			<MvASSIGN NAME = "l.settings:total_tax"		VALUE = "{ l.settings:total_tax + l.charge:amount }">
		</MvIF>
	</MvFOREACH>

	<MvASSIGN NAME = "l.settings:total_ship"			VALUE = "{ l.settings:total_ship ROUND 2 }">
	<MvASSIGN NAME = "l.settings:total_tax"				VALUE = "{ l.settings:total_tax ROUND 2 }">

	<MvEVAL EXPR = "{ MVGA_Load_OrderItems( l.all_settings, l.settings ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "MVGA_Load_OrderItems" PARAMETERS = "all_settings var, settings var" STANDARDOUTPUTLEVEL = "text, html">
	<MvFOREACH ITERATOR = "l.orderitem" ARRAY = "l.all_settings:order:items">
		<MvASSIGN NAME = "l.cancat"										VALUE = "">
		<MvASSIGN NAME = "l.product"									VALUE = "">
		<MvASSIGN NAME = "l.orderoption_label"							VALUE = "">

		<MvASSIGN NAME = "l.ga_orderitem"								VALUE = "">
		<MvASSIGN NAME = "l.ga_orderitem:order_id"						VALUE = "{ int( l.orderitem:order_id ) }">
	
		<MvIF EXPR = "{ [ g.Module_Library_DB ].Product_Load_ID( l.orderitem:product:id, l.product ) AND l.orderitem:product:cancat_id GT 0 }">
			<MvASSIGN NAME = "l.null"									VALUE = "{ [ g.Module_Library_DB ].Runtime_Category_Load_ID_Cached( l.orderitem:product:cancat_id, l.cancat ) }">

			<MvASSIGN NAME = "l.ga_orderitem:cancat_code"				VALUE = "{ [ g.Module_Admin ].JavaScriptEncode( l.cancat:code ) }">
			<MvASSIGN NAME = "l.ga_orderitem:cancat_code_unencoded"		VALUE = "{ l.cancat:code }">
		</MvIF>

		<MvASSIGN NAME = "l.ga_orderitem:code"							VALUE = "{ [ g.Module_Admin ].JavaScriptEncode( l.orderitem:code ) }">
		<MvASSIGN NAME = "l.ga_orderitem:code_unencoded"				VALUE = "{ l.orderitem:code }">
		<MvASSIGN NAME = "l.ga_orderitem:name"							VALUE = "{ [ g.Module_Admin ].JavaScriptEncode( l.orderitem:name ) }">
		<MvASSIGN NAME = "l.ga_orderitem:name_unencoded"				VALUE = "{ l.orderitem:name }">

		<MvASSIGN NAME = "l.orderoption_total"							VALUE = 0>

		<MvFOREACH ITERATOR = "l.orderoption" INDEX = "l.pos1" ARRAY = "l.orderoptions" COUNT = "{ [ g.Module_Library_DB ].OrderOptionList_Load_Line( l.orderitem:line_id, l.orderoptions ) }">
			<MvASSIGN NAME = "l.orderoption_total"						VALUE = "{ l.orderoption_total + l.orderoption:price }">

			<MvIF EXPR = "{ l.pos1 GT 1 }"> 
				<MvASSIGN NAME = "l.orderoption_label"					VALUE = "{ l.orderoption_label $ ' ' }">
			</MvIF>

			<MvASSIGN NAME = "l.orderoption_label"						VALUE = "{ l.orderoption_label $ l.orderoption:opt_code }">
		</MvFOREACH>

		<MvASSIGN NAME = "l.ga_orderitem:price"							VALUE = "{ ( l.orderitem:price + l.orderoption_total ) ROUND 2 }">
		<MvASSIGN NAME = "l.ga_orderitem:variant_label"					VALUE = "{ [ g.Module_Admin ].JavaScriptEncode( l.orderoption_label ) }">
		<MvASSIGN NAME = "l.ga_orderitem:variant_label_unencoded"		VALUE = "{ l.orderoption_label }">
		<MvASSIGN NAME = "l.ga_orderitem:quantity"						VALUE = "{ int( l.orderitem:quantity ) }">

		<MvASSIGN NAME = "l.settings:orderitem_count"					VALUE = "{ miva_array_insert( l.settings:orderitems, l.ga_orderitem, -1 ) }">
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item EQ 'ga_transaction' }">
		<MvIF EXPR = "{ l.settings:mode EQ 'classic' AND
						NOT l.settings:dsblecom }">
			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( 'ga-transaction.mvc', l.all_settings ) }">
		</MvIF>
	<MvELSEIF EXPR = "{ l.item EQ 'ga_jsencode' }">
		<MvREFERENCE NAME = "l.value" VARIABLE = "{ 'l.all_settings:' $ l.param }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScriptEncode( l.value ) }">
	<MvELSEIF EXPR = "{ l.item EQ 'ga_tracking' }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScript_SetVariables( 'g', 'Product_Code,Screen' ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( 'ga-tracking.mvc', l.all_settings ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Head" PARAMETERS = "module var, tab" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ MVGA_General_Validate( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ MVGA_General_Update( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Tabs" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT MVGA_Load( l.settings ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.tabs" VALUE = "{ l.module:code $ '_settings' $ ':' $ 'Google Analytics Settings,' $ l.module:code $ '_tracking' $ ':' $ 'Google Analytics Tracking Code' }">

	<MvIF EXPR = "{ l.settings:mode EQ 'classic' }">
		<MvASSIGN NAME = "l.tabs" VALUE = "{ l.tabs $ ',' $ l.module:code $ '_transaction' $ ':' $ 'Google Analytics Classic Ecommerce Code' }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.tabs }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ MVGA_General_Content( l.module, l.tab, l.load_fields ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Action" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Screen" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_LeftNavigation" PARAMETERS = "module var, indent" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Store" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.child" ARRAY = "l.provide_xml:children">
		<MvASSIGN NAME = "l.name" VALUE = "{ tolower( l.child:name ) }">

		<MvIF EXPR = "{ l.name EQ 'settings' }">		<MvEVAL EXPR = "{ Module_Provision_Store_Settings( l.module, l.child ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'tracking' }">	<MvEVAL EXPR = "{ Module_Provision_Store_Tracking( l.module, l.child ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'transaction' }">	<MvEVAL EXPR = "{ Module_Provision_Store_Transaction( l.module, l.child ) }">
		</MvIF>
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Store_Settings" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT MVGA_Load_Or_Create( l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
	
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O',		l.provide_xml,	'GoogleID',			l.settings:google_id ) 							OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O',		l.provide_xml,	'Domain',			l.settings:domain ) 							OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List( 'O',		l.provide_xml,	'Mode',				l.settings:mode,
																							'Universal,Enhanced,Classic', 'universal,enhanced,classic' ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT MVGA_Update( l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-SYS-MVGA-00003', 'Google Analytics configuration updated' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Store_Tracking" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT MVGA_Load_Or_Create( l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvASSIGN NAME = "l.working_settings" VALUE = "{ l.settings }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O',	l.provide_xml,	'TrackType',				l.working_settings:tracktype, 
																								'SingleTopNoSub,SingleTopMultiSub,MultiTopMultiSub,0,1,2', '0,1,2,0,1,2' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O',	l.provide_xml,	'Disable',					l.working_settings:disable )					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O',	l.provide_xml,	'AdSupport',				l.working_settings:ad_sup )						OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O',	l.provide_xml,	'LinkAttribution',			l.working_settings:link_att )					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O',	l.provide_xml,	'ForceSSL',					l.working_settings:force_ssl )   				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O',	l.provide_xml,	'AnonymizeIP',				l.working_settings:anon_ip ) 					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'O',	l.provide_xml,	'Domains',					l.working_settings:domains ) 					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O',	l.provide_xml,	'CrossDomainLinking',		l.working_settings:crossdom ) 					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O',	l.provide_xml,	'TrackSubscriptionOrders',	l.working_settings:tracksub ) 					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O',	l.provide_xml,	'SimpleEcommerce',			l.working_settings:simple ) 					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'O',	l.provide_xml,	'Notes',					l.notes ) }">	
		<MvFUNCTIONRETURN>
	</MvIF>
	
	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
		<MvASSIGN NAME = "l.source"						VALUE = "{ trim( l.provide_xml:tags:Template[ 1 ]:value ) }">
		<MvASSIGN NAME = "l.working_settings:advanced"	VALUE = 1>
	<MvELSE>
		<MvASSIGN NAME = "l.working_settings:advanced"	VALUE = 0>

		<MvEVAL EXPR = "{ MVGA_Generate_Tracking_Template( l.working_settings, l.source ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'ga-tracking.mvc', l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings:advanced" VALUE = "{ l.working_settings:advanced }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.source, l.null_settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT l.working_settings:advanced }">
		<MvASSIGN NAME = "l.settings" VALUE = "{ l.working_settings }">
	</MvIF>

	<MvIF EXPR = "{ NOT MVGA_Update( l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Store_Transaction" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT MVGA_Load_Or_Create( l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvIF EXPR = "{	NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml, 'DisableEcommerce', l.settings:dsblecom ) OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 		'O', l.provide_xml,	'Notes',			l.notes ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
		<MvASSIGN NAME = "l.source"	VALUE = "{ trim( l.provide_xml:tags:Template[ 1 ]:value ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ MVGA_Generate_Classic_Transaction_Template( l.source ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'ga-transaction.mvc', l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.source, l.null_settings ) }">
	    <MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT MVGA_Update( l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-SYS-MVGA-00009', 'Google Analytics configuration updated' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pages" VALUE = "{ [ g.Module_Feature_TUI_DB ].Item_Load_PageList( l.item ) }">
	
	<MvFUNCTIONRETURN VALUE = "{ 'Exports design/template from pages: ' $ l.pages }">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT MVGA_Load( l.settings ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvCAPTURE VARIABLE = "l.output">
<MIVA STANDARDOUTPUTLEVEL = "text, html"><Module code="mvga" feature="util">
	<Tracking><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:advanced }">
		<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( 'ga-tracking.mvc', l.track_managedtemplate ) }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.track_managedtemplate:source ) }"></Template><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
	<MvELSE>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<TrackType><MvEVAL EXPR = "{ int( l.settings:tracktype ) }"></TrackType>
		<LinkAttribution><MvEVAL EXPR = "{ int( l.settings:link_att ) }"></LinkAttribution>
		<AdSupport><MvEVAL EXPR = "{ int( l.settings:ad_sup ) }"></AdSupport>
		<Disable><MvEVAL EXPR = "{ int( l.settings:disable ) }"></Disable>
		<AnonymizeIP><MvEVAL EXPR = "{ int( l.settings:anon_ip ) }"></AnonymizeIP>
		<ForceSSL><MvEVAL EXPR = "{ int( l.settings:force_ssl ) }"></ForceSSL>
		<SimpleEcommerce><MvEVAL EXPR = "{ int( l.settings:simple ) }"></SimpleEcommerce><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<Notes>#Set_Current_Time#</Notes>
	</Tracking>
	<Transaction>
		<DisableEcommerce><MvEVAL EXPR = "{ int( l.settings:dsblecom ) }"></DisableEcommerce><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( 'ga-transaction.mvc', l.trans_managedtemplate ) }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.trans_managedtemplate:source ) }"></Template><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<Notes>#Set_Current_Time#</Notes>
	</Transaction>
</Module><MIVA STANDARDOUTPUTLEVEL = "">
	</MvCAPTURE>
	
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| MVGA Functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "MVGA_Load" PARAMETERS = "settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedPropertyAndVersion_Load_Code_Current( 'mvga', 'mvga', l.managedproperty ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.settings:google_id"	VALUE = "{ l.managedproperty:version:settings:google_id }">
	<MvASSIGN NAME = "l.settings:domain"	VALUE = "{ l.managedproperty:version:settings:domain }">
	<MvASSIGN NAME = "l.settings:link_att"	VALUE = "{ l.managedproperty:version:settings:link_att }">
	<MvASSIGN NAME = "l.settings:ad_sup"	VALUE = "{ l.managedproperty:version:settings:ad_sup }">
	<MvASSIGN NAME = "l.settings:disable"	VALUE = "{ l.managedproperty:version:settings:disable }">
	<MvASSIGN NAME = "l.settings:advanced"	VALUE = "{ l.managedproperty:version:settings:advanced }">
	<MvASSIGN NAME = "l.settings:tracktype"	VALUE = "{ l.managedproperty:version:settings:tracktype }">
	<MvASSIGN NAME = "l.settings:mode"		VALUE = "{ l.managedproperty:version:settings:mode }">
	<MvASSIGN NAME = "l.settings:force_ssl"	VALUE = "{ l.managedproperty:version:settings:force_ssl }">
	<MvASSIGN NAME = "l.settings:anon_ip"	VALUE = "{ l.managedproperty:version:settings:anon_ip }">
	<MvASSIGN NAME = "l.settings:crossdom"	VALUE = "{ l.managedproperty:version:settings:crossdom }">
	<MvASSIGN NAME = "l.settings:domains"	VALUE = "{ l.managedproperty:version:settings:domains }">
	<MvASSIGN NAME = "l.settings:dsblecom"	VALUE = "{ l.managedproperty:version:settings:dsblecom }">
	<MvASSIGN NAME = "l.settings:simple"	VALUE = "{ l.managedproperty:version:settings:simple }">
	<MvASSIGN NAME = "l.settings:tracksub"	VALUE = "{ l.managedproperty:version:settings:tracksub }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "MVGA_Settings_Copy" PARAMETERS = "source var, dest var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.dest:google_id"	VALUE = "{ l.source:google_id }">
	<MvASSIGN NAME = "l.dest:domain"	VALUE = "{ l.source:domain }">
	<MvASSIGN NAME = "l.dest:link_att"	VALUE = "{ l.source:link_att }">
	<MvASSIGN NAME = "l.dest:ad_sup"	VALUE = "{ l.source:ad_sup }">
	<MvASSIGN NAME = "l.dest:disable"	VALUE = "{ l.source:disable }">
	<MvASSIGN NAME = "l.dest:advanced"	VALUE = "{ l.source:advanced }">
	<MvASSIGN NAME = "l.dest:tracktype"	VALUE = "{ l.source:tracktype }">
	<MvASSIGN NAME = "l.dest:mode"		VALUE = "{ l.source:mode }">
	<MvASSIGN NAME = "l.dest:force_ssl"	VALUE = "{ l.source:force_ssl }">
	<MvASSIGN NAME = "l.dest:anon_ip"	VALUE = "{ l.source:anon_ip }">
	<MvASSIGN NAME = "l.dest:crossdom"	VALUE = "{ l.source:crossdom }">
	<MvASSIGN NAME = "l.dest:domains"	VALUE = "{ l.source:domains }">
	<MvASSIGN NAME = "l.dest:dsblecom"	VALUE = "{ l.source:dsblecom }">
	<MvASSIGN NAME = "l.dest:simple"	VALUE = "{ l.source:simple }">
	<MvASSIGN NAME = "l.dest:tracksub"	VALUE = "{ l.source:tracksub }">
</MvFUNCTION>

<MvFUNCTION NAME = "MVGA_Settings_Default" PARAMETERS = "settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:google_id" VALUE = "">
	<MvASSIGN NAME = "l.settings:advanced" 	VALUE = 0>
	<MvASSIGN NAME = "l.settings:domain" 	VALUE = "{ g.Domain:name }">
	<MvASSIGN NAME = "l.settings:link_att" 	VALUE = 0>
	<MvASSIGN NAME = "l.settings:ad_sup" 	VALUE = 0>
	<MvASSIGN NAME = "l.settings:disable" 	VALUE = 0>
	<MvASSIGN NAME = "l.settings:tracktype" VALUE = 0>
	<MvASSIGN NAME = "l.settings:mode" 		VALUE = "analytics">
	<MvASSIGN NAME = "l.settings:anon_ip"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:crossdom"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:domains"	VALUE = "">
	<MvASSIGN NAME = "l.settings:force_ssl"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:dsblecom"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:simple"	VALUE = 1>
	<MvASSIGN NAME = "l.settings:tracksub"	VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "MVGA_Load_Or_Create" PARAMETERS = "settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT MVGA_Load( l.settings ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvEVAL EXPR = "{ MVGA_Settings_Default( l.settings ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedProperty_Load_Code( 'mvga', 'mvga', l.managedproperty ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedProperty( 'mvga', 'mvga', '', l.null, l.settings, l.managedproperty ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'ga-tracking.mvc', l.null ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvEVAL EXPR = "{ MVGA_Generate_Tracking_Template( l.settings, l.track_template ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.track_template, l.null, 'ga-tracking.mvc' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'ga-transaction.mvc', l.null ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvEVAL EXPR = "{ MVGA_Generate_Classic_Transaction_Template( l.trans_template ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.trans_template, l.null, 'ga-transaction.mvc' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "MVGA_Update" PARAMETERS = "mvga var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedPropertyAndVersion_Load_Code_Current( 'mvga', 'mvga', l.managedproperty ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SYS-MVGA-00011', 'mvga property not found' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ MVGA_Settings_Copy( l.mvga, l.settings ) }">

	<MvCOMMENT>
	|
	| Do not create a new version if none of the settings have changed
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ crypto_md5( miva_array_serialize( l.managedproperty:version:settings ) ) EQ crypto_md5( miva_array_serialize( l.settings ) ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedPropertyVersion( l.managedproperty, l.settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "MVGA_General_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Tab NE l.module:code $ '_settings' AND
					g.Tab NE l.module:code $ '_tracking' AND
					g.Tab NE l.module:code $ '_transaction' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'ga-tracking.mvc', l.track_template ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'ga-transaction.mvc', l.trans_template ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "g.MVGA:google_id" 	VALUE = "{ trim( g.MVGA:google_id ) }">
	<MvASSIGN NAME = "g.MVGA:domain" 		VALUE = "{ trim( g.MVGA:domain ) }">

	<MvIF EXPR = "{ ISNULL g.MVGA:google_id }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.module:code $ '_settings', 'MVGA:google_id', 'Please enter a Google Analytics ID' ) }">
	</MvIF>

	<MvIF EXPR = "{ ISNULL g.MVGA:domain }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.module:code $ '_settings', 'MVGA:domain', 'Please enter a domain' ) }">
	</MvIF>

	<MvASSIGN NAME = "g.MVGA:ad_sup"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.MVGA:ad_sup ) }">
	<MvASSIGN NAME = "g.MVGA:link_att"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.MVGA:link_att ) }">
	<MvASSIGN NAME = "g.MVGA:disable"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.MVGA:disable ) }">
	<MvASSIGN NAME = "g.MVGA:advanced"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.MVGA:advanced ) }">
	<MvASSIGN NAME = "g.MVGA:mode"			VALUE = "{ trim( g.MVGA:mode ) }">
	<MvASSIGN NAME = "g.MVGA:anon_ip"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.MVGA:anon_ip ) }">
	<MvASSIGN NAME = "g.MVGA:crossdom"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.MVGA:crossdom ) }">
	<MvASSIGN NAME = "g.MVGA:force_ssl"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.MVGA:force_ssl ) }">
	<MvASSIGN NAME = "g.MVGA:dsblecom"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.MVGA:dsblecom ) }">
	<MvASSIGN NAME = "g.MVGA:simple"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.MVGA:simple ) }">
	<MvASSIGN NAME = "g.MVGA:tracksub"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.MVGA:tracksub ) }">

	<MvIF EXPR = "{ g.MVGA:advanced }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( g.MVGA:track:template_code ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.module:code $ '_tracking', 'MVGA:track:template_code', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( g.MVGA:trans:template_code ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.module:code $ '_transaction', 'MVGA:trans:template_code', g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "MVGA_General_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT MVGA_Load( l.mvga ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'ga-tracking.mvc', l.track_template ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'ga-transaction.mvc', l.trans_template ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.mvga:google_id" VALUE = "{ g.MVGA:google_id }">
	<MvASSIGN NAME = "l.mvga:domain" 	VALUE = "{ g.MVGA:domain }">
	<MvASSIGN NAME = "l.mvga:mode" 		VALUE = "{ g.MVGA:mode }">
	<MvASSIGN NAME = "l.mvga:dsblecom"	VALUE = "{ g.MVGA:dsblecom }">
	<MvASSIGN NAME = "l.mvga:tracksub"	VALUE = "{ g.MVGA:tracksub }">

	<MvASSIGN NAME = "l.mvga:advanced" 	VALUE = "{ g.MVGA:advanced }">
	
	<MvIF EXPR = "{ NOT g.MVGA:advanced }">
		<MvASSIGN NAME = "l.mvga:tracktype" VALUE = "{ g.MVGA:tracktype }">
		<MvASSIGN NAME = "l.mvga:ad_sup" 	VALUE = "{ g.MVGA:ad_sup }">
		<MvASSIGN NAME = "l.mvga:link_att" 	VALUE = "{ g.MVGA:link_att }">
		<MvASSIGN NAME = "l.mvga:disable" 	VALUE = "{ g.MVGA:disable }">
		<MvASSIGN NAME = "l.mvga:anon_ip"	VALUE = "{ g.MVGA:anon_ip }">
		<MvASSIGN NAME = "l.mvga:crossdom"	VALUE = "{ g.MVGA:crossdom }">
		<MvASSIGN NAME = "l.mvga:domains"	VALUE = "{ g.MVGA:domains }">
		<MvASSIGN NAME = "l.mvga:force_ssl"	VALUE = "{ g.MVGA:force_ssl }">
		<MvASSIGN NAME = "l.mvga:simple"	VALUE = "{ g.MVGA:simple }">

		<MvEVAL EXPR = "{ MVGA_Generate_Tracking_Template( l.mvga, g.MVGA:track:template_code ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.track_template, '', g.MVGA:track:template_code, l.null_settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.module:code $ '_tracking', 'MVGA:track:template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.trans_template, '', g.MVGA:trans:template_code, l.null_settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.module:code $ '_transaction', 'MVGA:trans:template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT MVGA_Update( l.mvga ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "MVGA_General_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "text, html">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( 'ga-tracking.mvc', l.track_template ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( 'ga-transaction.mvc', l.trans_template ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.load_fields }">
		<MvIF EXPR = "{ NOT MVGA_Load( g.MVGA ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>

		<MvASSIGN NAME = "g.MVGA:track:template_code"	VALUE = "{ l.track_template:source }">
		<MvASSIGN NAME = "g.MVGA:track:templ_id"		VALUE = "{ l.track_template:templ_id }">
		<MvASSIGN NAME = "g.MVGA:trans:template_code"	VALUE = "{ l.trans_template:source }">
		<MvASSIGN NAME = "g.MVGA:trans:templ_id"		VALUE = "{ l.trans_template:templ_id }">
	</MvIF>

	<MvIF EXPR = "{ l.tab EQ l.module:code $ '_settings' }">
		<MvASSIGN NAME = "l.null" VALUE = "{ MVGA_Tab_Settings() }">
	<MvELSE>
		<MvHIDE FIELDS = "g.MVGA:google_id, g.MVGA:domain, g.MVGA:mode">
	</MvIF>

	<MvIF EXPR = "{ l.tab EQ l.module:code $ '_tracking' }">
		<MvASSIGN NAME = "l.null" VALUE = "{ MVGA_Tab_Tracking() }">
	<MvELSE>
		<MvHIDE FIELDS = "g.MVGA:track:templ_id, g.MVGA:track:template_code, g.MVGA:link_att, g.MVGA:tracktype, g.MVGA:disable, g.MVGA:ad_sup,
						  g.MVGA:anon_ip, g.MVGA:crossdom, g.MVGA:domains, g.MVGA:force_ssl, g.MVGA:simple, g.MVGA:tracksub, g.MVGA:advanced">
	</MvIF>

	<MvIF EXPR = "{ l.tab EQ l.module:code $ '_transaction' }">
		<MvASSIGN NAME = "l.null" VALUE = "{ MVGA_Tab_Transaction() }">
	<MvELSE>
		<MvHIDE FIELDS = "g.MVGA:trans:templ_id, g.MVGA:trans:template_code, g.MVGA:dsblecom">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "MVGA_Generate_Analytics_Tracking_Template" PARAMETERS = "settings var, source var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.source">
	<MIVA STANDARDOUTPUTLEVEL = "text, html"><mvt:miva compresswhitespace="off" />
<script>
(function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
(i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
})(window,document,'script','//www.google-analytics.com/analytics.js','ga');

<MIVA STANDARDOUTPUTLEVEL = "">

	<MvIF EXPR = "{ l.settings:disable }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">window['ga-disable-<mvt:item name="ga_jsencode" param="ga_tracking:google_id_unencoded" />'] = true;
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>

	<MvIF EXPR = "{ l.settings:crossdom }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">ga( 'require', 'linker' );
ga( 'linker:autoLink', [
	<mvt:foreach iterator="domain" array="ga_tracking:domains_unencoded">
		<mvt:if expr="l.pos1 GT 1">,</mvt:if>
		'<mvt:item name="ga_jsencode" param="domain" />'
	</mvt:foreach>
] );
ga('create', '<mvt:item name="ga_jsencode" param="ga_tracking:google_id_unencoded" />', 'auto',
{ 'allowLinker': true });
<MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSE>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">ga('create', '<mvt:item name="ga_jsencode" param="ga_tracking:google_id_unencoded" />', 'auto');
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>

	<MvIF EXPR = "{ l.settings:link_att }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">ga('require', 'linkid');
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	
	<MvIF EXPR = "{ l.settings:force_ssl }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">ga('set', 'forceSSL', true);
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>

	<MvIF EXPR = "{ l.settings:anon_ip }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">ga('set', 'anonymizeIp', true);
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>

	<MvIF EXPR = "{ l.settings:ad_sup }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">ga('require', 'displayfeatures');
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	
	<MvIF EXPR = "{ l.settings:mode EQ 'enhanced' }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
var mvga_basketitems = [
<mvt:foreach iterator="item" array="basket:items">
	<mvt:if expr="l.pos1 GT 1">,</mvt:if>
	{
		'code'		:	'<mvt:item name="ga_jsencode" param="item:code" />',
		'name'		:	'<mvt:item name="ga_jsencode" param="item:name" />',
		'line_id'	:	<mvt:eval expr="int( l.settings:item:line_id )" />,
		'group_id'	:	<mvt:eval expr="int( l.settings:item:group_id )" />,
		'price'		:	'<mvt:eval expr="l.settings:item:price ROUND 2" />',
		'quantity'	:	<mvt:eval expr="int( l.settings:item:quantity )" />
	}
</mvt:foreach>
];

var mvga_orderitems = [
<mvt:foreach iterator="item" array="order:items">
	<mvt:if expr="l.pos1 GT 1">,</mvt:if>
	{
		'order_id'	:	<mvt:eval expr="int( l.settings:item:order_id )" />,
		'code'		:	'<mvt:item name="ga_jsencode" param="item:code" />',
		'name'		:	'<mvt:item name="ga_jsencode" param="item:name" />',
		'price'		:   '<mvt:eval expr="l.settings:item:price ROUND 2" />',
		'quantity'	:	<mvt:eval expr="int( l.settings:item:quantity )" />
	}
</mvt:foreach>
];

var mvga_productlist = [];

(function( obj, eventType, fn )
{
    if ( obj.addEventListener )
    {
        obj.addEventListener( eventType, fn, false );
    }
    else if ( obj.attachEvent )
    {
        obj.attachEvent( 'on' + eventType, fn );
    }
})( window, 'load', function()
{
	if ( window.ga && ga.create )
	{
		var mvga_tracker = new MVGA_Tracker( '<mvt:item name="ga_jsencode" param="ga_tracking:product_link_prefix" />', '<mvt:item name="ga_jsencode" param="category:name" />', '<mvt:item name="ga_jsencode" param="product:code" />', '<mvt:item name="ga_jsencode" param="product:name" />', mvga_basketitems, mvga_orderitems );
	}
});

<mvt:if expr="l.settings:page:code EQ 'PROD'">
	ga( 'require', 'ec' );

	<mvt:foreach iterator="list_product" array="ga_tracking:product_list">
	ga( 'ec:addImpression',
	{
		'id'		: '<mvt:item name="ga_jsencode" param="list_product:code" />',
		'name'		: '<mvt:item name="ga_jsencode" param="list_product:name" />',
		'list'		: 'Related Products',
		'position'	: '<mvt:eval expr="int( l.settings:list_product:disp_order )" />'
	} ); 

	mvga_productlist.push( 
	{
		'code'		: '<mvt:item name="ga_jsencode" param="list_product:code" />',
		'name'		: '<mvt:item name="ga_jsencode" param="list_product:name" />',
		'link'		: '<mvt:item name="ga_jsencode" param="list_product:link" />',
		'position'	: '<mvt:eval expr="int( l.settings:list_product:disp_order )" />'
	} );
	</mvt:foreach>

	ga( 'ec:addProduct',
	{
		'id'		: '<mvt:item name="ga_jsencode" param="product:code" />',
		'name'		: '<mvt:item name="ga_jsencode" param="product:name" />',
		'category'	: '<mvt:item name="ga_jsencode" param="category:name" />'
	} ); 

	ga( 'ec:setAction', 'detail' );
<mvt:elseif expr="l.settings:page:code EQ 'CTGY' OR
				  l.settings:page:code EQ 'PLST' OR
				  l.settings:page:code EQ 'SRCH'">
	ga( 'require', 'ec' );

	<mvt:foreach iterator="list_product" array="ga_tracking:product_list">
	ga( 'ec:addImpression',
	{
		'id'		: '<mvt:item name="ga_jsencode" param="list_product:code" />',
		'name'		: '<mvt:item name="ga_jsencode" param="list_product:name" />',
		'category'	: '<mvt:item name="ga_jsencode" param="category:name" />',
		'list'		: '<mvt:item name="ga_jsencode" param="ga_tracking:list_label" />',
		'position'	: '<mvt:eval expr="int( l.settings:list_product:disp_order )" />'
	} ); 

	mvga_productlist.push( 
	{
		'code'		: '<mvt:item name="ga_jsencode" param="list_product:code" />',
		'name'		: '<mvt:item name="ga_jsencode" param="list_product:name" />',
		'link'		: '<mvt:item name="ga_jsencode" param="list_product:link" />',
		'position'	: '<mvt:eval expr="int( l.settings:list_product:disp_order )" />'
	} );
	</mvt:foreach>
<mvt:elseif expr="l.settings:page:code EQ 'OSEL'">
	ga( 'require', 'ec' );

	var i;

	for ( i = 0; i < mvga_basketitems.length; i++ )
	{
		ga( 'ec:addProduct', 
		{
			'id'		:	mvga_basketitems[ i ].code,
			'name'		:	mvga_basketitems[ i ].name,
			'price'		:	mvga_basketitems[ i ].price,
			'quantity'	:	mvga_basketitems[ i ].quantity
		} );
	}

	ga( 'ec:setAction', 'checkout', { 'step' : 1 } );
<mvt:elseif expr="l.settings:page:code EQ 'OPAY'">
	ga( 'require', 'ec' );

	var i;

	for ( i = 0; i < mvga_basketitems.length; i++ )
	{
		ga( 'ec:addProduct', 
		{
			'id'		:	mvga_basketitems[ i ].code,
			'name'		:	mvga_basketitems[ i ].name,
			'price'		:	mvga_basketitems[ i ].price,
			'quantity'	:	mvga_basketitems[ i ].quantity
		} );
	}

	ga( 'ec:setAction', 'checkout', { 'step' : 2 } );
<mvt:elseif expr="l.settings:page:code EQ 'INVC'">
	ga( 'require', 'ec' );

	<mvt:foreach iterator="ga_orderitem" array="ga_tracking:orderitems">
	ga( 'ec:addProduct', {
		'id'		: '<mvt:item name="ga_jsencode" param="ga_orderitem:code_unencoded" />',
		'name'		: '<mvt:item name="ga_jsencode" param="ga_orderitem:name_unencoded" />',
		'price'		: '<mvt:item name="ga_jsencode" param="ga_orderitem:price" />',
		'variant'	: '<mvt:item name="ga_jsencode" param="ga_orderitem:variant_label_unencoded" />',
		'quantity' 	: <mvt:eval expr="l.settings:ga_orderitem:quantity" />
	} );
	</mvt:foreach>

	ga( 'ec:setAction', 'purchase', {
		'id' 			: '<mvt:eval expr="int( l.settings:ga_orderitem:order_id )" />',
		'revenue' 		: '<mvt:item name="ga_jsencode" param="ga_tracking:order_total" />',
		'affiliation'	: '<mvt:item name="ga_jsencode" param="store:name" />',
		'tax'			: '<mvt:item name="ga_jsencode" param="ga_tracking:total_tax" />',
		'shipping'		: '<mvt:item name="ga_jsencode" param="ga_tracking:total_ship" />'
	} );
</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSEIF EXPR = "{ l.settings:mode EQ 'universal' AND l.settings:simple }">
<MIVA STANDARDOUTPUTLEVEL = "text, html"><mvt:if expr="l.settings:page:code EQ 'INVC'">
	ga('require', 'ecommerce');

	ga( 'ecommerce:addTransaction', {
		'id'			: '<mvt:eval expr="int( l.settings:ga_tracking:order_id )" />',
		'revenue'		: '<mvt:item name="ga_jsencode" param="ga_tracking:order_total" />',
		'affiliation'	: '<mvt:item name="ga_jsencode" param="store:name" />',
		'tax'			: '<mvt:item name="ga_jsencode" param="ga_tracking:total_tax" />',
		'shipping'		: '<mvt:item name="ga_jsencode" param="ga_tracking:total_ship" />'
	} );

	<mvt:foreach iterator="ga_orderitem" array="ga_tracking:orderitems">
	ga( 'ecommerce:addItem', {
		'id'		: '<mvt:eval expr="int( l.settings:ga_tracking:order_id )" />',
		'name'		: '<mvt:item name="ga_jsencode" param="ga_orderitem:name_unencoded" />',
		'price'		: '<mvt:item name="ga_jsencode" param="ga_orderitem:price" />',
		'sku'		: '<mvt:item name="ga_jsencode" param="ga_orderitem:code_unencoded" />',
		'category'	: '<mvt:item name="ga_jsencode" param="ga_orderitem:cancat_code_unencoded" />',
		'quantity' 	: <mvt:eval expr="l.settings:ga_orderitem:quantity" />
	} );
	</mvt:foreach>

	ga( 'ecommerce:send' );
</mvt:if>
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MIVA STANDARDOUTPUTLEVEL = "text, html">
<mvt:if expr="( g.UI_Exception NE 1 ) OR ( g.Session:cache:last_ui_exception EQ 'order_invoice' )">
ga( 'send', 'pageview', { 'page':'<mvt:item name="ga_jsencode" param="ga_tracking:url_override_unencoded" />' } );
</mvt:if>

</script>
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "MVGA_Generate_Classic_Transaction_Template" PARAMETERS = "source var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.source">
	<MIVA STANDARDOUTPUTLEVEL = "text, html"><mvt:miva compresswhitespace="off" />
<script type="text/javascript">
_gaq.push( ['_addTrans',
	'&mvt:ga_transaction:order_id;',	// transaction ID - required
	'&mvt:ga_transaction:store_name;',	// affiliation or store name
	'&mvt:ga_transaction:order_total;',	// total - required
	'&mvt:ga_transaction:total_tax;',	// tax
	'&mvt:ga_transaction:total_ship;',	// shipping
	'&mvt:ga_transaction:ship_city;',	// city
	'&mvt:ga_transaction:ship_state;',	// state or province
	'&mvt:ga_transaction:ship_cntry;'	// country
] );
<mvt:foreach iterator="item" array="ga_transaction:orderitems">
_gaq.push( [ '_addItem',
	'&mvt:item:order_id;',				// transaction ID - required
	'&mvt:item:code;',					// SKU/code - required
	'&mvt:item:name;',					// product name
	'&mvt:item:cancat_code;',			// category or variation
	'&mvt:item:price;',					// unit price - required
	'&mvt:item:quantity;'				// quantity - required
] );
</mvt:foreach>

_gaq.push( ['_trackTrans'] );	//submits transaction to the Analytics servers

</script>
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "MVGA_Generate_Tracking_Template" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:mode EQ 'classic' }"><MvEVAL EXPR = "{ MVGA_Generate_Classic_Tracking_Template( l.settings, l.code ) }">
	<MvELSE> 										<MvEVAL EXPR = "{ MVGA_Generate_Analytics_Tracking_Template( l.settings, l.code ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "MVGA_Generate_Classic_Tracking_Template" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code">
		<MIVA STANDARDOUTPUTLEVEL = "text, html"><mvt:miva compresswhitespace="off" />
<script type="text/javascript">
(function(){
var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
<MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:ad_sup }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">ga.src = ('https:' == document.location.protocol ? 'https://' : 'http://') + 'stats.g.doubleclick.net/dc.js';
<MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSE>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MIVA STANDARDOUTPUTLEVEL = "text, html">var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
})();

var _gaq = _gaq || [];
<MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:disable }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">window['ga-disable-&mvt:ga_tracking:google_id;'] = true;
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MvIF EXPR = "{ l.settings:link_att }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">var pluginUrl = '//www.google-analytics.com/plugins/ga/inpage_linkid.js';
_gaq.push(['_require', 'inpage_linkid', pluginUrl]);
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MIVA STANDARDOUTPUTLEVEL = "text, html">_gaq.push(['_setAccount', '&mvt:ga_tracking:google_id;']);
<MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:tracktype EQ 1 OR
					l.settings:tracktype EQ 2 }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">_gaq.push(['_setDomainName', '&mvt:ga_tracking:domain;']);
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MvIF EXPR = "{ l.settings:tracktype EQ 2 }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">_gaq.push(['_setAllowLinker', true ]);
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
 	<MvIF EXPR = "{ l.settings:anon_ip }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">_gaq.push (['_gat._anonymizeIp']);
<MIVA STANDARDOUTPUTLEVEL = "">
 	</MvIF>
 	<MvIF EXPR = "{ l.settings:force_ssl }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">_gaq.push(['_gat._forceSSL']);
<MIVA STANDARDOUTPUTLEVEL = "">
 	</MvIF>
	<MIVA STANDARDOUTPUTLEVEL = "text, html">_gaq.push(['_trackPageview', '&mvt:ga_tracking:url_override_noquotes;' ]);
<MIVA STANDARDOUTPUTLEVEL = "">
	<MIVA STANDARDOUTPUTLEVEL = "text, html"></script>
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "MVGA_Tab_Tracking" PARAMETERS = "" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<input type="hidden" name="MVGA_Tracking_Advanced" value="">

	<MvIF EXPR = "{ NOT ISNULL g.MVGA_Tracking_Advanced }">
		<MvASSIGN NAME = "g.MVGA:advanced" VALUE = "{ g.MVGA_Tracking_Advanced }">
	</MvIF>

	<table cellspacing="0" cellpadding="2" border="0" width="100%">
	<tr>
		<td nowrap>
			&nbsp;
		</td>
		<td width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.MVGA:tracksub, 'MVGA:tracksub', 1, 'Track Subscription Orders' ) }">
		</td>
	</tr>
	</table>

	<MvIF EXPR = "{ NOT g.MVGA:advanced }">
		<MvHIDE FIELDS = "g.MVGA:track:templ_id, g.MVGA:track:template_code">

		<table cellspacing="0" cellpadding="2" border="0" width="100%">
		<MvIF EXPR = "{ g.MVGA:mode EQ 'universal' }">
			<tr>
				<td nowrap>
					&nbsp;
				</td>
				<td width="100%">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.MVGA:simple, 'MVGA:simple', 1, 'Enable Ecommerce Transaction Tracking' ) }">
				</td>
			</tr>
		</MvIF>
		<tr>
			<td nowrap>
				&nbsp;
			</td>
			<td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.MVGA:anon_ip, 'MVGA:anon_ip', 1, 'IP Anonymization' ) }">
			</td>
		</tr>
		<tr>
			<td nowrap>
				&nbsp;
			</td>
			<td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.MVGA:force_ssl, 'MVGA:force_ssl', 1, 'Force SSL' ) }">
			</td>
		</tr>
		<tr>
			<td nowrap>
				&nbsp;
			</td>
			<td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.MVGA:link_att, 'MVGA:link_att', 1, 'Enhanced Link Attribution' ) }">
			</td>
		</tr>
		<tr>
			<td nowrap>
				&nbsp;
			</td>
			<td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.MVGA:ad_sup, 'MVGA:ad_sup', 1, 'Display Advertising Support' ) }">
			</td>
		</tr>
		<tr><td colspan="2">&nbsp;</td></tr>
		<MvIF EXPR = "{ g.MVGA:mode EQ 'classic' }">
			<MvHIDE FIELDS = "g.MVGA:crossdom, g.MVGA:domains">
			<tr>
				<td nowrap>
					<b>Domain Tracking:</b>
				</td>
				<td width="100%">
					<select name="MVGA:tracktype">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0', g.MVGA:tracktype, 'Single top-level domain (no subdomains)' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '1', g.MVGA:tracktype, 'Single top-level domain (includes subdomains)' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '2', g.MVGA:tracktype, 'Multiple top-level domains (includes subdomains)' ) }">
					</select>
				</td>
			</tr>
		<MvELSE>
			<MvHIDE FIELDS = "g.MVGA:tracktype">
			<tr>
				<td nowrap>
					&nbsp;
				</td>
				<td width="100%">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.MVGA:crossdom, 'MVGA:crossdom', 1, 'Cross Domain Auto Linking' ) }">
				</td>
			</tr>
			<tr>
				<td valign="top" nowrap>
					&nbsp;
				</td>
				<td width="100%">
					<span style="font-size: 0.8em;">Enter comma separated list of domains to be auto linked</span>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea( 'MVGA:domains', g.MVGA:domains, 5, 50 ) }">
				</td>
			</tr>
		</MvIF>
		<tr><td colspan="2">&nbsp;</td></tr>
		<tr>
			<td nowrap>
				&nbsp;
			</td>
			<td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.MVGA:disable, 'MVGA:disable', 1, 'Temporarily Disable Tracking (retains all configuration)' ) }">
			</td>
		</tr>
	<MvELSE>
		<MvHIDE FIELDS = "g.MVGA:track:templ_id, g.MVGA:ad_sup, g.MVGA:disable, g.MVGA:link_att, g.MVGA:tracktype, g.MVGA:crossdom, 
						  g.MVGA:domains, g.MVGA:anon_ip, g.MVGA:force_ssl, g.MVGA:simple, g.MVGA:advanced"> 
		<br><br>
		<table cellspacing="0" cellpadding="2" border="0" width="100%">
		<tr>
			<td valign="top" nowrap>
				<b>Main Template:</b>
			</td>
			<td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( 'MVGA:track:template_code', g.MVGA:track:template_code, 20, 100, g.MVGA:track:templ_id ) }">
			</td>
		</tr>
	</MvIF>
		<tr><td colspan="2">&nbsp;</td></tr>
		<tr><td>
			&nbsp;
		</td><td width="100%">
			<input type="hidden" name="MVGA:advanced" value="{ encodeentities( g.MVGA:advanced ) }">
			<MvIF EXPR = "{ g.MVGA:advanced }">
				<a href="#" onclick="Toggle( 'MVGA_Tracking_Advanced', 0 ); return false;">Point + Click Mode</a>
			<MvELSE>
				<a href="#" onclick="Toggle( 'MVGA_Tracking_Advanced', 1 ); return false;">Advanced Mode</a>
			</MvIF>
		</td></tr>
	</table>
</MvFUNCTION>

<MvFUNCTION NAME = "MVGA_Tab_Settings" PARAMETERS = "" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<table cellpadding="2" cellspacing="0" border="0" width="100%">
		<tr>
			<td nowrap>
				<b>Google Analytics ID:</b>
			</td>
			<td width="100%">
				<input type="text" name="MVGA:google_id" size="15" value="{ encodeentities( g.MVGA:google_id ) }">
			</td>
		</tr>
		<tr>
			<td nowrap>
				<b>Domain:</b>
			</td>
			<td width="100%">
				<input type="text" name="MVGA:domain" size="35" value="{ encodeentities( g.MVGA:domain ) }">
			</td>
		</tr>
		<tr><td colspan="2">&nbsp;</td></tr>
		<tr>
			<td nowrap>
				<b>Mode:</b>
			</td>
			<td width="100%">
				<select name="MVGA:mode">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'universal', 	g.MVGA:mode, 'Universal (analytics.js)' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'enhanced', 	g.MVGA:mode, 'Universal Enhanced Ecommerce (analytics.js)' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'classic', 	g.MVGA:mode, 'Classic (ga.js)' ) }">
				</select>
			</td>
		</tr>
	</table>
</MvFUNCTION>

<MvFUNCTION NAME = "MVGA_Tab_Transaction" PARAMETERS = "" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvHIDE FIELDS = "g.MVGA:trans:templ_id">
	<table cellpadding="2" cellspacing="0" border="0" width="100%">
		<tr>
			<td valign="top" nowrap>
				&nbsp;
			</td>
			<td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.MVGA:dsblecom, 'MVGA:dsblecom', 1, 'Disable Ecommerce Tracking (retains all configuration)' ) }">
			</td>
		</tr>
		<tr>
			<td valign="top" nowrap>
				Ecommerce Tracking Template:
			</td>
			<td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( 'MVGA:trans:template_code', g.MVGA:trans:template_code, 20, 100, g.MVGA:trans:templ_id ) }">
			</td>
		</tr>
	</table>
</MvFUNCTION>

<MvFUNCTION NAME = "FulfillmentModule_ProcessOrder" PARAMETERS = "module var, order var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.order:source NE 'subscription' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT MVGA_Load( l.settings ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT l.settings:tracksub }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.tt" VALUE = "0.00">
	<MvASSIGN NAME = "l.ts" VALUE = "0.00">

	<MvFOREACH ITERATOR = "l.charge" ARRAY = "l.ordercharges" COUNT = "{ [ g.Module_Library_DB ].OrderChargeList_Load_Order( l.order:id, l.ordercharges ) }">
		<MvIF EXPR = "{ l.charge:type EQ 'TAX' }">
			<MvASSIGN NAME = "l.tt" VALUE = "{ l.tt + l.charge:amount }">
		</MvIF>

		<MvIF EXPR = "{ l.charge:type EQ 'SHIPPING' }">
			<MvASSIGN NAME = "l.ts" VALUE = "{ l.ts + l.charge:amount }">
		</MvIF>
	</MvFOREACH>

	<MvFOREACH ITERATOR = "l.coupon" ARRAY = "l.coupons" COUNT = "{ [ g.Module_Feature_PGR_DB ].OrderCouponList_Load_Order( l.order:id, l.coupons ) }">
		<MvIF EXPR = "{ NOT ISNULL l.tcc }">
			<MvASSIGN NAME = "l.tcc" VALUE = "{ l.tcc $ ', ' }">
		</MvIF>

		<MvASSIGN NAME = "l.tcc" VALUE = "{ l.tcc $ l.coupon:code }">
	</MvFOREACH>

	<MvASSIGN NAME = "l.v"		VALUE = "1">
	<MvASSIGN NAME = "l.tid"	VALUE = "{ l.settings:google_id }">
	<MvASSIGN NAME = "l.cid"	VALUE = "{ Generate_UUIDv4() }">
	<MvASSIGN NAME = "l.t"		VALUE = "pageview">
	<MvASSIGN NAME = "l.dh"		VALUE = "{ encodeattribute( g.Domain:name ) }">
	<MvASSIGN NAME = "l.dp"		VALUE = "/admin.mvc">
	<MvASSIGN NAME = "l.dt"		VALUE = "Subscription%20Order">
	<MvASSIGN NAME = "l.ti"		VALUE = "{ l.order:id }">
	<MvASSIGN NAME = "l.ta"		VALUE = "{ encodeattribute( g.Store:name ) }">
	<MvASSIGN NAME = "l.tr"		VALUE = "{ l.order:total }">
	<MvASSIGN NAME = "l.pa"		VALUE = "purchase">

	<MvASSIGN NAME = "l.fields"	VALUE = "l.v, l.tid, l.cid, l.t, l.dh, l.dp, l.dt, l.ti, l.ta, l.tr, l.tt, l.ts, l.tcc, l.pa">

	<MvFOREACH ITERATOR = "l.orderitem" INDEX = "l.pos" ARRAY = "l.orderitems" COUNT = "{ [ g.Module_Library_DB ].OrderItemList_Load_Order( l.order:id, l.orderitems ) }">
		<MvASSIGN NAME = "{ 'l.pr' $ l.pos $ 'id' }" VALUE = "{ l.orderitem:code }">
		<MvASSIGN NAME = "{ 'l.pr' $ l.pos $ 'nm' }" VALUE = "{ l.orderitem:name }">
		<MvASSIGN NAME = "{ 'l.pr' $ l.pos $ 'qt' }" VALUE = "{ int( l.orderitem:quantity ) }">
		<MvASSIGN NAME = "{ 'l.pr' $ l.pos $ 'pr' }" VALUE = "{ l.orderitem:price ROUND 2 }">
		
		<MvASSIGN NAME = "l.fields" VALUE = "{ l.fields $ ', l.pr' $ l.pos $ 'id' }">
		<MvASSIGN NAME = "l.fields" VALUE = "{ l.fields $ ', l.pr' $ l.pos $ 'nm' }">
		<MvASSIGN NAME = "l.fields" VALUE = "{ l.fields $ ', l.pr' $ l.pos $ 'qt' }">
		<MvASSIGN NAME = "l.fields" VALUE = "{ l.fields $ ', l.pr' $ l.pos $ 'pr' }">
	</MvFOREACH>

	<MvASSIGN NAME = "l.z"		VALUE = "{ random( 9999999999 ) }">

	<MvASSIGN NAME = "l.fields" VALUE = "{ l.fields $ ', l.z' }">

	<MvCALL METHOD = "POST"
			ACTION = "https://www.google-analytics.com/collect"
			FIELDS = "{ l.fields }">
	</MvCALL>
	<MIVA MvCALL_Error = "fatal, display">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_UUIDv4" PARAMETERS = "" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.random" VALUE = "{ crypto_rand_bytes( 12 ) }">

	<MvIF EXPR = "{ NOT crypto_sha256( l.random, 'hex', l.sha256 ) }">
		<MvFUNCTIONRETURN VALUE = "{ l.random }">
	</MvIF>

	<MvASSIGN NAME = "l.uuid"	VALUE = "{ substring( l.sha256, 1, 8 ) }">
	<MvASSIGN NAME = "l.uuid"	VALUE = "{ l.uuid $ '-' }">
	<MvASSIGN NAME = "l.uuid"	VALUE = "{ l.uuid $ substring( l.sha256, 9, 4 ) }">
	<MvASSIGN NAME = "l.uuid"	VALUE = "{ l.uuid $ '-' }">

	<MvCOMMENT> Reserved version indicator for UUID v4 </MvCOMMENT>
	
	<MvASSIGN NAME = "l.uuid"	VALUE = "{ l.uuid $ '4' }">

	<MvASSIGN NAME = "l.uuid"	VALUE = "{ l.uuid $ substring( l.sha256, 13, 3 ) }">
	<MvASSIGN NAME = "l.uuid"	VALUE = "{ l.uuid $ '-' }">

	<MvCOMMENT> Reserved for UUID v4 </MvCOMMENT>

	<MvASSIGN NAME = "l.reserved" INDEX = 1 VALUE = "8">
	<MvASSIGN NAME = "l.reserved" INDEX = 2 VALUE = "9">
	<MvASSIGN NAME = "l.reserved" INDEX = 3 VALUE = "a">
	<MvASSIGN NAME = "l.reserved" INDEX = 4 VALUE = "b">

	<MvASSIGN NAME = "l.pos"	VALUE = "{ 1 + random( 3 ) }">
	<MvASSIGN NAME = "l.uuid"	VALUE = "{ l.uuid $ l.reserved[ l.pos ] }">

	<MvASSIGN NAME = "l.uuid"	VALUE = "{ l.uuid $ substring( l.sha256, 17, 3 ) }">
	<MvASSIGN NAME = "l.uuid"	VALUE = "{ l.uuid $ '-' }">
	<MvASSIGN NAME = "l.uuid"	VALUE = "{ l.uuid $ substring( l.sha256, 21, 12 ) }">

	<MvFUNCTIONRETURN VALUE = "{ l.uuid }">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: clientside
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Clientside" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.len"	VALUE = "{ len_var( g.Filename ) }">

	<MvIF EXPR = "{ ( '.js' EIN g.Filename ) EQ l.len }">		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
	</MvIF>

	<MvIF EXPR = "{ Module_Clientside_Output_File( l.module, g.Filename ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.null"	VALUE = "{ miva_output_header( 'Status', '404 Not Found' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Clientside_Output_File" PARAMETERS = "module var, filename" STANDARDOUTPUTLEVEL = "">
	<MvINCLUDE FILE = "modules/util/mvga/output.mv">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Clientside_File_Integrity" PARAMETERS = "module var, filename, minified" STANDARDOUTPUTLEVEL = "">
	<MvINCLUDE FILE = "modules/util/mvga/integrity.mv">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Clientside_Combined_Integrity" PARAMETERS = "module var, filename, minified" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvINCLUDE FILE = "modules/component/seo_include.mv">

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2025 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-MOD-REVB-
| Next Error Code: 64  
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-reviewbaskets">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Review Baskets">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.1301">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "10.05">
	<MvASSIGN NAME = "l.module:description"	VALUE = "Review Baskets will allow an administrative user to view, search and manipulate shopper baskets.">
	<MvASSIGN NAME = "l.module:features"	VALUE = "util, vis_util, clientside, clientside_sri, json">
</MvFUNCTION>

<MvCOMMENT>
|
| Store Utilities Feature (util)
|
</MvCOMMENT>

<MvFUNCTION NAME = "StoreUtilityModule_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Action_Privileges" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL g.Admin_Open_Store }">							<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN VALUE = 0>	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Action" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Screen_Privileges" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL g.Admin_Open_Store }">							<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN VALUE = 0>	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Screen" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_Start( 'Review Baskets', '', '' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScript_SetVariables( 'g', 'adminurl,sessionurl,clientside_url,AdminGraphics_Path,Store_Code' ) }">

	<MvIF EXPR = "{ [ g.Module_Admin ].Element_Admin_Core_JavaScript_Combined_Begin() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_AJAX_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_UI_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_Functions_JavaScript() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_Admin_Core_JavaScript_Combined_End() }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_AdminUI_CSS() }">
	
	<MvEVAL EXPR = "{ Element_ReviewBasketsList_CSS( l.module ) }">

	<MvIF EXPR = "{ Element_ReviewBaskets_JavaScript_Combined_Begin( l.module ) }">
		<MvEVAL EXPR = "{ Element_ReviewBasketsList_JavaScript( l.module ) }">

		<MvEVAL EXPR = "{ Element_ReviewBaskets_JavaScript_Combined_End( l.module ) }">
	</MvIF>

	<script language="JavaScript">
		MMScreen_LoadFinished( function() { new ReviewBasketsList(); } );
	</script>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_End( 'Review Baskets', '', '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginContent() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawTabs( 'RBSK', 'RBSK:Baskets' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawButtons_Suppress( '[UPDATE]' ) }">
	<MvEVAL EXPR = "{ Element_ReviewBasketsList_HTML( l.module ) }">

	<MvHIDE FIELDS = "g.Module_Code">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].EndContent() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawButtons( '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].EndScreen() }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_LeftNavigation_Privileges" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL g.Admin_Open_Store }">							<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN VALUE = 0>	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_LeftNavigation" PARAMETERS = "module var, indent" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].LeftNavigation_Dot( l.indent - 1, 'Screen=SUTL&Store_Code=' $ encodeattribute( g.Store:code ) $ '&Module_Code=' $ encodeattribute( l.module:code ), 'Main', 'Review Baskets' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Head" PARAMETERS = "module var, tab" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Tabs" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature Clientside
|
</MvCOMMENT>

<MvINCLUDE FILE = "modules/util/reviewbaskets/combined.mv">

<MvFUNCTION NAME = "Module_Clientside" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( '.js' EIN g.Filename ) EQ len_var( g.Filename ) }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Filename EQ 'combined.js' }">									<MvFUNCTIONRETURN VALUE = "{ Element_ReviewBaskets_JavaScript_Combined( l.module ) }">
	<MvELSEIF EXPR = "{ Module_Clientside_Output_File( l.module, g.Filename ) }">	<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.null"	VALUE = "{ miva_output_header( 'Status', '404 Not Found' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Clientside_Output_File" PARAMETERS = "module var, filename" STANDARDOUTPUTLEVEL = "">
	<MvINCLUDE FILE = "modules/util/reviewbaskets/output.mv">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Clientside_File_Integrity" PARAMETERS = "module var, filename, minified" STANDARDOUTPUTLEVEL = "">
	<MvINCLUDE FILE = "modules/util/reviewbaskets/integrity.mv">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Clientside_Combined_Integrity" PARAMETERS = "module var, filename, minified" STANDARDOUTPUTLEVEL = "">
	<MvINCLUDE FILE = "modules/util/reviewbaskets/combined_integrity.mv">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature JSON
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_JSON" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_BasketIndex_Load_ID' }">					<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketIndex_Load_ID( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_BasketList_Load_Query' }">				<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketList_Load_Query( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_ItemList_Load' }">						<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_ItemList_Load() }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_ChargeList_Load' }">					<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_ChargeList_Load() }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_Update_Charges' }">						<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_Update_Charges() }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_BasketList_Delete' }">					<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketList_Delete() }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_Basket_Delete_All' }">					<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_Basket_Delete_All() }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_BasketItem_Add' }">						<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketItem_Add() }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_BasketItem_Update' }">					<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketItem_Update() }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_BasketItemList_Delete' }">				<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketItemList_Delete() }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_BasketItem_DetermineSKU' }">			<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketItem_DetermineSKU() }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_BasketItem_DetermineVariant' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketItem_DetermineVariant() }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_SubscriptionAndBasketItem_Add' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_SubscriptionAndBasketItem_Add() }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_SubscriptionAndBasketItem_Update' }">	<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_SubscriptionAndBasketItem_Update() }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_BasketPriceGroupList_Load_Query' }">	<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketPriceGroupList_Load_Query() }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_BasketPriceGroup_Update_Assigned' }">	<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketPriceGroup_Update_Assigned() }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_BasketCouponList_Load_Query' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketCouponList_Load_Query() }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_BasketCoupon_Update_Assigned' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketCoupon_Update_Assigned() }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ReviewBaskets_Order_Create' }">						<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_Order_Create() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketIndex_Load_ID" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Integer(	'R', 'Basket_ID',	l.basket_id )	OR
					NOT [ g.Module_JSON ].JSON_Input_Filter(	'o', 'Filter',		l.filter )		OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Sort',		l.sort ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvASSIGN NAME = "l.query"				VALUE = "">
	<MvASSIGN NAME = "l.record"				VALUE = "">
	<MvASSIGN NAME = "l.available_filters"	VALUE = "{ JSON_ReviewBaskets_Filters() }">
	<MvASSIGN NAME = "l.orderby_columns"	VALUE = "{ JSON_ReviewBaskets_OrderBy() }">

	<MvREFERENCE NAME = "l.basket"			VARIABLE = "l.record:bask">
	<MvREFERENCE NAME = "l.customer"		VARIABLE = "l.record:cust">
	<MvREFERENCE NAME = "l.businessaccount"	VARIABLE = "l.record:ba">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Load_ID( l.basket_id, l.basket ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00008', 'Basket not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.basket:cust_id }">
		<MvIF EXPR = "{ [ g.Module_Feature_CUS_DB ].Customer_Load_ID( l.basket:cust_id, l.customer ) }">
			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_CUS_DB ].BusinessAccount_Load( l.customer:account_id, l.businessaccount ) }">
		</MvIF>
	</MvIF>

	<MvCOMMENT>
	|
	| The output of Basket_Load_ID sets xxx_addr to xxx_addr $ ' ' $ xxx_addr2 from the database.  SQL_Query_Index doesn't know this so we need
	| to explicitly reset those values to what it expects.
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.basket:ship_addr"	VALUE = "{ l.basket:ship_addr1 }">
	<MvASSIGN NAME = "l.basket:bill_addr"	VALUE = "{ l.basket:bill_addr1 }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query, g.Store_Table_Prefix $ 'Baskets', 'bask' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, 'bask', g.Store_Table_Prefix $ 'Customers', 			'cust', 'cust.id = bask.cust_id',	'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, 'bask', g.Store_Table_Prefix $ 'BusinessAccounts', 	'ba', 	'cust.account_id = ba.id',	'' ) }">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter_Callback( l.query, l.filter, l.available_filters, g.Module_Root $ l.module:module, 'JSON_ReviewBaskets_BasketList_Load_Query_Filter', 'JSON_ReviewBaskets_BasketList_Load_Query_Advanced_Filter', l.null ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_OrderBy( l.query, l.sort, l.orderby_columns, 'bask.lastupdate;bask.basket_id' ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Index( l.query, l.record, l.index ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		"index":	<MvEVAL EXPR = "{ int( l.index ) }">
	}
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketList_Load_Query" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Filter(	'o', 'Filter',	l.filter )	OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Sort',	l.sort )	OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'o', 'Offset',	l.offset )	OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'o', 'Count',	l.count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvASSIGN NAME = "l.available_filters"	VALUE = "{ JSON_ReviewBaskets_Filters() }">
	<MvASSIGN NAME = "l.orderby_columns"	VALUE = "{ JSON_ReviewBaskets_OrderBy() }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'bask.*' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT_NULL_CHAR( l.search_query, 'cust.login',		'cust_login' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT_NULL_CHAR( l.search_query, 'cust.pw_email',	'cust_pw_email' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT_NULL_CHAR( l.search_query, 'ba.title',		'business_title' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'Baskets', 'bask' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'bask', g.Store_Table_Prefix $ 'Customers', 			'cust', 'cust.id = bask.cust_id',	'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'bask', g.Store_Table_Prefix $ 'BusinessAccounts', 	'ba', 	'cust.account_id = ba.id',	'' ) }">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter_Callback( l.search_query, l.filter, l.available_filters, g.Module_Root $ l.module:module, 'JSON_ReviewBaskets_BasketList_Load_Query_Filter', 'JSON_ReviewBaskets_BasketList_Load_Query_Advanced_Filter', l.null ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_OrderBy( l.search_query, l.sort, l.orderby_columns, 'bask.lastupdate;bask.basket_id' ) }">
																								
	<MvASSIGN NAME = "l.search_sql"	VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Count( l.search_query, l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'ReviewBaskets', l.search_sql, l.search_fields, l.offset, l.count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00001', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basket_count" VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		"data":
		[
		<MvWHILE EXPR = "{ ( NOT ReviewBaskets.d.EOF ) AND ( ( l.count EQ 0 ) OR ( l.basket_count LT l.count ) ) }">
			<MvASSIGN NAME = "l.basket_expired"			VALUE = "{ ReviewBaskets.d.lastupdate LT ( s.dyn_time_t - ( g.Store:baskexp * 60 ) ) }">

			<MvASSIGN NAME = "l.basket_subtotal"		VALUE = 0.00>
			<MvASSIGN NAME = "l.basket_total"			VALUE = 0.00>
			<MvASSIGN NAME = "l.basket_quantity"		VALUE = 0>

			<MvASSIGN NAME = "l.basket_subtotal" 		VALUE = "{ [ g.Module_Library_DB ].Basket_SubTotal( ReviewBaskets.d.basket_id ) }">
			<MvASSIGN NAME = "l.basket_chargetotal" 	VALUE = "{ [ g.Module_Library_DB ].BasketCharge_Total( ReviewBaskets.d.basket_id ) }">
			<MvASSIGN NAME = "l.basket_total" 			VALUE = "{ l.basket_subtotal + l.basket_chargetotal }">
			<MvASSIGN NAME = "l.basket_quantity" 		VALUE = "{ [ g.Module_Library_DB ].Basket_Quantity_All( ReviewBaskets.d.basket_id ) }">

			<MvASSIGN NAME = "l.formatted_total" 		VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.basket_total ) }">
			<MvASSIGN NAME = "l.formatted_subtotal" 	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.basket_subtotal ) }">		

			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.basket_count ) }">
			"cust_id":				<MvEVAL EXPR = "{ int( ReviewBaskets.d.cust_id ) }">,
			"cust_login":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.cust_login ) }">",
			"cust_pw_email":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.cust_pw_email ) }">",
			"business_title":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.business_title ) }">",
			"order_id":				<MvEVAL EXPR = "{ int( ReviewBaskets.d.order_id ) }">,
			"basket_id":			<MvEVAL EXPR = "{ int( ReviewBaskets.d.basket_id ) }">,
			"session_id":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.session_id ) }">",
			"order_proc":			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Boolean( ReviewBaskets.d.order_proc ) }">,
			"lastupdate":			<MvEVAL EXPR = "{ int( ReviewBaskets.d.lastupdate ) }">,
			"auth_fails":			<MvEVAL EXPR = "{ int( ReviewBaskets.d.auth_fails ) }">,
			"ship_res":				<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Boolean( ReviewBaskets.d.ship_res ) }">,
			"ship_fname":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.ship_fname ) }">",
			"ship_lname":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.ship_lname ) }">",
			"ship_email":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.ship_email ) }">",
			"ship_comp":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.ship_comp ) }">",
			"ship_phone":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.ship_phone ) }">",
			"ship_fax":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.ship_fax ) }">",
			"ship_addr1":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.ship_addr ) }">",
			"ship_addr2":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.ship_addr2 ) }">",
			"ship_city":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.ship_city ) }">",
			"ship_state":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.ship_state ) }">",
			"ship_zip":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.ship_zip ) }">",
			"ship_cntry":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.ship_cntry ) }">",
			"bill_fname":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.bill_fname ) }">",
			"bill_lname":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.bill_lname ) }">",
			"bill_email":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.bill_email ) }">",
			"bill_comp":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.bill_comp ) }">",
			"bill_phone":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.bill_phone ) }">",
			"bill_fax":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.bill_fax ) }">",
			"bill_addr1":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.bill_addr ) }">",
			"bill_addr2":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.bill_addr2 ) }">",
			"bill_city":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.bill_city ) }">",
			"bill_state":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.bill_state ) }">",
			"bill_zip":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.bill_zip ) }">",
			"bill_cntry":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( ReviewBaskets.d.bill_cntry ) }">",
			"quantity":				<MvEVAL EXPR = "{ int( l.basket_quantity ) }">,
			"subtotal":				<MvEVAL EXPR = "{ l.basket_subtotal ROUND 2 }">,
			"total":				<MvEVAL EXPR = "{ l.basket_total ROUND 2 }">,
			"expired":				<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Boolean( l.basket_expired ) }">,
			"formatted_subtotal":	"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.formatted_subtotal ) }">",
			"formatted_total":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.formatted_total ) }">"
			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">

			<MvSKIP NAME = "Merchant" VIEW = "ReviewBaskets" ROWS = 1>
		</MvWHILE>
		],
		"total_count": 	<MvEVAL EXPR = "{ int( l.total_count ) }">,
		"start_offset": <MvEVAL EXPR = "{ int( l.offset ) }">
	}
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ReviewBaskets">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketList_Load_Query_Filter" PARAMETERS = "query var, field_count var, filter_name, filter_value, data var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.filter_name EQ 'Basket_Show' }">
		<MvIF EXPR = "{ l.filter_value EQ 'All' }">
		<MvELSEIF EXPR = "{ l.filter_value EQ 'Non-Empty' }">
			<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_DISTINCT( l.query ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_DISTINCT_With_COUNT_SELECT( l.query, 'bask.basket_id' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query, g.Store_Table_Prefix $ 'BasketItems', 'bi' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'bi.basket_id = bask.basket_id', '' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketList_Load_Query_Advanced_Filter" PARAMETERS = "query var, field_count, filter_name, filter_operator, filter_value, data var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.filter_name EQ 'product_code' }">
		<MvASSIGN NAME = "g.ReviewBaskets_Product_Code"	VALUE = "{ l.filter_value }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'bask.basket_id = ( SELECT MAX( item.basket_id ) FROM ' $ g.Store_Table_Prefix $ 'BasketItems item WHERE item.basket_id = bask.basket_id AND item.code = ? )', 'g.ReviewBaskets_Product_Code' ) }">
		
		<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ l.filter_name EQ 'type' }">
		<MvIF EXPR = "{ l.filter_value EQ 'Anonymous' }">
			<MvIF EXPR = "{ l.filter_operator EQ 'NE' }">	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'bask.cust_id <> 0 OR bask.order_id <> 0',	'' ) }"> 
			<MvELSE>										<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'bask.cust_id = 0 AND bask.order_id = 0',	'' ) }"> 
			</MvIF>
		<MvELSEIF EXPR = "{ l.filter_value EQ 'Customer' }">
			<MvIF EXPR = "{ l.filter_operator EQ 'NE' }">	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'bask.cust_id = 0 OR bask.order_id <> 0',	'' ) }"> 
			<MvELSE>										<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'bask.cust_id <> 0 AND bask.order_id = 0',	'' ) }"> 
			</MvIF>
		<MvELSEIF EXPR = "{ l.filter_value EQ 'Checkout' }">
			<MvIF EXPR = "{ l.filter_operator EQ 'NE' }">	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'bask.order_id = 0',						'' ) }"> 
			<MvELSE>										<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'bask.order_id <> 0', '' ) }"> 
			</MvIF>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ l.filter_name EQ 'expired' }">
		<MvASSIGN NAME = "l.query_field" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Field( s.dyn_time_t - ( g.Store:baskexp * 60 ) ) }">

		<MvIF EXPR = "{ l.filter_value EQ 'false' }">
			<MvIF EXPR = "{ l.filter_operator EQ 'NE' }">	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'bask.lastupdate < ?', l.query_field ) }">
			<MvELSE>										<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'bask.lastupdate > ?', l.query_field ) }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.filter_value EQ 'true' }">
			<MvIF EXPR = "{ l.filter_operator EQ 'NE' }">	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'bask.lastupdate > ?', l.query_field ) }">
			<MvELSE>										<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'bask.lastupdate < ?', l.query_field ) }">
			</MvIF>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_Filters" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.available_filters" VALUE = "">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_INTEGER(	l.available_filters, 'bask.basket_id', 	'basket_id' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_INTEGER(	l.available_filters, 'bask.lastupdate',	'lastupdate' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_BOOL(		l.available_filters, 'bask.ship_res',	'ship_res' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.ship_fname',	'ship_fname' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.ship_lname',	'ship_lname' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.ship_email',	'ship_email' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.ship_comp',	'ship_comp' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.ship_phone',	'ship_phone' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.ship_fax',	'ship_fax' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.ship_addr',	'ship_addr1' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.ship_addr2',	'ship_addr2' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.ship_city',	'ship_city' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.ship_state',	'ship_state' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.ship_zip',	'ship_zip' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.ship_cntry',	'ship_cntry' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.bill_fname',	'bill_fname' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.bill_lname',	'bill_lname' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.bill_email',	'bill_email' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.bill_comp',	'bill_comp' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.bill_phone',	'bill_phone' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.bill_fax',	'bill_fax' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.bill_addr',	'bill_addr1' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.bill_addr2',	'bill_addr2' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.bill_city',	'bill_city' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.bill_state',	'bill_state' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.bill_zip',	'bill_zip' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'bask.bill_cntry',	'bill_cntry' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_INTEGER(	l.available_filters, 'bask.order_id',	'order_id' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_NULL_CHAR(	l.available_filters, 'cust.login',		'cust_login' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_NULL_CHAR(	l.available_filters, 'cust.pw_email',	'cust_pw_email' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_NULL_CHAR(	l.available_filters, 'ba.title',		'business_title' ) }">

	<MvFUNCTIONRETURN VALUE = "{ l.available_filters }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_OrderBy" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "basket_id:bask.basket_id,lastupdate:bask.lastupdate,ship_fname:bask.ship_fname,ship_lname:bask.ship_lname,
							   ship_email:bask.ship_email,ship_comp:bask.ship_comp,ship_phone:bask.ship_phone,ship_fax:bask.ship_fax,
							   ship_addr1:bask.ship_addr,ship_addr2:bask.ship_addr2,ship_city:bask.ship_city,ship_state:bask.ship_state,
							   ship_zip:bask.ship_zip,ship_cntry:bask.ship_cntry,bill_fname:bask.bill_fname,bill_lname:bask.bill_lname,
							   bill_email:bask.bill_email,bill_comp:bask.bill_comp,bill_phone:bask.bill_phone,bill_fax:bask.bill_fax,
							   bill_addr1:bask.bill_addr,bill_addr2:bask.bill_addr2,bill_city:bask.bill_city,bill_state:bask.bill_state,
							   bill_zip:bask.bill_zip,bill_cntry:bask.bill_cntry,cust_login:cust.login:null_char,cust_pw_email:cust.pw_email:null_char,
							   business_title:ba.title:null_char,order_id:bask.order_id,auth_fails:bask.auth_fails,ship_res:bask.ship_res">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_Basket_Delete_All" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 0, 0, 0, 1 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_INV_DB ].InventorySettings_Load_Cached( l.inventorysettings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ l.inventorysettings:mode EQ 'basket' }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_InventoryAdjust_ProductList( 0 ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Delete_All() }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00003', g.Error_Message ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-MOD-REVB-00004', 'All baskets deleted' ) }">

	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ [ g.Module_Library_DB ].StoreModuleList_Load_Features_Cached( 'cleanup_store', l.modules ) }">
		<MvIF EXPR = "{ l.module:api_ver GE 10.10 }">
			<MvIF EXPR = "{ NOT [ g.Module_Root $ l.module:module ].Module_Cleanup_Baskets( l.module ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
			</MvIF>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketList_Delete" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 0, 0, 0, 1 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Integer_Array( 'R', 'Basket_IDs', l.basket_ids, l.basket_id_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvASSIGN NAME = "l.processed"		VALUE = 0>
	<MvASSIGN NAME = "l.error_count"	VALUE = 0>
	<MvASSIGN NAME = "l.errors"			VALUE = "">
	<MvASSIGN NAME = "l.start_time"		VALUE = "{ s.dyn_time_t }">

	<MvFOREACH ITERATOR = "l.basket_id" ARRAY = "l.basket_ids" COUNT = "{ l.basket_id_count }">
		<MvASSIGN NAME = "l.processed"	VALUE = "{ l.processed + 1 }">

		<MvIF EXPR = "{ [ g.Module_Library_DB ].Basket_Load_ID( l.basket_id, l.basket ) }">
			<MvFOREACH ITERATOR = "l.basketitem" ARRAY = "l.basketitems" COUNT = "{ [ g.Module_Library_DB ].BasketItemList_Load_Basket( l.basket:basket_id, l.basketitems ) }">
				<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_Product_Load_BasketItem( l.basketitem, l.product ) }">
					<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
						<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
					</MvIF>

					<MvFOREACHCONTINUE>
				</MvIF>

				<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_BasketItem_Delete( l.product, l.basketitem ) }">
					<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
				</MvIF>
			</MvFOREACH>

			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Reset( l.basket ) }">
				<MvASSIGN NAME = "l.error_count" VALUE = "{ [ g.Module_JSON ].JSON_Response_ListProcessed_Error( l.basket_id, g.Error_Code, g.Error_Message, l.errors ) }">
			<MvELSE>
				<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-MOD-REVB-00005', 'Basket ID ' $ l.basket_id $ ' deleted' ) }">
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ ( s.dyn_time_t - l.start_time ) GE 60 }">								<MvFOREACHSTOP>
		<MvELSEIF EXPR = "{ ( s.dyn_time_remaining GE 0 ) AND ( s.dyn_time_remaining LE 3 ) }"> <MvFOREACHSTOP>
		</MvIF>
	</MvFOREACH>

	<MvIF EXPR = "{ l.error_count }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_ListProcessed_Errors( l.processed, l.errors ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_ListProcessed_Success( l.processed ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_ItemList_Load" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Integer( 'R', 'Basket_ID', l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	[
	<MvFOREACH ITERATOR = "l.basketitem" ARRAY = "l.basketitems" COUNT = "{ [ g.Module_Library_DB ].BasketItemList_Load_Basket( l.basket_id, l.basketitems ) }">
		<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.basketitem_pos ) }">
		"basket_id":		<MvEVAL EXPR = "{ int( l.basketitem:basket_id ) }">,
		"line_id":			<MvEVAL EXPR = "{ int( l.basketitem:line_id ) }">,
		"parent_id":		<MvEVAL EXPR = "{ int( l.basketitem:parent_id ) }">,
		"product_id":		<MvEVAL EXPR = "{ int( l.basketitem:product_id ) }">,
		"code":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.basketitem:code ) }">",
		"name":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.basketitem:name ) }">",
		"sku":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.basketitem:sku ) }">",
		"type":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.basketitem:type ) }">",
		"retail":			<MvEVAL EXPR = "{ encodejavascriptnumber( l.basketitem:retail ) }">,
		"base_price":		<MvEVAL EXPR = "{ encodejavascriptnumber( l.basketitem:base_price ) }">,
		"price":			<MvEVAL EXPR = "{ encodejavascriptnumber( l.basketitem:price ) }">,
		"total":			<MvEVAL EXPR = "{ encodejavascriptnumber( l.basketitem:total ) }">,
		"weight":			<MvEVAL EXPR = "{ encodejavascriptnumber( l.basketitem:weight ) }">,
		"formatted_weight":	"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( [ g.Module_Library_Utilities ].Format_Weight( l.basketitem:weight ) ) }">",
		"taxable":			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Boolean( l.basketitem:taxable ) }">,
		"upsold":			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Boolean( l.basketitem:upsold ) }">,
		"quantity":			<MvEVAL EXPR = "{ int( l.basketitem:quantity ) }">,
		"subscrp_id":		<MvEVAL EXPR = "{ int( l.basketitem:subscrp_id ) }">,
		"subterm_id":		<MvEVAL EXPR = "{ int( l.basketitem:subterm_id ) }">

		<MvASSIGN NAME = "l.discount_pos"	VALUE = 0>
		<MvASSIGN NAME = "l.discount_count"	VALUE = "{ [ g.Module_Library_DB ].BasketItemDiscountList_Load_Line( l.basketitem:line_id, l.discounts ) }">

		<MvIF EXPR = "{ l.discount_count }">
			, "discounts":
			[
				<MvFOREACH ITERATOR = "l.discount" ARRAY = "l.discounts" COUNT = "{ l.discount_count }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.discount_pos ) }">
					"basket_id":	<MvEVAL EXPR = "{ int( l.discount:basket_id ) }">,
					"line_id":		<MvEVAL EXPR = "{ int( l.discount:line_id ) }">,
					"pgrp_id":		<MvEVAL EXPR = "{ int( l.discount:pgrp_id ) }">,
					"display":		<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Boolean( l.discount:display ) }">,
					"descrip":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.discount:descrip ) }">",
					"discount":		<MvEVAL EXPR = "{ encodejavascriptnumber( l.discount:discount ) }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
				</MvFOREACH>
			]
		</MvIF>

		<MvASSIGN NAME = "l.option_pos"						VALUE = 0>
		<MvASSIGN NAME = "l.option_subtotal"				VALUE = 0.00>
		<MvASSIGN NAME = "l.option_count"					VALUE = "{ [ g.Module_Library_DB ].BasketOptionList_Load_Line( l.basketitem:line_id, l.options ) }">

		<MvIF EXPR = "{ l.option_count }">
			, "options":
			[
				<MvFOREACH ITERATOR = "l.option" ARRAY = "l.options" COUNT = "{ l.option_count }">
					<MvASSIGN NAME = "l.option_subtotal"	VALUE = "{ l.option_subtotal + l.option:price }">

					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.option_pos ) }">
						"id":				<MvEVAL EXPR = "{ int( l.option:id ) }">,
						"basket_id":		<MvEVAL EXPR = "{ int( l.option:basket_id ) }">,
						"line_id":			<MvEVAL EXPR = "{ int( l.option:line_id ) }">,
						"attr_id":			<MvEVAL EXPR = "{ int( l.option:attr_id ) }">,
						"attr_code":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:attr_code ) }">",
						"attmpat_id":		<MvEVAL EXPR = "{ int( l.option:attmpat_id ) }">,
						"option_id":		<MvEVAL EXPR = "{ int( l.option:option_id ) }">,
						"opt_code":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:opt_code ) }">",
						"retail":			<MvEVAL EXPR = "{ encodejavascriptnumber( l.option:retail ) }">,
						"base_price":		<MvEVAL EXPR = "{ encodejavascriptnumber( l.option:base_price ) }">,
						"price":			<MvEVAL EXPR = "{ encodejavascriptnumber( l.option:price ) }">,
						"weight":			<MvEVAL EXPR = "{ encodejavascriptnumber( l.option:weight ) }">,
						"formatted_weight":	"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( [ g.Module_Library_Utilities ].Format_Weight( l.option:weight ) ) }">",
						"data":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:data ) }">",
						"data_long":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:data_long ) }">",
						"attr_prompt":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:attr_prompt ) }">",
						"opt_prompt":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:opt_prompt ) }">",

						<MvCOMMENT>
						|
						| Compatibility fields from versions less than 10.00.00
						|
						</MvCOMMENT>

						"attribute":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:attr_code ) }">",

						<MvIF EXPR = "{ l.option:option_id }">
							"value":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:opt_code ) }">"
						<MvELSEIF EXPR = "{ NOT ISNULL l.option:data }">
							"value":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:data ) }">"
						<MvELSEIF EXPR = "{ NOT ISNULL l.option:data_long }">
							"value":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:data_long ) }">"
						<MvELSE>
							"value":		""
						</MvIF>

						<MvASSIGN NAME = "l.discount_pos"		VALUE = 0>
						<MvASSIGN NAME = "l.discount_count"		VALUE = "{ [ g.Module_Library_DB ].BasketOptionDiscountList_Load_LineOption_ID( l.option:id, l.discounts ) }">

						<MvIF EXPR = "{ l.discount_count }">
							, "discounts":
							[
								<MvFOREACH ITERATOR = "l.discount" ARRAY = "l.discounts" COUNT = "{ l.discount_count }">
									<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.discount_pos ) }">
									"basket_id":	<MvEVAL EXPR = "{ encodejavascriptnumber( l.discount:basket_id ) }">,
									"line_id":		<MvEVAL EXPR = "{ encodejavascriptnumber( l.discount:line_id ) }">,
									"lineopt_id":	<MvEVAL EXPR = "{ encodejavascriptnumber( l.discount:lineopt_id ) }">,
									"attr_id":		<MvEVAL EXPR = "{ encodejavascriptnumber( l.discount:attr_id ) }">,
									"attmpat_id":	<MvEVAL EXPR = "{ encodejavascriptnumber( l.discount:attmpat_id ) }">,
									"pgrp_id":		<MvEVAL EXPR = "{ encodejavascriptnumber( l.discount:pgrp_id ) }">,
									"display":		<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Boolean( l.discount:display ) }">,
									"descrip":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.discount:descrip ) }">",
									"discount":		<MvEVAL EXPR = "{ encodejavascriptnumber( l.discount:discount ) }">
									<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
								</MvFOREACH>
							]
						</MvIF>
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
				</MvFOREACH>
			]
		</MvIF>

		<MvIF EXPR = "{ l.basketitem:subterm_id GT 0 }">
			<MvIF EXPR = "{ [ g.Module_Feature_SUB_DB ].ProductSubscriptionTerm_Load_ID( l.basketitem:subterm_id, l.productsubscriptionterm ) }">
				, "productsubscriptionterm":
				{
					"frequency":	"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.productsubscriptionterm:frequency ) }">",
					"term":			<MvEVAL EXPR = "{ int( l.productsubscriptionterm:term ) }">,
					"descrip":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.productsubscriptionterm:descrip ) }">",
					"n":			<MvEVAL EXPR = "{ l.productsubscriptionterm:n ROUND 0 }">,
					"fixed_dow":	<MvEVAL EXPR = "{ l.productsubscriptionterm:fixed_dow ROUND 0 }">,
					"sub_count":	<MvEVAL EXPR = "{ l.productsubscriptionterm:sub_count ROUND 0 }">		
				}
			</MvIF>
		</MvIF>

		, "total":			<MvEVAL EXPR = "{ ( ( l.basketitem:price + l.option_subtotal ) * l.basketitem:quantity ) ROUND 2 }">
		<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
	</MvFOREACH>
	]
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_ChargeList_Load" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Integer( 'R', 'Basket_ID', l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	[
	<MvFOREACH ITERATOR = "l.basketcharge" ARRAY = "l.basketcharges" COUNT = "{ [ g.Module_Library_DB ].BasketChargeList_Load_Basket( l.basket_id, l.basketcharges ) }">
		<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.basketcharge_pos ) }">
			"basket_id":	<MvEVAL EXPR = "{ int( l.basketcharge:basket_id ) }">,
			"module_id":	<MvEVAL EXPR = "{ int( l.basketcharge:module_id ) }">,
			"charge_id":	<MvEVAL EXPR = "{ int( l.basketcharge:charge_id ) }">,
			"type":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.basketcharge:type ) }">",
			"descrip":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.basketcharge:descrip ) }">",
			"amount":		<MvEVAL EXPR = "{ l.basketcharge:amount ROUND 2 }">,
			"disp_amt":		<MvEVAL EXPR = "{ l.basketcharge:disp_amt ROUND 2 }">,
			"tax_exempt":	<MvEVAL EXPR = "{ [ g.Module_Admin ].Trim_Boolean( l.basketcharge:tax_exempt ) }">
		<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
	</MvFOREACH>
	]

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketItem_Add" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvASSIGN NAME = "l.basketitem" 			VALUE = "">
	<MvASSIGN NAME = "l.basketitem:wish_id" 	VALUE = 0>
	<MvASSIGN NAME = "l.basketitem:subscrp_id" 	VALUE = 0>
	<MvASSIGN NAME = "l.basketitem:subterm_id" 	VALUE = 0>
	<MvASSIGN NAME = "l.basketitem:product_id" 	VALUE = 0>
	<MvASSIGN NAME = "l.basketitem:variant_id" 	VALUE = 0>
	<MvASSIGN NAME = "l.basketitem:code" 		VALUE = "">
	<MvASSIGN NAME = "l.basketitem:name" 		VALUE = "">
	<MvASSIGN NAME = "l.basketitem:sku" 		VALUE = "">
	<MvASSIGN NAME = "l.basketitem:type" 		VALUE = "">
	<MvASSIGN NAME = "l.basketitem:retail" 		VALUE = 0.00>
	<MvASSIGN NAME = "l.basketitem:base_price" 	VALUE = 0.00>
	<MvASSIGN NAME = "l.basketitem:price" 		VALUE = 0.00>
	<MvASSIGN NAME = "l.basketitem:weight" 		VALUE = 0.00>
	<MvASSIGN NAME = "l.basketitem:taxable" 	VALUE = 0>
	<MvASSIGN NAME = "l.basketitem:upsold" 		VALUE = 0>
	<MvASSIGN NAME = "l.basketitem:quantity" 	VALUE = 0>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Integer( 	'R',			'Basket_ID',	l.basket_id )			OR
					NOT [ g.Module_JSON ].JSON_Input_Code(		'R',			'Code',			l.basketitem:code )		OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R:100',		'Name',			l.basketitem:name )		OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o:50',			'SKU',			l.basketitem:sku )		OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o:20', 		'Type',			l.basketitem:type )		OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'R:positive',	'Quantity',		l.basketitem:quantity ) OR
					NOT [ g.Module_JSON ].JSON_Input_Price(		'o',			'Price',		l.basketitem:price ) 	OR
					NOT [ g.Module_JSON ].JSON_Input_Weight(	'o',			'Weight',		l.basketitem:weight )	OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o',			'Taxable',		l.basketitem:taxable ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Load_ID( l.basket_id, l.basket ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00015', 'Basket not found' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basketitem:basket_id" 	VALUE = "{ l.basket:basket_id }">
	<MvASSIGN NAME = "l.basketitem:base_price" 	VALUE = "{ l.basketitem:price }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_Code_WithRuntimeInventory( l.basketitem:code, l.product ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvASSIGN NAME = "l.product"			VALUE = "">
		<MvASSIGN NAME = "l.product:id"			VALUE = 0>
		<MvASSIGN NAME = "l.variant_id"			VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT JSON_ReviewBaskets_BasketItem_ParseAttributes( l.product, l.basketitem, l.basketoptions, l.basketoption_count, l.attribute_total ) }">
		<MvFUNCTIONRETURN VALUE = 0>	
	</MvIF>

	<MvIF EXPR = "{ l.product:id }">
		<MvIF EXPR = "{ NOT JSON_ReviewBaskets_BasketItem_DetermineVariant_LowLevel( l.product, l.basketoptions, l.basketoption_count, l.variant_id ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvIF EXPR = "{ l.variant_id }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_INV_RT ].Inventory_Adjust_VariantPricing( l.product, l.variant_id, l.attr_price_override ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
			</MvIF>
		</MvIF>

		<MvASSIGN NAME = "l.basketitem:retail"	VALUE = "{ l.product:price }">
	</MvIF>	

	<MvASSIGN NAME = "l.basketitem:product_id" 	VALUE = "{ l.product:id }">
	<MvASSIGN NAME = "l.basketitem:variant_id" 	VALUE = "{ l.variant_id  }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_BasketItemAndOptions_Insert( l.product, l.basketitem, l.basketoptions, l.basketoption_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>
	
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Dirty( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].Discount_Basket( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketTotal_Response( l.basket ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketItem_Update" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Integer( 'R', 'Basket_ID',	l.basket_id ) OR
					NOT [ g.Module_JSON ].JSON_Input_Integer( 'R', 'Line_ID',	l.line_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Load_ID( l.basket_id, l.basket ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00018', 'Basket not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].BasketItem_Load_Line( l.basket:basket_id, l.line_id, l.basketitem ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00019', 'Item not found' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.original_basketitem" VALUE = "{ l.basketitem }">

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Code(		'R',			'Code',		l.basketitem:code )		OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R:100',		'Name',		l.basketitem:name )		OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o:50',			'SKU',		l.basketitem:sku )		OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o:20',			'Type',		l.basketitem:type )		OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'R:positive',	'Quantity',	l.basketitem:quantity )	OR
					NOT [ g.Module_JSON ].JSON_Input_Price(		'o',			'Price',	l.basketitem:price )	OR
					NOT [ g.Module_JSON ].JSON_Input_Weight(	'o',			'Weight',	l.basketitem:weight )	OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o',			'Taxable',	l.basketitem:taxable ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvASSIGN NAME = "l.basketitem:base_price" 	VALUE = "{ l.basketitem:price }">

	<MvASSIGN NAME = "l.variant_id"				VALUE = 0>
	<MvASSIGN NAME = "l.product:id"				VALUE = 0>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_Code_WithRuntimeInventory( l.basketitem:code, l.product ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.original_basketitem:product_id EQ l.product:id }">
		<MvASSIGN NAME = "l.original_product"			VALUE = "{ l.product }">
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_ID_WithRuntimeInventory( l.original_basketitem:product_id, l.original_product ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
			</MvIF>

			<MvASSIGN NAME = "l.original_product"		VALUE = "">
			<MvASSIGN NAME = "l.original_product:id"	VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT JSON_ReviewBaskets_BasketItem_ParseAttributes( l.product, l.basketitem, l.basketoptions, l.basketoption_count, l.attribute_total ) }">
		<MvFUNCTIONRETURN VALUE = 0>	
	</MvIF>

	<MvIF EXPR = "{ l.product:id }">
		<MvIF EXPR = "{ NOT JSON_ReviewBaskets_BasketItem_DetermineVariant_LowLevel( l.product, l.basketoptions, l.basketoption_count, l.variant_id ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvIF EXPR = "{ l.variant_id }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_INV_RT ].Inventory_Adjust_VariantPricing( l.product, l.variant_id, l.attr_price_override ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
			</MvIF>
		</MvIF>

		<MvASSIGN NAME = "l.basketitem:retail"	VALUE = "{ l.product:price }">
	</MvIF>

	<MvASSIGN NAME = "l.basketitem:product_id" 	VALUE = "{ l.product:id }">
	<MvASSIGN NAME = "l.basketitem:variant_id" 	VALUE = "{ l.variant_id  }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_BasketItem_Delete( l.original_product, l.original_basketitem ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_BasketItemAndOptions_Insert( l.product, l.basketitem, l.basketoptions, l.basketoption_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Dirty( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].Discount_Basket( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketTotal_Response( l.basket ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketItemList_Delete" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Integer( 		'R', 'Basket_ID',	l.basket_id ) OR
					NOT [ g.Module_JSON ].JSON_Input_Integer_Array(	'R', 'Line_IDs',	l.lines, l.line_count )  }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Load_ID( l.basket_id, l.basket ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00027', 'Basket not found' ) }">
	</MvIF>

	<MvFOREACH ITERATOR = "l.line" ARRAY = "l.lines" COUNT = "{ l.line_count }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].BasketItem_Load_Line( l.basket:basket_id, l.line, l.basketitem ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
			</MvIF>

			<MvFOREACHCONTINUE>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_Product_Load_BasketItem( l.basketitem, l.product ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
			</MvIF>

			<MvASSIGN NAME = "l.product"	VALUE = "">
			<MvASSIGN NAME = "l.product:id"	VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_BasketItem_Delete( l.product, l.basketitem ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>
	</MvFOREACH>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Dirty( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].Discount_Basket( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketTotal_Response( l.basket ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketItem_DetermineSKU" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Code( 'R', 'Code', l.code ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_Code_WithRuntimeInventory( l.code, l.product ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00032', 'Product not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT JSON_ReviewBaskets_BasketItem_ParseAttributes( l.product, l.null, l.basketoptions, l.basketoption_count, l.attribute_total ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT JSON_ReviewBaskets_BasketItem_DetermineVariant_LowLevel( l.product, l.basketoptions, l.basketoption_count, l.variant_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.part_count"			VALUE = "{ [ g.Module_Library_DB ].ProductList_Load_Variant( l.product:id, l.variant_id, l.parts ) }">

	<MvIF EXPR = "{ l.variant_id EQ 0 AND l.part_count EQ 0 }">
		<MvASSIGN NAME = "l.sku"			VALUE = "{ l.product:sku }">
	<MvELSEIF EXPR = "{ l.part_count NE 1 }">
		<MvASSIGN NAME = "l.sku"			VALUE = "">
	<MvELSE>
		<MvREFERENCEARRAY NAME = "l.part"	VARIABLE = "l.parts">
			<MvDIMENSION INDEX = 1>
		</MvREFERENCEARRAY>

		<MvASSIGN NAME = "l.sku"			VALUE = "{ l.part:sku }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		"sku": "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.sku ) }">"
	}
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketItem_DetermineVariant" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Product_Load_WithRuntimeInventory( l.product ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT JSON_ReviewBaskets_BasketItem_ParseAttributes( l.product, l.null, l.basketoptions, l.basketoption_count, l.attribute_total ) }">
		<MvFUNCTIONRETURN VALUE = 0>	
	</MvIF>

	<MvIF EXPR = "{ NOT JSON_ReviewBaskets_BasketItem_DetermineVariant_LowLevel( l.product, l.basketoptions, l.basketoption_count, l.variant_id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT l.variant_id }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00063', 'Variant not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_INV_RT ].Inventory_Adjust_VariantPricing( l.product, l.variant_id, l.attr_price_override ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.part_count" VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Product( l.product ) }">

		, "variant_id":			<MvEVAL EXPR = "{ int( l.variant_id ) }">,
		"attr_price_override":	<MvEVAL EXPR = "{ int( l.attr_price_override ) }">,
		"parts":
		[
			<MvFOREACH ITERATOR = "l.part" ARRAY = "l.parts" COUNT = "{ [ g.Module_Library_DB ].ProductList_Load_Variant( l.product:id, l.variant_id, l.parts ) }">
				<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.part_count ) }">
					"code":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.part:code ) }">",
					"sku":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.part:sku ) }">",
					"name":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.part:name ) }">",
					"quantity":	<MvEVAL EXPR = "{ int( l.part:quantity ) }">,
					"price":	<MvEVAL EXPR = "{ encodejavascriptnumber( l.part:price ) }">
				<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
			</MvFOREACH>
		]
	}
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketItem_DetermineVariant_LowLevel" PARAMETERS = "product var, basketoptions var, basketoption_count, variant_id var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.product:id EQ 0 }">
		<MvASSIGN NAME = "l.variant_id"				VALUE = 0>
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.product_attribute_count"	VALUE = "{ [ g.Module_Library_DB ].AttributeList_Load_Product( l.product:id, l.product_attributes ) }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].OrderItem_DetermineVariant_LowLevel( l.product, l.product_attributes, l.product_attribute_count, l.basketoptions, l.basketoption_count, l.variant_id ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_SubscriptionAndBasketItem_Add" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'SUBS', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Integer( 'R',			'Basket_ID', 	l.basket_id ) OR
					NOT [ g.Module_JSON ].JSON_Input_Integer( 'R:positive', 'Quantity', 	l.quantity ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Load_ID( l.basket_id, l.basket ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00034', 'Basket not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Product_Load_WithRuntimeInventory( l.product ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_SUB_JSON ].JSON_ProductSubscriptionTerm_Load( l.productsubscriptionterm ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ l.productsubscriptionterm:product_id NE l.product:id }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00012', 'Invalid Product' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basketitem:basket_id" 	VALUE = "{ l.basket:basket_id }">
	<MvASSIGN NAME = "l.basketitem:wish_id" 	VALUE = "{ 0 }">
	<MvASSIGN NAME = "l.basketitem:subscrp_id" 	VALUE = "{ 0 }">
	<MvASSIGN NAME = "l.basketitem:subterm_id" 	VALUE = "{ l.productsubscriptionterm:id }">
	<MvASSIGN NAME = "l.basketitem:code" 		VALUE = "{ l.product:code }">
	<MvASSIGN NAME = "l.basketitem:name" 		VALUE = "{ l.product:name }">
	<MvASSIGN NAME = "l.basketitem:sku" 		VALUE = "{ l.product:sku }">
	<MvASSIGN NAME = "l.basketitem:type" 		VALUE = "product">
	<MvASSIGN NAME = "l.basketitem:retail" 		VALUE = "{ l.product:price }">
	<MvASSIGN NAME = "l.basketitem:base_price" 	VALUE = "{ l.product:price }">
	<MvASSIGN NAME = "l.basketitem:price" 		VALUE = "{ l.product:price }">
	<MvASSIGN NAME = "l.basketitem:weight" 		VALUE = "{ l.product:weight }">
	<MvASSIGN NAME = "l.basketitem:taxable" 	VALUE = "{ l.product:taxable }">
	<MvASSIGN NAME = "l.basketitem:upsold" 		VALUE = "{ 0 }">
	<MvASSIGN NAME = "l.basketitem:quantity" 	VALUE = "{ l.quantity }">

	<MvASSIGN NAME = "l.variant_id" 			VALUE = 0>

	<MvIF EXPR = "{ NOT JSON_ReviewBaskets_BasketItem_ParseAttributes( l.product, l.basketitem, l.basketoptions, l.basketoption_count, l.attribute_total ) }">
		<MvFUNCTIONRETURN VALUE = 0>	
	</MvIF>

	<MvIF EXPR = "{ NOT JSON_ReviewBaskets_BasketItem_DetermineVariant_LowLevel( l.product, l.basketoptions, l.basketoption_count, l.variant_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ l.variant_id }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_INV_RT ].Inventory_Adjust_VariantPricing( l.product, l.variant_id, l.attr_price_override ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.basketitem:product_id" 	VALUE = "{ l.product:id }">
	<MvASSIGN NAME = "l.basketitem:variant_id" 	VALUE = "{ l.variant_id  }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_BasketItemAndOptions_Insert( l.product, l.basketitem, l.basketoptions, l.basketoption_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>
	
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Dirty( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].Discount_Basket( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketTotal_Response( l.basket ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_SubscriptionAndBasketItem_Update" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'SUBS', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Integer( 'R',			'Basket_ID',	l.basket_id )	OR
					NOT [ g.Module_JSON ].JSON_Input_Integer( 'R',			'Line_ID',		l.line_id )		OR
					NOT [ g.Module_JSON ].JSON_Input_Integer( 'R:positive', 'Quantity',		l.quantity ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Load_ID( l.basket_id, l.basket ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00043', 'Basket not found' ) }">
	</MvIF>
	
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].BasketItem_Load_Line( l.basket:basket_id, l.line_id, l.basketitem ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00044', 'Item not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Product_Load_WithRuntimeInventory( l.product ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_SUB_JSON ].JSON_ProductSubscriptionTerm_Load( l.productsubscriptionterm ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ l.productsubscriptionterm:product_id NE l.product:id }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00016', 'Invalid Product' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.original_basketitem"			VALUE = "{ l.basketitem }">

	<MvIF EXPR = "{ l.original_basketitem:product_id EQ l.product:id }">
		<MvASSIGN NAME = "l.original_product"			VALUE = "{ l.product }">
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_ID_WithRuntimeInventory( l.original_basketitem:product_id, l.original_product ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
			</MvIF>

			<MvASSIGN NAME = "l.original_product"		VALUE = "">
			<MvASSIGN NAME = "l.original_product:id"	VALUE = 0>
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.basketitem:basket_id" 	VALUE = "{ l.basket:basket_id }">
	<MvASSIGN NAME = "l.basketitem:wish_id" 	VALUE = 0>
	<MvASSIGN NAME = "l.basketitem:subscrp_id" 	VALUE = 0>
	<MvASSIGN NAME = "l.basketitem:subterm_id" 	VALUE = "{ l.productsubscriptionterm:id }">
	<MvASSIGN NAME = "l.basketitem:type" 		VALUE = "product">
	<MvASSIGN NAME = "l.basketitem:code" 		VALUE = "{ l.product:code }">
	<MvASSIGN NAME = "l.basketitem:name" 		VALUE = "{ l.product:name }">
	<MvASSIGN NAME = "l.basketitem:sku" 		VALUE = "{ l.product:sku }">
	<MvASSIGN NAME = "l.basketitem:retail" 		VALUE = "{ l.product:price }">
	<MvASSIGN NAME = "l.basketitem:base_price" 	VALUE = "{ l.product:price }">
	<MvASSIGN NAME = "l.basketitem:price" 		VALUE = "{ l.product:price }">
	<MvASSIGN NAME = "l.basketitem:weight" 		VALUE = "{ l.product:weight }">
	<MvASSIGN NAME = "l.basketitem:taxable" 	VALUE = "{ l.product:taxable }">
	<MvASSIGN NAME = "l.basketitem:upsold" 		VALUE = 0>
	<MvASSIGN NAME = "l.basketitem:quantity" 	VALUE = "{ l.quantity }">

	<MvASSIGN NAME = "l.variant_id" 			VALUE = 0>

	<MvIF EXPR = "{ NOT JSON_ReviewBaskets_BasketItem_ParseAttributes( l.product, l.basketitem, l.basketoptions, l.basketoption_count, l.attribute_total ) }">
		<MvFUNCTIONRETURN VALUE = 0>	
	</MvIF>

	<MvIF EXPR = "{ NOT JSON_ReviewBaskets_BasketItem_DetermineVariant_LowLevel( l.product, l.basketoptions, l.basketoption_count, l.variant_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ l.variant_id }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_INV_RT ].Inventory_Adjust_VariantPricing( l.product, l.variant_id, l.attr_price_override ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.basketitem:product_id" 	VALUE = "{ l.product:id }">
	<MvASSIGN NAME = "l.basketitem:variant_id" 	VALUE = "{ l.variant_id  }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_BasketItem_Delete( l.original_product, l.original_basketitem ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_BasketItemAndOptions_Insert( l.product, l.basketitem, l.basketoptions, l.basketoption_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>
	
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Dirty( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].Discount_Basket( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketTotal_Response( l.basket ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketItem_ParseAttributes" PARAMETERS = "product var, basketitem var, basketoptions var, basketoption_count var, attribute_price var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.input_attribute_count"	VALUE = 0>
	<MvASSIGN NAME = "l.attributes"				VALUE = "">
	<MvASSIGN NAME = "l.attribute_count"		VALUE = 0>
	<MvASSIGN NAME = "l.attribute_price"		VALUE = 0.00>
	<MvASSIGN NAME = "l.basketoptions"			VALUE = "">
	<MvASSIGN NAME = "l.basketoption_count"		VALUE = 0>

	<MvFOREACH ITERATOR = "l.input_attribute" ARRAY = "l.input_attributes" COUNT = "{ [ g.Module_JSON ].JSON_Input_Array( 'Attributes', l.input_attributes ) }">
		<MvASSIGN NAME = "l.attribute"					VALUE = "">
		<MvASSIGN NAME = "l.attribute:attr_code"		VALUE = "">
		<MvASSIGN NAME = "l.attribute:opt_code_or_data"	VALUE = "">
		<MvASSIGN NAME = "l.attribute:opt_code"			VALUE = "">
		<MvASSIGN NAME = "l.attribute:data"				VALUE = "">
		<MvASSIGN NAME = "l.attribute:price"			VALUE = 0.00>
		<MvASSIGN NAME = "l.attribute:weight"			VALUE = 0.00>

		<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Element_Text(		l.input_attribute, 'R', 'attr_code',		l.attribute:attr_code )			OR
						NOT [ g.Module_JSON ].JSON_Input_Element_Text(		l.input_attribute, 'o', 'opt_code_or_data',	l.attribute:opt_code_or_data )	OR
						NOT [ g.Module_JSON ].JSON_Input_Element_Text(		l.input_attribute, 'o', 'opt_code',			l.attribute:opt_code )			OR
						NOT [ g.Module_JSON ].JSON_Input_Element_Text(		l.input_attribute, 'o', 'data',				l.attribute:data )				OR
						NOT [ g.Module_JSON ].JSON_Input_Element_Price(		l.input_attribute, 'o', 'price',			l.attribute:price )				OR
						NOT [ g.Module_JSON ].JSON_Input_Element_Weight(	l.input_attribute, 'o', 'weight',			l.attribute:weight ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
		</MvIF>

		<MvASSIGN NAME = "l.attribute_count"			VALUE = "{ miva_array_insert_var( l.attributes, l.attribute, -1 ) }">
	</MvFOREACH>

	<MvFOREACH ITERATOR = "l.attribute" ARRAY = "l.attributes" COUNT = "{ l.attribute_count }">
		<MvREFERENCEARRAY NAME = "l.basketoption" VARIABLE = "l.basketoptions">
			<MvDIMENSION INDEX = "{ ++l.basketoption_count }">
		</MvREFERENCEARRAY>

		<MvASSIGN NAME = "l.basketoption:attr_id"						VALUE = "{ l.attribute:attr_id }">
		<MvASSIGN NAME = "l.basketoption:attr_code"						VALUE = "{ l.attribute:attr_code }">
		<MvASSIGN NAME = "l.basketoption:attmpat_id"					VALUE = 0>
		<MvASSIGN NAME = "l.basketoption:option_id"						VALUE = 0>
		<MvASSIGN NAME = "l.basketoption:opt_code"						VALUE = "">
		<MvASSIGN NAME = "l.basketoption:price"							VALUE = "{ l.attribute:price }">
		<MvASSIGN NAME = "l.basketoption:weight"						VALUE = "{ l.attribute:weight }">
		<MvASSIGN NAME = "l.basketoption:data"							VALUE = "">
		<MvASSIGN NAME = "l.basketoption:data_long"						VALUE = "">

		<MvIF EXPR = "{ l.product:id }">
			<MvIF EXPR = "{ NOT JSON_ReviewBaskets_BasketItem_Load_ProductAttribute( l.product, l.attribute, l.basketoption, l.loaded_attribute ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ NOT l.loaded_attribute:id }">
			<MvASSIGN NAME = "l.basketoption:attr_id"					VALUE = 0>
			<MvASSIGN NAME = "l.basketoption:attmpat_id"				VALUE = 0>
			<MvASSIGN NAME = "l.basketoption:attr_code"					VALUE = "{ l.attribute:attr_code }">
		<MvELSE>
			<MvASSIGN NAME = "l.basketoption:attr_code"					VALUE = "{ l.loaded_attribute:code }">
			<MvASSIGN NAME = "l.basketoption:type"						VALUE = "{ l.loaded_attribute:type }">

			<MvIF EXPR = "{ l.loaded_attribute:attemp_id }">
				<MvASSIGN NAME = "l.basketoption:attr_id"				VALUE = "{ l.loaded_attribute:attr_id }">
				<MvASSIGN NAME = "l.basketoption:attmpat_id"			VALUE = "{ l.loaded_attribute:id }">
			<MvELSE>
				<MvASSIGN NAME = "l.basketoption:attr_id"				VALUE = "{ l.loaded_attribute:id }">
				<MvASSIGN NAME = "l.basketoption:attmpat_id"			VALUE = 0>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ NOT ISNULL l.attribute:opt_code_or_data }">
			<MvIF EXPR = "{ l.basketoption:attr_id AND ( ( l.loaded_attribute:type EQ 'radio' ) OR ( l.loaded_attribute:type EQ 'select' ) OR ( l.loaded_attribute:type EQ 'swatch-select' ) ) }">
				<MvIF EXPR = "{ l.basketoption:attmpat_id }">
					<MvIF EXPR = "{ [ g.Module_Feature_ATT_DB ].AttributeTemplateOption_Load_Code( l.basketoption:attmpat_id, l.attribute:opt_code_or_data, l.loaded_option ) }">
						<MvASSIGN NAME = "l.basketoption:option_id"		VALUE = "{ l.loaded_option:id }">
						<MvASSIGN NAME = "l.basketoption:opt_code"		VALUE = "{ l.loaded_option:code }">
					<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
						<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
					<MvELSEIF EXPR = "{ len_var( l.attribute:opt_code_or_data ) LE 254 }">
						<MvASSIGN NAME = "l.basketoption:data"			VALUE = "{ l.attribute:opt_code_or_data }">
					<MvELSE>
						<MvASSIGN NAME = "l.basketoption:data_long"		VALUE = "{ l.attribute:opt_code_or_data }">
					</MvIF>
				<MvELSE>
					<MvIF EXPR = "{ [ g.Module_Library_DB ].Option_Load_Code( l.basketoption:attr_id, l.attribute:opt_code_or_data, l.loaded_option ) }">
						<MvASSIGN NAME = "l.basketoption:option_id"		VALUE = "{ l.loaded_option:id }">
						<MvASSIGN NAME = "l.basketoption:opt_code"		VALUE = "{ l.loaded_option:code }">
					<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
						<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
					<MvELSEIF EXPR = "{ len_var( l.attribute:opt_code_or_data ) LE 254 }">
						<MvASSIGN NAME = "l.basketoption:data"			VALUE = "{ l.attribute:opt_code_or_data }">
					<MvELSE>
						<MvASSIGN NAME = "l.basketoption:data_long"		VALUE = "{ l.attribute:opt_code_or_data }">
					</MvIF>
				</MvIF>
			<MvELSE>
				<MvIF EXPR = "{ len_var( l.attribute:opt_code_or_data ) LE 254 }">
					<MvASSIGN NAME = "l.basketoption:data"				VALUE = "{ l.attribute:opt_code_or_data }">
				<MvELSE>
					<MvASSIGN NAME = "l.basketoption:data_long"			VALUE = "{ l.attribute:opt_code_or_data }">
				</MvIF>
			</MvIF>
		<MvELSEIF EXPR = "{ NOT ISNULL l.attribute:opt_code OR NOT ISNULL l.attribute:data }">
			<MvASSIGN NAME = "l.basketoption:option_id"					VALUE = -1>
			<MvASSIGN NAME = "l.basketoption:opt_code"					VALUE = "{ l.attribute:opt_code }">

			<MvIF EXPR = "{ NOT ISNULL l.attribute:data }">
				<MvASSIGN NAME = "l.basketoption:option_id"				VALUE = 0>

				<MvIF EXPR = "{ len_var( l.attribute:data ) LE 254 }">	<MvASSIGN NAME = "l.basketoption:data"		VALUE = "{ l.attribute:data }">
				<MvELSE>												<MvASSIGN NAME = "l.basketoption:data_long"	VALUE = "{ l.attribute:data }">
				</MvIF>
			</MvIF>

			<MvIF EXPR = "{ l.loaded_attribute:id }">
				<MvIF EXPR = "{ l.basketoption:attmpat_id }">
					<MvIF EXPR = "{ NOT ISNULL l.basketoption:opt_code }">
						<MvIF EXPR = "{ [ g.Module_Feature_ATT_DB ].AttributeTemplateOption_Load_Code( l.loaded_attribute:id, l.basketoption:opt_code, l.loaded_option ) }">
							<MvASSIGN NAME = "l.basketoption:option_id"	VALUE = "{ l.loaded_option:id }">
						<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
							<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
						</MvIF>
					</MvIF>
				<MvELSE>
					<MvIF EXPR = "{ NOT ISNULL l.basketoption:opt_code }">
						<MvIF EXPR = "{ [ g.Module_Library_DB ].Option_Load_Code( l.loaded_attribute:id, l.basketoption:opt_code, l.loaded_option ) }">
							<MvASSIGN NAME = "l.basketoption:option_id"	VALUE = "{ l.loaded_option:id }">
						<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
							<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
						</MvIF>
					</MvIF>
				</MvIF>
			</MvIF>
		</MvIF>

		<MvASSIGN NAME = "l.attribute_price" VALUE = "{ l.attribute_price + l.basketoption:price }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketItem_Load_ProductAttribute" PARAMETERS = "product var, attribute var, basketoption var, loaded_attribute var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.loaded_attribute" 	VALUE = "">

	<MvIF EXPR = "{ l.attribute:attr_id AND l.attribute:attmpat_id }">
		<MvIF EXPR = "{ [ g.Module_Feature_ATT_DB ].AttributeTemplateAttr_Load_ID( l.attribute:attmpat_id, l.loaded_attribute ) }">
			<MvASSIGN NAME = "l.loaded_attribute:attr_id" 	VALUE = "{ l.attribute:attr_id }">

			<MvFUNCTIONRETURN VALUE = 1>
		<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	<MvELSEIF EXPR = "{ l.attribute:attr_id }">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].Attribute_Load_ID( l.basketoption:attr_id, l.loaded_attribute ) }">
			<MvFUNCTIONRETURN VALUE = 1>
		<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	<MvELSEIF EXPR = "{ NOT ISNULL l.basketoption:attr_code }">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].Attribute_Load_Code( l.product:id, l.basketoption:attr_code, l.loaded_attribute ) }">
			<MvFUNCTIONRETURN VALUE = 1>
		<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.loaded_attribute:id AND NOT ISNULL l.basketoption:attr_code }">
		<MvIF EXPR = "{ [ g.Module_Feature_ATT_DB ].AttributeTemplateAttr_Load_Product_Code( l.product:id, l.basketoption:attr_code, l.loaded_attribute ) }">
			<MvFUNCTIONRETURN VALUE = 1>
		<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketPriceGroupList_Load_Query" PARAMETERS = "" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'PGRP', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Filter(	'o', 'Filter',		l.filter )		OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Sort',		l.sort )		OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'o', 'Offset',		l.offset )		OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'o', 'Count',		l.count )		OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 'Assigned',	l.assigned )	OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 'Unassigned',	l.unassigned )	OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'R', 'Basket_ID',	l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvASSIGN NAME = "l.query" VALUE = "">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.query,	'pg.*,
																			 m.id			AS loaded_module_id,
																			 m.code			AS module_code,
																			 m.name			AS module_name,
																			 m.provider		AS module_provider,
																			 m.api_ver		AS module_api_ver,
																			 m.version		AS module_version,
																			 m.module		AS module_module,
																			 m.refcount		AS module_refcount,
																			 m.active		AS module_active,
																			 m.priority		AS module_priority,
																			 bdt.pgrp_id	AS assigned' ) }">

	<MvIF EXPR = "{ l.assigned AND ( NOT l.unassigned ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query,	g.Store_Table_Prefix $ 'BasketDiscountTotals',	'bdt' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query,	g.Store_Table_Prefix $ 'PriceGroups',			'pg' ) }">

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query,	'bdt.basket_id = ? AND pg.id = bdt.pgrp_id', [ g.Module_Library_DB ].SQL_Query_Field( l.basket_id ) ) }">
	<MvELSEIF EXPR = "{ l.assigned OR l.unassigned }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query,	g.Store_Table_Prefix $ 'PriceGroups',			'pg' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, 'pg', g.Store_Table_Prefix $ 'BasketDiscountTotals', 'bdt', 'bdt.basket_id = ? AND pg.id = bdt.pgrp_id', [ g.Module_Library_DB ].SQL_Query_Field( l.basket_id ) ) }">

		<MvIF EXPR = "{ NOT l.assigned }">
			<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query,	'bdt.pgrp_id IS NULL', '' ) }">
		</MvIF>
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query,	'Modules', 'm' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query,	'pg.module_id <> 0 AND m.id = pg.module_id AND m.active = 1', '' ) }">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter( l.query, l.filter,
													 'name:pg.name,type:m.name,module_id:pg.module_id,custscope:pg.custscope,rate:pg.rate,discount:pg.discount,markup:pg.markup,
													  dt_start:pg.dt_start,dt_end:pg.dt_end,priority:pg.priority,descrip:pg.descrip,display:pg.display,
													  qmn_subtot:pg.qmn_subtot,qmx_subtot:pg.qmx_subtot,qmn_quan:pg.qmn_quan,qmx_quan:pg.qmx_quan,
													  qmn_weight:pg.qmn_weight,qmx_weight:pg.qmx_weight,bmn_subtot:pg.bmn_subtot,bmx_subtot:pg.bmx_subtot,
													  bmn_quan:pg.bmn_quan,bmx_quan:pg.bmx_quan,bmn_weight:pg.bmn_weight,bmx_weight:pg.bmx_weight' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_OrderBy_Fields( l.query, l.sort,
																		'name:pg.name,type:m.name,custscope:pg.custscope,rate:pg.rate,discount:pg.discount,markup:pg.markup,
																		 dt_start:pg.dt_start,dt_end:pg.dt_end,priority:pg.priority;-pg.id,descrip:pg.descrip,display:pg.display',
																		'-pg.priority;pg.id' ) }">

	<MvASSIGN NAME = "l.sql" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Count( l.query, l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'PriceGroups', l.sql, l.search_fields, l.offset, l.count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00053', g.MvOPENVIEW_Error ) }">
	</MvIF>
	
	<MvASSIGN NAME = "l.pricegroup_count" VALUE = 0>
	
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		"total_count":	<MvEVAL EXPR = "{ int( l.total_count ) }">,
		"start_offset":	<MvEVAL EXPR = "{ int( l.offset ) }">,
		"data":
		[
		<MvWHILE EXPR = "{ ( NOT PriceGroups.d.EOF ) AND ( ( l.count EQ 0 ) OR ( l.pricegroup_count LT l.count ) ) }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_DB ].PriceGroupAndModule_Read( l.pricegroup ) }">

 			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.pricegroup_count ) }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_JSON ].JSON_PriceGroup( l.pricegroup ) }">
				, "assigned":	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Boolean( PriceGroups.d.assigned ) }">
			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">

			<MvSKIP NAME = "Merchant" VIEW = "PriceGroups" ROWS = 1>
		</MvWHILE>
		]
	}
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "PriceGroups">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketPriceGroup_Update_Assigned" PARAMETERS = "" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'PGRP', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Integer(	'R', 'Basket_ID',	l.basket_id ) OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 'Assigned',	l.assigned ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Load_ID( l.basket_id, l.basket ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00054', 'Basket not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_JSON ].JSON_PriceGroup_Load( l.pricegroup ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT l.pricegroup:module_id }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00055', 'Price Group \'' $ l.pricegroup:name $ '\' cannot be applied to basket' ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Module_Load_ID( l.pricegroup:module_id, l.pricegroup:module ) }">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00056', 'Unable to load discount module' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ l.assigned AND ( NOT l.pricegroup:module:active ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00057', 'Discount module \'' $ l.pricegroup:module:name $ '\' is deactivated' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.assigned }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].Basket_Assign_Persistent_PriceGroup( l.basket:basket_id, l.pricegroup:name ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].Basket_Remove_Persistent_PriceGroup( l.basket:basket_id, l.pricegroup:name ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].Discount_Basket( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketTotal_Response( l.basket ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketCouponList_Load_Query" PARAMETERS = "" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN> 	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'COUP', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN> 	</MvIF>

	<MvASSIGN NAME = "l.available_filters"	VALUE = "">
	<MvASSIGN NAME = "l.basket_id"			VALUE = 0>
	<MvASSIGN NAME = "l.filter"				VALUE = "">
	<MvASSIGN NAME = "l.sort"				VALUE = "">
	<MvASSIGN NAME = "l.offset"				VALUE = 0>
	<MvASSIGN NAME = "l.count"				VALUE = 0>
	<MvASSIGN NAME = "l.assigned"			VALUE = 1>
	<MvASSIGN NAME = "l.unassigned"			VALUE = 0>
	<MvASSIGN NAME = "l.query"				VALUE = "">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'code',		'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'descrip',		'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(		l.available_filters, 'custscope',	'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_RFC3339(	l.available_filters, 'dt_start',	'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_RFC3339(	l.available_filters, 'dt_end',		'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_INTEGER(	l.available_filters, 'max_use',		'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_INTEGER(	l.available_filters, 'max_per',		'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_BOOL(		l.available_filters, 'active',		'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_INTEGER(	l.available_filters, 'use_count',	'' ) }">

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Integer(	'R', 'Basket_ID',	l.basket_id )	OR
					NOT [ g.Module_JSON ].JSON_Input_Filter(	'o', 'Filter',		l.filter )		OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Sort',		l.sort )		OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'o', 'Offset',		l.offset )		OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'o', 'Count',		l.count )		OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 'Assigned',	l.assigned )	OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 'Unassigned',	l.unassigned ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.query,	'c.*,
																			 bc.coupon_id	AS assigned' ) }">

	<MvIF EXPR = "{ l.assigned AND ( NOT l.unassigned ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query,	g.Store_Table_Prefix $ 'BasketCoupons',	'bc' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query,	g.Store_Table_Prefix $ 'Coupons',		'c' ) }">

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query,	'bc.basket_id = ? AND c.id = bc.coupon_id', [ g.Module_Library_DB ].SQL_Query_Field( l.basket_id ) ) }">
	<MvELSEIF EXPR = "{ l.assigned OR l.unassigned }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query,	g.Store_Table_Prefix $ 'Coupons',		'c' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, 'c', g.Store_Table_Prefix $ 'BasketCoupons', 'bc', 'bc.basket_id = ? AND c.id = bc.coupon_id', [ g.Module_Library_DB ].SQL_Query_Field( l.basket_id ) ) }">

		<MvIF EXPR = "{ NOT l.assigned }">
			<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query,	'bc.coupon_id IS NULL', '' ) }">
		</MvIF>
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter( l.query, l.filter, l.available_filters ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_OrderBy( l.query, l.sort, 'code:c.code,descrip:c.descrip,custscope:c.custscope,dt_start:c.dt_start,dt_end:c.dt_end,max_use:c.max_use,max_per:c.max_per,active:c.active,use_count:c.use_count', 'c.id' ) }">

	<MvASSIGN NAME = "l.search_sql"	VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Count( l.query, l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'Coupons', l.search_sql, l.search_fields, l.offset, l.count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00059', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.coupon_count" VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		"total_count":	<MvEVAL EXPR = "{ int( l.total_count ) }">,
		"start_offset":	<MvEVAL EXPR = "{ int( l.offset ) }">,
		"data":
		[
		<MvWHILE EXPR = "{ ( NOT Coupons.d.EOF ) AND ( ( l.count EQ 0 ) OR ( l.coupon_count LT l.count ) ) }">
			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.coupon_count ) }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_DB ].Coupon_Read( l.coupon ) }">

				<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_JSON ].JSON_Coupon( l.coupon ) }">
				, "assigned":	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Boolean( Coupons.d.assigned ) }">
			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">

			<MvSKIP NAME = "Merchant" VIEW = "Coupons" ROWS = 1>
		</MvWHILE>
		]
	}
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Coupons">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketCoupon_Update_Assigned" PARAMETERS = "" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN> 	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'COUP', 1, 0, 1, 0 ) }">	<MvFUNCTIONRETURN> 	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Integer( 'R', 'Basket_ID',	l.basket_id ) OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean( 'o', 'Assigned',	l.assigned ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Load_ID( l.basket_id, l.basket ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00060', 'Basket not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_JSON ].JSON_Coupon_Load( l.coupon ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ l.assigned }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_DB ].BasketCoupon_Insert( l.basket:basket_id, l.coupon:id, 0.00 ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_Error_IsIndexDuplicate( g.Error_Message ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
			</MvIF>
		</MvIF>
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_DB ].BasketCoupon_Delete( l.basket:basket_id, l.coupon:id ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].Discount_Basket( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketTotal_Response( l.basket ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_Order_Create" PARAMETERS = "" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN> 	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'ORDR', 0, 1, 0, 0 ) }">	<MvFUNCTIONRETURN> 	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Integer( 'R', 'Basket_ID', l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Load_ID( l.basket_id, l.basket ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00014', 'Basket not found' ) }">
	</MvIF>

	<MvLOCKFILE FILE = "{ [ g.Module_Merchant ].Merchant_Basket_CheckoutLockFile( l.basket ) }">
		<MvASSIGN NAME = "l.result" VALUE = "{ JSON_ReviewBaskets_Order_Create_LowLevel( l.basket_id ) }">
	</MvLOCKFILE>

	<MvFUNCTIONRETURN VALUE = "{ l.result }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_Order_Create_LowLevel" PARAMETERS = "basket_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvCOMMENT>
	|
	| Reload the basket once the lock file is obtained
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Load_ID( l.basket_id, l.basket ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00062', 'Basket not found' ) }">
	</MvIF>	

	<MvASSIGN NAME = "l.basket:order_id"	VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'Orders' ) }">
	<MvASSIGN NAME = "l.basket:order_proc"	VALUE = 0>

	<MvASSIGN NAME = "l.total" 				VALUE = "{ [ g.Module_Library_DB ].Basket_Total( l.basket:basket_id ) }">

	<MvASSIGN NAME = "l.order"				VALUE = "">
	<MvASSIGN NAME = "l.order:source"		VALUE = "reviewbaskets">
	<MvASSIGN NAME = "l.order:source_id"	VALUE = "{ g.User:id }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_INV_RT ].Inventory_Adjust_AtCheckout( l.basket ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Order_Transaction_Begin() }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].v56_Order_Create( l.basket, l.total, l.order ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_WSH_UT ].WishList_Process_Basket( l.basket ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].Order_Transaction_Commit_PreserveError() }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Reset_Contents( l.basket ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].Order_Transaction_Commit_PreserveError() }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_GFT_UT ].GiftCertificateSales_Process_Order( l.order ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].Order_Transaction_Commit_PreserveError() }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_DDL_UT ].DigitalDownload_Process_Order( l.order ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].Order_Transaction_Commit_PreserveError() }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Order_Update_Status( l.order ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].Order_Transaction_Commit_PreserveError() }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Order_Transaction_Commit() }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		"order_id":	<MvEVAL EXPR = "{ encodejavascriptnumber( l.order:id ) }">
	}
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_Update_Charges" PARAMETERS = "" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'RBSK', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN> 	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Integer( 'R', 'Basket_ID', l.basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Load_ID( l.basket_id, l.basket ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">			
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-MOD-REVB-00017', 'Basket not found' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.basketcharges" 					VALUE = "">
	<MvASSIGN NAME = "l.basketcharge_count" 			VALUE = 0>

	<MvFOREACH ITERATOR = "l.input_charge" ARRAY = "l.input_charges" COUNT = "{ [ g.Module_JSON ].JSON_Input_Array( 'Charges', l.input_charges ) }">
		<MvASSIGN NAME = "l.basketcharge:basket_id"		VALUE = "{ l.basket:basket_id }">
		<MvASSIGN NAME = "l.basketcharge:module_id"		VALUE = 0>
		<MvASSIGN NAME = "l.basketcharge:type"			VALUE = "">
		<MvASSIGN NAME = "l.basketcharge:descrip"		VALUE = "">
		<MvASSIGN NAME = "l.basketcharge:amount"		VALUE = 0.00>
		<MvASSIGN NAME = "l.basketcharge:disp_amt"		VALUE = 0.00>
		<MvASSIGN NAME = "l.basketcharge:tax_exempt"	VALUE = 0>

		<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Element_Text(		l.input_charge, 'R', 'type',		l.basketcharge:type )			OR
						NOT [ g.Module_JSON ].JSON_Input_Element_Text(		l.input_charge, 'o', 'descrip',		l.basketcharge:descrip )		OR
						NOT [ g.Module_JSON ].JSON_Input_Element_Number(	l.input_charge, 'o', 'amount',		l.basketcharge:amount, 10, 2 )	OR
						NOT [ g.Module_JSON ].JSON_Input_Element_Integer(	l.input_charge, 'o', 'module_id',	l.basketcharge:module_id )		OR
						NOT [ g.Module_JSON ].JSON_Input_Element_Boolean(	l.input_charge, 'o', 'tax_exempt',	l.basketcharge:tax_exempt ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
		</MvIF>

		<MvASSIGN NAME = "l.basketcharge:disp_amt"		VALUE = "{ l.basketcharge:amount }">
		<MvASSIGN NAME = "l.basketcharge_count"			VALUE = "{ miva_array_insert_var( l.basketcharges, l.basketcharge, -1 ) }">
	</MvFOREACH>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].BasketCharge_Delete_All_Basket( l.basket:basket_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFOREACH ITERATOR = "l.basketcharge" ARRAY = "l.basketcharges" COUNT = "{ l.basketcharge_count }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].BasketCharge_Insert( l.basketcharge ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = "{ JSON_ReviewBaskets_BasketTotal_Response( l.basket ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ReviewBaskets_BasketTotal_Response" PARAMETERS = "basket var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvASSIGN NAME = "l.basket:total" 				VALUE = "{ [ g.Module_Library_DB ].Basket_Total( l.basket:basket_id ) }">
	<MvASSIGN NAME = "l.basket:subtotal" 			VALUE = "{ [ g.Module_Library_DB ].Basket_SubTotal( l.basket:basket_id ) }">
	<MvASSIGN NAME = "l.basket:formatted_total" 	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.basket:total ) }">
	<MvASSIGN NAME = "l.basket:formatted_subtotal" 	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.basket:subtotal ) }">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		"total":				<MvEVAL EXPR = "{ l.basket:total ROUND 2 }">,
		"subtotal":				<MvEVAL EXPR = "{ l.basket:subtotal ROUND 2 }">,
		"formatted_total":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.basket:formatted_total ) }">",
		"formatted_subtotal":	"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.basket:formatted_subtotal ) }">"
	}
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvINCLUDE FILE = "modules/util/reviewbaskets/reviewbasketslist.mv">
<MvINCLUDE FILE = "modules/util/reviewbaskets/functions.mv">
<MvINCLUDE FILE = "modules/util/reviewbaskets/overlay.mv">
<MvINCLUDE FILE = "modules/util/reviewbaskets/overlaydetails.mv">
<MvINCLUDE FILE = "modules/util/reviewbaskets/overlaylist.mv">
<MvINCLUDE FILE = "modules/util/reviewbaskets/basketitemaddeditdialog.mv">
<MvINCLUDE FILE = "modules/util/reviewbaskets/subscriptionaddeditdialog.mv">
<MvINCLUDE FILE = "modules/util/reviewbaskets/coupondialog.mv">
<MvINCLUDE FILE = "modules/util/reviewbaskets/couponlist.mv">
<MvINCLUDE FILE = "modules/util/reviewbaskets/pricegroupdialog.mv">
<MvINCLUDE FILE = "modules/util/reviewbaskets/pricegrouplist.mv">
<MvINCLUDE FILE = "modules/util/reviewbaskets/chargedialog.mv">

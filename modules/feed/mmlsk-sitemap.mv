<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-FEED-SMP-
| Next Error Code: 23
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-sitemap">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Standard Sitemap Protocol">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.1000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.00">
	<MvASSIGN NAME = "l.module:features"	VALUE = "feed, data_store, component">
</MvFUNCTION>

<MvCOMMENT>
|
| Store-level Module Data Support Feature (data_store)
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.item_code"				VALUE = "{ Sitemap_ExcludeItem_Code() }">
	<MvASSIGN NAME = "l.default_excluded_pages" VALUE = "ABAL,ACED,ACLN,CABK,CADA,CADE,CEML,CPWD,CTGY,OINF,OPAY,ORDH,ORDS,OSEL,OUS1,OUSL,OUSM,PROD,RGFT,UATM,UATR">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Item_Load_Code( l.item_code, l.null ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Item( l.item_code, l.module:code ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT Sitemap_Exclude_Default_Pages( l.item_code, l.default_excluded_pages ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Feed Module Feature (feed)
|
</MvCOMMENT>

<MvFUNCTION NAME = "FeedModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:output_custom"			VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:provision_settings"	VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Fields" PARAMETERS = "module var, feed var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT l.feed:id }">
		<MvEVAL EXPR = "{ Sitemap_Config_Defaults( l.feed:config ) }">
	</MvIF>

	<MvIF EXPR = "{ sexists( '/sitemap' $ l.feed:config:suffix $ '.xml' ) }">
		<MvASSIGN NAME = "l.fields"						VALUE = "sitemap_url,">
	</MvIF>

	<MvASSIGN NAME = "l.fields"							VALUE = "{ l.fields $ 'suffix,robots,ping_google,ping_bing,products,product_priority,exclude_free_products,categories,category_priority,pages,page_priority,non_canonical_priority,non_canonical,archived,last_modified,change_frequency' }">

	<MvASSIGN NAME = "g.Sitemap_URL"					VALUE = "{ Sitemap_Build_URL_From_URI( '/sitemap' $ l.feed:config:suffix $ '.xml' ) }">
	<MvASSIGN NAME = "g.Sitemap_Suffix"					VALUE = "{ l.feed:config:suffix }">
	<MvASSIGN NAME = "g.Sitemap_Robots"					VALUE = "{ l.feed:config:robots }">
	<MvASSIGN NAME = "g.Sitemap_Ping_Google"			VALUE = "{ l.feed:config:ping_google }">
	<MvASSIGN NAME = "g.Sitemap_Ping_Bing"				VALUE = "{ l.feed:config:ping_bing }">
	<MvASSIGN NAME = "g.Sitemap_Products"				VALUE = "{ l.feed:config:products }">
	<MvASSIGN NAME = "g.Sitemap_Exclude_Free_Products"	VALUE = "{ l.feed:config:exclude_free_products }">
	<MvASSIGN NAME = "g.Sitemap_Product_Priority" 		VALUE = "{ l.feed:config:product_priority ROUND 1 }">
	<MvASSIGN NAME = "g.Sitemap_Categories"				VALUE = "{ l.feed:config:categories }">
	<MvASSIGN NAME = "g.Sitemap_Category_Priority"		VALUE = "{ l.feed:config:category_priority ROUND 1 }">
	<MvASSIGN NAME = "g.Sitemap_Pages"					VALUE = "{ l.feed:config:pages }">
	<MvASSIGN NAME = "g.Sitemap_Page_Priority"			VALUE = "{ l.feed:config:page_priority ROUND 1 }">
	<MvASSIGN NAME = "g.Sitemap_Non_Canonical_Priority"	VALUE = "{ l.feed:config:non_canonical_priority ROUND 1 }">
	<MvASSIGN NAME = "g.Sitemap_Non_Canonical"			VALUE = "{ l.feed:config:non_canonical }">
	<MvASSIGN NAME = "g.Sitemap_Archived"				VALUE = "{ l.feed:config:archived }">
	<MvASSIGN NAME = "g.Sitemap_Last_Modified"			VALUE = "{ l.feed:config:last_modified }">
	<MvASSIGN NAME = "g.Sitemap_Change_Frequency"		VALUE = "{ l.feed:config:change_frequency }">

	<MvFUNCTIONRETURN VALUE = "{ l.fields }">
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.field_id EQ 'sitemap_url' }">					<MvFUNCTIONRETURN VALUE = "">
	<MvELSEIF EXPR = "{ l.field_id EQ 'suffix' }">					<MvFUNCTIONRETURN VALUE = "Suffix:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'robots' }">					<MvFUNCTIONRETURN VALUE = "">
	<MvELSEIF EXPR = "{ l.field_id EQ 'ping_google' }">				<MvFUNCTIONRETURN VALUE = "">
	<MvELSEIF EXPR = "{ l.field_id EQ 'ping_bing' }">				<MvFUNCTIONRETURN VALUE = "">
	<MvELSEIF EXPR = "{ l.field_id EQ 'products' }">				<MvFUNCTIONRETURN VALUE = "Products:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'product_priority' }">		<MvFUNCTIONRETURN VALUE = "Product URI Priority:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'exclude_free_products' }">	<MvFUNCTIONRETURN VALUE = "">
	<MvELSEIF EXPR = "{ l.field_id EQ 'categories' }">				<MvFUNCTIONRETURN VALUE = "Categories:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'category_priority' }">		<MvFUNCTIONRETURN VALUE = "Category URI Priority:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'pages' }">					<MvFUNCTIONRETURN VALUE = "">
	<MvELSEIF EXPR = "{ l.field_id EQ 'page_priority' }">			<MvFUNCTIONRETURN VALUE = "Page URI Priority:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'non_canonical_priority' }">	<MvFUNCTIONRETURN VALUE = "Non-Canonical URI Priority:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'non_canonical' }">			<MvFUNCTIONRETURN VALUE = "">
	<MvELSEIF EXPR = "{ l.field_id EQ 'archived' }">				<MvFUNCTIONRETURN VALUE = "">
	<MvELSEIF EXPR = "{ l.field_id EQ 'last_modified' }">			<MvFUNCTIONRETURN VALUE = "Last Modified:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'change_frequency' }">		<MvFUNCTIONRETURN VALUE = "Change Frequency:">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.field_id EQ 'sitemap_url' }">
		<a href="{ g.Sitemap_URL }" target="_blank">View Sitemap</a>
	<MvELSEIF EXPR = "{ l.field_id EQ 'suffix' }">
		<input type="text" name="Sitemap_Suffix" value="{ encodeentities( g.Sitemap_Suffix ) }" /> (Used to seperate sitemaps from different stores)
	<MvELSEIF EXPR = "{ l.field_id EQ 'robots' }">		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Sitemap_Robots, 		'Sitemap_Robots',		'1', 'Update robots.txt when sitemap is created' ) }">
	<MvELSEIF EXPR = "{ l.field_id EQ 'ping_google' }">	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Sitemap_Ping_Google,	'Sitemap_Ping_Google',	'1', 'Notify Google of an updated sitemap' ) }">
	<MvELSEIF EXPR = "{ l.field_id EQ 'ping_bing' }">	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Sitemap_Ping_Bing,		'Sitemap_Ping_Bing',	'1', 'Notify Bing of an updated sitemap' ) }">
	<MvELSEIF EXPR = "{ l.field_id EQ 'products' }">
		<select name="Sitemap_Products">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'none', 		g.Sitemap_Products, 'None' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'active', 		g.Sitemap_Products, 'Active' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'available', 	g.Sitemap_Products, 'Available' ) }">
		</select>
	<MvELSEIF EXPR = "{ l.field_id EQ 'product_priority' }">
		<select name="Sitemap_Product_Priority">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.0', g.Sitemap_Product_Priority, '0.0' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.1', g.Sitemap_Product_Priority, '0.1' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.2', g.Sitemap_Product_Priority, '0.2' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.3', g.Sitemap_Product_Priority, '0.3' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.4', g.Sitemap_Product_Priority, '0.4' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.5', g.Sitemap_Product_Priority, '0.5' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.6', g.Sitemap_Product_Priority, '0.6' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.7', g.Sitemap_Product_Priority, '0.7' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.8', g.Sitemap_Product_Priority, '0.8' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.9', g.Sitemap_Product_Priority, '0.9' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '1.0', g.Sitemap_Product_Priority, '1.0' ) }">
		</select>
	<MvELSEIF EXPR = "{ l.field_id EQ 'exclude_free_products' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Sitemap_Exclude_Free_Products, 'Sitemap_Exclude_Free_Products', '1', 'Exclude Free Products' ) }">
	<MvELSEIF EXPR = "{ l.field_id EQ 'categories' }">
		<select name="Sitemap_Categories">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'none', 		g.Sitemap_Categories, 'None' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'active', 		g.Sitemap_Categories, 'Active' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'available', 	g.Sitemap_Categories, 'Available' ) }">
		</select>
	<MvELSEIF EXPR = "{ l.field_id EQ 'category_priority' }">
		<select name="Sitemap_Category_Priority">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.0', g.Sitemap_Category_Priority, '0.0' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.1', g.Sitemap_Category_Priority, '0.1' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.2', g.Sitemap_Category_Priority, '0.2' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.3', g.Sitemap_Category_Priority, '0.3' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.4', g.Sitemap_Category_Priority, '0.4' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.5', g.Sitemap_Category_Priority, '0.5' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.6', g.Sitemap_Category_Priority, '0.6' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.7', g.Sitemap_Category_Priority, '0.7' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.8', g.Sitemap_Category_Priority, '0.8' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.9', g.Sitemap_Category_Priority, '0.9' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '1.0', g.Sitemap_Category_Priority, '1.0' ) }">
		</select>
	<MvELSEIF EXPR = "{ l.field_id EQ 'pages' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Sitemap_Pages, 'Sitemap_Pages', '1', 'Include pages in the sitemap' ) }">
	<MvELSEIF EXPR = "{ l.field_id EQ 'page_priority' }">
		<select name="Sitemap_Page_Priority">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.0', g.Sitemap_Page_Priority, '0.0' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.1', g.Sitemap_Page_Priority, '0.1' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.2', g.Sitemap_Page_Priority, '0.2' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.3', g.Sitemap_Page_Priority, '0.3' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.4', g.Sitemap_Page_Priority, '0.4' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.5', g.Sitemap_Page_Priority, '0.5' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.6', g.Sitemap_Page_Priority, '0.6' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.7', g.Sitemap_Page_Priority, '0.7' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.8', g.Sitemap_Page_Priority, '0.8' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.9', g.Sitemap_Page_Priority, '0.9' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '1.0', g.Sitemap_Page_Priority, '1.0' ) }">
		</select>
	<MvELSEIF EXPR = "{ l.field_id EQ 'non_canonical_priority' }">
		<select name="Sitemap_Non_Canonical_Priority">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.0', g.Sitemap_Non_Canonical_Priority, '0.0' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.1', g.Sitemap_Non_Canonical_Priority, '0.1' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.2', g.Sitemap_Non_Canonical_Priority, '0.2' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.3', g.Sitemap_Non_Canonical_Priority, '0.3' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.4', g.Sitemap_Non_Canonical_Priority, '0.4' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.5', g.Sitemap_Non_Canonical_Priority, '0.5' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.6', g.Sitemap_Non_Canonical_Priority, '0.6' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.7', g.Sitemap_Non_Canonical_Priority, '0.7' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.8', g.Sitemap_Non_Canonical_Priority, '0.8' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0.9', g.Sitemap_Non_Canonical_Priority, '0.9' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '1.0', g.Sitemap_Non_Canonical_Priority, '1.0' ) }">
		</select>
	<MvELSEIF EXPR = "{ l.field_id EQ 'non_canonical' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Sitemap_Non_Canonical, 'Sitemap_Non_Canonical', '1', 'Output Non-Canonical URIs' ) }">
	<MvELSEIF EXPR = "{ l.field_id EQ 'archived' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Sitemap_Archived, 'Sitemap_Archived', '1', 'Output Archived URIs' ) }">
	<MvELSEIF EXPR = "{ l.field_id EQ 'last_modified' }">
		<select name="Sitemap_Last_Modified">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'none', 		g.Sitemap_Last_Modified, 'None' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'date', 		g.Sitemap_Last_Modified, 'Date' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'datetime',	g.Sitemap_Last_Modified, 'Date & Time' ) }">
		</select>
	<MvELSEIF EXPR = "{ l.field_id EQ 'change_frequency' }">
		<select name="Sitemap_Change_Frequency">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '',		g.Sitemap_Change_Frequency, 'Do Not Send' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'always',	g.Sitemap_Change_Frequency, 'Always' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'hourly',	g.Sitemap_Change_Frequency, 'Hourly' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'daily',	g.Sitemap_Change_Frequency, 'Daily' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'weekly',	g.Sitemap_Change_Frequency, 'Weekly' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'monthly',	g.Sitemap_Change_Frequency, 'Monthly' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'yearly',	g.Sitemap_Change_Frequency, 'Yearly' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'never',	g.Sitemap_Change_Frequency, 'Never' ) }">
		</select>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Validate" PARAMETERS = "module var, feed var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Sitemap_Suffix"					VALUE = "{ [ g.Module_JSON ].JSON_Decode( g.Sitemap_Suffix ) }">
	<MvASSIGN NAME = "g.Sitemap_Robots"					VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Sitemap_Robots ) }">
	<MvASSIGN NAME = "g.Sitemap_Ping_Google"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Sitemap_Ping_Google ) }">
	<MvASSIGN NAME = "g.Sitemap_Ping_Yahoo"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Sitemap_Ping_Yahoo ) }">
	<MvASSIGN NAME = "g.Sitemap_Ping_Bing"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Sitemap_Ping_Bing ) }">
	<MvASSIGN NAME = "g.Sitemap_Products"				VALUE = "{ [ g.Module_JSON ].JSON_Decode( tolower( g.Sitemap_Products ) ) }">
	<MvASSIGN NAME = "g.Sitemap_Product_Priority"		VALUE = "{ g.Sitemap_Product_Priority ROUND 1 }">
	<MvASSIGN NAME = "g.Sitemap_Exclude_Free_Products"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Sitemap_Exclude_Free_Products ) }">
	<MvASSIGN NAME = "g.Sitemap_Categories"				VALUE = "{ [ g.Module_JSON ].JSON_Decode( tolower( g.Sitemap_categories ) ) }">
	<MvASSIGN NAME = "g.Sitemap_Category_Priority"		VALUE = "{ g.Sitemap_Category_Priority ROUND 1 }">
	<MvASSIGN NAME = "g.Sitemap_Pages"					VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Sitemap_Pages ) }">
	<MvASSIGN NAME = "g.Sitemap_Page_Priority"			VALUE = "{ g.Sitemap_Page_Priority ROUND 1 }">
	<MvASSIGN NAME = "g.Sitemap_Non_Canonical_Priority"	VALUE = "{ g.Sitemap_Non_Canonical_Priority ROUND 1 }">
	<MvASSIGN NAME = "g.Sitemap_Non_Canonical"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Sitemap_Non_Canonical ) }">
	<MvASSIGN NAME = "g.Sitemap_Archived"				VALUE = "{ [ g.Module_JSON ].JSON_Decode( g.Sitemap_Archived ) }">
	<MvASSIGN NAME = "g.Sitemap_Last_Modified"			VALUE = "{ [ g.Module_JSON ].JSON_Decode( tolower( g.Sitemap_Last_Modified ) ) }">
	<MvASSIGN NAME = "g.Sitemap_Change_Frequency"		VALUE = "{ [ g.Module_JSON ].JSON_Decode( tolower( g.Sitemap_Change_Frequency ) ) }">

	<MvIF EXPR = "{ NOT Sitemap_Validate_Value_In_List( g.Sitemap_Products, 'none,active,available' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select a Product value' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Sitemap_Validate_Value_In_List( g.Sitemap_Categories, 'none,active,available' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select a Category value' ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Sitemap_Product_Priority LT 0 OR g.Sitemap_Product_Priority GT 1 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Product priority must be between 0.0 and 1.0' ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Sitemap_Category_Priority LT 0 OR g.Sitemap_Category_Priority GT 1 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Category priority must be between 0.0 and 1.0' ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Sitemap_Page_Priority LT 0 OR g.Sitemap_Page_Priority GT 1 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Page priority must be between 0.0 and 1.0' ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Sitemap_Non_Canonical_Priority LT 0 OR g.Sitemap_Non_Canonical_Priority GT 1 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Non-Canonical URI priority must be between 0.0 and 1.0' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Sitemap_Validate_Value_In_List( g.Sitemap_Last_Modified, 'none,date,datetime' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select a Last Modified value' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Sitemap_Validate_Value_In_List( g.Sitemap_Change_Frequency, ',always,hourly,daily,weekly,monthly,yearly,never' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select a Change Frequency value' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Update" PARAMETERS = "module var, feed var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.feed:config:suffix" 				VALUE = "{ g.Sitemap_Suffix }">
	<MvASSIGN NAME = "l.feed:config:robots" 				VALUE = "{ g.Sitemap_Robots }">
	<MvASSIGN NAME = "l.feed:config:ping_google" 			VALUE = "{ g.Sitemap_Ping_Google }">
	<MvASSIGN NAME = "l.feed:config:ping_bing" 				VALUE = "{ g.Sitemap_Ping_Bing }">
	<MvASSIGN NAME = "l.feed:config:products" 				VALUE = "{ g.Sitemap_Products }">
	<MvASSIGN NAME = "l.feed:config:product_priority" 		VALUE = "{ g.Sitemap_Product_Priority }">
	<MvASSIGN NAME = "l.feed:config:exclude_free_products" 	VALUE = "{ g.Sitemap_Exclude_Free_Products }">
	<MvASSIGN NAME = "l.feed:config:categories" 			VALUE = "{ g.Sitemap_Categories }">
	<MvASSIGN NAME = "l.feed:config:category_priority" 		VALUE = "{ g.Sitemap_Category_Priority }">
	<MvASSIGN NAME = "l.feed:config:pages"					VALUE = "{ g.Sitemap_Pages }">
	<MvASSIGN NAME = "l.feed:config:page_priority" 			VALUE = "{ g.Sitemap_Page_Priority }">
	<MvASSIGN NAME = "l.feed:config:non_canonical_priority" VALUE = "{ g.Sitemap_Non_Canonical_Priority }">
	<MvASSIGN NAME = "l.feed:config:non_canonical"			VALUE = "{ g.Sitemap_Non_Canonical }">
	<MvASSIGN NAME = "l.feed:config:archived" 				VALUE = "{ g.Sitemap_Archived }">
	<MvASSIGN NAME = "l.feed:config:last_modified" 			VALUE = "{ g.Sitemap_Last_Modified }">
	<MvASSIGN NAME = "l.feed:config:change_frequency"		VALUE = "{ g.Sitemap_Change_Frequency }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Delete" PARAMETERS = "module var, feed var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Output_Custom" PARAMETERS = "module var, feed var" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCE NAME = "l.config" VARIABLE = "l.feed:config">

	<MvASSIGN NAME = "l.sitemaps"		VALUE = "">
	<MvASSIGN NAME = "l.sitemap_count"	VALUE = 0>

	<MvASSIGN NAME = "l.tag_settings:change_frequency"			VALUE = "{ Sitemap_Normalize_ChangeFrequency( l.config:change_frequency ) }">
	<MvASSIGN NAME = "l.tag_settings:last_modified"				VALUE = "{ Sitemap_Normalize_Date( l.config:last_modified ) }">
	<MvASSIGN NAME = "l.tag_settings:product_priority"			VALUE = "{ Sitemap_Normalize_Priority( l.config:product_priority ) }">
	<MvASSIGN NAME = "l.tag_settings:category_priority"			VALUE = "{ Sitemap_Normalize_Priority( l.config:category_priority ) }">
	<MvASSIGN NAME = "l.tag_settings:page_priority"				VALUE = "{ Sitemap_Normalize_Priority( l.config:page_priority ) }">
	<MvASSIGN NAME = "l.tag_settings:non_canonical_priority"	VALUE = "{ Sitemap_Normalize_Priority( l.config:non_canonical_priority ) }">

	<MvIF EXPR = "{ l.config:products EQ 'active' OR l.config:products EQ 'available' }">
		<MvIF EXPR = "{ NOT Sitemap_Write_Products( l.sitemaps, l.sitemap_count, l.tag_settings, l.config:products EQ 'available', l.config:exclude_free_products ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.config:categories EQ 'active' OR l.config:categories EQ 'available' }">
		<MvIF EXPR = "{ NOT Sitemap_Write_Categories( l.sitemaps, l.sitemap_count, l.tag_settings, l.config:categories EQ 'available' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.config:pages }">
		<MvIF EXPR = "{ NOT Sitemap_Write_Pages( l.sitemaps, l.sitemap_count, l.tag_settings ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.config:non_canonical }">
		<MvIF EXPR = "{ NOT Sitemap_Write_URIs( l.sitemaps, l.sitemap_count, l.tag_settings, 0 ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.config:archived }">
		<MvIF EXPR = "{ NOT Sitemap_Write_URIs( l.sitemaps, l.sitemap_count, l.tag_settings, 1 ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.sitemap_count EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT Sitemap_Finialize_Sitemaps( l.sitemaps, l.sitemap_count, l.config, l.sitemap_url ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.config:robots }">
		<MvIF EXPR = "{ NOT Sitemap_Update_robots( l.sitemap_url ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.config:ping_google OR l.config:ping_bing }">
		<MvIF EXPR = "{ NOT Sitemap_Ping_SearchEngines( l.config, l.sitemap_url ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Provision_Settings" PARAMETERS = "module var, feed var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.feed:config }">
		<MvEVAL EXPR = "{ Sitemap_Config_Defaults( l.feed:config ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 		'o', l.provide_xml, 'Suffix', 				l.feed:config:suffix )																								OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean( 	'o', l.provide_xml, 'Robots', 				l.feed:config:robots )																								OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List( 		'o', l.provide_xml, 'Products', 			l.feed:config:products, 		'none,active,available', 								'none,active,available' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Number( 	'o', l.provide_xml, 'ProductPriority',		l.feed:config:product_priority, 		1, 1 )																		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean( 	'o', l.provide_xml, 'ExcludeFreeProducts',	l.feed:config:exclude_free_products )																				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List( 		'o', l.provide_xml, 'Categories', 			l.feed:config:categories,		'none,active,available', 								'none,active,available' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Number( 	'o', l.provide_xml, 'CategoryPriority',		l.feed:config:category_priority, 		1, 1 )																		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean( 	'o', l.provide_xml, 'Pages', 				l.feed:config:pages ) 																								OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Number( 	'o', l.provide_xml, 'PagePriority',			l.feed:config:page_priority, 			1, 1 )																		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Number( 	'o', l.provide_xml, 'NonCanonicalPriority', l.feed:config:non_canonical_priority,	1, 1 )																		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml, 'NonCanonical',			l.feed:config:non_canonical )																						OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean( 	'o', l.provide_xml, 'Archived', 			l.feed:config:archived )																							OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'o', l.provide_xml, 'LastModified', 		l.feed:config:last_modified, 	'none,date,datetime', 									'none,date,datetime' )		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'o', l.provide_xml, 'ChangeFrequency', 		l.feed:config:change_frequency,	'always,hourly,daily,weekly,monthly,yearly,never', 	'always,hourly,daily,weekly,monthly,yearly,never' ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ ( l.feed:config:product_priority LT 0 ) OR ( l.feed:config:product_priority GT 1 ) }">					<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Invalid value for ProductPriority: Value must be between 0.0 and 1.0' ) }">
	<MvELSEIF EXPR = "{ ( l.feed:config:category_priority LT 0 ) OR ( l.feed:config:category_priority GT 1 ) }">			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Invalid value for CategoryPriority: Value must be between 0.0 and 1.0' ) }">
	<MvELSEIF EXPR = "{ ( l.feed:config:page_priority LT 0 ) OR ( l.feed:config:page_priority GT 1 ) }">					<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Invalid value for PagePriority: Value must be between 0.0 and 1.0' ) }">
	<MvELSEIF EXPR = "{ ( l.feed:config:non_canonical_priority LT 0 ) OR ( l.feed:config:non_canonical_priority GT 1 ) }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Invalid value for NonCanonicalPriority: Value must be between 0.0 and 1.0' ) }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'NotifySearchEngines' ) }">
		<MvASSIGN NAME = "l.feed:config:ping_google"	VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:NotifySearchEngines[ 1 ], 'Google' ) }">
		<MvASSIGN NAME = "l.feed:config:ping_bing"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:NotifySearchEngines[ 1 ], 'Bing' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Component Feature (component)
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Output xxx Functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "Sitemap_Write_Products" PARAMETERS = "sitemaps var, sitemap_count var, tag_settings var, available_only, exclude_free" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.search_query"		VALUE = "">
	<MvASSIGN NAME = "l.flags:nosession"	VALUE = 1>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, '*' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'Products', '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'active = 1', '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY( l.search_query, 'disp_order', '' ) }">

	<MvIF EXPR = "{ l.available_only }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'agrpcount = 0', '' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.exclude_free }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'price <> 0', '' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.query" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.search_query, l.fields ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "Products"
				QUERY 	= "{ l.query }"
				FIELDS	= "{ l.fields }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-SMP-00019', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT Products.d.EOF }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Product_Read( l.product ) }">

		<MvASSIGN NAME = "l.url"	VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Product_URL( l.product, l.flags ) }">
		<MvASSIGN NAME = "l.data"	VALUE = "{ Sitemap_Generate_SitemapTag_URL( l.url, l.tag_settings:last_modified, l.tag_settings:change_frequency, l.tag_settings:product_priority ) }">

		<MvIF EXPR = "{ NOT Sitemap_Write_Sitemap_URL( l.sitemaps, l.sitemap_count, l.data ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "Products">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "Products" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Products">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Write_Categories" PARAMETERS = "sitemaps var, sitemap_count var, tag_settings var, available_only" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.search_query"		VALUE = "">
	<MvASSIGN NAME = "l.flags:nosession"	VALUE = 1>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, '*' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'Categories', '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'active = 1', '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY( l.search_query, 'disp_order', '' ) }">

	<MvIF EXPR = "{ l.available_only }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'agrpcount = 0', '' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.query" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.search_query, l.fields ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "Categories"
				QUERY 	= "{ l.query }"
				FIELDS	= "{ l.fields }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-SMP-00020', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT Categories.d.EOF }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Category_Read( l.category ) }">

		<MvASSIGN NAME = "l.url"	VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Category_URL( l.category, l.flags ) }">
		<MvASSIGN NAME = "l.data"	VALUE = "{ Sitemap_Generate_SitemapTag_URL( l.url, l.tag_settings:last_modified, l.tag_settings:change_frequency, l.tag_settings:category_priority ) }">

		<MvIF EXPR = "{ NOT Sitemap_Write_Sitemap_URL( l.sitemaps, l.sitemap_count, l.data ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "Categories">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "Categories" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Categories">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Write_Pages" PARAMETERS = "sitemaps var, sitemap_count var, tag_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.search_query"		VALUE = "">
	<MvASSIGN NAME = "l.flags:nosession"	VALUE = 1>
	<MvASSIGN NAME = "l.item_code"			VALUE = "{ Sitemap_ExcludeItem_Code() }">

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Item_Load_Code( l.item_code, l.item ) }">	<MvASSIGN NAME = "l.item_id" VALUE = "{ l.item:id }">
	<MvELSE>																				<MvASSIGN NAME = "l.item_id" VALUE = 0>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'p.*,
																				 pv.id			AS version_id,
																				 pv.xref_id		AS version_xref_id,
																				 pv.user_id		AS version_user_id,
																				 pv.admin		AS version_admin,
																				 pv.layout		AS version_layout,
																				 pv.secure		AS version_secure,
																				 pv.public		AS version_public,
																				 pv.code		AS version_code,
																				 pv.name		AS version_name,
																				 pv.title		AS version_title,
																				 pv.ui_id		AS version_ui_id,
																				 pv.cache		AS version_cache,
																				 pv.cacheset	AS version_cacheset' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'PageBranchVersions', 'pbv' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'Pages', 'p' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'PageVersions', 'pv' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'pv', g.Store_Table_Prefix $ 'PageXItem', 'pxi', 'pxi.page_id = pv.page_id AND pxi.xref_id = pv.xref_id AND pxi.item_id = ?', 'l.item_id' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'pbv.branch_id	= ?					AND
																				pbv.head		= 1					AND
																				p.id			= pbv.page_id		AND
																				pv.id			= pbv.version_id	AND
																				pv.admin		= 0					AND
																				pv.layout		= 0					AND
																				pv.fragment		= 0					AND
																				pxi.item_id IS NULL',
															   [ g.Module_Library_DB ].SQL_Query_Field( [ g.Module_Feature_TUI_MGR ].TemplateManager_Working_Branch_ID() ) ) }">

	<MvASSIGN NAME = "l.query" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.search_query, l.fields ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "Pages"
				QUERY 	= "{ l.query }"
				FIELDS	= "{ l.fields }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-SMP-00021', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT Pages.d.EOF }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Read( l.page ) }">

		<MvASSIGN NAME = "l.url"	VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Page_URL( l.page, l.flags ) }">
		<MvASSIGN NAME = "l.data"	VALUE = "{ Sitemap_Generate_SitemapTag_URL( l.url, l.tag_settings:last_modified, l.tag_settings:change_frequency, l.tag_settings:page_priority ) }">

		<MvIF EXPR = "{ NOT Sitemap_Write_Sitemap_URL( l.sitemaps, l.sitemap_count, l.data ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "Pages">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "Pages" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Pages">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Write_URIs" PARAMETERS = "sitemaps var, sitemap_count var, tag_settings var, archived" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.search_query"	VALUE = "">
	<MvASSIGN NAME = "l.item_code"		VALUE = "{ Sitemap_ExcludeItem_Code() }">

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Item_Load_Code( l.item_code, l.item ) }">	<MvASSIGN NAME = "l.item_id" VALUE = "{ l.item:id }">
	<MvELSE>																				<MvASSIGN NAME = "l.item_id" VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT l.archived }">	<MvASSIGN NAME = "l.change_frequency" VALUE = "{ l.tag_settings:change_frequency }">
	<MvELSE>							<MvASSIGN NAME = "l.change_frequency" VALUE = "never">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, '*' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, 'URIs', 'u' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'u', g.Store_Table_Prefix $ 'PageBranchVersions',	'pbv', 'u.page_code <> \'\' AND pbv.branch_id = ? AND pbv.head = 1 AND ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'pbv.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'u.page_code' ), [ g.Module_Library_DB ].SQL_Query_Field( [ g.Module_Feature_TUI_MGR ].TemplateManager_Working_Branch_ID() ) ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'u', g.Store_Table_Prefix $ 'PageXItem',			'pxi', 'pxi.page_id = pbv.page_id AND pxi.item_id = ?', 'l.item_id' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'feed_id = 0 AND canonical = 0 AND pxi.item_id IS NULL', '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY( l.search_query, 'uri', 'ASC' ) }">

	<MvIF EXPR = "{ NOT l.archived }">	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'status = 200', '' ) }">
	<MvELSE>							<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'status <> 200', '' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.query" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.search_query, l.fields ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "URIs"
				QUERY 	= "{ l.query }"
				FIELDS	= "{ l.fields }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-SMP-00022', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT URIs.d.EOF }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_URI_DB ].URI_Read( l.uri ) }">

		<MvASSIGN NAME = "l.url"	VALUE = "{ Sitemap_Build_URL_From_URI( l.uri:uri ) }">
		<MvASSIGN NAME = "l.data"	VALUE = "{ Sitemap_Generate_SitemapTag_URL( l.url, l.tag_settings:last_modified, l.change_frequency, l.tag_settings:non_canonical_priority ) }">

		<MvIF EXPR = "{ NOT Sitemap_Write_Sitemap_URL( l.sitemaps, l.sitemap_count, l.data ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "URIs">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "URIs" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "URIs">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Used to create sitemap files
|
</MvCOMMENT>

<MvFUNCTION NAME = "Sitemap_Create_Sitemap" PARAMETERS = "sitemaps var, sitemap_count var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Sitemap_Generate_TempFile( l.file ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.sitemap"		VALUE = "">
	<MvASSIGN NAME = "l.sitemap:file"	VALUE = "{ l.file }">
	<MvASSIGN NAME = "l.sitemap:count"	VALUE = 0>
	<MvASSIGN NAME = "l.sitemap:size"	VALUE = 0>
	<MvASSIGN NAME = "l.sitemap_count"	VALUE = "{ miva_array_insert_var( l.sitemaps, l.sitemap, -1 ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Write_Sitemap_URL" PARAMETERS = "sitemaps var, sitemap_count var, data var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.sitemap_count EQ 0 }">																				<MvASSIGN NAME = "l.create" VALUE = 1>
	<MvELSEIF EXPR = "{ l.sitemaps[ l.sitemap_count ]:count EQ 50000 OR l.sitemaps[ l.sitemap_count ]:size GT 9500000 }">	<MvASSIGN NAME = "l.create" VALUE = 1>
	<MvELSE>																												<MvASSIGN NAME = "l.create" VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.create }">
		<MvIF EXPR = "{ NOT Sitemap_Create_Sitemap( l.sitemaps, l.sitemap_count ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT Sitemap_Write_Sitemap_Header( l.sitemaps[ l.sitemap_count ] ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.sitemap" VARIABLE = "l.sitemaps">
		<MvDIMENSION INDEX = "{ l.sitemap_count }">
	</MvREFERENCEARRAY>

	<MvASSIGN NAME = "l.write_size"		VALUE = "{ file_append( l.sitemap:file, 'data', l.data ) }">

	<MvIF EXPR = "{ l.write_size EQ -1 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-SMP-00009', 'Failed writing to temporary sitemap \'' $ l.sitemap:file $ '\': ' $ file_last_error() ) }">
	</MvIF>

	<MvASSIGN NAME = "l.sitemap:count"	VALUE = "{ l.sitemap:count + 1 }">
	<MvASSIGN NAME = "l.sitemap:size"	VALUE = "{ l.sitemap:size + l.write_size }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Write_Sitemap_Header" PARAMETERS = "sitemap var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.data"			VALUE = "{ Sitemap_Generate_Sitemap_Header() }">
	<MvASSIGN NAME = "l.write_size"		VALUE = "{ file_append( l.sitemap:file, 'data', l.data ) }">

	<MvIF EXPR = "{ l.write_size EQ -1 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-SMP-00010', 'Failed writing sitemap header to temporary sitemap file \'' $ l.sitemap:file $ '\': ' $ file_last_error() ) }">
	</MvIF>

	<MvASSIGN NAME = "l.sitemap:size"	VALUE = "{ l.write_size }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Write_Sitemap_Footer" PARAMETERS = "sitemap var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.data"			VALUE = "{ Sitemap_Generate_Sitemap_Footer() }">
	<MvASSIGN NAME = "l.write_size"		VALUE = "{ l.sitemap:size + file_append( l.sitemap:file, 'data', l.data ) }">

	<MvIF EXPR = "{ l.write_size EQ -1 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-SMP-00011', 'Failed writing to temporary sitemap file ' $ l.sitemap:file $ ': ' $ file_last_error() ) }">
	</MvIF>

	<MvASSIGN NAME = "l.sitemap:size"	VALUE = "{ l.sitemap:size + l.write_size }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Generate_Sitemap_Header" PARAMETERS = "" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ '<?xml version="1.0" encoding="UTF-8"?>' $ asciichar( 10 ) $ '<urlset xmlns="http://www.sitemaps.org/schemas/sitemap/0.9">' $ asciichar( 10 ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Generate_Sitemap_Footer" PARAMETERS = "" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "</urlset>">
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Generate_SitemapTag_URL" PARAMETERS = "loc, lastmod, changefreq, priority" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.data">
	<MIVA STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<url>
		<loc><MvEVAL EXPR = "{ Sitemap_Escape_URL( l.loc ) }"></loc>

		<MvIF EXPR = "{ l.lastmod }">
			<lastmod><MvEVAL EXPR = "{ l.lastmod }"></lastmod>
		</MvIF>

		<MvIF EXPR = "{ l.changefreq }">
			<changefreq><MvEVAL EXPR = "{ l.changefreq }"></changefreq>
		</MvIF>

		<MvIF EXPR = "{ NOT ISNULL l.priority AND l.priority NE 0.5 }">
			<priority><MvEVAL EXPR = "{ l.priority ROUND 1 }"></priority>
		</MvIF>
	</url>
	<MIVA STANDARDOUTPUTLEVEL = "">
	</MvCAPTURE>

	<MvFUNCTIONRETURN VALUE = "{ l.data }">
</MvFUNCTION>

<MvCOMMENT>
|
| Used to create sitemap index files
|
</MvCOMMENT>

<MvFUNCTION NAME = "Sitemap_Generate_SitemapIndex_Header" PARAMETERS = "" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ '<?xml version="1.0" encoding="UTF-8"?>' $ asciichar( 10 ) $ '<sitemapindex xmlns="http://www.sitemaps.org/schemas/sitemap/0.9">' $ asciichar( 10 ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Generate_SitemapIndex_Footer" PARAMETERS = "" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "</sitemapindex>">
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Generate_SitemapIndexTag_Sitemap" PARAMETERS = "loc, lastmod" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.data">
	<MIVA STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<sitemap>
		<loc><MvEVAL EXPR = "{ Sitemap_Escape_URL( l.loc ) }"></loc>

		<MvIF EXPR = "{ l.lastmod }">
			<lastmod><MvEVAL EXPR = "{ l.lastmod }"></lastmod>
		</MvIF>
	</sitemap>
	<MIVA STANDARDOUTPUTLEVEL = "">
	</MvCAPTURE>

	<MvFUNCTIONRETURN VALUE = "{ l.data }">
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Generate_SitemapIndex" PARAMETERS = "sitemaps var, sitemap_count, tag_settings var, file var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Sitemap_Generate_TempFile( l.file ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.header" VALUE = "{ Sitemap_Generate_SitemapIndex_Header() }">
	<MvASSIGN NAME = "l.footer" VALUE = "{ Sitemap_Generate_SitemapIndex_Footer() }">

	<MvCAPTURE VARIABLE = "l.sitemap_index_data">
		<MvEVAL EXPR = "{ l.header }">
		<MvFOREACH ITERATOR = "l.sitemap" ARRAY = "l.sitemaps" COUNT = "{ l.sitemap_count }">
			<MvEVAL EXPR = "{ Sitemap_Generate_SitemapIndexTag_Sitemap( Sitemap_Build_URL_From_URI( l.sitemap:script_file ), l.tag_settings:last_modified ) }">
		</MvFOREACH>
		<MvEVAL EXPR = "{ l.footer }">
	</MvCAPTURE>

	<MvIF EXPR = "{ file_append( l.file, 'data', l.sitemap_index_data ) EQ -1 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-SMP-00012', 'Failed writing to the sitemap index file' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Helper Functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "Sitemap_Generate_TempFile" PARAMETERS = "file var" STANDARDOUTPUTLEVEL = "">
	<MvFOR COUNT = "{ 100 }">
		<MvASSIGN NAME = "l.file" VALUE = "{ 'sitemap_temp_' $ MakeSessionID() $ '.xml' }">

		<MvIF EXPR = "{ file_create( l.file, 'data', l.null ) NE -1 }">
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>
	</MvFOR>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-SMP-00013', 'Failed to generate a unique temporary file after 100 attempts' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Finialize_Sitemaps" PARAMETERS = "sitemaps var, sitemap_count, config var, sitemap_url var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.multiple_sitemaps" VALUE = "{ l.sitemap_count GT 1 }">

	<MvFOREACH ITERATOR = "l.sitemap" ARRAY = "l.sitemaps" INDEX = "l.pos" COUNT = "{ l.sitemap_count }">
		<MvIF EXPR = "{ NOT Sitemap_Write_Sitemap_Footer( l.sitemap ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT l.multiple_sitemaps }">	<MvASSIGN NAME = "l.sitemap:script_file" VALUE = "{ 'sitemap' $ l.config:suffix $ '.xml' }">
		<MvELSE>									<MvASSIGN NAME = "l.sitemap:script_file" VALUE = "{ 'sitemap' $ l.config:suffix $ l.pos $ '.xml' }">
		</MvIF>

		<MvIF EXPR = "{ NOT fsrename( l.sitemap:file, '/' $ l.sitemap:script_file ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-SMP-00015', 'Failed moving the sitemap file \'' $ l.sitemap:file $ '\' to the script directory: ' $ file_last_error() ) }">
		</MvIF>
	</MvFOREACH>

	<MvIF EXPR = "{ NOT l.multiple_sitemaps }">
		<MvASSIGN NAME = "l.sitemapindex_name" VALUE = "{ l.sitemaps[ 1 ]:script_file }">
	<MvELSE>
		<MvIF EXPR = "{ NOT Sitemap_Generate_SitemapIndex( l.sitemaps, l.sitemap_count, l.tag_settings, l.sitemap_index_file ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.sitemapindex_name" VALUE = "{ 'sitemap' $ l.config:suffix $ '.xml' }">

		<MvIF EXPR = "{ NOT fsrename( l.sitemap_index_file, '/' $ l.sitemapindex_name ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-SMP-00016', 'Failed moving the sitemap index file \'' $ l.sitemap_index_file $ '\' to the script directory: ' $ file_last_error() ) }">
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.sitemap_url" VALUE = "{ Sitemap_Build_URL_From_URI( l.sitemapindex_name ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Build_URL_From_URI" PARAMETERS = "uri" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_URI_DB ].URISettings_Load_Cached( l.urisettings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.base" VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Base_Secure( l.urisettings ) }">

	<MvIF EXPR = "{ ( '/' IN l.uri ) EQ 1 }">
		<MvASSIGN NAME = "l.uri" VALUE = "{ substring_var( l.uri, 2, len_var( l.uri ) - 1 ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.base $ l.uri }">
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Update_robots" PARAMETERS = "sitemap_url" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.robots"			VALUE = "/robots.txt">
	<MvASSIGN NAME = "l.sitemap_line"	VALUE = "{ 'Sitemap: ' $ l.sitemap_url }">

	<MvLOCKFILE FILE = "robots.txt">
		<MvASSIGN NAME = "l.null"		VALUE = "{ file_read( l.robots, 'script', l.existing_robots ) }">
		<MvASSIGN NAME = "l.start_pos"	VALUE = "{ l.sitemap_line IN l.existing_robots }">

		<MvIF EXPR = "{ l.start_pos EQ 0 }">
			<MvIF EXPR = "{ NOT sexists( l.robots ) }">
				<MvASSIGN NAME = "l.null" VALUE = "{ file_create( l.robots, 'script', l.sitemap_line ) }">
			<MvELSE>
				<MvIF EXPR = "{ ( NOT ISNULL l.existing_robots ) AND ( substring_var( l.robots, len_var( l.existing_robots ), 1 ) NE asciichar( 10 ) ) }">
					<MvASSIGN NAME = "l.sitemap_line" VALUE = "{ asciichar( 10 ) $ l.sitemap_line }">
				</MvIF>

				<MvASSIGN NAME = "l.null" VALUE = "{ file_append( l.robots, 'script', l.sitemap_line ) }">
			</MvIF>
		</MvIF>
	</MvLOCKFILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Config_Defaults" PARAMETERS = "config var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.config:suffix"					VALUE = "">
	<MvASSIGN NAME = "l.config:robots"					VALUE = 1>
	<MvASSIGN NAME = "l.config:ping_google"				VALUE = 0>
	<MvASSIGN NAME = "l.config:ping_bing"				VALUE = 0>
	<MvASSIGN NAME = "l.config:products"				VALUE = "active">
	<MvASSIGN NAME = "l.config:product_priority"		VALUE = 0.5>
	<MvASSIGN NAME = "l.config:exclude_free_products"	VALUE = 0>
	<MvASSIGN NAME = "l.config:categories"				VALUE = "active">
	<MvASSIGN NAME = "l.config:category_priority"		VALUE = 0.5>
	<MvASSIGN NAME = "l.config:pages"					VALUE = 1>
	<MvASSIGN NAME = "l.config:page_priority"			VALUE = 0.5>
	<MvASSIGN NAME = "l.config:non_canonical_priority"	VALUE = 0.5>
	<MvASSIGN NAME = "l.config:non_canonical"			VALUE = 0>
	<MvASSIGN NAME = "l.config:archived"				VALUE = "">
	<MvASSIGN NAME = "l.config:last_modified"			VALUE = "date">
	<MvASSIGN NAME = "l.config:change_frequency"		VALUE = "weekly">
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Exclude_Default_Pages" PARAMETERS = "item_code, pages" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Item_Load_Code( l.item_code, l.item ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-SMP-00017', 'Failed to load item \'' $ l.item_code $ '\'' ) }">
	</MvIF>

	<MvFOREACH ITERATOR = "l.page_code" ARRAY = "l.page_codes" COUNT = "{ miva_splitstring( l.pages, ',', l.page_codes, 'trim' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_Code( l.page_code, l.page ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFOREACHCONTINUE>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item_LowLevel( l.page, l.item ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Validate_Value_In_List" PARAMETERS = "value, allowed" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.field" ARRAY = "l.fields" COUNT = "{ miva_splitstring( l.allowed, ',', l.fields, 'trim,lower' ) }">
		<MvIF EXPR = "{ l.value EQ l.field }">
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Ping_SearchEngines" PARAMETERS = "config var, sitemap_url" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.domain_count"	VALUE = 0>
	<MvASSIGN NAME = "l.domains"		VALUE = "">

	<MvIF EXPR = "{ l.config:ping_google }">
		<MvASSIGN NAME = "l.domain_count" VALUE = "{ miva_array_insert( l.domains, 'www.google.com', -1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.config:ping_bing }">
		<MvASSIGN NAME = "l.domain_count" VALUE = "{ miva_array_insert( l.domains, 'www.bing.com', -1 ) }">
	</MvIF>

	<MIVA MvCALL_ERROR = "nonfatal, nodisplay">
	<MvFOREACH ITERATOR = "l.domain" ARRAY = "l.domains" COUNT = "{ l.domain_count }">
		<MvCALL METHOD = "GET"
				ACTION = "{ 'https://' $ l.domain $ '/ping?sitemap=' $ l.sitemap_url }">
		</MvCALL>

		<MvIF EXPR = "{ g.MvCALL_ERROR }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-SMP-00018', 'Submitting sitemap failed: ' $ g.MvCALL_ERROR ) }">
		</MvIF>
	</MvFOREACH>
	<MIVA MvCALL_ERROR = "fatal, display">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Escape_URL" PARAMETERS = "url" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ glosub( l.url, '&', '&amp;' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Normalize_ChangeFrequency" PARAMETERS = "change_frequency" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.cf" VALUE = "{ tolower( l.change_frequency ) }">

	<MvIF EXPR = "{ l.cf NE 'always'	AND
					l.cf NE 'hourly'	AND
					l.cf NE 'daily'		AND
					l.cf NE 'weekly'	AND
					l.cf NE 'monthly'	AND
					l.cf NE 'yearly'	AND
					l.cf NE 'never' }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.cf }">
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Normalize_Priority" PARAMETERS = "priority" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.priority" VALUE = "{ l.priority ROUND 1 }">

	<MvIF EXPR = "{ l.priority LT 0 OR l.priority GT 1 }">
		<MvFUNCTIONRETURN VALUE = 0.5>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.priority }">
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_Normalize_Date" PARAMETERS = "format" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.format" VALUE = "{ tolower( l.format ) }">
	<MvASSIGN NAME = "l.date"	VALUE = "{ time_t_year( s.time_t, 0 ) $ '-' $ padl( time_t_month( s.time_t, 0 ), 2, '0' ) $ '-' $ padl( time_t_dayofmonth( s.time_t, 0 ), 2, '0' ) }">
	<MvASSIGN NAME = "l.time"	VALUE = "{ padl( time_t_hour( s.time_t, 0 ), 2, '0' ) $ ':' $ padl( time_t_min( s.time_t, 0 ), 2, '0' ) $ ':' $ padl( time_t_sec( s.time_t, 0 ), 2, '0' ) }">

	<MvIF EXPR = "{ l.format EQ 'date' }">			<MvFUNCTIONRETURN VALUE = "{ l.date }">
	<MvELSEIF EXPR = "{ l.format EQ 'datetime' }">	<MvFUNCTIONRETURN VALUE = "{ l.date $ 'T' $ l.time $ 'Z' }">
	</MvIF>

	<MvFUNCTIONRETURN>
</MvFUNCTION>

<MvFUNCTION NAME = "Sitemap_ExcludeItem_Code" PARAMETERS = "" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "sitemap_exclude">
</MvFUNCTION>

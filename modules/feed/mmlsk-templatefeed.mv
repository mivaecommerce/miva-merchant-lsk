<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-FEED-TFD-
| Next Error Code: 8
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-templatefeed">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Template Based Feed">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0900">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "10.08">
	<MvASSIGN NAME = "l.module:description"	VALUE = "">
	<MvASSIGN NAME = "l.module:features"	VALUE = "feed, component, component_prov, skins">
</MvFUNCTION>

<MvCOMMENT>
|
| Feed Module Feature (feed)
|
</MvCOMMENT>

<MvFUNCTION NAME = "FeedModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:provision_settings" VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Fields" PARAMETERS = "module var, feed var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.feed:id }">
		<MvASSIGN NAME = "g.TemplateFeed_Template"	VALUE = "{ l.feed:config:template }">
		<MvASSIGN NAME = "g.TemplateFeed_PageCode"	VALUE = "{ l.feed:config:page_code }">
		<MvASSIGN NAME = "l.fields"					VALUE = "view_template">
	<MvELSE>
		<MvASSIGN NAME = "g.TemplateFeed_Template"	VALUE = "">
		<MvASSIGN NAME = "g.TemplateFeed_PageCode"	VALUE = "">
		<MvASSIGN NAME = "l.fields"					VALUE = "template">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.fields }">
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.field_id EQ 'view_template' }">	<MvFUNCTIONRETURN VALUE = "">
	<MvELSEIF EXPR = "{ l.field_id EQ 'template' }">	<MvFUNCTIONRETURN VALUE = "<b>Template:</b>">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.field_id EQ 'view_template' }">
		<a href="{ glosub( g.sessionurl, 'json.mvc', 'admin.mvc' ) $ 'Screen=PAGE&Edit_Page=' $ encodeattribute( g.TemplateFeed_PageCode ) $ '&Store_Code=' $ encodeattribute( g.Store_Code ) }">Edit Template</a>
	<MvELSEIF EXPR = "{ l.field_id EQ 'template' }">
		<select name="TemplateFeed_Template">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '',			g.TemplateFeed_Template, 'None' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'product', 	g.TemplateFeed_Template, 'Product' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'category', 	g.TemplateFeed_Template, 'Category' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'customer', 	g.TemplateFeed_Template, 'Customer' ) }">
		</select>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Validate" PARAMETERS = "module var, feed var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.TemplateFeed_Template" VALUE = "{ [ g.Module_JSON ].JSON_Decode( tolower( g.TemplateFeed_Template ) ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].Value_In_List( g.TemplateFeed_Template, ',product,category,customer' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select a template' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Update" PARAMETERS = "module var, feed var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ TemplateFeed_Generate_Or_Update_Template( l.module, l.feed, g.TemplateFeed_Template ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Delete" PARAMETERS = "module var, feed var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.feed:config:page_code }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_GlobalDelete_Page_Code_Admin_UI( l.feed:config:page_code, l.module:id ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Output" PARAMETERS = "module var, feed var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Admin( l.feed:config:page_code ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "FeedModule_Provision_Settings" PARAMETERS = "module var, feed var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List( 'O', l.provide_xml, 'Template', l.template, 'none,product,category,customer', ',product,category,customer' ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT TemplateFeed_Generate_Or_Update_Template( l.module, l.feed, l.template ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Component Feature (component)
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ 'GT_PAGE/' $ l.item $ ':Template Based Feed' }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session:cache:templatefeed:feed_create_template EQ 'product' }">		<MvEVAL EXPR = "{ TemplateFeed_Product_DefaultSettings(	l.settings:settings_template ) }">
	<MvELSEIF EXPR = "{ g.Session:cache:templatefeed:feed_create_template EQ 'category' }">	<MvEVAL EXPR = "{ TemplateFeed_Category_DefaultSettings( l.settings:settings_template ) }">
	<MvELSEIF EXPR = "{ g.Session:cache:templatefeed:feed_create_template EQ 'customer' }">	<MvEVAL EXPR = "{ TemplateFeed_Customer_DefaultSettings( l.settings:settings_template ) }">
	<MvELSE>																				<MvEVAL EXPR = "{ TemplateFeed_Product_DefaultSettings(	l.settings:settings_template ) }">
	</MvIF>

	<MvIF EXPR = "{ l.settings:settings_template:mode EQ 'product' }">		<MvEVAL EXPR = "{ TemplateFeed_Product_GenerateCode_Master(	l.settings:settings_template, l.header_template_source, l.iterator_template_source, l.footer_template_source ) }">
	<MvELSEIF EXPR = "{ l.settings:settings_template:mode EQ 'category' }">	<MvEVAL EXPR = "{ TemplateFeed_Category_GenerateCode(		l.settings:settings_template, l.header_template_source, l.iterator_template_source, l.footer_template_source ) }">
	<MvELSEIF EXPR = "{ l.settings:settings_template:mode EQ 'customer' }">	<MvEVAL EXPR = "{ TemplateFeed_Customer_GenerateCode(		l.settings:settings_template, l.header_template_source, l.iterator_template_source, l.footer_template_source ) }">
	</MvIF>

	<MvASSIGN NAME = "l.name"								VALUE = "{ tolower( l.page:code ) $ '-' $ tolower( l.item ) $ '.mvc' }">

	<MvASSIGN NAME = "l.settings:settings_template:name"	VALUE = "{ 'TEMPLATEFEED_settings-' $ l.name }">
	<MvASSIGN NAME = "l.settings:header_template:name"		VALUE = "{ 'TEMPLATEFEED_header-' $ l.name }">
	<MvASSIGN NAME = "l.settings:iterator_template:name"	VALUE = "{ 'TEMPLATEFEED_iterator-' $ l.name }">
	<MvASSIGN NAME = "l.settings:footer_template:name"		VALUE = "{ 'TEMPLATEFEED_footer-' $ l.name }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.null,						l.settings:settings_template,	l.settings:settings_template:name )	OR
					NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.header_template_source,		l.settings:header_template,		l.settings:header_template:name )	OR
					NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.iterator_template_source,	l.settings:iterator_template,	l.settings:iterator_template:name )	OR
					NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.footer_template_source,		l.settings:footer_template,		l.settings:footer_template:name ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:settings_template:name ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:header_template:name ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:iterator_template:name ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:footer_template:name ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:settings_template:name,	l.source_settings_templateversion )	OR
					NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:header_template:name,	l.source_header_templateversion )	OR
					NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:iterator_template:name, l.source_iterator_templateversion )	OR
					NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:footer_template:name,	l.source_footer_templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:settings_template:name,	l.dest_settings_managedtemplate )	OR
					NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:header_template:name,		l.dest_header_managedtemplate )		OR
					NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:iterator_template:name,	l.dest_iterator_managedtemplate )	OR
					NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:footer_template:name,		l.dest_footer_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.dest_settings"							VALUE = "{ l.source_settings }">
	<MvASSIGN NAME = "l.dest_settings:settings_template:name"	VALUE = "{ l.dest_settings_managedtemplate:version:settings:name }">
	<MvASSIGN NAME = "l.dest_settings:header_template:name"		VALUE = "{ l.dest_header_managedtemplate:version:settings:name }">
	<MvASSIGN NAME = "l.dest_settings:iterator_template:name"	VALUE = "{ l.dest_iterator_managedtemplate:version:settings:name }">
	<MvASSIGN NAME = "l.dest_settings:footer_template:name"		VALUE = "{ l.dest_footer_managedtemplate:version:settings:name }">

	<MvASSIGN NAME = "l.dest_header_template_settings"			VALUE = "{ l.source_header_templateversion:settings }">
	<MvASSIGN NAME = "l.dest_header_template_settings:name"		VALUE = "{ l.dest_header_managedtemplate:version:settings:name }">

	<MvASSIGN NAME = "l.dest_iterator_template_settings"		VALUE = "{ l.source_iterator_templateversion:settings }">
	<MvASSIGN NAME = "l.dest_iterator_template_settings:name"	VALUE = "{ l.dest_iterator_managedtemplate:version:settings:name }">

	<MvASSIGN NAME = "l.dest_footer_template_settings"			VALUE = "{ l.source_footer_templateversion:settings }">
	<MvASSIGN NAME = "l.dest_footer_template_settings:name"		VALUE = "{ l.dest_footer_managedtemplate:version:settings:name }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_settings_managedtemplate,	'', l.source_settings_templateversion:source,	l.dest_settings )					OR
					NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_header_managedtemplate,	'', l.source_header_templateversion:source,		l.dest_header_template_settings )	OR
					NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_iterator_managedtemplate,	'', l.source_iterator_templateversion:source,	l.dest_iterator_template_settings ) OR
					NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_footer_managedtemplate,	'', l.source_footer_templateversion:source,		l.dest_footer_template_settings )  }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.source_branch:id EQ l.dest_branch:id }">	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-FEED-TFD-00006', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' on branch \'' $ l.dest_branch:name $ '\'' ) }">
	<MvELSE>													<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-FEED-TFD-00007', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' from branch \'' $ l.source_branch:name $ '\' to branch \'' $ l.dest_branch:name $ '\'' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.all_settings:eol:cr"	VALUE = "{ asciichar( 13 ) }">
	<MvASSIGN NAME = "l.all_settings:eol:lf"	VALUE = "{ asciichar( 10 ) }">
	<MvASSIGN NAME = "l.all_settings:eol:crlf"	VALUE = "{ l.all_settings:eol:cr $ l.all_settings:eol:lf }">

	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:header_template:name, l.all_settings ) }">

	<MvIF EXPR = "{ l.settings:settings_template:mode EQ 'category' }">				<MvEVAL EXPR = "{ TemplateFeed_Category_IteratorTemplate_Render(		l.settings:iterator_template:name, l.settings:settings_template, l.all_settings ) }">
	<MvELSEIF EXPR = "{ l.settings:settings_template:mode EQ 'customer' }">			<MvEVAL EXPR = "{ TemplateFeed_Customer_IteratorTemplate_Render(		l.settings:iterator_template:name, l.settings:settings_template, l.all_settings ) }">
	<MvELSEIF EXPR = "{ l.settings:settings_template:mode EQ 'product' }">
		<MvIF EXPR = "{ NOT l.settings:settings_template:products:load_variants }">	<MvEVAL EXPR = "{ TemplateFeed_Product_IteratorTemplate_Render_Master(	l.settings:iterator_template:name, l.settings:settings_template, l.all_settings ) }">
		<MvELSE>																	<MvEVAL EXPR = "{ TemplateFeed_Product_IteratorTemplate_Render_Variant(	l.settings:iterator_template:name, l.settings:settings_template, l.all_settings ) }">
		</MvIF>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:footer_template:name, l.all_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT TemplateFeed_Load_Templates( l.fields:settings_template:name,	l.settings_managedtemplate,
													 l.fields:header_template:name,		l.header_managedtemplate,
													 l.fields:iterator_template:name,	l.iterator_managedtemplate,
													 l.fields:footer_template:name,		l.footer_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.fields:mode"							VALUE = "{ trim( tolower( l.fields:mode ) ) }">
	<MvASSIGN NAME = "l.fields:load"							VALUE = "{ trim( tolower( l.fields:load ) ) }">
	<MvASSIGN NAME = "l.fields:in_value"						VALUE = "{ trim( l.fields:in_value ) }">
	<MvASSIGN NAME = "l.fields:encoding"						VALUE = "{ trim( tolower( l.fields:encoding ) ) }">
	<MvASSIGN NAME = "l.fields:delimiter"						VALUE = "{ trim( tolower( l.fields:delimiter ) ) }">
	<MvASSIGN NAME = "l.fields:advanced"						VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:advanced ) }">

	<MvIF EXPR = "{ l.fields:mode EQ 'product' }">
		<MvASSIGN NAME = "l.fields:products:predict_discounts"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:products:predict_discounts ) }">
		<MvASSIGN NAME = "l.fields:products:load_variants"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:products:load_variants ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].Value_In_List( l.fields:mode, 'product,category,customer' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, '', 'Please select a mode' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].Value_In_List( l.fields:encoding, 'none,csv' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, '', 'Please select an encoding method' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].Value_In_List( l.fields:delimiter, 'comma,tab,pipe' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, '', 'Please select a delimiter' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.validate_categories"	VALUE = 0>
	<MvASSIGN NAME = "l.validate_pricegroups"	VALUE = 0>
	<MvASSIGN NAME = "l.validate_availgroups"	VALUE = 0>

	<MvIF EXPR = "{ l.fields:mode EQ 'product' }">
		<MvIF EXPR = "{ l.fields:load EQ 'in_category' OR l.fields:load EQ 'not_in_category' }">							<MvASSIGN NAME = "l.validate_categories"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.fields:load EQ 'in_price_group' OR l.fields:load EQ 'not_in_price_group' }">					<MvASSIGN NAME = "l.validate_pricegroups"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.fields:load EQ 'in_availability_group' OR l.fields:load EQ 'not_in_availability_group' }">	<MvASSIGN NAME = "l.validate_availgroups"	VALUE = 1>
		</MvIF>
	<MvELSEIF EXPR = "{ l.fields:mode EQ 'category' }">
		<MvIF EXPR = "{ l.fields:load EQ 'child_categories_from' }">														<MvASSIGN NAME = "l.validate_categories"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.fields:load EQ 'in_availability_group' OR l.fields:load EQ 'not_in_availability_group' }">	<MvASSIGN NAME = "l.validate_availgroups"	VALUE = 1>
		</MvIF>
	<MvELSEIF EXPR = "{ l.fields:mode EQ 'customer' }">
		<MvIF EXPR = "{ l.fields:load EQ 'in_price_group' OR l.fields:load EQ 'not_in_price_group' }">						<MvASSIGN NAME = "l.validate_pricegroups"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.fields:load EQ 'in_availability_group' OR l.fields:load EQ 'not_in_availability_group' }">	<MvASSIGN NAME = "l.validate_availgroups"	VALUE = 1>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.validate_categories }">
		<MvIF EXPR = "{ ISNULL l.fields:in_value }">												<MvASSIGN NAME = "l.error" VALUE = "Category cannot be blank">
		<MvELSEIF EXPR = "{ NOT TemplateFeed_Validate_Categories( l.fields:in_value ) }">			<MvASSIGN NAME = "l.error" VALUE = "{ g.Error_Message }">
		</MvIF>
	<MvELSEIF EXPR = "{ l.validate_pricegroups }">
		<MvIF EXPR = "{ ISNULL l.fields:in_value }">												<MvASSIGN NAME = "l.error" VALUE = "Price Group cannot be blank">
		<MvELSEIF EXPR = "{ NOT TemplateFeed_Validate_PriceGroups( l.fields:in_value ) }">			<MvASSIGN NAME = "l.error" VALUE = "{ g.Error_Message }">
		</MvIF>
	<MvELSEIF EXPR = "{ l.validate_availgroups }">
		<MvIF EXPR = "{ ISNULL l.fields:in_value }">												<MvASSIGN NAME = "l.error" VALUE = "Availability Group cannot be blank">
		<MvELSEIF EXPR = "{ NOT TemplateFeed_Validate_AvailabilityGroups( l.fields:in_value ) }">	<MvASSIGN NAME = "l.error" VALUE = "{ g.Error_Message }">
		</MvIF>
	<MvELSE>
		<MvASSIGN NAME = "l.fields:in_value" VALUE = "">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'in_value', l.error ) }">
	</MvIF>

	<MvIF EXPR = "{ l.fields:advanced AND l.fields:mode EQ l.settings:settings_template:mode }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.fields:header_template:code ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'header_template:code', g.Error_Message ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.fields:iterator_template:code ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'iterator_template:code', g.Error_Message ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.fields:footer_template:code ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'footer_template:code', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT TemplateFeed_Load_Templates( l.settings:settings_template:name,	l.settings_managedtemplate,
													 l.settings:header_template:name,	l.header_managedtemplate,
													 l.settings:iterator_template:name,	l.iterator_managedtemplate,
													 l.settings:footer_template:name,	l.footer_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.original_mode"												VALUE = "{ l.settings:settings_template:mode }">

	<MvASSIGN NAME = "l.settings:settings_template:mode"							VALUE = "{ l.fields:mode }">
	<MvASSIGN NAME = "l.settings:settings_template:load"							VALUE = "{ l.fields:load }">
	<MvASSIGN NAME = "l.settings:settings_template:in_value"						VALUE = "{ l.fields:in_value }">
	<MvASSIGN NAME = "l.settings:settings_template:encoding"						VALUE = "{ l.fields:encoding }">
	<MvASSIGN NAME = "l.settings:settings_template:delimiter"						VALUE = "{ l.fields:delimiter }">
	<MvASSIGN NAME = "l.settings:settings_template:advanced"						VALUE = "{ l.fields:advanced }">

	<MvIF EXPR = "{ l.settings:settings_template:mode EQ 'product' }">
		<MvASSIGN NAME = "l.settings:settings_template:products:predict_discounts"	VALUE = "{ l.fields:products:predict_discounts }">
		<MvASSIGN NAME = "l.settings:settings_template:products:load_variants"		VALUE = "{ l.fields:products:load_variants }">
		<MvASSIGN NAME = "l.null"													VALUE = "{ [ g.Module_Library_Utilities ].CustomFieldSelect_Selected( l.fields:products:customfields, l.settings:settings_template:products:customfields ) }">

		<MvIF EXPR = "{ NOT l.fields:advanced }">
			<MvASSIGN NAME = "l.settings:settings_template:products:fields"			VALUE = "">
			<MvASSIGN NAME = "l.settings:settings_template:products:variant_fields"	VALUE = "">
			<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_To_Fields( 'product', l.fields:products:multiselect_fields, l.settings:settings_template:products:fields ) }">
			<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_To_Fields( 'variant', l.fields:products:multiselect_variant_fields, l.settings:settings_template:products:variant_fields ) }">

			<MvEVAL EXPR = "{ TemplateFeed_Product_GenerateCode_Master( l.settings:settings_template, l.fields:header_template:code, l.fields:iterator_template:code, l.fields:footer_template:code ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.settings:settings_template:mode EQ 'category' }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_Utilities ].CustomFieldSelect_Selected( l.fields:categories:customfields, l.settings:settings_template:categories:customfields ) }">

		<MvIF EXPR = "{ NOT l.fields:advanced }">
			<MvASSIGN NAME = "l.settings:settings_template:categories:fields"	VALUE = "">
			<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_To_Fields( 'category', l.fields:categories:multiselect_fields, l.settings:settings_template:categories:fields ) }">
			<MvEVAL EXPR = "{ TemplateFeed_Category_GenerateCode( l.settings:settings_template, l.fields:header_template:code, l.fields:iterator_template:code, l.fields:footer_template:code ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.settings:settings_template:mode EQ 'customer' }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_Utilities ].CustomFieldSelect_Selected( l.fields:customers:customfields, l.settings:settings_template:customers:customfields ) }">

		<MvIF EXPR = "{ NOT l.fields:advanced }">
			<MvASSIGN NAME = "l.settings:settings_template:customers:fields"	VALUE = "">
			<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_To_Fields( 'customer', l.fields:customers:multiselect_fields, l.settings:settings_template:customers:fields ) }">
			<MvEVAL EXPR = "{ TemplateFeed_Customer_GenerateCode( l.settings:settings_template, l.fields:header_template:code, l.fields:iterator_template:code, l.fields:footer_template:code ) }">
		</MvIF>
	</MvIF>

	<MvCOMMENT>
	|
	| Settings Template
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.original_mode NE l.settings:settings_template:mode }">
		<MvCOMMENT>
		|
		| Switching modes sets the settings to the new mode's default settings
		|
		</MvCOMMENT>

		<MvASSIGN NAME = "l.template_filename"					VALUE = "{ l.settings:settings_template:name }">

		<MvIF EXPR = "{ l.settings:settings_template:mode EQ 'product' }">
			<MvEVAL EXPR = "{ TemplateFeed_Product_DefaultSettings( l.settings:settings_template ) }">
			<MvEVAL EXPR = "{ TemplateFeed_Product_GenerateCode_Master( l.settings:settings_template, l.fields:header_template:code, l.fields:iterator_template:code, l.fields:footer_template:code ) }">
		<MvELSEIF EXPR = "{ l.settings:settings_template:mode EQ 'category' }">
			<MvEVAL EXPR = "{ TemplateFeed_Category_DefaultSettings( l.settings:settings_template ) }">
			<MvEVAL EXPR = "{ TemplateFeed_Category_GenerateCode( l.settings:settings_template, l.fields:header_template:code, l.fields:iterator_template:code, l.fields:footer_template:code ) }">
		<MvELSEIF EXPR = "{ l.settings:settings_template:mode EQ 'customer' }">
			<MvEVAL EXPR = "{ TemplateFeed_Customer_DefaultSettings( l.settings:settings_template ) }">
			<MvEVAL EXPR = "{ TemplateFeed_Customer_GenerateCode( l.settings:settings_template, l.fields:header_template:code, l.fields:iterator_template:code, l.fields:footer_template:code ) }">
		</MvIF>

		<MvASSIGN NAME = "l.settings:settings_template:name"	VALUE = "{ l.template_filename }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.settings_managedtemplate, l.notes, l.null_source, l.settings:settings_template ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, '', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvCOMMENT>
	|
	| Header Template
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.header_managedtemplate, '', l.fields:header_template:code, l.settings:header_template ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'header_template:code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvCOMMENT>
	|
	| Iterator Template
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.iterator_managedtemplate, '', l.fields:iterator_template:code, l.settings:iterator_template ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'iterator_template:code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvCOMMENT>
	|
	| Footer Template
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.footer_managedtemplate, '', l.fields:footer_template:code, l.settings:footer_template ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'footer_template:code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update_Requires_Version" PARAMETERS = "module var, page var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_PostUpdate" PARAMETERS = "module var, page var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT TemplateFeed_Load_TemplateVersions( l.settings:settings_template:name,	l.settings_templateversion,
															l.settings:header_template:name,	l.header_templateversion,
															l.settings:iterator_template:name,	l.iterator_templateversion,
															l.settings:footer_template:name,	l.footer_templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.load_fields }">
		<MvASSIGN NAME = "l.load_fields_settings_template"	VALUE = 1>
		<MvASSIGN NAME = "l.load_fields_header_template"	VALUE = 1>
		<MvASSIGN NAME = "l.load_fields_iterator_template"	VALUE = 1>
		<MvASSIGN NAME = "l.load_fields_footer_template"	VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.load_fields_settings_template }">
		<MvASSIGN NAME = "l.fields:mode"								VALUE = "{ l.settings:settings_template:mode }">
		<MvASSIGN NAME = "l.fields:load"								VALUE = "{ l.settings:settings_template:load }">
		<MvASSIGN NAME = "l.fields:in_value"							VALUE = "{ l.settings:settings_template:in_value }">
		<MvASSIGN NAME = "l.fields:encoding"							VALUE = "{ l.settings:settings_template:encoding }">
		<MvASSIGN NAME = "l.fields:delimiter"							VALUE = "{ l.settings:settings_template:delimiter }">
		<MvASSIGN NAME = "l.fields:advanced"							VALUE = "{ l.settings:settings_template:advanced }">

		<MvASSIGN NAME = "l.fields:products:predict_discounts"			VALUE = "{ l.settings:settings_template:products:predict_discounts }">
		<MvASSIGN NAME = "l.fields:products:load_variants"				VALUE = "{ l.settings:settings_template:products:load_variants }">

		<MvASSIGN NAME = "l.fields:products:fields"						VALUE = "{ l.settings:settings_template:products:fields }">
		<MvASSIGN NAME = "l.fields:products:variant_fields"				VALUE = "{ l.settings:settings_template:products:variant_fields }">
		<MvASSIGN NAME = "l.fields:categories:fields"					VALUE = "{ l.settings:settings_template:categories:fields }">
		<MvASSIGN NAME = "l.fields:customers:fields"					VALUE = "{ l.settings:settings_template:customers:fields }">

		<MvASSIGN NAME = "l.fields:products:multiselect_fields"			VALUE = "">
		<MvASSIGN NAME = "l.fields:products:multiselect_variant_fields"	VALUE = "">
		<MvASSIGN NAME = "l.fields:categories:multiselect_fields"		VALUE = "">
		<MvASSIGN NAME = "l.fields:customers:multiselect_fields"		VALUE = "">

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].InitializeCustomFieldSelect( l.settings:settings_template:products:customfields,	l.fields:products:customfields ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].InitializeCustomFieldSelect( l.settings:settings_template:categories:customfields, l.fields:categories:customfields ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].InitializeCustomFieldSelect( l.settings:settings_template:customers:customfields,	l.fields:customers:customfields ) }">

		<MvASSIGN NAME = "l.fields:settings_template:name"				VALUE = "{ l.settings:settings_template:name }">
		<MvASSIGN NAME = "l.fields:settings_template:code"				VALUE = "">
	</MvIF>

	<MvIF EXPR = "{ l.load_fields_header_template }">
		<MvASSIGN NAME = "l.fields:header_template:name"				VALUE = "{ l.settings:header_template:name }">
		<MvASSIGN NAME = "l.fields:header_template:code"				VALUE = "{ l.header_templateversion:source }">
	</MvIF>

	<MvIF EXPR = "{ l.load_fields_iterator_template }">
		<MvASSIGN NAME = "l.fields:iterator_template:name"				VALUE = "{ l.settings:iterator_template:name }">
		<MvASSIGN NAME = "l.fields:iterator_template:code"				VALUE = "{ l.iterator_templateversion:source }">
	</MvIF>

	<MvIF EXPR = "{ l.load_fields_footer_template }">
		<MvASSIGN NAME = "l.fields:footer_template:name"				VALUE = "{ l.settings:footer_template:name }">
		<MvASSIGN NAME = "l.fields:footer_template:code"				VALUE = "{ l.footer_templateversion:source }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'mode,load,in_value,encoding,delimiter,
																								 products,categories,customers,
																								 advanced,settings_template,header_template,iterator_template,footer_template' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item ) }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_CategoryLookup_Dialog_CSS() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_CategoryLookup_Dialog_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_CategoryLookup_Dialog_HTML() }">

		<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_AD ].Element_PriceGroupLookup_Dialog_CSS() }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_AD ].Element_PriceGroupLookup_Dialog_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_AD ].Element_PriceGroupLookup_Dialog_HTML() }">

		<MvEVAL EXPR = "{ [ g.Module_Feature_AGR_AD ].Element_AvailabilityGroupLookup_Dialog_CSS() }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_AGR_AD ].Element_AvailabilityGroupLookup_Dialog_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_AGR_AD ].Element_AvailabilityGroupLookup_Dialog_HTML() }">

		<script type="text/javascript">
			MMScreen_LoadFinished( function() { TemplateFeed_Loaded(); } );

			function TemplateFeed_Loaded()
			{
				var lookup_span, load_type, drawmmbutton;

				drawmmbutton				= document.getElementById( '<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'lookup_button' }">' );

				lookup_span					= document.getElementById( '<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'lookup' }">' );
				lookup_span.style.display	= 'none';

				load_type					= document.getElementById( '<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'load' }">' );
				load_type.onchange			= function()
				{
					switch ( this.value )
					{
						case 'in_category'					:
						case 'not_in_category'				:
						case 'child_categories_from'		:
						{
							lookup_span.style.display	= '';
							drawmmbutton.onclick		= function() { CategoryLookupDialog( '<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'in_value' }">' ); };

							break;
						}
						case 'in_price_group'				:
						case 'not_in_price_group'			:
						{
							lookup_span.style.display	= '';
							drawmmbutton.onclick		= function() { PriceGroupLookupDialog( '<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'in_value' }">' ); };

							break;
						}
						case 'in_availability_group'		:
						case 'not_in_availability_group'	:
						{
							lookup_span.style.display	= '';
							drawmmbutton.onclick		= function() { AvailabilityGroupLookupDialog( '<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'in_value' }">' ); };

							break;
						}
						default								:
						{
							lookup_span.style.display	= 'none';
							drawmmbutton.onclick		= null;

							break;
						}
					}
				};

				load_type.value = '<MvEVAL EXPR = "{ l.fields:load }">';
				load_type.onchange();
			}
		</script>

		<table width="100%" cellpadding="2" cellspacing="0">
			<tr>
				<td nowrap><b>Mode:</b></td>
				<td width="100%">
					<select name="{ encodeentities( l.field_prefix ) $ 'mode' }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'product',							l.fields:mode, 'Product' ) }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'category',						l.fields:mode, 'Category' ) }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'customer',						l.fields:mode, 'Customer' ) }">
					</select>
				</td>
			</tr>
			<tr>
				<td nowrap><b>Load:</b></td>
				<td width="100%">
					<select name="{ encodeentities( l.field_prefix ) $ 'load' }" id="{ encodeentities( l.field_prefix ) $ 'load' }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'all',								l.fields:load, 'All' ) }">

						<MvIF EXPR = "{ l.fields:mode EQ 'product' }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'active', 						l.fields:load, 'Active' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'available', 					l.fields:load, 'Available' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'in_category',					l.fields:load, 'In Category' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'in_price_group',				l.fields:load, 'In Price Group' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'in_availability_group',		l.fields:load, 'In Availability Group' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'not_in_category',				l.fields:load, 'Not In Category' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'not_in_price_group',			l.fields:load, 'Not In Price Group' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'not_in_availability_group',	l.fields:load, 'Not In Availability Group' ) }">
						<MvELSEIF EXPR = "{ l.fields:mode EQ 'category' }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'active', 						l.fields:load, 'Active' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'available', 					l.fields:load, 'Available' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'child_categories_from',		l.fields:load, 'Child Categories From' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'in_availability_group',		l.fields:load, 'In Availability Group' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'not_in_availability_group',	l.fields:load, 'Not In Availability Group' ) }">
						<MvELSEIF EXPR = "{ l.fields:mode EQ 'customer' }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'in_price_group',				l.fields:load, 'In Price Group' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'in_availability_group',		l.fields:load, 'In Availability Group' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'not_in_price_group',			l.fields:load, 'Not In Price Group' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'not_in_availability_group',	l.fields:load, 'Not In Availability Group' ) }">
						</MvIF>
					</select>

					<span id="{ encodeentities( l.field_prefix ) $ 'lookup' }">
						<input type="text" name="{ encodeentities( l.field_prefix ) $ 'in_value' }" id="{ encodeentities( l.field_prefix ) $ 'in_value' }" value="{ encodeentities( l.fields:in_value ) }" size="30" />
						<mm-button id="{ encodeentities( l.field_prefix ) $ 'lookup_button' }" text="Lookup" class="mm_vertical_align_top" button-class="mm10_button_style_alternative_1 small"></mm-button>
					</span>
				</td>
			</tr>
			<tr>
				<td nowrap><b>Encoding:</b></td>
				<td width="100%">
					<select name="{ encodeentities( l.field_prefix ) $ 'encoding' }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'none',	l.fields:encoding, 'None' ) }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'csv',		l.fields:encoding, 'CSV' ) }">
					</select>
				</td>
			</tr>

			<MvIF EXPR = "{ NOT l.fields:advanced }">
				<tr>
					<td nowrap><b>Delimiter:</b></td>
					<td width="100%">
						<select name="{ encodeentities( l.field_prefix ) $ 'delimiter' }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'comma',	l.fields:delimiter, 'Comma' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'tab',		l.fields:delimiter, 'Tab' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'pipe',	l.fields:delimiter, 'Pipe' ) }">
						</select>
					</td>
				</tr>
			</MvIF>

			<MvIF EXPR = "{ l.fields:mode EQ 'product' }">
				<tr>
					<td nowrap>&nbsp;</td>
					<td width="100%">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:products:predict_discounts, encodeentities( l.field_prefix ) $ 'products:predict_discounts', '1', 'Predict Product Discounts' ) }">
					</td>
				</tr>
				<tr>
					<td nowrap>&nbsp;</td>
					<td width="100%">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:products:load_variants, encodeentities( l.field_prefix ) $ 'products:load_variants', '1', 'Load Variants' ) }">
					</td>
				</tr>

				<MvIF EXPR = "{ NOT l.fields:advanced }">
					<tr>
						<td valign="top" nowrap><b>Product Fields:</b></td>
						<td width="100%">
							<MvIF EXPR = "{ NOT ISNULL l.fields:products:multiselect_fields }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_To_Fields( 'product', l.fields:products:multiselect_fields, l.fields:products:fields ) }">
							</MvIF>

							<select id="{ encodeentities( l.field_prefix ) $ 'products:multiselect_fields' }" name="{ encodeentities( l.field_prefix ) $ 'products:multiselect_fields' }" multiple>
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:code,					'code',						'Code' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:name,					'name', 					'Name' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:sku,					'sku',						'SKU' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:price,					'price',					'Price' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:cost,					'cost',						'Cost' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:weight,					'weight',					'Weight' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:description,			'description',				'Description' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:fullsize_image,			'fullsize_image',			'Full-sized Image' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:thumbnail_image,		'thumbnail_image',			'Thumbnail Image' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:active,					'active',					'Active' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:dt_created,				'dt_created',				'Created Timestamp' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:dt_updated,				'dt_updated',				'Last Updated Timestamp' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:taxable,				'taxable',					'Taxable' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:url,					'url',						'URL' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:page_title,				'page_title',				'Page Title' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:alternate_display_page,	'alternate_display_page',	'Alternate Display Page' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:canonical_category,		'canonical_category',		'Canonical Category' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:canonical_uri,			'canonical_uri', 			'Canonical URI' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:inventory,				'inventory',				'Inventory' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:shipping_width,			'shipping_width',			'Shipping Width' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:shipping_length,		'shipping_length',			'Shipping Length' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:fields:shipping_height,		'shipping_height',			'Shipping Height' ) }">
							</select>

							<script type="text/javascript">
								new MultipleSelect( '<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'products:multiselect_fields' }">' );
							</script>
						</td>
					</tr>
					<MvIF EXPR = "{ l.fields:products:load_variants }">
						<tr>
							<td valign="top" nowrap><b>Variant Fields:</b></td>
							<td width="100%">
								<MvIF EXPR = "{ NOT ISNULL l.fields:products:multiselect_variant_fields }">
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_To_Fields( 'variant', l.fields:products:multiselect_variant_fields, l.fields:products:variant_fields ) }">
								</MvIF>

								<select id="{ encodeentities( l.field_prefix ) $ 'products:multiselect_variant_fields' }" name="{ encodeentities( l.field_prefix ) $ 'products:multiselect_variant_fields' }" multiple>
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:variant_fields:code,					'code',						'Code' ) }">
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:variant_fields:name,					'name', 					'Name' ) }">
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:variant_fields:sku,					'sku',						'SKU' ) }">
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:variant_fields:price,					'price',					'Price' ) }">
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:variant_fields:cost,					'cost',						'Cost' ) }">
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:variant_fields:weight,					'weight',					'Weight' ) }">
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:variant_fields:description,			'description',				'Description' ) }">
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:variant_fields:fullsize_image,			'fullsize_image',			'Full-sized Image' ) }">
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:variant_fields:thumbnail_image,		'thumbnail_image',			'Thumbnail Image' ) }">
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:variant_fields:active,					'active',					'Active' ) }">
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:variant_fields:dt_created,				'dt_created',				'Created Timestamp' ) }">
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:variant_fields:dt_updated,				'dt_updated',				'Last Updated Timestamp' ) }">
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:variant_fields:taxable,				'taxable',					'Taxable' ) }">
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:variant_fields:inventory,				'inventory',				'Inventory' ) }">
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:variant_fields:shipping_width,			'shipping_width',			'Shipping Width' ) }">
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:variant_fields:shipping_length,		'shipping_length',			'Shipping Length' ) }">
									<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:products:variant_fields:shipping_height,		'shipping_height',			'Shipping Height' ) }">
								</select>

								<script type="text/javascript">
									new MultipleSelect( '<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'products:multiselect_variant_fields' }">' );
								</script>
							</td>
						</tr>
					</MvIF>
				</MvIF>
				<tr>
					<td valign="top" nowrap>Custom Fields:</td>
					<td width="100%">
						<MvASSIGN NAME = "l.customfield_count"	VALUE = "{ [ g.Module_Library_Utilities ].ProductCustomFieldList_Load( l.customfields ) }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCustomFieldSelect( encodeentities( l.field_prefix ) $ 'products:customfields', l.fields:products:customfields, l.customfields, l.customfield_count ) }">
					</td>
				</tr>
			</MvIF>

			<MvIF EXPR = "{ l.fields:mode EQ 'category' }">
				<MvIF EXPR = "{ NOT l.fields:advanced }">
					<tr>
						<td valign="top" nowrap><b>Category Fields:</b></td>
						<td width="100%">
							<MvIF EXPR = "{ NOT ISNULL l.fields:categories:multiselect_fields }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_To_Fields( 'category', l.fields:categories:multiselect_fields, l.fields:categories:fields ) }">
							</MvIF>

							<select id="{ encodeentities( l.field_prefix ) $ 'categories:multiselect_fields' }" name="{ encodeentities( l.field_prefix ) $ 'categories:multiselect_fields' }" multiple>
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:categories:fields:code,						'code',						'Code' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:categories:fields:name,						'name', 					'Name' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:categories:fields:page_title,				'page_title',				'Page Title' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:categories:fields:active,					'active',					'Active' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:categories:fields:dt_created,				'dt_created',				'Created Timestamp' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:categories:fields:dt_updated,				'dt_updated',				'Last Updated Timestamp' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:categories:fields:url,						'url',						'URL' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:categories:fields:parent_category,			'parent_category',			'Parent Category' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:categories:fields:alternate_display_page,	'alternate_display_page',	'Alternate Display Page' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:categories:fields:canonical_uri,			'canonical_uri', 			'Canonical URI' ) }">
							</select>

							<script type="text/javascript">
								new MultipleSelect( '<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'categories:multiselect_fields' }">' );
							</script>
						</td>
					</tr>
				</MvIF>
				<tr>
					<td valign="top" nowrap>Custom Fields:</td>
					<td width="100%">
						<MvASSIGN NAME = "l.customfield_count"	VALUE = "{ [ g.Module_Library_Utilities ].CategoryCustomFieldList_Load( l.customfields ) }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCustomFieldSelect( encodeentities( l.field_prefix ) $ 'categories:customfields', l.fields:categories:customfields, l.customfields, l.customfield_count ) }">
					</td>
				</tr>
			</MvIF>

			<MvIF EXPR = "{ l.fields:mode EQ 'customer' }">
				<MvIF EXPR = "{ NOT l.fields:advanced }">
					<tr>
						<td valign="top" nowrap><b>Customer Fields:</b></td>
						<td width="100%">
							<MvIF EXPR = "{ NOT ISNULL l.fields:customers:multiselect_fields }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_To_Fields( 'customer', l.fields:customers:multiselect_fields, l.fields:customers:fields ) }">
							</MvIF>

							<select id="{ encodeentities( l.field_prefix ) $ 'customers:multiselect_fields' }" name="{ encodeentities( l.field_prefix ) $ 'customers:multiselect_fields' }" multiple>
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:login,				'login',			'Login' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:password,			'password', 		'Password' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:pw_email,			'pw_email',			'Password Recovery Email' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:business_account,	'business_account',	'Business Account' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:dt_created,		'dt_created',		'Created Timestamp' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:dt_updated,		'dt_updated',		'Last Updated Timestamp' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:dt_login,			'dt_login',			'Last Login Timestamp' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:dt_pwchg,			'dt_pwchg',			'Password Last Changed Timestamp' ) }">

								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:ship_fname,		'ship_fname',		'Ship. First Name' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:ship_lname,		'ship_lname',		'Ship. Last Name' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:ship_email,		'ship_email',		'Ship. Email' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:ship_phone,		'ship_phone',		'Ship. Phone' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:ship_fax,			'ship_fax',			'Ship. Fax' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:ship_comp,			'ship_comp',		'Ship. Company' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:ship_addr,			'ship_addr',		'Ship. Address' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:ship_addr2,		'ship_addr2',		'Ship. Address2' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:ship_city,			'ship_city',		'Ship. City' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:ship_state,		'ship_state',		'Ship. State' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:ship_zip,			'ship_zip',			'Ship. Zip' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:ship_cntry,		'ship_cntry',		'Ship. Country' ) }">

								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:bill_fname,		'bill_fname',		'Bill. First Name' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:bill_lname,		'bill_lname',		'Bill. Last Name' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:bill_email,		'bill_email',		'Bill. Email' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:bill_phone,		'bill_phone',		'Bill. Phone' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:bill_fax,			'bill_fax',			'Bill. Fax' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:bill_comp,			'bill_comp',		'Bill. Company' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:bill_addr,			'bill_addr',		'Bill. Address' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:bill_addr2,		'bill_addr2',		'Bill. Address2' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:bill_city,			'bill_city',		'Bill. City' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:bill_state,		'bill_state',		'Bill. State' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:bill_zip,			'bill_zip',			'Bill. Zip' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:bill_cntry,		'bill_cntry',		'Bill. Country' ) }">

								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:note_count,		'note_count',		'Note Count' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:tax_exempt,		'tax_exempt',		'Tax Exempt' ) }">

								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:order_cnt,			'order_cnt',		'Order Count' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:order_avg,			'order_avg',		'Order Average' ) }">
								<MvEVAL EXPR = "{ TemplateFeed_MultiSelect_DrawOption( l.fields:customers:fields:order_tot,			'order_tot',		'Order Total' ) }">
							</select>

							<script type="text/javascript">
								new MultipleSelect( '<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'customers:multiselect_fields' }">' );
							</script>
						</td>
					</tr>
				</MvIF>
				<tr>
					<td valign="top" nowrap>Custom Fields:</td>
					<td width="100%">
						<MvASSIGN NAME = "l.customfield_count"	VALUE = "{ [ g.Module_Library_Utilities ].CustomerCustomFieldList_Load( l.customfields ) }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCustomFieldSelect( encodeentities( l.field_prefix ) $ 'customers:customfields', l.fields:customers:customfields, l.customfields, l.customfield_count ) }">
					</td>
				</tr>
			</MvIF>
			<tr>
				<td nowrap>&nbsp;</td>
				<td width="100%">
					<input type="hidden" name="{ encodeentities( l.field_prefix ) $ 'advanced' }" value="{ encodeentities( l.fields:advanced ) }">
					<MvIF EXPR = "{ l.fields:advanced }">	<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 0 ); return false;' }">Point + Click Mode</a>
					<MvELSE>								<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 1 ); return false;' }">Advanced Mode</a>
					</MvIF>
				</td>
			</tr>
			<tr>
				<td colspan="2">&nbsp;</td>
			</tr>
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'settings_template:name' ) }">

			<MvIF EXPR = "{ NOT l.fields:advanced }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'header_template,iterator_template,footer_template' ) }">
			<MvELSE>
				<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'delimiter,
																										 products:fields,products:multiselect_fields,products:variant_fields,products:multiselect_variant_fields,
																										 categories:fields,categories:multiselect_fields,
																										 customers:fields,customers:multiselect_fields' ) }">

				<tr>
					<td colspan="2">&nbsp;</td>
				</tr>
				<tr>
					<td valign="top" nowrap><b>Header Template:</b></td>
					<td width="100%">
						<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'header_template:name' ) }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( encodeentities( l.field_prefix ) $ 'header_template:code', l.fields:header_template:code, 20, 100, l.header_templateversion:templ_id ) }">
					</td>
				</tr>
				<tr>
					<td colspan="2">&nbsp;</td>
				</tr>
				<tr>
					<td valign="top" nowrap><b>Iterator Template:</b></td>
					<td width="100%">
						<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'iterator_template:name' ) }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( encodeentities( l.field_prefix ) $ 'iterator_template:code', l.fields:iterator_template:code, 20, 100, l.iterator_templateversion:templ_id ) }">
					</td>
				</tr>
				<tr>
					<td colspan="2">&nbsp;</td>
				</tr>
				<tr>
					<td valign="top" nowrap><b>Footer Template:</b></td>
					<td width="100%">
						<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'footer_template:name' ) }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( encodeentities( l.field_prefix ) $ 'footer_template:code', l.fields:footer_template:code, 20, 100, l.footer_templateversion:templ_id ) }">
					</td>
				</tr>
			</MvIF>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Component Provision Feature (component_prov)
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModuleProvision_Product" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml, 'PredictDiscounts', l.settings:products:predict_discounts ) OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml, 'LoadVariants',		l.settings:products:load_variants )	}">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Load' ) }">
		<MvASSIGN NAME = "l.load_tag" VALUE = "{ l.provide_xml:tags:Load[ 1 ] }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_List( l.load_tag, 'type', l.settings:load, 'all,active,available,in_category,in_price_group,in_availability_group,not_in_category,not_in_price_group,not_in_availability_group',
																												 'all,active,available,in_category,in_price_group,in_availability_group,not_in_category,not_in_price_group,not_in_availability_group' ) }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvASSIGN NAME = "l.settings:in_value" VALUE = "{ trim( l.load_tag:value ) }">

		<MvIF EXPR = "{ l.settings:load EQ 'in_category' OR l.settings:load EQ 'not_in_category' }">
			<MvIF EXPR = "{ NOT TemplateFeed_Validate_Categories( l.settings:in_value ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Load: ' $ g.Error_Message ) }">
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.settings:load EQ 'in_price_group' OR l.settings:load EQ 'not_in_price_group' }">
			<MvIF EXPR = "{ NOT TemplateFeed_Validate_PriceGroups( l.settings:in_value ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Load: ' $ g.Error_Message ) }">
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.settings:load EQ 'in_availability_group' OR l.settings:load EQ 'not_in_availability_group' }">
			<MvIF EXPR = "{ NOT TemplateFeed_Validate_AvailabilityGroups( l.settings:in_value ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Load: ' $ g.Error_Message ) }">
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Fields' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:code"									VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'Code' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:name"									VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'Name' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:sku"									VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'SKU' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:price"									VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'Price' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:cost"									VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'Cost' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:weight"								VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'Weight' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:description"							VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'Description' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:fullsize_image"						VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'FullSizeImage' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:thumbnail_image"						VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'ThumbnailImage' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:active"								VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'Active' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:dt_created"							VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'CreatedTimestamp' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:dt_updated"							VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'LastUpdatedTimestamp' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:taxable"								VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'Taxable' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:url"									VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'URL' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:page_title"							VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'PageTitle' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:alternate_display_page"				VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'AlternateDisplayPage' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:canonical_category"					VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'CanonicalCategory' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:canonical_uri"							VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'CanonicalURI' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:inventory"								VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'Inventory' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:shipping_width"						VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'ShippingWidth' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:shipping_length"						VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'ShippingLength' ) }">
		<MvASSIGN NAME = "l.settings:products:fields:shipping_height"						VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'ShippingHeight' ) }">

		<MvIF EXPR = "{ l.settings:products:load_variants AND [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'VariantFields' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:code"						VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'Code' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:name"						VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'Name' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:sku"						VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'SKU' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:price"						VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'Price' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:cost"						VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'Cost' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:weight"					VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'Weight' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:description"				VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'Description' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:fullsize_image"			VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'FullSizeImage' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:thumbnail_image"			VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'ThumbnailImage' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:active"					VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'Active' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:dt_created"				VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'CreatedTimestamp' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:dt_updated"				VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'LastUpdatedTimestamp' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:taxable"					VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'Taxable' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:page_title"				VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'PageTitle' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:alternate_display_page"	VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'AlternateDisplayPage' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:canonical_category"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'CanonicalCategory' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:canonical_uri"				VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'CanonicalURI' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:inventory"					VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'Inventory' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:shipping_width"			VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'ShippingWidth' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:shipping_length"			VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'ShippingLength' ) }">
			<MvASSIGN NAME = "l.settings:products:variant_fields:shipping_height"			VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ]:tags:VariantFields[ 1 ], 'ShippingHeight' ) }">
		</MvIF>

		<MvEVAL EXPR = "{ ComponentModuleProvision_CustomFields( l.provide_xml, l.settings:products:customfields ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModuleProvision_Category" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Load' ) }">
		<MvASSIGN NAME = "l.load_tag" VALUE = "{ l.provide_xml:tags:Load[ 1 ] }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_List( l.load_tag, 'type', l.settings:load, 'all,active,available,child_categories_from,in_availability_group,not_in_availability_group',
																												 'all,active,available,child_categories_from,in_availability_group,not_in_availability_group' ) }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvASSIGN NAME = "l.settings:in_value" VALUE = "{ trim( l.load_tag:value ) }">

		<MvIF EXPR = "{ l.settings:load EQ 'child_categories_from' }">
			<MvIF EXPR = "{ NOT TemplateFeed_Validate_Categories( l.settings:in_value ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Load: ' $ g.Error_Message ) }">
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.settings:load EQ 'in_availability_group' OR l.settings:load EQ 'not_in_availability_group' }">
			<MvIF EXPR = "{ NOT TemplateFeed_Validate_AvailabilityGroups( l.settings:in_value ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Load: ' $ g.Error_Message ) }">
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Fields' ) }">
		<MvASSIGN NAME = "l.settings:categories:fields:code"					VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'Code' ) }">
		<MvASSIGN NAME = "l.settings:categories:fields:name"					VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'Name' ) }">
		<MvASSIGN NAME = "l.settings:categories:fields:page_title"				VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'PageTitle' ) }">
		<MvASSIGN NAME = "l.settings:categories:fields:active"					VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'Active' ) }">
		<MvASSIGN NAME = "l.settings:categories:fields:dt_created"				VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'CreatedTimestamp' ) }">
		<MvASSIGN NAME = "l.settings:categories:fields:dt_updated"				VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'LastUpdatedTimestamp' ) }">
		<MvASSIGN NAME = "l.settings:categories:fields:url"						VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'URL' ) }">
		<MvASSIGN NAME = "l.settings:categories:fields:parent_category"			VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'ParentCategory' ) }">
		<MvASSIGN NAME = "l.settings:categories:fields:alternate_display_page"	VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'AlternateDisplayPage' ) }">
		<MvASSIGN NAME = "l.settings:categories:fields:canonical_uri"			VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'CanonicalURI' ) }">

		<MvEVAL EXPR = "{ ComponentModuleProvision_CustomFields( l.provide_xml, l.settings:categories:customfields ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModuleProvision_Customer" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Load' ) }">
		<MvASSIGN NAME = "l.load_tag" VALUE = "{ l.provide_xml:tags:Load[ 1 ] }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_List( l.load_tag, 'type', l.settings:load, 'all,in_price_group,in_availability_group,not_in_price_group,not_in_availability_group',
																												 'all,in_price_group,in_availability_group,not_in_price_group,not_in_availability_group' ) }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvASSIGN NAME = "l.settings:in_value" VALUE = "{ trim( l.load_tag:value ) }">

		<MvIF EXPR = "{ l.settings:load EQ 'in_price_group' OR l.settings:load EQ 'not_in_price_group' }">
			<MvIF EXPR = "{ NOT TemplateFeed_Validate_PriceGroups( l.settings:in_value ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Load: ' $ g.Error_Message ) }">
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.settings:load EQ 'in_availability_group' OR l.settings:load EQ 'not_in_availability_group' }">
			<MvIF EXPR = "{ NOT TemplateFeed_Validate_AvailabilityGroups( l.settings:in_value ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Load: ' $ g.Error_Message ) }">
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Fields' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:login"			VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'Login' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:password"			VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'Password' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:pw_email"			VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'PasswordRecoveryEmail' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:business_account"	VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'BusinessAccount' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:dt_created"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'CreatedTimestamp' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:dt_updated"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'LastUpdatedTimestamp' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:dt_login"			VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'LastLoginTimestamp' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:dt_pwchg"			VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'PasswordLastChangedTimestamp' ) }">

		<MvASSIGN NAME = "l.settings:customers:fields:ship_fname"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'ShipFirstName' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:ship_lname"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'ShipLastName' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:ship_email"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'ShipEmail' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:ship_phone"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'ShipPhone' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:ship_fax"			VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'ShipFax' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:ship_comp"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'ShipCompany' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:ship_addr"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'ShipAddress' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:ship_addr2"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'ShipAddress2' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:ship_city"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'ShipCity' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:ship_state"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'ShipState' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:ship_zip"			VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'ShipZip' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:ship_cntry"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'ShipCountry' ) }">

		<MvASSIGN NAME = "l.settings:customers:fields:bill_fname"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'BillFirstName' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:bill_lname"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'BillLastName' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:bill_email"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'BillEmail' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:bill_phone"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'BillPhone' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:bill_fax"			VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'BillFax' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:bill_comp"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'BillCompany' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:bill_addr"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'BillAddress' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:bill_addr2"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'BillAddress2' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:bill_city"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'BillCity' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:bill_state"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'BillState' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:bill_zip"			VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'BillZip' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:bill_cntry"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'BillCountry' ) }">

		<MvASSIGN NAME = "l.settings:customers:fields:note_count"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'NoteCount' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:tax_exempt"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'TaxExempt' ) }">

		<MvASSIGN NAME = "l.settings:customers:fields:order_cnt"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'OrderCount' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:order_avg"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'OrderAverage' ) }">
		<MvASSIGN NAME = "l.settings:customers:fields:order_tot"		VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Fields[ 1 ], 'OrderTotal' ) }">

		<MvEVAL EXPR = "{ ComponentModuleProvision_CustomFields( l.provide_xml, l.settings:customers:customfields ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModuleProvision_CustomFields" PARAMETERS = "provide_xml var, customfields var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.customfields" VALUE = "">

	<MvFOREACH ITERATOR = "l.customfield_tag" ARRAY = "l.provide_xml:tags:Fields[ 1 ]:tags:Custom">
		<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.customfield_tag, 'module',	l.module_code ) OR
						[ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.customfield_tag, 'code',		l.code ) }">
			<MvASSIGN NAME = "l.customfield:module" VALUE = "{ l.module_code }">
			<MvASSIGN NAME = "l.customfield:code"	VALUE = "{ l.code }">
			<MvASSIGN NAME = "l.null"				VALUE = "{ miva_array_insert_var( l.customfields, l.customfield, -1 ) }">
		</MvIF>
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT TemplateFeed_Load_Templates( l.settings:settings_template:name,	l.settings_managedtemplate,
													 l.settings:header_template:name,	l.header_managedtemplate,
													 l.settings:iterator_template:name,	l.iterator_managedtemplate,
													 l.settings:footer_template:name,	l.footer_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings_tag_exists" VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Settings' ) }">
	<MvASSIGN NAME = "l.template_tag_exists" VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Templates' ) }">

	<MvIF EXPR = "{ l.settings_tag_exists OR l.template_tag_exists }">
		<MvIF EXPR = "{ l.settings_tag_exists }">
			<MvIF EXPR = "{ NOT ComponentModule_Provision_Settings( l.module, l.provide_xml:tags:Settings[ 1 ], l.settings, l.settings_managedtemplate ) }">
				<MvFUNCTIONRETURN>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.template_tag_exists }">
			<MvIF EXPR = "{ NOT ComponentModule_Provision_Templates( l.module, l.provide_xml:tags:Templates[ 1 ], l.settings, l.settings_managedtemplate, l.header_managedtemplate, l.iterator_managedtemplate, l.footer_managedtemplate ) }">
				<MvFUNCTIONRETURN>
			</MvIF>
		</MvIF>
	<MvELSE>
		<MvCOMMENT>
		|
		| Backwards compatibility code that handles the settings / templates as top-level tags
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ NOT ComponentModule_Provision_Settings( l.module, l.provide_xml, l.settings, l.settings_managedtemplate ) }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvIF EXPR = "{ NOT ComponentModule_Provision_Templates( l.module, l.provide_xml, l.settings, l.settings_managedtemplate, l.header_managedtemplate, l.iterator_managedtemplate, l.footer_managedtemplate ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision_Settings" PARAMETERS = "module var, provide_xml var, settings var, settings_managedtemplate var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List( 'o', l.provide_xml, 'Mode', l.mode, 'product,category,customer', 'product,category,customer' ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.mode AND l.mode NE l.settings:settings_template:mode }">
		<MvCOMMENT>
		|
		| Switching between modes resets all settings...
		|
		</MvCOMMENT>

		<MvASSIGN NAME = "l.template_filename"					VALUE = "{ l.settings:settings_template:name }">

		<MvIF EXPR = "{ l.mode EQ 'product' }">			<MvEVAL EXPR = "{ TemplateFeed_Product_DefaultSettings( l.settings:settings_template ) }">
		<MvELSEIF EXPR = "{ l.mode EQ 'category' }">	<MvEVAL EXPR = "{ TemplateFeed_Category_DefaultSettings( l.settings:settings_template ) }">
		<MvELSEIF EXPR = "{ l.mode EQ 'customer' }">	<MvEVAL EXPR = "{ TemplateFeed_Customer_DefaultSettings( l.settings:settings_template ) }">
		</MvIF>

		<MvASSIGN NAME = "l.settings:settings_template:name"	VALUE = "{ l.template_filename }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List( 'o', l.provide_xml, 'Encoding',	l.settings:settings_template:encoding, 	'none,csv',			'none,csv' ) OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List( 'o', l.provide_xml, 'Delimiter',	l.settings:settings_template:delimiter,	'comma,tab,pipe',	'comma,tab,pipe' ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ l.settings:settings_template:mode EQ 'product' }">
		<MvIF EXPR = "{ NOT ComponentModuleProvision_Product( l.module, l.provide_xml, l.settings:settings_template ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	<MvELSEIF EXPR = "{ l.settings:settings_template:mode EQ 'category' }">
		<MvIF EXPR = "{ NOT ComponentModuleProvision_Category( l.module, l.provide_xml, l.settings:settings_template ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	<MvELSEIF EXPR = "{ l.settings:settings_template:mode EQ 'customer' }">
		<MvIF EXPR = "{ NOT ComponentModuleProvision_Customer( l.module, l.provide_xml, l.settings:settings_template ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.settings_managedtemplate, '', l.null, l.settings:settings_template ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision_Templates" PARAMETERS = "module var, provide_xml var, settings var, settings_managedtemplate var, header_managedtemplate var, iterator_managedtemplate var, footer_managedtemplate var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.header_template_exists"					VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'HeaderTemplate' ) }">
	<MvASSIGN NAME = "l.iterator_template_exists"				VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'IteratorTemplate' ) }">
	<MvASSIGN NAME = "l.footer_template_exists"					VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'FooterTemplate' ) }">

	<MvASSIGN NAME = "l.update_header_template"					VALUE = "{ l.header_template_exists }">
	<MvASSIGN NAME = "l.update_iterator_template"				VALUE = "{ l.iterator_template_exists }">
	<MvASSIGN NAME = "l.update_foooter_template"				VALUE = "{ l.footer_template_exists }">

	<MvASSIGN NAME = "l.original_advanced"						VALUE = "{ l.settings:settings_template:advanced }">
	<MvASSIGN NAME = "l.settings:settings_template:advanced"	VALUE = "{ ( l.header_template_exists + l.iterator_template_exists + l.footer_template_exists ) GT 0 }">

	<MvIF EXPR = "{ NOT l.settings:settings_template:advanced }">
		<MvASSIGN NAME = "l.update_header_template"				VALUE = 1>
		<MvASSIGN NAME = "l.update_iterator_template"			VALUE = 1>
		<MvASSIGN NAME = "l.update_foooter_template"			VALUE = 1>

		<MvIF EXPR = "{ l.settings:settings_template:mode EQ 'product' }">		<MvEVAL EXPR = "{ TemplateFeed_Product_GenerateCode_Master( l.settings:settings_template, l.header_template, l.iterator_template, l.footer_template ) }">
		<MvELSEIF EXPR = "{ l.settings:settings_template:mode EQ 'category' }">	<MvEVAL EXPR = "{ TemplateFeed_Category_GenerateCode(		l.settings:settings_template, l.header_template, l.iterator_template, l.footer_template ) }">
		<MvELSEIF EXPR = "{ l.settings:settings_template:mode EQ 'customer' }">	<MvEVAL EXPR = "{ TemplateFeed_Customer_GenerateCode(		l.settings:settings_template, l.header_template, l.iterator_template, l.footer_template ) }">
		</MvIF>
	<MvELSE>
		<MvIF EXPR = "{ l.header_template_exists }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'o', l.provide_xml:tags:HeaderTemplate[ 1 ],		'Template',	l.header_template )		OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml:tags:HeaderTemplate[ 1 ],		'Notes',	l.header_notes ) }">
				<MvFUNCTIONRETURN>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.iterator_template_exists }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'o', l.provide_xml:tags:IteratorTemplate[ 1 ],	'Template',	l.iterator_template )	OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml:tags:IteratorTemplate[ 1 ],	'Notes',	l.iterator_notes ) }">
				<MvFUNCTIONRETURN>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.footer_template_exists }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'o', l.provide_xml:tags:FooterTemplate[ 1 ],		'Template',	l.footer_template )		OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml:tags:FooterTemplate[ 1 ],		'Notes',	l.footer_notes ) }">
				<MvFUNCTIONRETURN>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.original_advanced NE l.settings:settings_template:advanced }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.settings_managedtemplate, '', l.null, l.settings:settings_template ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.update_header_template }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.header_managedtemplate, l.header_notes, l.header_template, l.settings:header_template ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.update_iterator_template }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.iterator_managedtemplate, l.iterator_notes, l.iterator_template, l.settings:iterator_template ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.update_foooter_template }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.footer_managedtemplate, l.footer_notes, l.footer_template, l.settings:footer_template ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Framework Support Feature (skins)
|
</MvCOMMENT>

<MvFUNCTION NAME = "SkinsComponentModuleExportItem_CreateFeed" PARAMETERS = "module var, feed_id, output var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_FDS_DB ].Feed_Load_ID( l.feed_id, l.feed ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MIVA STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvCAPTURE VARIABLE = "l.output">
		<Feed_Add>
			<MvEVAL EXPR = "{ [ g.Module_Feature_FDS_UT ].Feed_Generate_Internal_Provision_Tag( l.feed ) }">

			<Settings>
				<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.feed:config:template ) }"></Template>
			</Settings>
		</Feed_Add>
	</MvCAPTURE>
	<MIVA STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModuleExportItem_Dependencies" PARAMETERS = "module var, page_settings var, settings var, dependency_output var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.page_settings:feed_id }">
		<MvEVAL EXPR = "{ SkinsComponentModuleExportItem_CreateFeed( l.module, l.page_settings:feed_id, l.feed_output ) }">

		<MvASSIGN NAME = "l.dependency_output" VALUE = "{ l.dependency_output $ l.feed_output }">
	</MvIF>

	<MvCOMMENT>
	|
	| Only generate categories and availability groups.  Price groups would cause too many issues.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.settings:load EQ 'in_category' OR l.settings:load EQ 'not_in_category' OR l.settings:load EQ 'child_categories_from' }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<MvCAPTURE VARIABLE = "l.categories_output">
			<MvFOREACH ITERATOR = "l.category_code" ARRAY = "l.category_codes" COUNT = "{ miva_splitstring( l.settings:in_value, ',', l.category_codes, 'trim' ) }">
				<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Category_Load_Code_Cached( l.category_code, l.category ) }">
					<Category_Add>
						<Code><MvEVAL EXPR = "{ miva_cdata_encode( l.category:code ) }"></Code>
						<Name><MvEVAL EXPR = "{ miva_cdata_encode( l.category:code ) }"></Name>
					</Category_Add>
				<MvELSE>
					<Category_Add>
						<Code><MvEVAL EXPR = "{ miva_cdata_encode( l.category:code ) }"></Code>
						<Name><MvEVAL EXPR = "{ miva_cdata_encode( l.category:name ) }"></Name>
						<Active><MvEVAL EXPR = "{ l.category:active EQ 1 }"></Active>
					</Category_Add>
				</MvIF>
			</MvFOREACH>
		</MvCAPTURE>
		<MIVA STANDARDOUTPUTLEVEL = "">

		<MvASSIGN NAME = "l.dependency_output" VALUE = "{ l.dependency_output $ l.categories_output }">
	</MvIF>

	<MvIF EXPR = "{ l.settings:load EQ 'in_availability_group' OR l.settings:load EQ 'not_in_availability_group' }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<MvCAPTURE VARIABLE = "l.availabilitygroups_output">
			<MvFOREACH ITERATOR = "l.availgroup_name" ARRAY = "l.availgroup_names" COUNT = "{ miva_splitstring( l.settings:in_value, ',', l.availgroup_names, 'trim' ) }">
				<AvailabilityGroup_Add name="{ l.settings:in_value }" />
			</MvFOREACH>
		</MvCAPTURE>
		<MIVA STANDARDOUTPUTLEVEL = "">

		<MvASSIGN NAME = "l.dependency_output" VALUE = "{ l.dependency_output $ l.availabilitygroups_output }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModuleExportItem_Product" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT l.settings:products:predict_discounts }">	<PredictDiscounts>false</PredictDiscounts>
	<MvELSE>														<PredictDiscounts>true</PredictDiscounts>
	</MvIF>

	<MvIF EXPR = "{ NOT l.settings:products:load_variants }">	<LoadVariants>false</LoadVariants>
	<MvELSE>													<LoadVariants>true</LoadVariants>
	</MvIF>

	<Fields>
		<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:products:customfields">
			<Custom module="{ l.customfield:module }" code="{ l.customfield:code }" />
		</MvFOREACH>

		<MvIF EXPR = "{ NOT l.settings:advanced }">
			<MvIF EXPR = "{ l.settings:products:fields:code }">										<Code />					</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:name }">										<Name />					</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:sku }">										<SKU />						</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:price }">									<Price />					</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:cost }">										<Cost />					</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:weight }">									<Weight />					</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:description }">								<Description />				</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:fullsize_image }">							<FullSizeImage /> 			</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:thumbnail_image }">							<ThumbnailImage /> 			</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:active }">									<Active />					</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:dt_created }">								<CreatedTimestamp />		</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:dt_updated }">								<LastUpdatedTimestamp />	</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:taxable }">									<Taxable />					</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:url }">										<URL />						</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:page_title }">								<PageTitle />				</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:alternate_display_page }">					<AlternateDisplayPage />	</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:canonical_category }">						<CanonicalCategory /> 		</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:canonical_uri }">							<CanonicalURI />			</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:inventory }">								<Inventory /> 				</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:shipping_width }">							<ShippingWidth />			</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:shipping_length }">							<ShippingLength />			</MvIF>
			<MvIF EXPR = "{ l.settings:products:fields:shipping_height }">							<ShippingHeight />			</MvIF>

			<MvIF EXPR = "{ l.settings:products:load_variants }">
				<VariantFields>
					<MvIF EXPR = "{ l.settings:products:variant_fields:code }">						<Code />					</MvIF>
					<MvIF EXPR = "{ l.settings:products:variant_fields:name }">						<Name />					</MvIF>
					<MvIF EXPR = "{ l.settings:products:variant_fields:sku }">						<SKU />						</MvIF>
					<MvIF EXPR = "{ l.settings:products:variant_fields:price }">					<Price />					</MvIF>
					<MvIF EXPR = "{ l.settings:products:variant_fields:cost }">						<Cost />					</MvIF>
					<MvIF EXPR = "{ l.settings:products:variant_fields:weight }">					<Weight />					</MvIF>
					<MvIF EXPR = "{ l.settings:products:variant_fields:description }">				<Description />				</MvIF>
					<MvIF EXPR = "{ l.settings:products:variant_fields:fullsize_image }">			<FullSizeImage /> 			</MvIF>
					<MvIF EXPR = "{ l.settings:products:variant_fields:thumbnail_image }">			<ThumbnailImage /> 			</MvIF>
					<MvIF EXPR = "{ l.settings:products:variant_fields:active }">					<Active />					</MvIF>
					<MvIF EXPR = "{ l.settings:products:variant_fields:dt_created }">				<CreatedTimestamp />		</MvIF>
					<MvIF EXPR = "{ l.settings:products:variant_fields:dt_updated }">				<LastUpdatedTimestamp />	</MvIF>
					<MvIF EXPR = "{ l.settings:products:variant_fields:taxable }">					<Taxable />					</MvIF>
					<MvIF EXPR = "{ l.settings:products:variant_fields:inventory }">				<Inventory /> 				</MvIF>
					<MvIF EXPR = "{ l.settings:products:variant_fields:shipping_width }">			<ShippingWidth />			</MvIF>
					<MvIF EXPR = "{ l.settings:products:variant_fields:shipping_length }">			<ShippingLength />			</MvIF>
					<MvIF EXPR = "{ l.settings:products:variant_fields:shipping_height }">			<ShippingHeight />			</MvIF>
				</VariantFields>
			</MvIF>
		</MvIF>
	</Fields>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModuleExportItem_Category" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<Fields>
		<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:categories:customfields">
			<Custom module="{ l.customfield:module }" code="{ l.customfield:code }" />
		</MvFOREACH>

		<MvIF EXPR = "{ NOT l.settings:advanced }">
			<MvIF EXPR = "{ l.settings:categories:fields:code }">						<Code />					</MvIF>
			<MvIF EXPR = "{ l.settings:categories:fields:name }">						<Name />					</MvIF>
			<MvIF EXPR = "{ l.settings:categories:fields:page_title }">					<PageTitle />				</MvIF>
			<MvIF EXPR = "{ l.settings:categories:fields:active }">						<Active />					</MvIF>
			<MvIF EXPR = "{ l.settings:categories:fields:dt_created }">					<CreatedTimestamp />		</MvIF>
			<MvIF EXPR = "{ l.settings:categories:fields:dt_updated }">					<LastUpdatedTimestamp />	</MvIF>
			<MvIF EXPR = "{ l.settings:categories:fields:url }">						<URL />						</MvIF>
			<MvIF EXPR = "{ l.settings:categories:fields:parent_category }">			<ParentCategory />			</MvIF>
			<MvIF EXPR = "{ l.settings:categories:fields:alternate_display_page }">		<AlternateDisplayPage />	</MvIF>
			<MvIF EXPR = "{ l.settings:categories:fields:canonical_uri }">				<CanonicalURI />			</MvIF>
		</MvIF>
	</Fields>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModuleExportItem_Customer" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<Fields>
		<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:customers:customfields">
			<Custom module="{ l.customfield:module }" code="{ l.customfield:code }" />
		</MvFOREACH>

		<MvIF EXPR = "{ NOT l.settings:advanced }">
			<MvIF EXPR = "{ l.settings:customers:fields:login }">				<Login />							</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:password }">			<Password />						</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:pw_email }">			<PasswordRecoveryEmail />			</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:business_account }">	<BusinessAccount />					</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:dt_created }">			<CreatedTimestamp />				</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:dt_updated }">			<LastUpdatedTimestamp />			</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:dt_login }">			<LastLoginTimestamp />				</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:dt_pwchg }">			<PasswordLastChangedTimestamp />	</MvIF>

			<MvIF EXPR = "{ l.settings:customers:fields:ship_fname }">			<ShipFirstName />					</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:ship_lname }">			<ShipLastName />					</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:ship_email }">			<ShipEmail />						</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:ship_phone }">			<ShipPhone />						</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:ship_fax }">			<ShipFax />							</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:ship_comp }">			<ShipCompany />						</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:ship_addr }">			<ShipAddress />						</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:ship_addr2 }">			<ShipAddress2 />					</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:ship_city }">			<ShipCity />						</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:ship_state }">			<ShipState />						</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:ship_zip }">			<ShipZip />							</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:ship_cntry }">			<ShipCountry />						</MvIF>

			<MvIF EXPR = "{ l.settings:customers:fields:bill_fname }">			<BillFirstName />					</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:bill_lname }">			<BillLastName />					</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:bill_email }">			<BillEmail />						</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:bill_phone }">			<BillPhone />						</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:bill_fax }">			<BillFax />							</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:bill_comp }">			<BillCompany />						</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:bill_addr }">			<BillAddress />						</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:bill_addr2 }">			<BillAddress2 />					</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:bill_city }">			<BillCity />						</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:bill_state }">			<BillState />						</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:bill_zip }">			<BillZip />							</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:bill_cntry }">			<BillCountry />						</MvIF>

			<MvIF EXPR = "{ l.settings:customers:fields:note_count }">			<NoteCount />						</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:tax_exempt }">			<TaxExempt />						</MvIF>

			<MvIF EXPR = "{ l.settings:customers:fields:order_cnt }">			<OrderCount />						</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:order_avg }">			<OrderAverage />					</MvIF>
			<MvIF EXPR = "{ l.settings:customers:fields:order_tot }">			<OrderTotal />						</MvIF>
		</MvIF>
	</Fields>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item var, output var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item( l.item:id, l.pages ) }">
		<MvASSIGN NAME = "l.dependency_output" VALUE = "">

		<MvREFERENCEARRAY NAME = "l.page_settings" VARIABLE = "l.page:settings">
			<MvMEMBER NAME = "{ l.item:code }">
		</MvREFERENCEARRAY>

		<MvEVAL EXPR = "{ SkinsComponentModuleExportItem_Dependencies( l.module, l.page:settings, l.page_settings:settings_template, l.dependency_output ) }">

		<MIVA STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<MvCAPTURE VARIABLE = "l.page_output">
			<Page_Update code="{ l.page:code }">
				<Item code="{ l.item:code }">
					<MvIF EXPR = "{ l.page_settings:settings_template:mode EQ 'product' }">			<Mode>product</Mode>
					<MvELSEIF EXPR = "{ l.page_settings:settings_template:mode EQ 'category' }">	<Mode>category</Mode>
					<MvELSEIF EXPR = "{ l.page_settings:settings_template:mode EQ 'customer' }">	<Mode>customer</Mode>
					</MvIF>

					<MvIF EXPR = "{ l.page_settings:settings_template:encoding EQ 'csv' }">			<Encoding>csv</Encoding>
					<MvELSE>																		<Encoding>none</Encoding>
					</MvIF>

					<MvIF EXPR = "{ l.page_settings:settings_template:delimiter EQ 'tab' }">		<Delimiter>tab</Delimiter>
					<MvELSEIF EXPR = "{ l.page_settings:settings_template:delimiter EQ 'pipe' }">	<Delimiter>pipe</Delimiter>
					<MvELSE>																		<Delimiter>comma</Delimiter>
					</MvIF>

					<MvIF EXPR = "{ NOT ISNULL l.page_settings:settings_template:in_value }">		<Load type="{ l.page_settings:settings_template:load }"><MvEVAL EXPR = "{ miva_cdata_encode( l.page_settings:settings_template:in_value ) }"></Load>
					<MvELSE>																		<Load type="{ l.page_settings:settings_template:load }" />
					</MvIF>

					<MvIF EXPR = "{ l.page_settings:settings_template:mode EQ 'product' }">			<MvEVAL EXPR = "{ SkinsComponentModuleExportItem_Product( l.module, l.page_settings:settings_template ) }">
					<MvELSEIF EXPR = "{ l.page_settings:settings_template:mode EQ 'category' }">	<MvEVAL EXPR = "{ SkinsComponentModuleExportItem_Category( l.module, l.page_settings:settings_template ) }">
					<MvELSEIF EXPR = "{ l.page_settings:settings_template:mode EQ 'customer' }">	<MvEVAL EXPR = "{ SkinsComponentModuleExportItem_Customer( l.module, l.page_settings:settings_template ) }">
					</MvIF>

					<MvIF EXPR = "{ l.page_settings:settings_template:advanced }">
						<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.page_settings:header_template:name, l.header_templateversion ) }">
							<HeaderTemplate>
								<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.header_templateversion:source ) }"></Template>
								<Notes>#Set_Current_Time#</Notes>
							</HeaderTemplate>
						</MvIF>

						<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.page_settings:iterator_template:name, l.iterator_templateversion ) }">
							<IteratorTemplate>
								<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.iterator_templateversion:source ) }"></Template>
								<Notes>#Set_Current_Time#</Notes>
							</IteratorTemplate>
						</MvIF>

						<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.page_settings:footer_template:name, l.footer_templateversion ) }">
							<FooterTemplate>
								<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.footer_templateversion:source ) }"></Template>
								<Notes>#Set_Current_Time#</Notes>
							</FooterTemplate>
						</MvIF>
					</MvIF>
				</Item>
			</Page_Update>
		</MvCAPTURE>
		<MIVA STANDARDOUTPUTLEVEL = "">

		<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.dependency_output $ l.page_output }">
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pages" VALUE = "{ [ g.Module_Feature_TUI_DB ].Item_Load_PageList( l.item ) }">

	<MvFUNCTIONRETURN VALUE = "{ 'Exports Template Based Feed templates from pages: ' $ l.pages }">
</MvFUNCTION>

<MvCOMMENT>
|
| Custom Iterator Templates
|
</MvCOMMENT>

<MvFUNCTION NAME = "TemplateFeed_Product_IteratorTemplate_Render_Master" PARAMETERS = "iterator_template_name, settings var, all_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.search_query"					VALUE = "">
	<MvASSIGN NAME = "l.data"							VALUE = "">
	<MvASSIGN NAME = "l.data:discount_state"			VALUE = "">
	<MvASSIGN NAME = "l.data:discount_state_signature"	VALUE = "">
	<MvASSIGN NAME = "l.data:delimiter"					VALUE = "{ TemplateFeed_Convert_Delimiter_To_Char( l.settings:delimiter ) }">

	<MvIF EXPR = "{ NOT TemplateFeed_Product_Initialize_DiscountState( l.data:discount_state, l.data:discount_state_signature ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT TemplateFeed_Product_Initialize( l.settings, l.plans, l.plan_count ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT(	l.search_query, 'p.*,
																				 a.id			AS attribute_id,
																				 a.product_id	AS attribute_product_id,
																				 a.default_id	AS attribute_default_id,
																				 a.disp_order	AS attribute_disp_order,
																				 a.code			AS attribute_code,
																				 a.type			AS attribute_type,
																				 a.attemp_id	AS attribute_attemp_id,
																				 a.prompt		AS attribute_prompt,
																				 a.price		AS attribute_price,
																				 a.cost			AS attribute_cost,
																				 a.weight		AS attribute_weight,
																				 a.required		AS attribute_required,
																				 a.inventory	AS attribute_inventory,
																				 a.image		AS attribute_image,
																				 o.id			AS option_id,
																				 o.product_id	AS option_product_id,
																				 o.attr_id		AS option_attr_id,
																				 o.disp_order	AS option_disp_order,
																				 o.code			AS option_code,
																				 o.prompt		AS option_prompt,
																				 o.price		AS option_price,
																				 o.cost			AS option_cost,
																				 o.weight		AS option_weight,
																				 o.image		AS option_image,
																				 ata.id 		AS attribute_template_attribute_id,
																				 ata.attemp_id 	AS attribute_template_attribute_attemp_id,
																				 ata.default_id AS attribute_template_attribute_default_id,
																				 ata.disporder 	AS attribute_template_attribute_disporder,
																				 ata.code 		AS attribute_template_attribute_code,
																				 ata.type 		AS attribute_template_attribute_type,
																				 ata.prompt 	AS attribute_template_attribute_prompt,
																				 ata.price 		AS attribute_template_attribute_price,
																				 ata.cost 		AS attribute_template_attribute_cost,
																				 ata.weight 	AS attribute_template_attribute_weight,
																				 ata.required 	AS attribute_template_attribute_required,
																				 ata.inventory 	AS attribute_template_attribute_inventory,
																				 ata.image 		AS attribute_template_attribute_image,
																				 ato.id			AS attribute_template_option_id,
																				 ato.attemp_id	AS attribute_template_option_attemp_id,
																				 ato.attmpat_id	AS attribute_template_option_attmpat_id,
																				 ato.disporder	AS attribute_template_option_disporder,
																				 ato.code		AS attribute_template_option_code,
																				 ato.prompt		AS attribute_template_option_prompt,
																				 ato.price		AS attribute_template_option_price,
																				 ato.cost		AS attribute_template_option_cost,
																				 ato.weight		AS attribute_template_option_weight,
																				 ato.image		AS attribute_template_option_image' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'Products', 'p' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'p', 	g.Store_Table_Prefix $ 'Attributes',				'a',	'a.product_id = p.id', 			'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'p', 	g.Store_Table_Prefix $ 'Options',					'o',	'o.attr_id = a.id',				'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'p',	g.Store_Table_Prefix $ 'AttributeTemplateAttrs', 	'ata', 	'ata.attemp_id = a.attemp_id',	'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'p',	g.Store_Table_Prefix $ 'AttributeTemplateOptions',	'ato', 	'ato.attmpat_id = ata.id',		'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY( l.search_query, 'p.disp_order', '' ) }">

	<MvIF EXPR = "{ l.settings:load NE 'all' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'p.active = 1', '' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ TemplateFeed_Product_QueryAdditions_Master( l.search_query, 'p', l.settings, l.data ) }">

	<MvCOMMENT>
	|
	| Execution
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.query" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.search_query, l.fields ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "TemplateFeed_Products"
				QUERY 	= "{ l.query }"
				FIELDS	= "{ l.fields }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.record" VARIABLE = "l.all_settings">
		<MvMEMBER NAME = "record">
	</MvREFERENCEARRAY>

	<MvASSIGN NAME = "l.pending_record"					VALUE = "">
	<MvASSIGN NAME = "l.counter"						VALUE = 0>
	<MvASSIGN NAME = "l.product_url_flags"				VALUE = "">
	<MvASSIGN NAME = "l.product_url_flags:nosession"	VALUE = 1>

	<MvWHILE EXPR = "{ NOT TemplateFeed_Products.d.EOF }">
		<MvIF EXPR = "{ NOT ISNULL l.pending_record AND ( l.pending_record:id NE TemplateFeed_Products.d.id ) }">
			<MvEVAL EXPR = "{ TemplateFeed_Product_IteratorTemplate_Finalize_Record_Master( l.settings, l.pending_record, l.data ) }">

			<MvASSIGN NAME = "l.record"							VALUE = "{ l.pending_record }">
			<MvASSIGN NAME = "l.pending_record"					VALUE = "">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.pending_record }">
			<MvEVAL EXPR = "{ TemplateFeed_Product_Read_Master( l.pending_record, l.plans, l.plan_count, l.product_url_flags, l.settings:encoding ) }">
		</MvIF>

		<MvEVAL EXPR = "{ TemplateFeed_Product_Read_Attribute( l.pending_record ) }">

		<MvIF EXPR = "{ NOT ISNULL l.record }">
			<MvEVAL EXPR = "{ TemplateFeed_IteratorTemplate_Render( l.iterator_template_name, l.all_settings, l.counter ) }">

			<MvASSIGN NAME = "l.record"	VALUE = "">
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TemplateFeed_Products" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TemplateFeed_Products">

	<MvIF EXPR = "{ NOT ISNULL l.pending_record }">
		<MvEVAL EXPR = "{ TemplateFeed_Product_IteratorTemplate_Finalize_Record_Master( l.settings, l.pending_record, l.data ) }">

		<MvASSIGN NAME = "l.record"	VALUE = "{ l.pending_record }">

		<MvEVAL EXPR = "{ TemplateFeed_IteratorTemplate_Render( l.iterator_template_name, l.all_settings, l.counter ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_IteratorTemplate_Render_Variant" PARAMETERS = "iterator_template_name, settings var, all_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.search_query"					VALUE = "">
	<MvASSIGN NAME = "l.data"							VALUE = "">
	<MvASSIGN NAME = "l.data:discount_state"			VALUE = "">
	<MvASSIGN NAME = "l.data:discount_state_signature"	VALUE = "">
	<MvASSIGN NAME = "l.data:delimiter"					VALUE = "{ TemplateFeed_Convert_Delimiter_To_Char( l.settings:delimiter ) }">

	<MvIF EXPR = "{ NOT TemplateFeed_Product_Initialize_DiscountState( l.data:discount_state, l.data:discount_state_signature ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT TemplateFeed_Product_Initialize( l.settings, l.plans, l.plan_count ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'p.*,
																				 part.id 			AS variant_id,
																				 part.catcount 		AS variant_catcount,
																				 part.cancat_id 	AS variant_cancat_id,
																				 part.agrpcount 	AS variant_agrpcount,
																				 part.pgrpcount 	AS variant_pgrpcount,
																				 part.disp_order 	AS variant_disp_order,
																				 part.page_code 	AS variant_page_code,
																				 part.code 			AS variant_code,
																				 part.sku 			AS variant_sku,
																				 part.name 			AS variant_name,
																				 part.page_title 	AS variant_page_title,
																				 part.thumbnail 	AS variant_thumbnail,
																				 part.image 		AS variant_image,
																				 part.price 		AS variant_price,
																				 part.cost 			AS variant_cost,
																				 part.descrip 		AS variant_descrip,
																				 part.weight 		AS variant_weight,
																				 part.taxable 		AS variant_taxable,
																				 part.active 		AS variant_active,
																				 part.dt_created	AS variant_dt_created,
																				 part.dt_updated	AS variant_dt_updated,
																				 pv.part_count		AS pv_part_count,
																				 pv.attmpat_id		AS pv_attmpat_id,
																				 pv.option_id 		AS pv_option_id,
																				 pvp.variant_id		AS pvp_variant_id,
																				 pvp.quantity		AS pvp_quantity,
																				 pvprice.method		AS pvprice_method,
																				 pvprice.price		AS pvprice_price,
																				 pvprice.cost		AS pvprice_cost,
																				 pvprice.weight		AS pvprice_weight,
																				 va.id				AS variant_attribute_id,
																				 va.product_id		AS variant_attribute_product_id,
																				 va.default_id		AS variant_attribute_default_id,
																				 va.disp_order		AS variant_attribute_disp_order,
																				 va.code			AS variant_attribute_code,
																				 va.type			AS variant_attribute_type,
																				 va.attemp_id		AS variant_attribute_attemp_id,
																				 va.prompt			AS variant_attribute_prompt,
																				 va.price			AS variant_attribute_price,
																				 va.cost			AS variant_attribute_cost,
																				 va.weight			AS variant_attribute_weight,
																				 va.required		AS variant_attribute_required,
																				 va.inventory		AS variant_attribute_inventory,
																				 va.image			AS variant_attribute_image,
																				 vo.id				AS variant_option_id,
																				 vo.product_id		AS variant_option_product_id,
																				 vo.attr_id			AS variant_option_attr_id,
																				 vo.disp_order		AS variant_option_disp_order,
																				 vo.code			AS variant_option_code,
																				 vo.prompt			AS variant_option_prompt,
																				 vo.price			AS variant_option_price,
																				 vo.cost			AS variant_option_cost,
																				 vo.weight			AS variant_option_weight,
																				 vo.image			AS variant_option_image,
																				 vata.id 			AS variant_attribute_template_attribute_id,
																				 vata.attemp_id 	AS variant_attribute_template_attribute_attemp_id,
																				 vata.default_id 	AS variant_attribute_template_attribute_default_id,
																				 vata.disporder 	AS variant_attribute_template_attribute_disporder,
																				 vata.code 			AS variant_attribute_template_attribute_code,
																				 vata.type 			AS variant_attribute_template_attribute_type,
																				 vata.prompt 		AS variant_attribute_template_attribute_prompt,
																				 vata.price 		AS variant_attribute_template_attribute_price,
																				 vata.cost 			AS variant_attribute_template_attribute_cost,
																				 vata.weight 		AS variant_attribute_template_attribute_weight,
																				 vata.required 		AS variant_attribute_template_attribute_required,
																				 vata.inventory 	AS variant_attribute_template_attribute_inventory,
																				 vata.image 		AS variant_attribute_template_attribute_image,
																				 vato.id			AS variant_attribute_template_option_id,
																				 vato.attemp_id		AS variant_attribute_template_option_attemp_id,
																				 vato.attmpat_id	AS variant_attribute_template_option_attmpat_id,
																				 vato.disporder		AS variant_attribute_template_option_disporder,
																				 vato.code			AS variant_attribute_template_option_code,
																				 vato.prompt		AS variant_attribute_template_option_prompt,
																				 vato.price			AS variant_attribute_template_option_price,
																				 vato.cost			AS variant_attribute_template_option_cost,
																				 vato.weight		AS variant_attribute_template_option_weight,
																				 vato.image			AS variant_attribute_template_option_image' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'Products', 'p' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'p', 	g.Store_Table_Prefix $ 'ProductVariants',			'pv',		'pv.product_id = p.id', 											'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'p', 	g.Store_Table_Prefix $ 'ProductVariantParts',		'pvp',		'pvp.product_id = p.id AND pvp.variant_id = pv.variant_id',			'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'p', 	g.Store_Table_Prefix $ 'ProductVariantPricing',		'pvprice',	'pvprice.product_id = p.id AND pvprice.variant_id = pv.variant_id',	'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'p', 	g.Store_Table_Prefix $ 'Products',					'part',		'part.id = pvp.part_id',											'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'p', 	g.Store_Table_Prefix $ 'Attributes',				'va',		'va.id = pv.attr_id',												'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'p', 	g.Store_Table_Prefix $ 'Options',					'vo',		'vo.id = pv.option_id',												'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'p', 	g.Store_Table_Prefix $ 'AttributeTemplateAttrs',	'vata',		'vata.id = pv.attmpat_id',											'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'p', 	g.Store_Table_Prefix $ 'AttributeTemplateOptions',	'vato',		'vato.id = pv.option_id',											'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY( l.search_query, 'p.id', 			'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY( l.search_query, 'pv.variant_id', 	'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY( l.search_query, 'part.id', 		'' ) }">

	<MvIF EXPR = "{ l.settings:load NE 'all' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'p.active = 1', '' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ TemplateFeed_Product_QueryAdditions_Master( l.search_query, 'p', l.settings, l.data ) }">
	<MvEVAL EXPR = "{ TemplateFeed_Product_QueryAdditions_Variant( l.search_query, 'p', 'part', 'pv', l.settings, l.data ) }">

	<MvCOMMENT>
	|
	| Execution
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.query" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.search_query, l.fields ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "TemplateFeed_Products"
				QUERY 	= "{ l.query }"
				FIELDS	= "{ l.fields }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.record" VARIABLE = "l.all_settings">
		<MvMEMBER NAME = "record">
	</MvREFERENCEARRAY>

	<MvASSIGN NAME = "l.pending_record"									VALUE = "">
	<MvASSIGN NAME = "l.counter"										VALUE = 0>
	<MvASSIGN NAME = "l.product_url_flags"								VALUE = "">
	<MvASSIGN NAME = "l.product_url_flags:nosession"					VALUE = 1>

	<MvWHILE EXPR = "{ NOT TemplateFeed_Products.d.EOF }">
		<MvIF EXPR = "{ ISNULL TemplateFeed_Products.d.pvp_variant_id }">
			<MvIF EXPR = "{ NOT ISNULL l.pending_record AND ( l.pending_record:variant:id OR l.pending_record:id NE TemplateFeed_Products.d.id ) }">
				<MvEVAL EXPR = "{ TemplateFeed_Product_IteratorTemplate_Finalize_Record_Variant( l.settings, l.pending_record, l.data ) }">

				<MvASSIGN NAME = "l.record"								VALUE = "{ l.pending_record }">
				<MvASSIGN NAME = "l.pending_record" 					VALUE = "">
			</MvIF>

			<MvIF EXPR = "{ ISNULL l.pending_record }">
				<MvEVAL EXPR = "{ TemplateFeed_Product_Read_Master( l.pending_record, l.plans, l.plan_count, l.product_url_flags, l.settings:encoding ) }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ ( ( l.pending_record:variant:id EQ 0 ) AND NOT ISNULL l.pending_record ) OR
							(
								( ( l.pending_record:variant_id ) AND ( l.pending_record:variant_id NE TemplateFeed_Products.d.pvp_variant_id ) ) OR
							 	( ( l.pending_record:variant:id ) AND ( l.pending_record:variant:id NE TemplateFeed_Products.d.variant_id ) )
							 ) }">
				<MvEVAL EXPR = "{ TemplateFeed_Product_IteratorTemplate_Finalize_Record_Variant( l.settings, l.pending_record, l.data ) }">

				<MvASSIGN NAME = "l.record"								VALUE = "{ l.pending_record }">
				<MvASSIGN NAME = "l.pending_record"						VALUE = "">
			</MvIF>

			<MvIF EXPR = "{ ISNULL l.pending_record }">
				<MvEVAL EXPR = "{ TemplateFeed_Product_Read_Master( l.pending_record, l.plans, l.plan_count, l.product_url_flags, l.settings:encoding ) }">
				<MvEVAL EXPR = "{ TemplateFeed_Product_Read_Variant( l.pending_record, l.pending_record:variant, l.plans, l.plan_count, l.settings:encoding ) }">
			</MvIF>

			<MvEVAL EXPR = "{ TemplateFeed_Product_Read_Attribute( l.pending_record ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT ISNULL l.record }">
			<MvEVAL EXPR = "{ TemplateFeed_IteratorTemplate_Render( l.iterator_template_name, l.all_settings, l.counter ) }">

			<MvASSIGN NAME = "l.record"	VALUE = "">
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TemplateFeed_Products" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TemplateFeed_Products">

	<MvIF EXPR = "{ NOT ISNULL l.pending_record }">
		<MvEVAL EXPR = "{ TemplateFeed_Product_IteratorTemplate_Finalize_Record_Variant( l.settings, l.pending_record, l.data ) }">

		<MvASSIGN NAME = "l.record" VALUE = "{ l.pending_record }">

		<MvEVAL EXPR = "{ TemplateFeed_IteratorTemplate_Render( l.iterator_template_name, l.all_settings, l.counter ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_IteratorTemplate_Finalize_Record_Master" PARAMETERS = "settings var, record var, data var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ TemplateFeed_Product_IteratorTemplate_Finalize_Record_Attributes( l.record ) }">
	<MvEVAL EXPR = "{ TemplateFeed_Product_IteratorTemplate_Finalize_Record_Master_Price( l.settings, l.record, l.data ) }">

	<MvCOMMENT>
	|
	| Ensure backwards compatibility with previous versions loading attribute and option codes only
	|
	</MvCOMMENT>

	<MvFOREACH ITERATOR = "l.attribute" ARRAY = "l.record:attribute_data" COUNT = "{ l.record:attribute_data_count }">
		<MvIF EXPR = "{ l.attribute:attemp_id }">	<MvASSIGN NAME = "l.attribute_code" VALUE = "{ l.attribute:code $ l.attribute:template_code }">
		<MvELSE>									<MvASSIGN NAME = "l.attribute_code" VALUE = "{ l.attribute:code }">
		</MvIF>

		<MvREFERENCEARRAY NAME = "l.attributes_array" VARIABLE = "l.record:attributes">
			<MvMEMBER NAME = "{ l.attribute_code }">
		</MvREFERENCEARRAY>

		<MvIF EXPR = "{ l.attribute:option_count EQ 0 }">
			<MvASSIGN NAME = "l.null" VALUE = "{ miva_array_insert( l.attributes_array, '', -1 ) }">
		<MvELSE>
			<MvFOREACH ITERATOR = "l.option" ARRAY = "l.attribute:options" COUNT = "{ l.attribute:option_count }">
				<MvIF EXPR = "{ l.attribute:attemp_id }">	<MvASSIGN NAME = "l.index" VALUE = "{ l.option:disporder }">
				<MvELSE>									<MvASSIGN NAME = "l.index" VALUE = "{ l.option:disp_order }">
				</MvIF>

				<MvASSIGN NAME = "l.attributes_array" INDEX = "{ l.index }" VALUE = "{ l.option:code }">
			</MvFOREACH>

			<MvASSIGN NAME = "l.null" VALUE = "{ miva_array_collapse( l.attributes_array ) }">
		</MvIF>
	</MvFOREACH>

	<MvEVAL EXPR = "{ TemplateFeed_Encode_Record( l.record, l.settings:encoding, l.data:delimiter ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_IteratorTemplate_Finalize_Record_Master_Price" PARAMETERS = "settings var, record var, data var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT ISNULL l.record:predicted_sale_price }">
		<MvASSIGN NAME = "l.record:price" VALUE = "{ l.record:predicted_sale_price }">
	<MvELSEIF EXPR = "{ l.settings:products:predict_discounts }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_Predict_Product_Price( l.data:discount_state, l.record, l.record:base_price, l.record:predicted_sale_price ) }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvASSIGN NAME = "l.record:price"			VALUE = "{ l.record:predicted_sale_price }">
		<MvASSIGN NAME = "l.record:price_formatted"	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.record:price ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_IteratorTemplate_Finalize_Record_Variant" PARAMETERS = "settings var, record var, data var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ TemplateFeed_Product_IteratorTemplate_Finalize_Record_Attributes( l.record ) }">

	<MvIF EXPR = "{ ISNULL l.record:variant_id }">	<MvEVAL EXPR = "{ TemplateFeed_Product_IteratorTemplate_Finalize_Record_Master_Price( l.settings, l.record, l.data ) }">
	<MvELSE>										<MvEVAL EXPR = "{ TemplateFeed_Product_IteratorTemplate_Finalize_Record_Variant_Price( l.settings, l.record, l.data ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| Ensure backwards compatibility with previous versions loading attribute and option codes only
	|
	</MvCOMMENT>

	<MvFOREACH ITERATOR = "l.attribute" ARRAY = "l.record:attribute_data" COUNT = "{ l.record:attribute_data_count }">
		<MvIF EXPR = "{ l.attribute:attemp_id }">	<MvASSIGN NAME = "l.attribute_code" VALUE = "{ l.attribute:template_code }">
		<MvELSE>									<MvASSIGN NAME = "l.attribute_code" VALUE = "{ l.attribute:code }">
		</MvIF>

		<MvREFERENCEARRAY NAME = "l.attributes_array" VARIABLE = "l.record:attributes">
			<MvMEMBER NAME = "{ l.attribute_code }">
		</MvREFERENCEARRAY>

		<MvIF EXPR = "{ NOT TemplateFeed_Attribute_Supports_Options( l.attribute ) }">	<MvASSIGN NAME = "l.attributes_array" VALUE = "{ l.attribute:value }">
		<MvELSE>																		<MvASSIGN NAME = "l.attributes_array" VALUE = "{ l.attribute:options[ 1 ]:code }">
		</MvIF>
	</MvFOREACH>

	<MvEVAL EXPR = "{ TemplateFeed_Encode_Record( l.record, l.settings:encoding, l.data:delimiter ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_IteratorTemplate_Finalize_Record_Variant_Price" PARAMETERS = "settings var, record var, data var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.record:variant_id NE l.data:last_variant_id }">
		<MvASSIGN NAME = "l.data:cached_variant_pricing"	VALUE = "">
		<MvASSIGN NAME = "l.data:last_variant_id"			VALUE = "{ l.record:variant_id }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.data:cached_variant_pricing }">
		<MvASSIGN NAME = "l.record:cost"						VALUE = "{ l.data:cached_variant_pricing:cost }">
		<MvASSIGN NAME = "l.record:price"						VALUE = "{ l.data:cached_variant_pricing:price }">
		<MvASSIGN NAME = "l.record:weight"						VALUE = "{ l.data:cached_variant_pricing:weight }">
		<MvASSIGN NAME = "l.record:original_price"				VALUE = "{ l.data:cached_variant_pricing:original_price }">
		<MvASSIGN NAME = "l.record:predicted_sale_price"		VALUE = "{ l.data:cached_variant_pricing:predicted_sale_price }">

		<MvASSIGN NAME = "l.record:price_formatted"				VALUE = "{ l.data:cached_variant_pricing:price_formatted }">
		<MvASSIGN NAME = "l.record:cost_formatted"				VALUE = "{ l.data:cached_variant_pricing:cost_formatted }">
		<MvASSIGN NAME = "l.record:original_price_formatted"	VALUE = "{ l.data:cached_variant_pricing:original_price_formatted }">

		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ l.record:pricing_method EQ 0 }">
		<MvCOMMENT>
		|
		| Sum of master product and attributes
		|
		</MvCOMMENT>

		<MvASSIGN NAME = "l.attr_price_override" VALUE = 0>

		<MvFOREACH ITERATOR = "l.attribute" ARRAY = "l.record:attribute_data" COUNT = "{ l.record:attribute_data_count }">
			<MvIF EXPR = "{ NOT TemplateFeed_Attribute_Supports_Options( l.attribute ) }">
				<MvASSIGN NAME = "l.record:cost"			VALUE = "{ l.record:cost + l.attribute:cost }">
				<MvASSIGN NAME = "l.record:weight"			VALUE = "{ l.record:weight + l.attribute:weight }">
				<MvASSIGN NAME = "l.record:original_price"	VALUE = "{ l.record:original_price + l.attribute:price }">
			<MvELSE>
				<MvFOREACH ITERATOR = "l.option" ARRAY = "l.attribute:options" COUNT = "{ l.attribute:option_count }">
					<MvASSIGN NAME = "l.record:cost"			VALUE = "{ l.record:cost + l.option:cost }">
					<MvASSIGN NAME = "l.record:weight"			VALUE = "{ l.record:weight + l.option:weight }">
					<MvASSIGN NAME = "l.record:original_price"	VALUE = "{ l.record:original_price + l.option:price }">
				</MvFOREACH>
			</MvIF>
		</MvFOREACH>
	<MvELSEIF EXPR = "{ l.record:pricing_method EQ 1 }">
		<MvCOMMENT>
		|
		| Specific variant values
		|
		</MvCOMMENT>

		<MvASSIGN NAME = "l.attr_price_override"		VALUE = 1>

		<MvASSIGN NAME = "l.record:cost"				VALUE = "{ l.record:pricing_cost }">
		<MvASSIGN NAME = "l.record:price"				VALUE = "{ l.record:pricing_price }">
		<MvASSIGN NAME = "l.record:weight"				VALUE = "{ l.record:pricing_weight }">
		<MvASSIGN NAME = "l.record:original_price"		VALUE = "{ l.record:price }">
	<MvELSEIF EXPR = "{ l.record:pricing_method EQ 2 }">
		<MvCOMMENT>
		|
		| Sum of parts
		|
		</MvCOMMENT>

		<MvASSIGN NAME = "l.attr_price_override"		VALUE = 1>

		<MvIF EXPR = "{ l.record:part_count EQ 1 }">
			<MvASSIGN NAME = "l.record:cost"			VALUE = "{ l.record:variant:cost * l.record:variant:quantity }">
			<MvASSIGN NAME = "l.record:price"			VALUE = "{ l.record:variant:price * l.record:variant:quantity }">
			<MvASSIGN NAME = "l.record:weight"			VALUE = "{ l.record:variant:weight * l.record:variant:quantity }">
			<MvASSIGN NAME = "l.record:original_price"	VALUE = "{ l.record:price }">
		<MvELSE>
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ProductVariant_Sum_Pricing( l.record:id, l.record:variant_id, l.record:price, l.record:cost, l.record:weight ) }">
				<MvFUNCTIONRETURN>
			</MvIF>

			<MvASSIGN NAME = "l.record:original_price"	VALUE = "{ l.record:price }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.record:predicted_sale_price }">
		<MvASSIGN NAME = "l.record:price" VALUE = "{ l.record:predicted_sale_price }">
	<MvELSEIF EXPR = "{ NOT l.settings:products:predict_discounts }">
		<MvASSIGN NAME = "l.record:price" VALUE = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Adjusted_Price( l.data:discount_state, l.record, l.record:price, l.record:cost ) }">

		<MvIF EXPR = "{ NOT l.attr_price_override }">
			<MvFOREACH ITERATOR = "l.attribute" ARRAY = "l.record:attribute_data" COUNT = "{ l.record:attribute_data_count }">
				<MvIF EXPR = "{ NOT TemplateFeed_Attribute_Supports_Options( l.attribute ) }">
					<MvASSIGN NAME = "l.record:price" VALUE = "{ l.record:price + [ g.Module_Feature_PGR_UT ].DiscountState_Adjusted_Price( l.data:discount_state, l.record, l.attribute:price, l.attribute:cost ) }">
				<MvELSE>
					<MvFOREACH ITERATOR = "l.option" ARRAY = "l.attribute:options" COUNT = "{ l.attribute:option_count }">
						<MvASSIGN NAME = "l.record:price" VALUE = "{ l.record:price + [ g.Module_Feature_PGR_UT ].DiscountState_Adjusted_Price( l.data:discount_state, l.record, l.option:price, l.option:cost ) }">
					</MvFOREACH>
				</MvIF>
			</MvFOREACH>
		</MvIF>
	<MvELSE>
		<MvASSIGN NAME = "l.predict_options"		VALUE = "">
		<MvASSIGN NAME = "l.predict_option_count"	VALUE = 0>

		<MvFOREACH ITERATOR = "l.attribute" ARRAY = "l.record:attribute_data" COUNT = "{ l.record:attribute_data_count }">
			<MvIF EXPR = "{ NOT TemplateFeed_Attribute_Supports_Options( l.attribute ) }">
				<MvIF EXPR = "{ l.attribute:value EQ 0 }">
					<MvFOREACHCONTINUE>
				</MvIF>

				<MvASSIGN NAME = "l.predict_option"					VALUE = "">
				<MvASSIGN NAME = "l.predict_option:attr_id"			VALUE = "{ l.attribute:id }">
				<MvASSIGN NAME = "l.predict_option:attmpat_id"		VALUE = "{ l.attribute:attmpat_id }">
				<MvASSIGN NAME = "l.predict_option:option_id"		VALUE = "{ l.attribute:value }">
				<MvASSIGN NAME = "l.predict_option:cost"			VALUE = 0.00>
				<MvASSIGN NAME = "l.predict_option:price"			VALUE = 0.00>
				<MvASSIGN NAME = "l.predict_option:weight"			VALUE = 0.00>

				<MvIF EXPR = "{ NOT l.attr_price_override }">
					<MvASSIGN NAME = "l.predict_option:cost"		VALUE = "{ l.attribute:cost }">
					<MvASSIGN NAME = "l.predict_option:price"		VALUE = "{ l.attribute:price }">
					<MvASSIGN NAME = "l.predict_option:weight"		VALUE = "{ l.attribute:weight }">
				</MvIF>

				<MvASSIGN NAME = "l.predict_option_count"			VALUE = "{ miva_array_insert_var( l.predict_options, l.predict_option, -1 ) }">
			<MvELSE>
				<MvFOREACH ITERATOR = "l.option" ARRAY = "l.attribute:options" COUNT = "{ l.attribute:option_count }">
					<MvASSIGN NAME = "l.predict_option"				VALUE = "">
					<MvASSIGN NAME = "l.predict_option:attr_id"		VALUE = "{ l.attribute:id }">
					<MvASSIGN NAME = "l.predict_option:attmpat_id"	VALUE = "{ l.attribute:attmpat_id }">
					<MvASSIGN NAME = "l.predict_option:option_id"	VALUE = "{ l.option:id }">
					<MvASSIGN NAME = "l.predict_option:cost"		VALUE = 0.00>
					<MvASSIGN NAME = "l.predict_option:price"		VALUE = 0.00>
					<MvASSIGN NAME = "l.predict_option:weight"		VALUE = 0.00>

					<MvIF EXPR = "{ NOT l.attr_price_override }">
						<MvASSIGN NAME = "l.predict_option:cost"	VALUE = "{ l.option:cost }">
						<MvASSIGN NAME = "l.predict_option:price"	VALUE = "{ l.option:price }">
						<MvASSIGN NAME = "l.predict_option:weight"	VALUE = "{ l.option:weight }">
					</MvIF>

					<MvASSIGN NAME = "l.predict_option_count"		VALUE = "{ miva_array_insert_var( l.predict_options, l.predict_option, -1 ) }">
				</MvFOREACH>
			</MvIF>
		</MvFOREACH>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_Predict_Product_Price_WithProductSubscriptionTermAndOptions( l.data:discount_state, l.record, 0, l.record:variant_id, 1, l.predict_options, l.predict_option_count, l.record:base_price, l.record:predicted_sale_price ) }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvASSIGN NAME = "l.record:price" VALUE = "{ l.record:predicted_sale_price }">
	</MvIF>

	<MvASSIGN NAME = "l.record:price_formatted"									VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.record:price ) }">
	<MvASSIGN NAME = "l.record:cost_formatted"									VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.record:cost ) }">
	<MvASSIGN NAME = "l.record:original_price_formatted"						VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.record:original_price ) }">

	<MvASSIGN NAME = "l.data:cached_variant_pricing:cost"						VALUE = "{ l.record:cost }">
	<MvASSIGN NAME = "l.data:cached_variant_pricing:price"						VALUE = "{ l.record:price }">
	<MvASSIGN NAME = "l.data:cached_variant_pricing:weight"						VALUE = "{ l.record:weight }">
	<MvASSIGN NAME = "l.data:cached_variant_pricing:original_price"				VALUE = "{ l.record:original_price }">
	<MvASSIGN NAME = "l.data:cached_variant_pricing:predicted_sale_price"		VALUE = "{ l.record:predicted_sale_price }">

	<MvASSIGN NAME = "l.data:cached_variant_pricing:price_formatted"			VALUE = "{ l.record:price_formatted }">
	<MvASSIGN NAME = "l.data:cached_variant_pricing:cost_formatted"				VALUE = "{ l.record:cost_formatted }">
	<MvASSIGN NAME = "l.data:cached_variant_pricing:original_price_formatted"	VALUE = "{ l.record:original_price_formatted }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_IteratorTemplate_Finalize_Record_Attributes" PARAMETERS = "record var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.record:attribute_data_count"					VALUE = "{ miva_array_collapse( l.record:attribute_data ) }">
	<MvASSIGN NAME = "l.index" 											VALUE = "{ l.record:attribute_data_count }">

	<MvWHILE EXPR = "{ l.index }">
		<MvREFERENCEARRAY NAME = "l.product_attribute" VARIABLE = "l.record:attribute_data">
			<MvDIMENSION INDEX = "{ l.index }">
		</MvREFERENCEARRAY>

		<MvIF EXPR = "{ NOT l.product_attribute:attemp_id }">
			<MvASSIGN NAME = "l.product_attribute:option_count"			VALUE = "{ miva_array_collapse( l.product_attribute:options ) }">
		<MvELSE>
			<MvASSIGN NAME = "l.attribute_index"						VALUE = "{ l.index + 1 }">

			<MvFOREACH ITERATOR = "l.template_attribute" ARRAY = "l.product_attribute:attributes">
				<MvASSIGN NAME = "l.template_attribute:option_count"	VALUE = "{ miva_array_collapse( l.template_attribute:options ) }">

				<MvASSIGN NAME = "l.attribute"							VALUE = "{ l.template_attribute }">
				<MvASSIGN NAME = "l.attribute:id"						VALUE = "{ l.product_attribute:id }">
				<MvASSIGN NAME = "l.attribute:attmpat_id"				VALUE = "{ l.template_attribute:id }">
				<MvASSIGN NAME = "l.attribute:template_code"			VALUE = "{ l.template_attribute:code }">
				<MvASSIGN NAME = "l.attribute:code"						VALUE = "{ l.product_attribute:code }">

				<MvASSIGN NAME = "l.record:attribute_data_count" 		VALUE = "{ miva_array_insert_var( l.record:attribute_data, l.attribute, l.attribute_index++ ) }">
			</MvFOREACH>

			<MvASSIGN NAME = "l.record:attribute_data_count" 			VALUE = "{ miva_array_delete( l.record:attribute_data, l.index, 1 ) }">
		</MvIF>

		<MvASSIGN NAME = "l.index" 										VALUE = "{ l.index - 1 }">
	</MvWHILE>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Category_IteratorTemplate_Render" PARAMETERS = "iterator_template_name, settings var, all_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.search_query" VALUE = "">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'c.*' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'Categories', 'c' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY( l.search_query, 'c.disp_order', '' ) }">

	<MvCOMMENT>
	|
	| Get the parent category code
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'cat1.code AS parent_category' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'c', g.Store_Table_Prefix $ 'Categories', 'cat1', 'cat1.id = c.parent_id', '' ) }">

	<MvCOMMENT>
	|
	| Get the alternate display page id
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'c.page_code AS alternate_display_page' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT_NULL_INTEGER( l.search_query, 'pbv.page_id',	'page_id' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'c', g.Store_Table_Prefix $ 'PageBranchVersions', 'pbv', 'c.page_code <> \'\' AND pbv.branch_id = ? AND pbv.head = 1 AND ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'pbv.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'c.page_code' ), [ g.Module_Library_DB ].SQL_Query_Field( [ g.Module_Feature_TUI_MGR ].TemplateManager_Working_Branch_ID() ) ) }">

	<MvCOMMENT>
	|
	| Get the canonical URI
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'uris.uri AS canonical_uri' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'c', 'URIs', 'uris', 'uris.store_id = ? AND uris.screen = \'\' AND uris.page_code = \'\' AND uris.cat_id = c.id AND uris.feed_id = 0 AND uris.product_id = 0 AND uris.canonical = 1', 'g.Store:id' ) }">

	<MvCOMMENT>
	|
	| Build the inclusions / exclusions
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.settings:load NE 'all' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'c.active = 1', '' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.settings:load EQ 'available' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'c.agrpcount = 0', '' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.settings:load EQ 'child_categories_from' }">
		<MvFOREACH ITERATOR = "l.category_code" ARRAY = "l.category_codes" INDEX = "l.pos" COUNT = "{ miva_splitstring( l.settings:in_value, ',', l.category_codes, 'trim' ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Category_Load_Code_Cached( l.category_code, l.category ) }">	<MvASSIGN NAME = "l.category_id" VALUE = -1>
			<MvELSE>																									<MvASSIGN NAME = "l.category_id" VALUE = "{ l.category:id }">
			</MvIF>

			<MvIF EXPR = "{ l.pos GT 1 }">
				<MvASSIGN NAME = "l.category_where_clause"								VALUE = "{ l.category_where_clause $ ' OR ' }">
				<MvASSIGN NAME = "l.fields"												VALUE = "{ l.fields $ ',' }">
			</MvIF>

			<MvASSIGN NAME = "g.TempateFeed_Category_CategoryIDs" INDEX = "{ l.pos }"	VALUE = "{ l.category_id }">
			<MvASSIGN NAME = "l.category_where_clause"									VALUE = "{ l.category_where_clause $ 'c.parent_id = ?' }">
			<MvASSIGN NAME = "l.fields"													VALUE = "{ l.fields $ 'g.TempateFeed_Category_CategoryIDs[' $ l.pos $ ']' }">
		</MvFOREACH>

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, '( ' $ l.category_where_clause $ ' )', l.fields ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| Availability Group inclusions / exclusions
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ( l.settings:load EQ 'in_availability_group' ) OR ( l.settings:load EQ 'not_in_availability_group' ) }">
		<MvASSIGN NAME = "l.in_operator" VALUE = "{ l.settings:load EQ 'in_availability_group' }">

		<MvFOREACH ITERATOR = "l.availgroup_name" ARRAY = "l.availgroup_names" INDEX = "l.pos" COUNT = "{ miva_splitstring( l.settings:in_value, ',', l.availgroup_names, 'trim' ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_AGR_DB ].AvailabilityGroup_Load_Name( l.availgroup_name, l.availgroup ) }">	<MvASSIGN NAME = "l.availgroup_id" VALUE = 0>
			<MvELSE>																											<MvASSIGN NAME = "l.availgroup_id" VALUE = "{ l.availgroup:id }">
			</MvIF>

			<MvASSIGN NAME = "l.join_prefix"														VALUE = "{ 'agxc' $ l.pos }">
			<MvASSIGN NAME = "g.TemplateFeed_Category_AvailabilityGroupIDs" INDEX = "{ l.pos }"		VALUE = "{ l.availgroup_id }">

			<MvIF EXPR = "{ l.pos GT 1 }">
				<MvIF EXPR = "{ l.in_operator }">	<MvASSIGN NAME = "l.availgroup_where_clause"	VALUE = "{ l.availgroup_where_clause $ ' OR ' }">
				<MvELSE>							<MvASSIGN NAME = "l.availgroup_where_clause"	VALUE = "{ l.availgroup_where_clause $ ' AND ' }">
				</MvIF>
			</MvIF>

			<MvIF EXPR = "{ l.in_operator }">		<MvASSIGN NAME = "l.availgroup_where_clause"	VALUE = "{ l.availgroup_where_clause $ l.join_prefix $ '.agrp_id IS NOT NULL' }">
			<MvELSE>								<MvASSIGN NAME = "l.availgroup_where_clause"	VALUE = "{ l.availgroup_where_clause $ l.join_prefix $ '.agrp_id IS NULL' }">
			</MvIF>

			<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'c', g.Store_Table_Prefix $ 'AvailGroupXCategory', l.join_prefix, l.join_prefix $ '.cat_id = c.id AND ' $ l.join_prefix $ '.agrp_id = ?', 'g.TemplateFeed_Category_AvailabilityGroupIDs[' $ l.pos $ ']'  ) }">
		</MvFOREACH>

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, '( ' $ l.availgroup_where_clause $ ' )', '' ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| Set up custom fields
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.plans"		VALUE = "">
	<MvASSIGN NAME = "l.plan_count"	VALUE = 0>

	<MvEVAL EXPR = "{ TemplateFeed_Category_Initialize_CustomField_Lookup( l.settings:categories:customfields, l.plans, l.plan_count ) }">

	<MvCOMMENT>
	|
	| Execution
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.query" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.search_query, l.fields ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "TemplateFeed_Categories"
				QUERY 	= "{ l.query }"
				FIELDS	= "{ l.fields }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.record" VARIABLE = "l.all_settings">
		<MvMEMBER NAME = "record">
	</MvREFERENCEARRAY>

	<MvASSIGN NAME = "l.delimiter"						VALUE = "{ TemplateFeed_Convert_Delimiter_To_Char( l.settings:delimiter ) }">
	<MvASSIGN NAME = "l.counter"						VALUE = 0>
	<MvASSIGN NAME = "l.category_url_flags"				VALUE = "">
	<MvASSIGN NAME = "l.category_url_flags:nosession"	VALUE = 1>

	<MvWHILE EXPR = "{ NOT TemplateFeed_Categories.d.EOF }">
		<MvEVAL EXPR = "{ TemplateFeed_Category_Read( l.record, l.plans, l.plan_count, l.category_url_flags, l.settings:encoding ) }">
		<MvEVAL EXPR = "{ TemplateFeed_Encode_Record( l.record, l.settings:encoding, l.delimiter ) }">
		<MvEVAL EXPR = "{ TemplateFeed_IteratorTemplate_Render( l.iterator_template_name, l.all_settings, l.counter ) }">

		<MvSKIP NAME = "Merchant" VIEW = "TemplateFeed_Categories" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TemplateFeed_Categories">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Customer_IteratorTemplate_Render" PARAMETERS = "iterator_template_name, settings var, all_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.search_query" VALUE = "">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'c.*' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'Customers', 'c' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY( l.search_query, 'c.login', '' ) }">

	<MvCOMMENT>
	|
	| Get the business account title
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'ba.title AS business_account' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'c', g.Store_Table_Prefix $ 'BusinessAccounts', 'ba', 'ba.id = c.account_id', '' ) }">

	<MvCOMMENT>
	|
	| Price Group inclusions / exclusions
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ( l.settings:load EQ 'in_price_group' ) OR ( l.settings:load EQ 'not_in_price_group' ) }">
		<MvASSIGN NAME = "l.in_operator" VALUE = "{ l.settings:load EQ 'in_price_group' }">

		<MvFOREACH ITERATOR = "l.pricegroup_name" ARRAY = "l.pricegroup_names" INDEX = "l.pos" COUNT = "{ miva_splitstring( l.settings:in_value, ',', l.pricegroup_names, 'trim' ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_DB ].PriceGroup_Load_Name( l.pricegroup_name, l.pricegroup ) }">	<MvASSIGN NAME = "l.pricegroup_id" VALUE = 0>
			<MvELSE>																									<MvASSIGN NAME = "l.pricegroup_id" VALUE = "{ l.pricegroup:id }">
			</MvIF>

			<MvASSIGN NAME = "l.join_prefix"														VALUE = "{ 'pgxc' $ l.pos }">
			<MvASSIGN NAME = "g.TemplateFeed_Customer_PriceGroupIDs" INDEX = "{ l.pos }"			VALUE = "{ l.pricegroup_id }">

			<MvIF EXPR = "{ l.pos GT 1 }">
				<MvIF EXPR = "{ l.in_operator }">	<MvASSIGN NAME = "l.pricegroup_where_clause"	VALUE = "{ l.pricegroup_where_clause $ ' OR ' }">
				<MvELSE>							<MvASSIGN NAME = "l.pricegroup_where_clause"	VALUE = "{ l.pricegroup_where_clause $ ' AND ' }">
				</MvIF>
			</MvIF>

			<MvIF EXPR = "{ l.in_operator }">		<MvASSIGN NAME = "l.pricegroup_where_clause"	VALUE = "{ l.pricegroup_where_clause $ l.join_prefix $ '.pgrp_id IS NOT NULL' }">
			<MvELSE>								<MvASSIGN NAME = "l.pricegroup_where_clause"	VALUE = "{ l.pricegroup_where_clause $ l.join_prefix $ '.pgrp_id IS NULL' }">
			</MvIF>

			<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'c', g.Store_Table_Prefix $ 'PriceGroupXCustomer', l.join_prefix, l.join_prefix $ '.cust_id = c.id AND ' $ l.join_prefix $ '.pgrp_id = ?', 'g.TemplateFeed_Customer_PriceGroupIDs[' $ l.pos $ ']'  ) }">
		</MvFOREACH>

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, '( ' $ l.pricegroup_where_clause $ ' )', '' ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| Availability Group inclusions / exclusions
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ( l.settings:load EQ 'in_availability_group' ) OR ( l.settings:load EQ 'not_in_availability_group' ) }">
		<MvASSIGN NAME = "l.in_operator" VALUE = "{ l.settings:load EQ 'in_availability_group' }">

		<MvFOREACH ITERATOR = "l.availgroup_name" ARRAY = "l.availgroup_names" INDEX = "l.pos" COUNT = "{ miva_splitstring( l.settings:in_value, ',', l.availgroup_names, 'trim' ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_AGR_DB ].AvailabilityGroup_Load_Name( l.availgroup_name, l.availgroup ) }">	<MvASSIGN NAME = "l.availgroup_id" VALUE = 0>
			<MvELSE>																											<MvASSIGN NAME = "l.availgroup_id" VALUE = "{ l.availgroup:id }">
			</MvIF>

			<MvASSIGN NAME = "l.join_prefix"														VALUE = "{ 'agxp' $ l.pos }">
			<MvASSIGN NAME = "g.TemplateFeed_Customer_AvailabilityGroupIDs" INDEX = "{ l.pos }"		VALUE = "{ l.availgroup_id }">

			<MvIF EXPR = "{ l.pos GT 1 }">
				<MvIF EXPR = "{ l.in_operator }">	<MvASSIGN NAME = "l.availgroup_where_clause"	VALUE = "{ l.availgroup_where_clause $ ' OR ' }">
				<MvELSE>							<MvASSIGN NAME = "l.availgroup_where_clause"	VALUE = "{ l.availgroup_where_clause $ ' AND ' }">
				</MvIF>
			</MvIF>

			<MvIF EXPR = "{ l.in_operator }">		<MvASSIGN NAME = "l.availgroup_where_clause"	VALUE = "{ l.availgroup_where_clause $ l.join_prefix $ '.agrp_id IS NOT NULL' }">
			<MvELSE>								<MvASSIGN NAME = "l.availgroup_where_clause"	VALUE = "{ l.availgroup_where_clause $ l.join_prefix $ '.agrp_id IS NULL' }">
			</MvIF>

			<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'c', g.Store_Table_Prefix $ 'AvailGroupXCustomer', l.join_prefix, l.join_prefix $ '.cust_id = c.id AND ' $ l.join_prefix $ '.agrp_id = ?', 'g.TemplateFeed_Customer_AvailabilityGroupIDs[' $ l.pos $ ']'  ) }">
		</MvFOREACH>

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, '( ' $ l.availgroup_where_clause $ ' )', '' ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| Set up custom fields
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.plans"		VALUE = "">
	<MvASSIGN NAME = "l.plan_count"	VALUE = 0>

	<MvEVAL EXPR = "{ TemplateFeed_Customer_Initialize_CustomField_Lookup( l.settings:customers:customfields, l.plans, l.plan_count ) }">

	<MvCOMMENT>
	|
	| Execution
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.query" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.search_query, l.fields ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "Customers"
				QUERY 	= "{ l.query }"
				FIELDS	= "{ l.fields }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.record" VARIABLE = "l.all_settings">
		<MvMEMBER NAME = "record">
	</MvREFERENCEARRAY>

	<MvASSIGN NAME = "l.delimiter"	VALUE = "{ TemplateFeed_Convert_Delimiter_To_Char( l.settings:delimiter ) }">
	<MvASSIGN NAME = "l.counter"	VALUE = 0>

	<MvWHILE EXPR = "{ NOT Customers.d.EOF }">
		<MvEVAL EXPR = "{ TemplateFeed_Customer_Read( l.record, l.plans, l.plan_count, l.settings:encoding ) }">
		<MvEVAL EXPR = "{ TemplateFeed_Encode_Record( l.record, l.settings:encoding, l.delimiter ) }">
		<MvEVAL EXPR = "{ TemplateFeed_IteratorTemplate_Render( l.iterator_template_name, l.all_settings, l.counter ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Customers" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Customers">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_IteratorTemplate_Render" PARAMETERS = "iterator_template_name, all_settings var, counter var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.all_settings:record:pos1" VALUE = "{ ++l.counter }">

	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.iterator_template_name, l.all_settings ) }">
</MvFUNCTION>

<MvCOMMENT>
|
| Product Related Helper Functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "TemplateFeed_Product_DefaultSettings" PARAMETERS = "settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings"								VALUE = "">
	<MvASSIGN NAME = "l.settings:mode"							VALUE = "product">
	<MvASSIGN NAME = "l.settings:load"							VALUE = "active">
	<MvASSIGN NAME = "l.settings:in_value"						VALUE = "">
	<MvASSIGN NAME = "l.settings:encoding"						VALUE = "csv">
	<MvASSIGN NAME = "l.settings:delimiter"						VALUE = "comma">
	<MvASSIGN NAME = "l.settings:advanced"						VALUE = 0>
	<MvASSIGN NAME = "l.settings:products:predict_discounts"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:products:load_variants"		VALUE = 0>

	<MvEVAL EXPR = "{ TemplateFeed_Product_DefaultFields_Master( l.settings:products:fields ) }">
	<MvEVAL EXPR = "{ TemplateFeed_Product_DefaultFields_Variant( l.settings:products:variant_fields ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_DefaultFields_Master" PARAMETERS = "fields var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.fields:code"					VALUE = 1>
	<MvASSIGN NAME = "l.fields:name"					VALUE = 1>
	<MvASSIGN NAME = "l.fields:sku"						VALUE = 1>
	<MvASSIGN NAME = "l.fields:price"					VALUE = 1>
	<MvASSIGN NAME = "l.fields:cost"					VALUE = 1>
	<MvASSIGN NAME = "l.fields:weight"					VALUE = 1>
	<MvASSIGN NAME = "l.fields:description"				VALUE = 1>
	<MvASSIGN NAME = "l.fields:fullsize_image"			VALUE = 0>
	<MvASSIGN NAME = "l.fields:thumbnail_image"			VALUE = 0>
	<MvASSIGN NAME = "l.fields:active"					VALUE = 1>
	<MvASSIGN NAME = "l.fields:dt_created"				VALUE = 0>
	<MvASSIGN NAME = "l.fields:dt_updated"				VALUE = 0>
	<MvASSIGN NAME = "l.fields:taxable"					VALUE = 1>
	<MvASSIGN NAME = "l.fields:url"						VALUE = 1>
	<MvASSIGN NAME = "l.fields:page_title"				VALUE = 0>
	<MvASSIGN NAME = "l.fields:alternate_display_page"	VALUE = 0>
	<MvASSIGN NAME = "l.fields:canonical_category"		VALUE = 1>
	<MvASSIGN NAME = "l.fields:canonical_uri"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:inventory"				VALUE = 1>
	<MvASSIGN NAME = "l.fields:shipping_width"			VALUE = 0>
	<MvASSIGN NAME = "l.fields:shipping_length"			VALUE = 0>
	<MvASSIGN NAME = "l.fields:shipping_height"			VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_DefaultFields_Variant" PARAMETERS = "fields var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.fields:code"					VALUE = 0>
	<MvASSIGN NAME = "l.fields:name"					VALUE = 0>
	<MvASSIGN NAME = "l.fields:sku"						VALUE = 0>
	<MvASSIGN NAME = "l.fields:price"					VALUE = 0>
	<MvASSIGN NAME = "l.fields:cost"					VALUE = 0>
	<MvASSIGN NAME = "l.fields:weight"					VALUE = 0>
	<MvASSIGN NAME = "l.fields:description"				VALUE = 0>
	<MvASSIGN NAME = "l.fields:fullsize_image"			VALUE = 0>
	<MvASSIGN NAME = "l.fields:thumbnail_image"			VALUE = 0>
	<MvASSIGN NAME = "l.fields:active"					VALUE = 0>
	<MvASSIGN NAME = "l.fields:dt_created"				VALUE = 0>
	<MvASSIGN NAME = "l.fields:dt_updated"				VALUE = 0>
	<MvASSIGN NAME = "l.fields:taxable"					VALUE = 0>
	<MvASSIGN NAME = "l.fields:inventory"				VALUE = 0>
	<MvASSIGN NAME = "l.fields:shipping_width"			VALUE = 0>
	<MvASSIGN NAME = "l.fields:shipping_length"			VALUE = 0>
	<MvASSIGN NAME = "l.fields:shipping_height"			VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_GenerateCode_Master" PARAMETERS = "settings var, header_template_source var, iterator_template_source var, footer_template_source var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.headers"		VALUE = "">
	<MvASSIGN NAME = "l.iterators"		VALUE = "">
	<MvASSIGN NAME = "l.footers"		VALUE = "">
	<MvASSIGN NAME = "l.header_count"	VALUE = 0>
	<MvASSIGN NAME = "l.iterator_count" VALUE = 0>
	<MvASSIGN NAME = "l.footer_count"	VALUE = 0>

	<MvREFERENCE NAME = "l.product_settings" VARIABLE = "l.settings:products">

	<MvIF EXPR = "{ l.product_settings:fields:code }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers, 	'PRODUCT_CODE',							-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:code;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:name }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_NAME',							-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:name;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:sku }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_SKU',							-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:sku;',						-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:price }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_PRICE',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:price;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:cost }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_COST',							-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:cost;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:weight }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_WEIGHT',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:weight;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:description }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_DESCRIPTION',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:descrip;', 				-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:fullsize_image }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_IMAGE',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:image;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:thumbnail_image }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_THUMBNAIL',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:thumbnail;',				-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:active }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_ACTIVE',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:active;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:dt_created }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_CREATED_TIMESTAMP',			-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:dt_created;',				-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:dt_updated }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_LAST_UPDATED_TIMESTAMP',		-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:dt_updated;',				-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:taxable }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_TAXABLE',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:taxable;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:url }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_URL',							-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:url;',						-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:page_title }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_PAGE_TITLE',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:page_title;',				-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:alternate_display_page }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_ALTERNATE_DISPLAY_PAGE',		-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:alternate_display_page;',	-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:canonical_category }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_CANONICAL_CATEGORY',			-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:canonical_category;',		-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:canonical_uri }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_CANONICAL_URI',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:canonical_uri;',			-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:inventory }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_INVENTORY',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:inventory;',				-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:shipping_width }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_SHIPPING_WIDTH',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:shipping_width;',			-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:shipping_length }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_SHIPPING_LENGTH',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:shipping_length;',			-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:fields:shipping_height }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'PRODUCT_SHIPPING_HEIGHT',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:shipping_height;',			-1 ) }">
	</MvIF>

	<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.product_settings:customfields">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ModuleAndFeatures_Load_Code( l.customfield:module, l.module ) OR
						NOT l.module:feature_hash:fields_prod }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.customfield_name"	VALUE = "{ TemplateFeed_Normalize_CustomField_Name( [ g.Module_Root $ l.module:module ].Module_Product_Field_Name( l.module, l.customfield:code ) ) }">
		<MvASSIGN NAME = "l.header_count"		VALUE = "{ miva_array_insert( l.headers, 'PRODUCT_CUSTOMFIELD_' $ l.customfield_name, -1 ) }">
		<MvASSIGN NAME = "l.iterator_count"		VALUE = "{ miva_array_insert( l.iterators, '&mvt:record:customfield:' $ TemplateFeed_Normalize_CustomField_Code( l.customfield:code ) $ ';', -1 ) }">
	</MvFOREACH>

	<MvIF EXPR = "{ l.product_settings:load_variants }">
		<MvEVAL EXPR = "{ TemplateFeed_Product_GenerateCode_Variant( l.product_settings, l.headers, l.header_count, l.iterators, l.iterator_count, l.footers, l.footer_count ) }">
	</MvIF>

	<MvASSIGN NAME = "l.header_template_source"		VALUE = "{ TemplateFeed_Generate_RowCode( l.settings:delimiter, l.headers,		l.header_count )	$ '&mvt:eol:crlf;' }">
	<MvASSIGN NAME = "l.iterator_template_source"	VALUE = "{ TemplateFeed_Generate_RowCode( l.settings:delimiter, l.iterators,	l.iterator_count )	$ '&mvt:eol:crlf;' }">
	<MvASSIGN NAME = "l.footer_template_source"		VALUE = "{ TemplateFeed_Generate_RowCode( l.settings:delimiter, l.footers,		l.footer_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_GenerateCode_Variant" PARAMETERS = "product_settings var, headers var, header_count var, iterators var, iterator_count var, footers var, footer_count var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.product_settings:variant_fields:code }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers, 	'VARIANT_PRODUCT_CODE',							-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:variant:code;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:variant_fields:name }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'VARIANT_PRODUCT_NAME',							-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:variant:name;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:variant_fields:sku }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'VARIANT_PRODUCT_SKU',							-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:variant:sku;',						-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:variant_fields:price }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'VARIANT_PRODUCT_PRICE',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:variant:price;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:variant_fields:cost }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'VARIANT_PRODUCT_COST',							-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:variant:cost;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:variant_fields:weight }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'VARIANT_PRODUCT_WEIGHT',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:variant:weight;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:variant_fields:description }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'VARIANT_PRODUCT_DESCRIPTION',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:variant:descrip;', 				-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:variant_fields:fullsize_image }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'VARIANT_PRODUCT_IMAGE',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:variant:image;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:variant_fields:thumbnail_image }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'VARIANT_PRODUCT_THUMBNAIL',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:variant:thumbnail;',				-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:variant_fields:active }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'VARIANT_PRODUCT_ACTIVE',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:variant:active;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:variant_fields:dt_created }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'VARIANT_PRODUCT_CREATED_TIMESTAMP',			-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:variant:dt_created;',				-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:variant_fields:dt_updated }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'VARIANT_PRODUCT_LAST_UPDATED_TIMESTAMP',		-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:variant:dt_updated;',				-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:variant_fields:taxable }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'VARIANT_PRODUCT_TAXABLE',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:variant:taxable;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:variant_fields:inventory }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'VARIANT_PRODUCT_INVENTORY',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:variant:inventory;',				-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:variant_fields:shipping_width }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'VARIANT_PRODUCT_SHIPPING_WIDTH',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:variant:shipping_width;',			-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:variant_fields:shipping_length }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'VARIANT_PRODUCT_SHIPPING_LENGTH',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:variant:shipping_length;',			-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.product_settings:variant_fields:shipping_height }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'VARIANT_PRODUCT_SHIPPING_HEIGHT',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:variant:shipping_height;',			-1 ) }">
	</MvIF>

	<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.product_settings:customfields">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ModuleAndFeatures_Load_Code( l.customfield:module, l.module ) OR
						NOT l.module:feature_hash:fields_prod }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.customfield_name"	VALUE = "{ TemplateFeed_Normalize_CustomField_Name( [ g.Module_Root $ l.module:module ].Module_Product_Field_Name( l.module, l.customfield:code ) ) }">
		<MvASSIGN NAME = "l.header_count"		VALUE = "{ miva_array_insert( l.headers, 'VARIANT_PRODUCT_CUSTOMFIELD_' $ l.customfield_name, -1 ) }">
		<MvASSIGN NAME = "l.iterator_count"		VALUE = "{ miva_array_insert( l.iterators, '&mvt:record:variant:customfield:' $ TemplateFeed_Normalize_CustomField_Code( l.customfield:code ) $ ';', -1 ) }">
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_Read_Master" PARAMETERS = "product var, plans var, plan_count, product_url_flags var, encoding" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ TemplateFeed_Product_CustomFields_Read_Values( TemplateFeed_Products.d.id, l.plans, l.plan_count, l.customfields_structure ) }">

	<MvASSIGN NAME = "l.product"								VALUE = "">
	<MvASSIGN NAME = "l.product:id"								VALUE = "{ TemplateFeed_Products.d.id }">
	<MvASSIGN NAME = "l.product:catcount"						VALUE = "{ TemplateFeed_Products.d.catcount }">
	<MvASSIGN NAME = "l.product:cancat_id"						VALUE = "{ TemplateFeed_Products.d.cancat_id }">
	<MvASSIGN NAME = "l.product:agrpcount"						VALUE = "{ TemplateFeed_Products.d.agrpcount }">
	<MvASSIGN NAME = "l.product:pgrpcount"						VALUE = "{ TemplateFeed_Products.d.pgrpcount }">
	<MvASSIGN NAME = "l.product:disp_order"						VALUE = "{ TemplateFeed_Products.d.disp_order }">
	<MvASSIGN NAME = "l.product:page_code"						VALUE = "{ TemplateFeed_Products.d.page_code }">
	<MvASSIGN NAME = "l.product:page_id"						VALUE = "{ TemplateFeed_Products.d.page_id }">
	<MvASSIGN NAME = "l.product:code"							VALUE = "{ TemplateFeed_Products.d.code }">
	<MvASSIGN NAME = "l.product:sku"							VALUE = "{ TemplateFeed_Products.d.sku }">
	<MvASSIGN NAME = "l.product:name"							VALUE = "{ TemplateFeed_Products.d.name }">
	<MvASSIGN NAME = "l.product:page_title"						VALUE = "{ TemplateFeed_Products.d.page_title }">
	<MvASSIGN NAME = "l.product:thumbnail"						VALUE = "{ TemplateFeed_Products.d.thumbnail }">
	<MvASSIGN NAME = "l.product:image"							VALUE = "{ TemplateFeed_Products.d.image }">
	<MvASSIGN NAME = "l.product:price"							VALUE = "{ TemplateFeed_Products.d.price }">
	<MvASSIGN NAME = "l.product:original_price"					VALUE = "{ TemplateFeed_Products.d.price }">
	<MvASSIGN NAME = "l.product:cost"							VALUE = "{ TemplateFeed_Products.d.cost }">
	<MvASSIGN NAME = "l.product:descrip"						VALUE = "{ TemplateFeed_Products.d.descrip }">
	<MvASSIGN NAME = "l.product:weight"							VALUE = "{ TemplateFeed_Products.d.weight }">
	<MvASSIGN NAME = "l.product:taxable"						VALUE = "{ TemplateFeed_Products.d.taxable }">
	<MvASSIGN NAME = "l.product:active"							VALUE = "{ TemplateFeed_Products.d.active }">
	<MvASSIGN NAME = "l.product:dt_created"						VALUE = "{ TemplateFeed_Products.d.dt_created }">
	<MvASSIGN NAME = "l.product:dt_updated"						VALUE = "{ TemplateFeed_Products.d.dt_updated }">

	<MvASSIGN NAME = "l.product:predicted_sale_price"			VALUE = "">

	<MvIF EXPR = "{ NOT ISNULL TemplateFeed_Products.d.sale_price }">
		<MvASSIGN NAME = "l.product:predicted_sale_price"		VALUE = "{ TemplateFeed_Products.d.sale_price }">
	</MvIF>

	<MvASSIGN NAME = "l.product:url"							VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Product_URL( l.product, l.product_url_flags ) }">

	<MvASSIGN NAME = "l.product:alternate_display_page"			VALUE = "{ TemplateFeed_Products.d.alternate_display_page }">
	<MvASSIGN NAME = "l.product:canonical_category"				VALUE = "{ TemplateFeed_Products.d.canonical_category }">
	<MvASSIGN NAME = "l.product:canonical_uri"					VALUE = "{ TemplateFeed_Build_URL_From_URI( TemplateFeed_Products.d.canonical_uri ) }">
	<MvASSIGN NAME = "l.product:inventory"						VALUE = "{ TemplateFeed_Products.d.inventory }">
	<MvASSIGN NAME = "l.product:shipping_width"					VALUE = "{ TemplateFeed_Products.d.shipping_width }">
	<MvASSIGN NAME = "l.product:shipping_height"				VALUE = "{ TemplateFeed_Products.d.shipping_height }">
	<MvASSIGN NAME = "l.product:shipping_length"				VALUE = "{ TemplateFeed_Products.d.shipping_length }">

	<MvASSIGN NAME = "l.product:cost_formatted"					VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.product:cost ) }">
	<MvASSIGN NAME = "l.product:price_formatted"				VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.product:price ) }">
	<MvASSIGN NAME = "l.product:original_price_formatted"		VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.product:original_price ) }">

	<MvEVAL EXPR = "{ TemplateFeed_CustomFields_Build_Values( l.product, l.customfields_structure, l.encoding ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_Read_Variant" PARAMETERS = "product var, variant var, plans var, plan_count, encoding" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ TemplateFeed_Product_CustomFields_Read_Values( TemplateFeed_Products.d.variant_id, l.plans, l.plan_count, l.customfields_structure ) }">

	<MvASSIGN NAME = "l.variant"									VALUE = "">
	<MvASSIGN NAME = "l.variant:id"									VALUE = "{ TemplateFeed_Products.d.variant_id }">
	<MvASSIGN NAME = "l.variant:catcount"							VALUE = "{ TemplateFeed_Products.d.variant_catcount }">
	<MvASSIGN NAME = "l.variant:cancat_id"							VALUE = "{ TemplateFeed_Products.d.variant_cancat_id }">
	<MvASSIGN NAME = "l.variant:agrpcount"							VALUE = "{ TemplateFeed_Products.d.variant_agrpcount }">
	<MvASSIGN NAME = "l.variant:pgrpcount"							VALUE = "{ TemplateFeed_Products.d.variant_pgrpcount }">
	<MvASSIGN NAME = "l.variant:disp_order"							VALUE = "{ TemplateFeed_Products.d.variant_disp_order }">
	<MvASSIGN NAME = "l.variant:page_id"							VALUE = "{ TemplateFeed_Products.d.variant_page_id }">
	<MvASSIGN NAME = "l.variant:page_code"							VALUE = "{ TemplateFeed_Products.d.variant_page_code }">
	<MvASSIGN NAME = "l.variant:code"								VALUE = "{ TemplateFeed_Products.d.variant_code }">
	<MvASSIGN NAME = "l.variant:sku"								VALUE = "{ TemplateFeed_Products.d.variant_sku }">
	<MvASSIGN NAME = "l.variant:name"								VALUE = "{ TemplateFeed_Products.d.variant_name }">
	<MvASSIGN NAME = "l.variant:thumbnail"							VALUE = "{ TemplateFeed_Products.d.variant_thumbnail }">
	<MvASSIGN NAME = "l.variant:image"								VALUE = "{ TemplateFeed_Products.d.variant_image }">
	<MvASSIGN NAME = "l.variant:price"								VALUE = "{ TemplateFeed_Products.d.variant_price }">
	<MvASSIGN NAME = "l.variant:cost"								VALUE = "{ TemplateFeed_Products.d.variant_cost }">
	<MvASSIGN NAME = "l.variant:descrip"							VALUE = "{ TemplateFeed_Products.d.variant_descrip }">
	<MvASSIGN NAME = "l.variant:weight"								VALUE = "{ TemplateFeed_Products.d.variant_weight }">
	<MvASSIGN NAME = "l.variant:taxable"							VALUE = "{ TemplateFeed_Products.d.variant_taxable }">
	<MvASSIGN NAME = "l.variant:active"								VALUE = "{ TemplateFeed_Products.d.variant_active }">
	<MvASSIGN NAME = "l.variant:dt_created"							VALUE = "{ TemplateFeed_Products.d.variant_dt_created }">
	<MvASSIGN NAME = "l.variant:dt_updated"							VALUE = "{ TemplateFeed_Products.d.variant_dt_updated }">
	<MvASSIGN NAME = "l.variant:inventory"							VALUE = "{ TemplateFeed_Products.d.variant_inventory }">
	<MvASSIGN NAME = "l.variant:shipping_width"						VALUE = "{ TemplateFeed_Products.d.variant_shipping_width }">
	<MvASSIGN NAME = "l.variant:shipping_length"					VALUE = "{ TemplateFeed_Products.d.variant_shipping_length }">
	<MvASSIGN NAME = "l.variant:shipping_height"					VALUE = "{ TemplateFeed_Products.d.variant_shipping_height }">
	<MvASSIGN NAME = "l.variant:variant_id"							VALUE = "{ TemplateFeed_Products.d.pvp_variant_id }">
	<MvASSIGN NAME = "l.variant:quantity"							VALUE = "{ TemplateFeed_Products.d.pvp_quantity }">

	<MvASSIGN NAME = "l.variant:price_formatted"					VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.variant:price ) }">
	<MvASSIGN NAME = "l.variant:cost_formatted"						VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.variant:cost ) }">

	<MvASSIGN NAME = "l.product:variant_id"							VALUE = "{ TemplateFeed_Products.d.pvp_variant_id }">

	<MvCOMMENT>
	|
	| Clear the master predicted_sale_price as the value should contain the part product
	| predicted_sale_price (if one exists)
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.product:predicted_sale_price"				VALUE = "">

	<MvIF EXPR = "{ NOT ISNULL TemplateFeed_Products.d.variant_sale_price }">
		<MvASSIGN NAME = "l.product:predicted_sale_price"			VALUE = "{ TemplateFeed_Products.d.variant_sale_price }">
	</MvIF>

	<MvASSIGN NAME = "l.product:part_count"							VALUE = "{ TemplateFeed_Products.d.pv_part_count }">

	<MvIF EXPR = "{ ISNULL TemplateFeed_Products.d.pvprice_method }">
		<MvASSIGN NAME = "l.product:pricing_method"					VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.product:pricing_method"					VALUE = "{ TemplateFeed_Products.d.pvprice_method }">
		<MvASSIGN NAME = "l.product:pricing_price"					VALUE = "{ TemplateFeed_Products.d.pvprice_price }">
		<MvASSIGN NAME = "l.product:pricing_cost"					VALUE = "{ TemplateFeed_Products.d.pvprice_cost }">
		<MvASSIGN NAME = "l.product:pricing_weight"					VALUE = "{ TemplateFeed_Products.d.pvprice_weight }">
	</MvIF>

	<MvEVAL EXPR = "{ TemplateFeed_CustomFields_Build_Values( l.variant, l.customfields_structure, l.encoding ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_Read_Attribute" PARAMETERS = "pending_record var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ TemplateFeed_Products.d.variant_attribute_id }">
		<MvREFERENCEARRAY NAME = "l.attribute" VARIABLE = "l.pending_record:attribute_data">
			<MvDIMENSION INDEX = "{ TemplateFeed_Products.d.variant_attribute_disp_order }">
		</MvREFERENCEARRAY>

		<MvIF EXPR = "{ ISNULL l.attribute }">
			<MvEVAL EXPR = "{ TemplateFeed_Product_Read_Attribute_Variant( l.attribute ) }">
		</MvIF>

		<MvIF EXPR = "{ TemplateFeed_Products.d.variant_attribute_template_attribute_id }">
			<MvREFERENCEARRAY NAME = "l.attribute" VARIABLE = "l.attribute:attributes">
				<MvDIMENSION INDEX = "{ TemplateFeed_Products.d.variant_attribute_template_attribute_disporder }">
			</MvREFERENCEARRAY>

			<MvIF EXPR = "{ ISNULL l.attribute }">
				<MvEVAL EXPR = "{ TemplateFeed_Product_Read_Attribute_Template_Attribute_Variant( l.attribute ) }">
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ NOT TemplateFeed_Attribute_Supports_Options( l.attribute ) }">
			<MvASSIGN NAME = "l.attribute:value"	VALUE = "{ TemplateFeed_Products.d.pv_option_id }">
		<MvELSEIF EXPR = "{ TemplateFeed_Products.d.variant_option_id OR ( TemplateFeed_Products.d.variant_attribute_template_attribute_id AND TemplateFeed_Products.d.variant_attribute_template_option_id ) }">
			<MvIF EXPR = "{ TemplateFeed_Products.d.variant_attribute_template_attribute_id AND TemplateFeed_Products.d.variant_attribute_template_option_id }">
				<MvREFERENCEARRAY NAME = "l.option" VARIABLE = "l.attribute:options">
					<MvDIMENSION INDEX = "{ TemplateFeed_Products.d.variant_attribute_template_option_disporder }">
				</MvREFERENCEARRAY>

				<MvEVAL EXPR = "{ TemplateFeed_Product_Read_Attribute_Template_Option_Variant( l.option ) }">
			<MvELSE>
				<MvREFERENCEARRAY NAME = "l.option" VARIABLE = "l.attribute:options">
					<MvDIMENSION INDEX = "{ TemplateFeed_Products.d.variant_option_disp_order }">
				</MvREFERENCEARRAY>

				<MvEVAL EXPR = "{ TemplateFeed_Product_Read_Attribute_Option_Variant( l.option ) }">
			</MvIF>
		</MvIF>
	<MvELSEIF EXPR = "{ TemplateFeed_Products.d.attribute_id }">
		<MvREFERENCEARRAY NAME = "l.attribute" VARIABLE = "l.pending_record:attribute_data">
			<MvDIMENSION INDEX = "{ TemplateFeed_Products.d.attribute_disp_order }">
		</MvREFERENCEARRAY>

		<MvIF EXPR = "{ ISNULL l.attribute }">
			<MvEVAL EXPR = "{ TemplateFeed_Product_Read_Attribute_Master( l.attribute ) }">
		</MvIF>

		<MvIF EXPR = "{ TemplateFeed_Products.d.attribute_template_attribute_id }">
			<MvREFERENCEARRAY NAME = "l.attribute" VARIABLE = "l.attribute:attributes">
				<MvDIMENSION INDEX = "{ TemplateFeed_Products.d.attribute_template_attribute_disporder }">
			</MvREFERENCEARRAY>

			<MvIF EXPR = "{ ISNULL l.attribute }">
				<MvEVAL EXPR = "{ TemplateFeed_Product_Read_Attribute_Template_Attribute_Master( l.attribute ) }">
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ TemplateFeed_Products.d.option_id OR TemplateFeed_Products.d.attribute_template_option_id }">
			<MvIF EXPR = "{ TemplateFeed_Products.d.attribute_template_option_id }">
				<MvREFERENCEARRAY NAME = "l.option" VARIABLE = "l.attribute:options">
					<MvDIMENSION INDEX = "{ TemplateFeed_Products.d.attribute_template_option_disporder }">
				</MvREFERENCEARRAY>

				<MvEVAL EXPR = "{ TemplateFeed_Product_Read_Attribute_Template_Option_Master( l.option ) }">
			<MvELSE>
				<MvREFERENCEARRAY NAME = "l.option" VARIABLE = "l.attribute:options">
					<MvDIMENSION INDEX = "{ TemplateFeed_Products.d.option_disp_order }">
				</MvREFERENCEARRAY>

				<MvEVAL EXPR = "{ TemplateFeed_Product_Read_Attribute_Option_Master( l.option ) }">
			</MvIF>
		</MvIF>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_Read_Attribute_Master" PARAMETERS = "attribute var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.attribute" 						VALUE = "">
	<MvASSIGN NAME = "l.attribute:option_count" 		VALUE = 0>
	<MvASSIGN NAME = "l.attribute:options" 				VALUE = "">

	<MvASSIGN NAME = "l.attribute:id"					VALUE = "{ TemplateFeed_Products.d.attribute_id }">
	<MvASSIGN NAME = "l.attribute:product_id"			VALUE = "{ TemplateFeed_Products.d.attribute_product_id }">
	<MvASSIGN NAME = "l.attribute:default_id"			VALUE = "{ TemplateFeed_Products.d.attribute_default_id }">
	<MvASSIGN NAME = "l.attribute:disp_order"			VALUE = "{ TemplateFeed_Products.d.attribute_disp_order }">
	<MvASSIGN NAME = "l.attribute:code"					VALUE = "{ TemplateFeed_Products.d.attribute_code }">
	<MvASSIGN NAME = "l.attribute:type"					VALUE = "{ TemplateFeed_Products.d.attribute_type }">
	<MvASSIGN NAME = "l.attribute:attemp_id"			VALUE = "{ TemplateFeed_Products.d.attribute_attemp_id }">
	<MvASSIGN NAME = "l.attribute:prompt"				VALUE = "{ TemplateFeed_Products.d.attribute_prompt }">
	<MvASSIGN NAME = "l.attribute:price"				VALUE = "{ TemplateFeed_Products.d.attribute_price }">
	<MvASSIGN NAME = "l.attribute:cost"					VALUE = "{ TemplateFeed_Products.d.attribute_cost }">
	<MvASSIGN NAME = "l.attribute:weight"				VALUE = "{ TemplateFeed_Products.d.attribute_weight }">
	<MvASSIGN NAME = "l.attribute:required"				VALUE = "{ TemplateFeed_Products.d.attribute_required }">
	<MvASSIGN NAME = "l.attribute:inventory"			VALUE = "{ TemplateFeed_Products.d.attribute_inventory }">
	<MvASSIGN NAME = "l.attribute:image"				VALUE = "{ TemplateFeed_Products.d.attribute_image }">

	<MvASSIGN NAME = "l.attribute:formatted_price"		VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.attribute:price ) }">
	<MvASSIGN NAME = "l.attribute:formatted_cost"		VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.attribute:cost ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_Read_Attribute_Template_Attribute_Master" PARAMETERS = "attribute var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.attribute" 						VALUE = "">
	<MvASSIGN NAME = "l.attribute:option_count" 		VALUE = 0>
	<MvASSIGN NAME = "l.attribute:options" 				VALUE = "">

	<MvASSIGN NAME = "l.attribute:id"				VALUE = "{ TemplateFeed_Products.d.attribute_template_attribute_id }">
	<MvASSIGN NAME = "l.attribute:attemp_id"		VALUE = "{ TemplateFeed_Products.d.attribute_template_attribute_attemp_id }">
	<MvASSIGN NAME = "l.attribute:default_id"		VALUE = "{ TemplateFeed_Products.d.attribute_template_attribute_default_id }">
	<MvASSIGN NAME = "l.attribute:disporder"		VALUE = "{ TemplateFeed_Products.d.attribute_template_attribute_disporder }">
	<MvASSIGN NAME = "l.attribute:code"				VALUE = "{ TemplateFeed_Products.d.attribute_template_attribute_code }">
	<MvASSIGN NAME = "l.attribute:type"				VALUE = "{ TemplateFeed_Products.d.attribute_template_attribute_type }">
	<MvASSIGN NAME = "l.attribute:prompt"			VALUE = "{ TemplateFeed_Products.d.attribute_template_attribute_prompt }">
	<MvASSIGN NAME = "l.attribute:price"			VALUE = "{ TemplateFeed_Products.d.attribute_template_attribute_price }">
	<MvASSIGN NAME = "l.attribute:cost"				VALUE = "{ TemplateFeed_Products.d.attribute_template_attribute_cost }">
	<MvASSIGN NAME = "l.attribute:weight"			VALUE = "{ TemplateFeed_Products.d.attribute_template_attribute_weight }">
	<MvASSIGN NAME = "l.attribute:required"			VALUE = "{ TemplateFeed_Products.d.attribute_template_attribute_required }">
	<MvASSIGN NAME = "l.attribute:inventory"		VALUE = "{ TemplateFeed_Products.d.attribute_template_attribute_inventory }">
	<MvASSIGN NAME = "l.attribute:image"			VALUE = "{ TemplateFeed_Products.d.attribute_template_attribute_image }">

	<MvASSIGN NAME = "l.attribute:formatted_price"	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.attribute:price ) }">
	<MvASSIGN NAME = "l.attribute:formatted_cost"	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.attribute:cost ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_Read_Attribute_Option_Master" PARAMETERS = "option var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.option" 						VALUE = "">
	<MvASSIGN NAME = "l.option:id" 						VALUE = "{ TemplateFeed_Products.d.option_id }">
	<MvASSIGN NAME = "l.option:product_id" 				VALUE = "{ TemplateFeed_Products.d.option_product_id }">
	<MvASSIGN NAME = "l.option:attr_id" 				VALUE = "{ TemplateFeed_Products.d.option_attr_id }">
	<MvASSIGN NAME = "l.option:disp_order" 				VALUE = "{ TemplateFeed_Products.d.option_disp_order }">
	<MvASSIGN NAME = "l.option:code" 					VALUE = "{ TemplateFeed_Products.d.option_code }">
	<MvASSIGN NAME = "l.option:prompt" 					VALUE = "{ TemplateFeed_Products.d.option_prompt }">
	<MvASSIGN NAME = "l.option:price" 					VALUE = "{ TemplateFeed_Products.d.option_price }">
	<MvASSIGN NAME = "l.option:cost" 					VALUE = "{ TemplateFeed_Products.d.option_cost }">
	<MvASSIGN NAME = "l.option:weight" 					VALUE = "{ TemplateFeed_Products.d.option_weight }">
	<MvASSIGN NAME = "l.option:image" 					VALUE = "{ TemplateFeed_Products.d.option_image }">

	<MvASSIGN NAME = "l.option:formatted_price"			VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.option:price ) }">
	<MvASSIGN NAME = "l.option:formatted_cost"			VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.option:cost ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_Read_Attribute_Template_Option_Master" PARAMETERS = "option var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.option" 					VALUE = "">
	<MvASSIGN NAME = "l.option:id"					VALUE = "{ TemplateFeed_Products.d.attribute_template_option_id }">
	<MvASSIGN NAME = "l.option:attemp_id"			VALUE = "{ TemplateFeed_Products.d.attribute_template_option_attemp_id }">
	<MvASSIGN NAME = "l.option:attmpat_id"			VALUE = "{ TemplateFeed_Products.d.attribute_template_option_attmpat_id }">
	<MvASSIGN NAME = "l.option:disporder"			VALUE = "{ TemplateFeed_Products.d.attribute_template_option_disporder }">
	<MvASSIGN NAME = "l.option:code"				VALUE = "{ TemplateFeed_Products.d.attribute_template_option_code }">
	<MvASSIGN NAME = "l.option:prompt"				VALUE = "{ TemplateFeed_Products.d.attribute_template_option_prompt }">
	<MvASSIGN NAME = "l.option:price"				VALUE = "{ TemplateFeed_Products.d.attribute_template_option_price }">
	<MvASSIGN NAME = "l.option:cost"				VALUE = "{ TemplateFeed_Products.d.attribute_template_option_cost }">
	<MvASSIGN NAME = "l.option:weight"				VALUE = "{ TemplateFeed_Products.d.attribute_template_option_weight }">
	<MvASSIGN NAME = "l.option:image"				VALUE = "{ TemplateFeed_Products.d.attribute_template_option_image }">

	<MvASSIGN NAME = "l.option:formatted_price"		VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.option:price ) }">
	<MvASSIGN NAME = "l.option:formatted_cost"		VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.option:cost ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_Read_Attribute_Variant" PARAMETERS = "attribute var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.attribute" 					VALUE = "">
	<MvASSIGN NAME = "l.attribute:option_count" 	VALUE = 0>
	<MvASSIGN NAME = "l.attribute:options" 			VALUE = "">

	<MvASSIGN NAME = "l.attribute:id"				VALUE = "{ TemplateFeed_Products.d.variant_attribute_id }">
	<MvASSIGN NAME = "l.attribute:product_id"		VALUE = "{ TemplateFeed_Products.d.variant_attribute_product_id }">
	<MvASSIGN NAME = "l.attribute:default_id"		VALUE = "{ TemplateFeed_Products.d.variant_attribute_default_id }">
	<MvASSIGN NAME = "l.attribute:disp_order"		VALUE = "{ TemplateFeed_Products.d.variant_attribute_disp_order }">
	<MvASSIGN NAME = "l.attribute:code"				VALUE = "{ TemplateFeed_Products.d.variant_attribute_code }">
	<MvASSIGN NAME = "l.attribute:type"				VALUE = "{ TemplateFeed_Products.d.variant_attribute_type }">
	<MvASSIGN NAME = "l.attribute:attemp_id"		VALUE = "{ TemplateFeed_Products.d.variant_attribute_attemp_id }">
	<MvASSIGN NAME = "l.attribute:prompt"			VALUE = "{ TemplateFeed_Products.d.variant_attribute_prompt }">
	<MvASSIGN NAME = "l.attribute:price"			VALUE = "{ TemplateFeed_Products.d.variant_attribute_price }">
	<MvASSIGN NAME = "l.attribute:cost"				VALUE = "{ TemplateFeed_Products.d.variant_attribute_cost }">
	<MvASSIGN NAME = "l.attribute:weight"			VALUE = "{ TemplateFeed_Products.d.variant_attribute_weight }">
	<MvASSIGN NAME = "l.attribute:required"			VALUE = "{ TemplateFeed_Products.d.variant_attribute_required }">
	<MvASSIGN NAME = "l.attribute:inventory"		VALUE = "{ TemplateFeed_Products.d.variant_attribute_inventory }">
	<MvASSIGN NAME = "l.attribute:image"			VALUE = "{ TemplateFeed_Products.d.variant_attribute_image }">

	<MvASSIGN NAME = "l.attribute:formatted_price"	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.attribute:price ) }">
	<MvASSIGN NAME = "l.attribute:formatted_cost"	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.attribute:cost ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_Read_Attribute_Template_Attribute_Variant" PARAMETERS = "attribute var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.attribute" 					VALUE = "">
	<MvASSIGN NAME = "l.attribute:option_count" 	VALUE = 0>
	<MvASSIGN NAME = "l.attribute:options" 			VALUE = "">

	<MvASSIGN NAME = "l.attribute:id"				VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_attribute_id }">
	<MvASSIGN NAME = "l.attribute:attemp_id"		VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_attribute_attemp_id }">
	<MvASSIGN NAME = "l.attribute:default_id"		VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_attribute_default_id }">
	<MvASSIGN NAME = "l.attribute:disporder"		VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_attribute_disporder }">
	<MvASSIGN NAME = "l.attribute:code"				VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_attribute_code }">
	<MvASSIGN NAME = "l.attribute:type"				VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_attribute_type }">
	<MvASSIGN NAME = "l.attribute:prompt"			VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_attribute_prompt }">
	<MvASSIGN NAME = "l.attribute:price"			VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_attribute_price }">
	<MvASSIGN NAME = "l.attribute:cost"				VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_attribute_cost }">
	<MvASSIGN NAME = "l.attribute:weight"			VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_attribute_weight }">
	<MvASSIGN NAME = "l.attribute:required"			VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_attribute_required }">
	<MvASSIGN NAME = "l.attribute:inventory"		VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_attribute_inventory }">
	<MvASSIGN NAME = "l.attribute:image"			VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_attribute_image }">

	<MvASSIGN NAME = "l.attribute:formatted_price"	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.attribute:price ) }">
	<MvASSIGN NAME = "l.attribute:formatted_cost"	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.attribute:cost ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_Read_Attribute_Option_Variant" PARAMETERS = "option var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.option" 					VALUE = "">
	<MvASSIGN NAME = "l.option:id" 					VALUE = "{ TemplateFeed_Products.d.variant_option_id }">
	<MvASSIGN NAME = "l.option:product_id" 			VALUE = "{ TemplateFeed_Products.d.variant_option_product_id }">
	<MvASSIGN NAME = "l.option:attr_id" 			VALUE = "{ TemplateFeed_Products.d.variant_option_attr_id }">
	<MvASSIGN NAME = "l.option:disp_order" 			VALUE = "{ TemplateFeed_Products.d.variant_option_disp_order }">
	<MvASSIGN NAME = "l.option:code" 				VALUE = "{ TemplateFeed_Products.d.variant_option_code }">
	<MvASSIGN NAME = "l.option:prompt" 				VALUE = "{ TemplateFeed_Products.d.variant_option_prompt }">
	<MvASSIGN NAME = "l.option:price" 				VALUE = "{ TemplateFeed_Products.d.variant_option_price }">
	<MvASSIGN NAME = "l.option:cost" 				VALUE = "{ TemplateFeed_Products.d.variant_option_cost }">
	<MvASSIGN NAME = "l.option:weight" 				VALUE = "{ TemplateFeed_Products.d.variant_option_weight }">
	<MvASSIGN NAME = "l.option:image" 				VALUE = "{ TemplateFeed_Products.d.variant_option_image }">

	<MvASSIGN NAME = "l.option:formatted_price"		VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.option:price ) }">
	<MvASSIGN NAME = "l.option:formatted_cost"		VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.option:cost ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_Read_Attribute_Template_Option_Variant" PARAMETERS = "option var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.option" 					VALUE = "">
	<MvASSIGN NAME = "l.option:id"					VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_option_id }">
	<MvASSIGN NAME = "l.option:attemp_id"			VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_option_attemp_id }">
	<MvASSIGN NAME = "l.option:attmpat_id"			VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_option_attmpat_id }">
	<MvASSIGN NAME = "l.option:disporder"			VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_option_disporder }">
	<MvASSIGN NAME = "l.option:code"				VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_option_code }">
	<MvASSIGN NAME = "l.option:prompt"				VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_option_prompt }">
	<MvASSIGN NAME = "l.option:price"				VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_option_price }">
	<MvASSIGN NAME = "l.option:cost"				VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_option_cost }">
	<MvASSIGN NAME = "l.option:weight"				VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_option_weight }">
	<MvASSIGN NAME = "l.option:image"				VALUE = "{ TemplateFeed_Products.d.variant_attribute_template_option_image }">

	<MvASSIGN NAME = "l.option:formatted_price"		VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.option:price ) }">
	<MvASSIGN NAME = "l.option:formatted_cost"		VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.option:cost ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_Query_Build_Categories_Master" PARAMETERS = "search_query var, master_correlation_name, in_operator, in_value" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.category_code" ARRAY = "l.category_codes" INDEX = "l.pos" COUNT = "{ miva_splitstring( l.in_value, ',', l.category_codes, 'trim' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Category_Load_Code_Cached( l.category_code, l.category ) }">	<MvASSIGN NAME = "l.category_id" VALUE = 0>
		<MvELSE>																									<MvASSIGN NAME = "l.category_id" VALUE = "{ l.category:id }">
		</MvIF>

		<MvASSIGN NAME = "l.join_prefix"													VALUE = "{ 'cxp' $ l.pos }">
		<MvASSIGN NAME = "g.TemplateFeed_Product_CategoryIDs" INDEX = "{ l.pos }"			VALUE = "{ l.category_id }">

		<MvIF EXPR = "{ l.pos GT 1 }">
			<MvIF EXPR = "{ l.in_operator }">	<MvASSIGN NAME = "l.category_where_clause"	VALUE = "{ l.category_where_clause $ ' OR ' }">
			<MvELSE>							<MvASSIGN NAME = "l.category_where_clause"	VALUE = "{ l.category_where_clause $ ' AND ' }">
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.in_operator }">		<MvASSIGN NAME = "l.category_where_clause"	VALUE = "{ l.category_where_clause $ l.join_prefix $ '.cat_id IS NOT NULL' }">
		<MvELSE>								<MvASSIGN NAME = "l.category_where_clause"	VALUE = "{ l.category_where_clause $ l.join_prefix $ '.cat_id IS NULL' }">
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, l.master_correlation_name, g.Store_Table_Prefix $ 'CategoryXProduct', l.join_prefix, l.join_prefix $ '.product_id = ' $ l.master_correlation_name $ '.id AND ' $ l.join_prefix $ '.cat_id = ?', 'g.TemplateFeed_Product_CategoryIDs[' $ l.pos $ ']'  ) }">
	</MvFOREACH>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, '( ' $ l.category_where_clause $ ' )', '' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_Query_Build_PriceGroups_Master" PARAMETERS = "search_query var, master_correlation_name, in_operator, in_value" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.pricegroup_name" ARRAY = "l.pricegroup_names" INDEX = "l.pos" COUNT = "{ miva_splitstring( l.in_value, ',', l.pricegroup_names, 'trim' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_DB ].PriceGroup_Load_Name( l.pricegroup_name, l.pricegroup ) }">	<MvASSIGN NAME = "l.pricegroup_id" VALUE = 0>
		<MvELSE>																									<MvASSIGN NAME = "l.pricegroup_id" VALUE = "{ l.pricegroup:id }">
		</MvIF>

		<MvASSIGN NAME = "l.join_prefix"														VALUE = "{ 'pgxp' $ l.pos }">
		<MvASSIGN NAME = "g.TemplateFeed_Product_PriceGroupIDs" INDEX = "{ l.pos }"				VALUE = "{ l.pricegroup_id }">

		<MvIF EXPR = "{ l.pos GT 1 }">
			<MvIF EXPR = "{ l.in_operator }">	<MvASSIGN NAME = "l.pricegroup_where_clause"	VALUE = "{ l.pricegroup_where_clause $ ' OR ' }">
			<MvELSE>							<MvASSIGN NAME = "l.pricegroup_where_clause"	VALUE = "{ l.pricegroup_where_clause $ ' AND ' }">
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.in_operator }">		<MvASSIGN NAME = "l.pricegroup_where_clause"	VALUE = "{ l.pricegroup_where_clause $ l.join_prefix $ '.pgrp_id IS NOT NULL' }">
		<MvELSE>								<MvASSIGN NAME = "l.pricegroup_where_clause"	VALUE = "{ l.pricegroup_where_clause $ l.join_prefix $ '.pgrp_id IS NULL' }">
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, l.master_correlation_name, g.Store_Table_Prefix $ 'PriceGroupXProduct', l.join_prefix, l.join_prefix $ '.product_id = ' $ l.master_correlation_name $ '.id AND ' $ l.join_prefix $ '.pgrp_id = ?', 'g.TemplateFeed_Product_PriceGroupIDs[' $ l.pos $ ']'  ) }">
	</MvFOREACH>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, '( ' $ l.pricegroup_where_clause $ ' )', '' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_Query_Build_AvailabilityGroups_Master" PARAMETERS = "search_query var, master_correlation_name, in_operator, in_value" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.availgroup_name" ARRAY = "l.availgroup_names" INDEX = "l.pos" COUNT = "{ miva_splitstring( l.in_value, ',', l.availgroup_names, 'trim' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_AGR_DB ].AvailabilityGroup_Load_Name( l.availgroup_name, l.availgroup ) }">	<MvASSIGN NAME = "l.availgroup_id" VALUE = 0>
		<MvELSE>																											<MvASSIGN NAME = "l.availgroup_id" VALUE = "{ l.availgroup:id }">
		</MvIF>

		<MvASSIGN NAME = "l.join_prefix"														VALUE = "{ 'agxp' $ l.pos }">
		<MvASSIGN NAME = "g.TemplateFeed_Product_AvailabilityGroupIDs" INDEX = "{ l.pos }"		VALUE = "{ l.availgroup_id }">

		<MvIF EXPR = "{ l.pos GT 1 }">
			<MvIF EXPR = "{ l.in_operator }">	<MvASSIGN NAME = "l.availgroup_where_clause"	VALUE = "{ l.availgroup_where_clause $ ' OR ' }">
			<MvELSE>							<MvASSIGN NAME = "l.availgroup_where_clause"	VALUE = "{ l.availgroup_where_clause $ ' AND ' }">
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.in_operator }">		<MvASSIGN NAME = "l.availgroup_where_clause"	VALUE = "{ l.availgroup_where_clause $ l.join_prefix $ '.agrp_id IS NOT NULL' }">
		<MvELSE>								<MvASSIGN NAME = "l.availgroup_where_clause"	VALUE = "{ l.availgroup_where_clause $ l.join_prefix $ '.agrp_id IS NULL' }">
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, l.master_correlation_name, g.Store_Table_Prefix $ 'AvailGroupXProduct', l.join_prefix, l.join_prefix $ '.product_id = ' $ l.master_correlation_name $ '.id AND ' $ l.join_prefix $ '.agrp_id = ?', 'g.TemplateFeed_Product_AvailabilityGroupIDs[' $ l.pos $ ']'  ) }">
	</MvFOREACH>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, '( ' $ l.availgroup_where_clause $ ' )', '' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_QueryAdditions_Master" PARAMETERS = "search_query var, master_correlation_name, settings var, data var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| Build the inclusions / exclusions
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.settings:load EQ 'available' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, l.master_correlation_name $ '.agrpcount = 0', '' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.settings:load EQ 'in_category' }">					<MvEVAL EXPR = "{ TemplateFeed_Product_Query_Build_Categories_Master(			l.search_query, l.master_correlation_name, 1, l.settings:in_value ) }">
	<MvELSEIF EXPR = "{ l.settings:load EQ 'in_price_group' }">				<MvEVAL EXPR = "{ TemplateFeed_Product_Query_Build_PriceGroups_Master( 			l.search_query, l.master_correlation_name, 1, l.settings:in_value ) }">
	<MvELSEIF EXPR = "{ l.settings:load EQ 'in_availability_group' }">		<MvEVAL EXPR = "{ TemplateFeed_Product_Query_Build_AvailabilityGroups_Master(	l.search_query, l.master_correlation_name, 1, l.settings:in_value ) }">
	<MvELSEIF EXPR = "{ l.settings:load EQ 'not_in_category' }">			<MvEVAL EXPR = "{ TemplateFeed_Product_Query_Build_Categories_Master(			l.search_query, l.master_correlation_name, 0, l.settings:in_value ) }">
	<MvELSEIF EXPR = "{ l.settings:load EQ 'not_in_price_group' }">			<MvEVAL EXPR = "{ TemplateFeed_Product_Query_Build_PriceGroups_Master( 			l.search_query, l.master_correlation_name, 0, l.settings:in_value ) }">
	<MvELSEIF EXPR = "{ l.settings:load EQ 'not_in_availability_group' }">	<MvEVAL EXPR = "{ TemplateFeed_Product_Query_Build_AvailabilityGroups_Master(	l.search_query, l.master_correlation_name, 0, l.settings:in_value ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| Get the alternate display page id
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, l.master_correlation_name $ '.page_code AS alternate_display_page' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT_NULL_INTEGER( l.search_query, 'pbv.page_id',	'page_id' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, l.master_correlation_name, g.Store_Table_Prefix $ 'PageBranchVersions', 'pbv', l.master_correlation_name $ '.page_code <> \'\' AND pbv.branch_id = ? AND pbv.head = 1 AND ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'pbv.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( l.master_correlation_name $ '.page_code' ), [ g.Module_Library_DB ].SQL_Query_Field( [ g.Module_Feature_TUI_MGR ].TemplateManager_Working_Branch_ID() ) ) }">

	<MvCOMMENT>
	|
	| Get the canonical category code
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'cat1.code AS canonical_category' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, l.master_correlation_name, g.Store_Table_Prefix $ 'Categories', 'cat1', 'cat1.id = ' $ l.master_correlation_name $ '.cancat_id', '' ) }">

	<MvCOMMENT>
	|
	| Get the canonical URI
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'uris.uri AS canonical_uri' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, l.master_correlation_name, 'URIs', 'uris', 'uris.store_id = ? AND uris.screen = \'\' AND uris.page_code = \'\' AND uris.cat_id = 0 AND uris.product_id = ' $ l.master_correlation_name $ '.id AND uris.feed_id = 0 AND uris.canonical = 1', 'g.Store:id' ) }">

	<MvCOMMENT>
	|
	| Get inventory if enabled
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ [ g.Module_Feature_INV_DB ].InventorySettings_Load_Cached( l.inventorysettings ) AND l.inventorysettings:active }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'ipc.inventory AS inventory' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, l.master_correlation_name, g.Store_Table_Prefix $ 'InventoryProductCounts', 'ipc', 'ipc.product_id = ' $ l.master_correlation_name $ '.id', '' ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| Get product shipping dimensions
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'psr.width	AS shipping_width,
																				 psr.length AS shipping_length,
																				 psr.height AS shipping_height' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, l.master_correlation_name, g.Store_Table_Prefix $ 'ProductShippingRules', 'psr', 'psr.product_id = ' $ l.master_correlation_name $ '.id', '' ) }">

	<MvCOMMENT>
	|
	| Get predicted discounts
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.settings:products:predict_discounts }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'dp.price AS sale_price' ) }">

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, l.master_correlation_name, g.Store_Table_Prefix $ 'DiscountSignatures',	'ds',	'ds.signature = ?',																	[ g.Module_Library_DB ].SQL_Query_Field( l.data:discount_state_signature ) ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, l.master_correlation_name, g.Store_Table_Prefix $ 'DiscountPrices',		'dp',	'dp.product_id = ' $ l.master_correlation_name $ '.id AND dp.dscsig_id = ds.id',	'' ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_QueryAdditions_Variant" PARAMETERS = "search_query var, master_correlation_name, variant_correlation_name, productvariant_correlation_name, settings var, data var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| Get the alternate display page id
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT_NULL_INTEGER( l.search_query, 'vpbv.page_id', 'variant_page_id' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, l.master_correlation_name, g.Store_Table_Prefix $ 'PageBranchVersions', 'vpbv', l.variant_correlation_name $ '.page_code <> \'\' AND vpbv.branch_id = ? AND vpbv.head = 1 AND ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'vpbv.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( l.variant_correlation_name $ '.page_code' ), [ g.Module_Library_DB ].SQL_Query_Field( [ g.Module_Feature_TUI_MGR ].TemplateManager_Working_Branch_ID() ) ) }">

	<MvCOMMENT>
	|
	| Get inventory if enabled
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ [ g.Module_Feature_INV_DB ].InventorySettings_Load_Cached( l.inventorysettings ) AND l.inventorysettings:active }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'variant_ipc.inventory AS variant_inventory' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, l.master_correlation_name, g.Store_Table_Prefix $ 'InventoryProductCounts', 'variant_ipc', 'variant_ipc.product_id = ' $ l.variant_correlation_name $ '.id', '' ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| Get product shipping dimensions
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'variant_psr.width	AS variant_shipping_width,
																				 variant_psr.length	AS variant_shipping_length,
																				 variant_psr.height	AS variant_shipping_height' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, l.master_correlation_name, g.Store_Table_Prefix $ 'ProductShippingRules', 'variant_psr', 'variant_psr.product_id = ' $ l.variant_correlation_name $ '.id', '' ) }">

	<MvCOMMENT>
	|
	| Get predicted discounts
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.settings:products:predict_discounts }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'dvp.price AS variant_sale_price' ) }">

		<MvCOMMENT>
		|
		| sNN_DiscountSignatures ds is joined via TemplateFeed_Product_QueryAdditions_Master
		|
		</MvCOMMENT>

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, l.master_correlation_name, g.Store_Table_Prefix $ 'DiscountVariantPrices',	'dvp',	'dvp.product_id = ' $ l.productvariant_correlation_name $ '.product_id AND dvp.dscsig_id = ds.id',	'' ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_Initialize" PARAMETERS = "settings var, plans var, plan_count var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.plans"			VALUE = "">
	<MvASSIGN NAME = "l.plan_count"		VALUE = 0>

	<MvCOMMENT>
	|
	| Set up custom fields
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ TemplateFeed_Product_Initialize_CustomField_Lookup( l.settings:products:customfields, l.plans, l.plan_count ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_Initialize_DiscountState" PARAMETERS = "discount_state var, discount_state_signature var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_CreateEmpty( l.discount_state ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.pricegroup_count"			VALUE = "{ [ g.Module_Feature_PGR_UT ].ResolvedPriceGroupAndModuleList_Load_Customer_Cached( 0, l.pricegroups ) }">

	<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Set_PriceGroups( l.discount_state, l.pricegroups, l.pricegroup_count ) }">

	<MvASSIGN NAME = "l.discount_state_signature"	VALUE = "{ [ g.Module_Feature_PGR_UT ].PriceGroupList_Signature( l.pricegroups, l.pricegroup_count ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_Initialize_CustomField_Lookup" PARAMETERS = "fields_custom var, plans var, plan_count var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.fields"			VALUE = "">
	<MvASSIGN NAME = "l.field_count"	VALUE = 0>

	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ TemplateFeed_Initialize_xxx_CustomField_ModuleList_Load( l.fields_custom, 'fields_prod', l.modules ) }">
		<MvASSIGN NAME = "l.module_fields"		VALUE = "">
		<MvASSIGN NAME = "l.module_field_count"	VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Product_Fields( l.module, l.module_fields ) }">

		<MvEVAL EXPR = "{ TemplateFeed_Initialize_xxx_CustomField_Filter_ModuleFields( l.module, l.fields_custom, l.module_fields, l.module_field_count, l.fields, l.field_count ) }">
	</MvFOREACH>

	<MvEVAL EXPR = "{ TemplateFeed_Initialize_xxx_CustomField_Lookup( l.fields, l.field_count, 'fields_prod_map', l.plans, l.plan_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Product_CustomFields_Read_Values" PARAMETERS = "product_id, plans var, plan_count, output var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.plan" ARRAY = "l.plans" COUNT = "{ l.plan_count }">
		<MvIF EXPR = "{ NOT ISNULL l.plan:field_map }">
			<MvREFERENCEARRAY NAME = "l.data" VARIABLE = "l.output">
				<MvMEMBER NAME = "{ l.plan:module:code }">
			</MvREFERENCEARRAY>

			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.plan:module:module ].Module_Product_Fields_Mapped( l.plan:module, l.product_id, l.plan:field_map, l.data ) }">
		<MvELSEIF EXPR = "{ l.plan:field_array }">
			<MvIF EXPR = "{ l.plan:module:api_ver LT 9.08 }">
				<MvREFERENCEARRAY NAME = "l.data" VARIABLE = "l.output">
					<MvMEMBER NAME = "{ l.plan:module:code }">
					<MvMEMBER NAME = "{ l.plan:field_code }">
				</MvREFERENCEARRAY>

				<MvASSIGN NAME = "l.data" VALUE = "{ [ g.Module_Root $ l.plan:module:module ].Module_Product_Field_Value( l.plan:module, l.product_id, l.plan:field_code ) }">
			<MvELSE>
				<MvREFERENCEARRAY NAME = "l.data" VARIABLE = "l.output">
					<MvMEMBER NAME = "{ l.plan:module:code }">
					<MvMEMBER NAME = "{ l.plan:field_code }">
				</MvREFERENCEARRAY>

				<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.plan:module:module ].Module_Product_Field_Value_Array( l.plan:module, l.product_id, l.plan:field_code, l.data ) }">
			</MvIF>
		<MvELSE>
			<MvREFERENCEARRAY NAME = "l.data" VARIABLE = "l.output">
				<MvMEMBER NAME = "{ l.plan:module:code }">
				<MvMEMBER NAME = "{ l.plan:field_code }">
			</MvREFERENCEARRAY>

			<MvASSIGN NAME = "l.data" VALUE = "{ [ g.Module_Root $ l.plan:module:module ].Module_Product_Field_Value( l.plan:module, l.product_id, l.plan:field_code ) }">
		</MvIF>
	</MvFOREACH>
</MvFUNCTION>

<MvCOMMENT>
|
| Category Related Helper Functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "TemplateFeed_Category_DefaultSettings" PARAMETERS = "settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings"			VALUE = "">
	<MvASSIGN NAME = "l.settings:mode"		VALUE = "category">
	<MvASSIGN NAME = "l.settings:load"		VALUE = "active">
	<MvASSIGN NAME = "l.settings:in_value"	VALUE = "">
	<MvASSIGN NAME = "l.settings:encoding"	VALUE = "csv">
	<MvASSIGN NAME = "l.settings:delimiter"	VALUE = "comma">
	<MvASSIGN NAME = "l.settings:advanced"	VALUE = 0>

	<MvEVAL EXPR = "{ TemplateFeed_Category_DefaultFields( l.settings:categories:fields ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Category_DefaultFields" PARAMETERS = "fields var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.fields:code"					VALUE = 1>
	<MvASSIGN NAME = "l.fields:name"					VALUE = 1>
	<MvASSIGN NAME = "l.fields:page_title"				VALUE = 0>
	<MvASSIGN NAME = "l.fields:active"					VALUE = 1>
	<MvASSIGN NAME = "l.fields:dt_created"				VALUE = 0>
	<MvASSIGN NAME = "l.fields:dt_updated"				VALUE = 0>
	<MvASSIGN NAME = "l.fields:url"						VALUE = 1>
	<MvASSIGN NAME = "l.fields:parent_category"			VALUE = 0>
	<MvASSIGN NAME = "l.fields:alternate_display_page"	VALUE = 0>
	<MvASSIGN NAME = "l.fields:canonical_uri"			VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Category_GenerateCode" PARAMETERS = "settings var, header_template_source var, iterator_template_source var, footer_template_source var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.headers"			VALUE = "">
	<MvASSIGN NAME = "l.iterators"			VALUE = "">
	<MvASSIGN NAME = "l.footers"			VALUE = "">
	<MvASSIGN NAME = "l.header_count"		VALUE = 0>
	<MvASSIGN NAME = "l.iterator_count"		VALUE = 0>
	<MvASSIGN NAME = "l.footer_count"		VALUE = 0>

	<MvREFERENCE NAME = "l.category_settings" VARIABLE = "l.settings:categories">

	<MvIF EXPR = "{ l.category_settings:fields:code }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CATEGORY_CODE',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:code;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.category_settings:fields:name }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CATEGORY_NAME',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:name;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.category_settings:fields:page_title }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CATEGORY_PAGE_TITLE',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:page_title;',				-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.category_settings:fields:active }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CATEGORY_ACTIVE',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:active;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.category_settings:fields:dt_created }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CATEGORY_CREATED_TIMESTAMP',			-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:dt_created;',				-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.category_settings:fields:dt_updated }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CATEGORY_LAST_UPDATED_TIMESTAMP',		-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:dt_updated;',				-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.category_settings:fields:url }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CATEGORY_URL',							-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:url;',						-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.category_settings:fields:parent_category }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CATEGORY_PARENT_CATEGORY',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:parent_category;',			-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.category_settings:fields:alternate_display_page }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CATEGORY_ALTERNATE_DISPLAY_PAGE',		-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:alternate_display_page;',	-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.category_settings:fields:canonical_uri }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CATEGORY_CANONICAL_URI',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:canonical_uri;',			-1 ) }">
	</MvIF>

	<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.category_settings:customfields">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ModuleAndFeatures_Load_Code( l.customfield:module, l.module ) OR
						NOT l.module:feature_hash:fields_cat }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.customfield_name"	VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Category_Field_Name( l.module, l.customfield:code ) }">
		<MvASSIGN NAME = "l.header_count"		VALUE = "{ miva_array_insert( l.headers,	'CATEGORY_CUSTOMFIELD_' $ TemplateFeed_Normalize_CustomField_Name( l.customfield_name ), -1 ) }">
		<MvASSIGN NAME = "l.iterator_count"		VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:customfield:' $ TemplateFeed_Normalize_CustomField_Code( l.customfield:code ) $ ';', -1 ) }">
	</MvFOREACH>

	<MvASSIGN NAME = "l.header_template_source"		VALUE = "{ TemplateFeed_Generate_RowCode( l.settings:delimiter, l.headers,		l.header_count )	$ '&mvt:eol:crlf;' }">
	<MvASSIGN NAME = "l.iterator_template_source"	VALUE = "{ TemplateFeed_Generate_RowCode( l.settings:delimiter, l.iterators,	l.iterator_count )	$ '&mvt:eol:crlf;' }">
	<MvASSIGN NAME = "l.footer_template_source"		VALUE = "{ TemplateFeed_Generate_RowCode( l.settings:delimiter, l.footers,		l.footer_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Category_Read" PARAMETERS = "category var, plans var, plan_count, category_url_flags var, encoding" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ TemplateFeed_Category_CustomFields_Read_Values( TemplateFeed_Categories.d.id, l.plans, l.plan_count, l.customfields_structure ) }">

	<MvASSIGN NAME = "l.category"							VALUE = "">
	<MvASSIGN NAME = "l.category:id"						VALUE = "{ TemplateFeed_Categories.d.id }">
	<MvASSIGN NAME = "l.category:parent_id"					VALUE = "{ TemplateFeed_Categories.d.parent_id }">
	<MvASSIGN NAME = "l.category:validated_parent_id"		VALUE = "{ TemplateFeed_Categories.d.parent_id }">
	<MvASSIGN NAME = "l.category:original_parent_id"		VALUE = "{ TemplateFeed_Categories.d.parent_id }">
	<MvASSIGN NAME = "l.category:agrpcount"					VALUE = "{ TemplateFeed_Categories.d.agrpcount }">
	<MvASSIGN NAME = "l.category:depth"						VALUE = "{ TemplateFeed_Categories.d.depth }">
	<MvASSIGN NAME = "l.category:disp_order"				VALUE = "{ TemplateFeed_Categories.d.disp_order }">
	<MvASSIGN NAME = "l.category:page_code"					VALUE = "{ TemplateFeed_Categories.d.page_code }">
	<MvASSIGN NAME = "l.category:page_id"					VALUE = "{ TemplateFeed_Categories.d.page_id }">
	<MvASSIGN NAME = "l.category:code"						VALUE = "{ TemplateFeed_Categories.d.code }">
	<MvASSIGN NAME = "l.category:name"						VALUE = "{ TemplateFeed_Categories.d.name }">
	<MvASSIGN NAME = "l.category:page_title"				VALUE = "{ TemplateFeed_Categories.d.page_title }">
	<MvASSIGN NAME = "l.category:active"					VALUE = "{ TemplateFeed_Categories.d.active }">
	<MvASSIGN NAME = "l.category:dt_created"				VALUE = "{ TemplateFeed_Categories.d.dt_created }">
	<MvASSIGN NAME = "l.category:dt_updated"				VALUE = "{ TemplateFeed_Categories.d.dt_updated }">

	<MvASSIGN NAME = "l.category:url"						VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Category_URL( l.category, l.category_url_flags ) }">
	<MvASSIGN NAME = "l.category:parent_category"			VALUE = "{ TemplateFeed_Categories.d.parent_category }">
	<MvASSIGN NAME = "l.category:alternate_display_page"	VALUE = "{ TemplateFeed_Categories.d.alternate_display_page }">
	<MvASSIGN NAME = "l.category:canonical_uri"				VALUE = "{ TemplateFeed_Build_URL_From_URI( TemplateFeed_Categories.d.canonical_uri ) }">

	<MvEVAL EXPR = "{ TemplateFeed_CustomFields_Build_Values( l.category, l.customfields_structure, l.encoding ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Category_Initialize_CustomField_Lookup" PARAMETERS = "fields_custom var, plans var, plan_count var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.fields"			VALUE = "">
	<MvASSIGN NAME = "l.field_count"	VALUE = 0>

	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ TemplateFeed_Initialize_xxx_CustomField_ModuleList_Load( l.fields_custom, 'fields_cat', l.modules ) }">
		<MvASSIGN NAME = "l.module_fields"		VALUE = "">
		<MvASSIGN NAME = "l.module_field_count"	VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Category_Fields( l.module, l.module_fields ) }">

		<MvEVAL EXPR = "{ TemplateFeed_Initialize_xxx_CustomField_Filter_ModuleFields( l.module, l.fields_custom, l.module_fields, l.module_field_count, l.fields, l.field_count ) }">
	</MvFOREACH>

	<MvEVAL EXPR = "{ TemplateFeed_Initialize_xxx_CustomField_Lookup( l.fields, l.field_count, 'fields_cat_map', l.plans, l.plan_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Category_CustomFields_Read_Values" PARAMETERS = "cat_id, plans var, plan_count, output var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.plan" ARRAY = "l.plans" COUNT = "{ l.plan_count }">
		<MvIF EXPR = "{ NOT ISNULL l.plan:field_map }">
			<MvREFERENCEARRAY NAME = "l.data" VARIABLE = "l.output">
				<MvMEMBER NAME = "{ l.plan:module:code }">
			</MvREFERENCEARRAY>

			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.plan:module:module ].Module_Category_Fields_Mapped( l.plan:module, l.cat_id, l.plan:field_map, l.data ) }">
		<MvELSEIF EXPR = "{ l.plan:field_array }">
			<MvIF EXPR = "{ l.plan:module:api_ver LT 9.08 }">
				<MvREFERENCEARRAY NAME = "l.data" VARIABLE = "l.output">
					<MvMEMBER NAME = "{ l.plan:module:code }">
					<MvMEMBER NAME = "{ l.plan:field_code }">
				</MvREFERENCEARRAY>

				<MvASSIGN NAME = "l.data" VALUE = "{ [ g.Module_Root $ l.plan:module:module ].Module_Category_Field_Value( l.plan:module, l.cat_id, l.plan:field_code ) }">
			<MvELSE>
				<MvREFERENCEARRAY NAME = "l.data" VARIABLE = "l.output">
					<MvMEMBER NAME = "{ l.plan:module:code }">
					<MvMEMBER NAME = "{ l.plan:field_code }">
				</MvREFERENCEARRAY>

				<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.plan:module:module ].Module_Category_Field_Value_Array( l.plan:module, l.cat_id, l.plan:field_code, l.data ) }">
			</MvIF>
		<MvELSE>
			<MvREFERENCEARRAY NAME = "l.data" VARIABLE = "l.output">
				<MvMEMBER NAME = "{ l.plan:module:code }">
				<MvMEMBER NAME = "{ l.plan:field_code }">
			</MvREFERENCEARRAY>

			<MvASSIGN NAME = "l.data" VALUE = "{ [ g.Module_Root $ l.plan:module:module ].Module_Category_Field_Value( l.plan:module, l.cat_id, l.plan:field_code ) }">
		</MvIF>
	</MvFOREACH>
</MvFUNCTION>

<MvCOMMENT>
|
| Customer Related Helper Functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "TemplateFeed_Customer_DefaultSettings" PARAMETERS = "settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings"			VALUE = "">
	<MvASSIGN NAME = "l.settings:mode"		VALUE = "customer">
	<MvASSIGN NAME = "l.settings:load"		VALUE = "all">
	<MvASSIGN NAME = "l.settings:in_value"	VALUE = "">
	<MvASSIGN NAME = "l.settings:encoding"	VALUE = "csv">
	<MvASSIGN NAME = "l.settings:delimiter"	VALUE = "comma">
	<MvASSIGN NAME = "l.settings:advanced"	VALUE = 0>

	<MvEVAL EXPR = "{ TemplateFeed_Customer_DefaultFields( l.settings:customers:fields ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Customer_DefaultFields" PARAMETERS = "fields var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.fields:login" 				VALUE = 1>
	<MvASSIGN NAME = "l.fields:password" 			VALUE = 0>
	<MvASSIGN NAME = "l.fields:pw_email" 			VALUE = 1>
	<MvASSIGN NAME = "l.fields:business_account"	VALUE = 1>
	<MvASSIGN NAME = "l.fields:dt_created"			VALUE = 0>
	<MvASSIGN NAME = "l.fields:dt_updated"			VALUE = 0>
	<MvASSIGN NAME = "l.fields:dt_login"			VALUE = 0>
	<MvASSIGN NAME = "l.fields:dt_pwchg"			VALUE = 0>
	<MvASSIGN NAME = "l.fields:ship_fname"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:ship_lname"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:ship_email"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:ship_phone"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:ship_fax"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:ship_comp"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:ship_addr"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:ship_addr2"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:ship_city"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:ship_state"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:ship_zip"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:ship_cntry"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:bill_fname"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:bill_lname"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:bill_email"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:bill_phone"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:bill_fax"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:bill_comp"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:bill_addr"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:bill_addr2"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:bill_city"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:bill_state"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:bill_zip"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:bill_cntry"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:note_count"			VALUE = 0>
	<MvASSIGN NAME = "l.fields:tax_exempt"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:order_cnt"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:order_avg"			VALUE = 1>
	<MvASSIGN NAME = "l.fields:order_tot"			VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Customer_GenerateCode" PARAMETERS = "settings var, header_template_source var, iterator_template_source var, footer_template_source var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.headers"			VALUE = "">
	<MvASSIGN NAME = "l.iterators"			VALUE = "">
	<MvASSIGN NAME = "l.footers"			VALUE = "">
	<MvASSIGN NAME = "l.header_count"		VALUE = 0>
	<MvASSIGN NAME = "l.iterator_count"		VALUE = 0>
	<MvASSIGN NAME = "l.footer_count"		VALUE = 0>

	<MvREFERENCE NAME = "l.customer_settings" VARIABLE = "l.settings:customers">

	<MvIF EXPR = "{ l.customer_settings:fields:login }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_LOGIN',							-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:login;',						-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:password }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_PASSWORD',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:password;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:pw_email }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_LOST_PASSWORD_EMAIL',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:pw_email;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:business_account }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_BUSINESS_ACCOUNT',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:business_account;',			-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:dt_created }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_CREATED_TIMESTAMP',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:dt_created;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:dt_updated }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_LAST_UPDATED_TIMESTAMP',			-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:dt_updated;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:dt_login }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_LAST_LOGIN_TIMESTAMP',			-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:dt_login;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:dt_pwchg }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_PASSWORD_LAST_CHANGED_TIMESTAMP',	-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:dt_pwchg;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:ship_fname }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_SHIPPING_FIRST_NAME',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:ship_fname;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:ship_lname }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_SHIPPING_LAST_NAME',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:ship_lname;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:ship_email }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_SHIPPING_EMAIL',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:ship_email;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:ship_phone }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_SHIPPING_PHONE',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:ship_phone;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:ship_fax }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_SHIPPING_FAX',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:ship_fax;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:ship_comp }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_SHIPPING_COMPANY',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:ship_comp;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:ship_addr }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_SHIPPING_ADDRESS',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:ship_addr1;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:ship_addr2 }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_SHIPPING_ADDRESS2',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:ship_addr2;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:ship_city }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_SHIPPING_CITY',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:ship_city;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:ship_state }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_SHIPPING_STATE',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:ship_state;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:ship_zip }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_SHIPPING_ZIP',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:ship_zip;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:ship_cntry }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_SHIPPING_COUNTRY',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:ship_cntry;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:bill_fname }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_BILLING_FIRST_NAME',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:bill_fname;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:bill_lname }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_BILLING_LAST_NAME',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:bill_lname;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:bill_email }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_BILLING_EMAIL',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:bill_email;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:bill_phone }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_BILLING_PHONE',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:bill_phone;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:bill_fax }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_BILLING_FAX',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:bill_fax;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:bill_comp }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_BILLING_COMPANY',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:bill_comp;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:bill_addr }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_BILLING_ADDRESS',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:bill_addr1;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:bill_addr2 }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_BILLING_ADDRESS2',				-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:bill_addr2;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:bill_city }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_BILLING_CITY',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:bill_city;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:bill_state }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_BILLING_STATE',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:bill_state;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:bill_zip }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_BILLING_ZIP',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:bill_zip;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:bill_cntry }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_BILLING_COUNTRY',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:bill_cntry;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:note_count }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_NOTE_COUNT',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:note_count;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:tax_exempt }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_TAX_EXEMPT',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:tax_exempt;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:order_cnt }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_ORDER_COUNT',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:order_cnt;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:order_avg }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_ORDER_AVERAGE',					-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:order_avg;',					-1 ) }">
	</MvIF>

	<MvIF EXPR = "{ l.customer_settings:fields:order_tot }">
		<MvASSIGN NAME = "l.header_count"	VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_ORDER_TOTAL',						-1 ) }">
		<MvASSIGN NAME = "l.iterator_count" VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:order_tot;',					-1 ) }">
	</MvIF>

	<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.customer_settings:customfields">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ModuleAndFeatures_Load_Code( l.customfield:module, l.module ) OR
						NOT l.module:feature_hash:fields_cust }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.customfield_name"	VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Customer_Field_Name( l.module, l.customfield:code ) }">
		<MvASSIGN NAME = "l.header_count"		VALUE = "{ miva_array_insert( l.headers,	'CUSTOMER_CUSTOMFIELD_' $ TemplateFeed_Normalize_CustomField_Name( l.customfield_name ), -1 ) }">
		<MvASSIGN NAME = "l.iterator_count"		VALUE = "{ miva_array_insert( l.iterators,	'&mvt:record:customfield:' $ TemplateFeed_Normalize_CustomField_Code( l.customfield:code ) $ ';', -1 ) }">
	</MvFOREACH>

	<MvASSIGN NAME = "l.header_template_source"		VALUE = "{ TemplateFeed_Generate_RowCode( l.settings:delimiter, l.headers,		l.header_count )	$ '&mvt:eol:crlf;' }">
	<MvASSIGN NAME = "l.iterator_template_source"	VALUE = "{ TemplateFeed_Generate_RowCode( l.settings:delimiter, l.iterators,	l.iterator_count )	$ '&mvt:eol:crlf;' }">
	<MvASSIGN NAME = "l.footer_template_source"		VALUE = "{ TemplateFeed_Generate_RowCode( l.settings:delimiter, l.footers,		l.footer_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Customer_Read" PARAMETERS = "customer var, plans var, plan_count, encoding" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ TemplateFeed_Customer_CustomFields_Read_Values( Customers.d.id, l.plans, l.plan_count, l.customfields_structure ) }">

	<MvASSIGN NAME = "l.customer"					VALUE = "">
	<MvASSIGN NAME = "l.customer:id"				VALUE = "{ Customers.d.id }">
	<MvASSIGN NAME = "l.customer:account_id"		VALUE = "{ Customers.d.account_id }">
	<MvASSIGN NAME = "l.customer:pgrpcount"			VALUE = "{ Customers.d.pgrpcount }">
	<MvASSIGN NAME = "l.customer:login"				VALUE = "{ Customers.d.login }">
	<MvASSIGN NAME = "l.customer:pw_email"			VALUE = "{ Customers.d.pw_email }">
	<MvASSIGN NAME = "l.customer:password"			VALUE = "{ Customers.d.password }">
	<MvASSIGN NAME = "l.customer:business_account"	VALUE = "{ Customers.d.business_account }">
	<MvASSIGN NAME = "l.customer:dt_created"		VALUE = "{ Customers.d.dt_created }">
	<MvASSIGN NAME = "l.customer:dt_updated"		VALUE = "{ Customers.d.dt_updated }">
	<MvASSIGN NAME = "l.customer:dt_login"			VALUE = "{ Customers.d.dt_login }">
	<MvASSIGN NAME = "l.customer:dt_pwchg"			VALUE = "{ Customers.d.dt_pwchg }">

	<MvASSIGN NAME = "l.customer:ship_res"			VALUE = "{ Customers.d.ship_res }">
	<MvASSIGN NAME = "l.customer:ship_id"			VALUE = "{ Customers.d.ship_id }">
	<MvASSIGN NAME = "l.customer:ship_fname"		VALUE = "{ Customers.d.ship_fname }">
	<MvASSIGN NAME = "l.customer:ship_lname"		VALUE = "{ Customers.d.ship_lname }">
	<MvASSIGN NAME = "l.customer:ship_email"		VALUE = "{ Customers.d.ship_email }">
	<MvASSIGN NAME = "l.customer:ship_comp"			VALUE = "{ Customers.d.ship_comp }">
	<MvASSIGN NAME = "l.customer:ship_phone"		VALUE = "{ Customers.d.ship_phone }">
	<MvASSIGN NAME = "l.customer:ship_fax"			VALUE = "{ Customers.d.ship_fax }">

	<MvIF EXPR = "{ ISNULL Customers.d.ship_addr2 }">
		<MvASSIGN NAME = "l.customer:ship_addr"		VALUE = "{ Customers.d.ship_addr }">
	<MvELSE>
		<MvASSIGN NAME = "l.customer:ship_addr"		VALUE = "{ Customers.d.ship_addr $ ' ' $ Customers.d.ship_addr2 }">
	</MvIF>

	<MvASSIGN NAME = "l.customer:ship_addr1"		VALUE = "{ Customers.d.ship_addr }">
	<MvASSIGN NAME = "l.customer:ship_addr2"		VALUE = "{ Customers.d.ship_addr2 }">
	<MvASSIGN NAME = "l.customer:ship_city"			VALUE = "{ Customers.d.ship_city }">
	<MvASSIGN NAME = "l.customer:ship_state"		VALUE = "{ Customers.d.ship_state }">
	<MvASSIGN NAME = "l.customer:ship_zip"			VALUE = "{ Customers.d.ship_zip }">
	<MvASSIGN NAME = "l.customer:ship_cntry"		VALUE = "{ Customers.d.ship_cntry }">

	<MvASSIGN NAME = "l.customer:bill_id"			VALUE = "{ Customers.d.bill_id }">
	<MvASSIGN NAME = "l.customer:bill_fname"		VALUE = "{ Customers.d.bill_fname }">
	<MvASSIGN NAME = "l.customer:bill_lname"		VALUE = "{ Customers.d.bill_lname }">
	<MvASSIGN NAME = "l.customer:bill_email"		VALUE = "{ Customers.d.bill_email }">
	<MvASSIGN NAME = "l.customer:bill_comp"			VALUE = "{ Customers.d.bill_comp }">
	<MvASSIGN NAME = "l.customer:bill_phone"		VALUE = "{ Customers.d.bill_phone }">
	<MvASSIGN NAME = "l.customer:bill_fax"			VALUE = "{ Customers.d.bill_fax }">

	<MvIF EXPR = "{ ISNULL Customers.d.bill_addr2 }">
		<MvASSIGN NAME = "l.customer:bill_addr"		VALUE = "{ Customers.d.bill_addr }">
	<MvELSE>
		<MvASSIGN NAME = "l.customer:bill_addr"		VALUE = "{ Customers.d.bill_addr $ ' ' $ Customers.d.bill_addr2 }">
	</MvIF>

	<MvASSIGN NAME = "l.customer:bill_addr1"		VALUE = "{ Customers.d.bill_addr }">
	<MvASSIGN NAME = "l.customer:bill_addr2"		VALUE = "{ Customers.d.bill_addr2 }">
	<MvASSIGN NAME = "l.customer:bill_city"			VALUE = "{ Customers.d.bill_city }">
	<MvASSIGN NAME = "l.customer:bill_state"		VALUE = "{ Customers.d.bill_state }">
	<MvASSIGN NAME = "l.customer:bill_zip"			VALUE = "{ Customers.d.bill_zip }">
	<MvASSIGN NAME = "l.customer:bill_cntry"		VALUE = "{ Customers.d.bill_cntry }">
	<MvASSIGN NAME = "l.customer:credit"			VALUE = "{ Customers.d.credit }">
	<MvASSIGN NAME = "l.customer:note_count"		VALUE = "{ Customers.d.note_count }">
	<MvASSIGN NAME = "l.customer:tax_exempt"		VALUE = "{ Customers.d.tax_exempt }">
	<MvASSIGN NAME = "l.customer:order_cnt"			VALUE = "{ Customers.d.order_cnt }">
	<MvASSIGN NAME = "l.customer:order_avg"			VALUE = "{ Customers.d.order_avg }">
	<MvASSIGN NAME = "l.customer:order_tot"			VALUE = "{ Customers.d.order_tot }">

	<MvEVAL EXPR = "{ TemplateFeed_CustomFields_Build_Values( l.customer, l.customfields_structure, l.encoding ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Customer_Initialize_CustomField_Lookup" PARAMETERS = "fields_custom var, plans var, plan_count var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.fields"			VALUE = "">
	<MvASSIGN NAME = "l.field_count"	VALUE = 0>

	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ TemplateFeed_Initialize_xxx_CustomField_ModuleList_Load( l.fields_custom, 'fields_cust', l.modules ) }">
		<MvASSIGN NAME = "l.module_fields"		VALUE = "">
		<MvASSIGN NAME = "l.module_field_count"	VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Customer_Fields( l.module, l.module_fields ) }">

		<MvEVAL EXPR = "{ TemplateFeed_Initialize_xxx_CustomField_Filter_ModuleFields( l.module, l.fields_custom, l.module_fields, l.module_field_count, l.fields, l.field_count ) }">
	</MvFOREACH>

	<MvEVAL EXPR = "{ TemplateFeed_Initialize_xxx_CustomField_Lookup( l.fields, l.field_count, 'fields_cust_map', l.plans, l.plan_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Customer_CustomFields_Read_Values" PARAMETERS = "cust_id, plans var, plan_count, output var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.plan" ARRAY = "l.plans" COUNT = "{ l.plan_count }">
		<MvIF EXPR = "{ NOT ISNULL l.plan:field_map }">
			<MvREFERENCEARRAY NAME = "l.data" VARIABLE = "l.output">
				<MvMEMBER NAME = "{ l.plan:module:code }">
			</MvREFERENCEARRAY>

			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.plan:module:module ].Module_Customer_Fields_Mapped( l.plan:module, l.cust_id, l.plan:field_map, l.data ) }">
		<MvELSEIF EXPR = "{ l.plan:field_array }">
			<MvIF EXPR = "{ l.plan:module:api_ver LT 9.08 }">
				<MvREFERENCEARRAY NAME = "l.data" VARIABLE = "l.output">
					<MvMEMBER NAME = "{ l.plan:module:code }">
					<MvMEMBER NAME = "{ l.plan:field_code }">
				</MvREFERENCEARRAY>

				<MvASSIGN NAME = "l.data" VALUE = "{ [ g.Module_Root $ l.plan:module:module ].Module_Customer_Field_Value( l.plan:module, l.cust_id, l.plan:field_code ) }">
			<MvELSE>
				<MvREFERENCEARRAY NAME = "l.data" VARIABLE = "l.output">
					<MvMEMBER NAME = "{ l.plan:module:code }">
					<MvMEMBER NAME = "{ l.plan:field_code }">
				</MvREFERENCEARRAY>

				<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.plan:module:module ].Module_Customer_Field_Value_Array( l.plan:module, l.cust_id, l.plan:field_code, l.data ) }">
			</MvIF>
		<MvELSE>
			<MvREFERENCEARRAY NAME = "l.data" VARIABLE = "l.output">
				<MvMEMBER NAME = "{ l.plan:module:code }">
				<MvMEMBER NAME = "{ l.plan:field_code }">
			</MvREFERENCEARRAY>

			<MvASSIGN NAME = "l.data" VALUE = "{ [ g.Module_Root $ l.plan:module:module ].Module_Customer_Field_Value( l.plan:module, l.cust_id, l.plan:field_code ) }">
		</MvIF>
	</MvFOREACH>
</MvFUNCTION>

<MvCOMMENT>
|
| Helper Functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "TemplateFeed_Load_Templates" PARAMETERS = "settings_template_filename,	settings_managedtemplate var,
															   header_template_filename,	header_managedtemplate var,
															   iterator_template_filename,	iterator_managedtemplate var,
															   footer_template_filename,	footer_managedtemplate var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings_template_filename, l.settings_managedtemplate ) }">		<MvFUNCTIONRETURN VALUE = 0>
	<MvELSEIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.header_template_filename, l.header_managedtemplate ) }">		<MvFUNCTIONRETURN VALUE = 0>
	<MvELSEIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.iterator_template_filename, l.iterator_managedtemplate ) }">	<MvFUNCTIONRETURN VALUE = 0>
	<MvELSEIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.footer_template_filename, l.footer_managedtemplate ) }">		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Load_TemplateVersions" PARAMETERS = "settings_template_filename,	settings_templateversion var,
																	  header_template_filename,		header_templateversion var,
																	  iterator_template_filename,	iterator_templateversion var,
																	  footer_template_filename,		footer_templateversion var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings_template_filename, l.settings_templateversion ) }">		<MvFUNCTIONRETURN VALUE = 0>
	<MvELSEIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.header_template_filename, l.header_templateversion ) }">		<MvFUNCTIONRETURN VALUE = 0>
	<MvELSEIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.iterator_template_filename, l.iterator_templateversion ) }">	<MvFUNCTIONRETURN VALUE = 0>
	<MvELSEIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.footer_template_filename, l.footer_templateversion ) }">		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Generate_Template" PARAMETERS = "module var, feed var, template, page_code" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Session:cache:templatefeed:feed_create_template" VALUE = "{ l.template }">

	<MvIF EXPR = "{ l.template EQ '' }">
		<MvASSIGN NAME = "l.source"				VALUE = "">
		<MvASSIGN NAME = "l.assign_item"		VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.source"				VALUE = "{ '<mvt:item name="templatefeed" />' }">
		<MvASSIGN NAME = "l.assign_item"		VALUE = 1>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Item_Load_Code( 'templatefeed', l.null ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Item( 'templatefeed', l.module:code ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.page:code"				VALUE = "{ l.page_code }">
	<MvASSIGN NAME = "l.page:name"				VALUE = "{ l.feed:name }">
	<MvASSIGN NAME = "l.page:ui_id"				VALUE = "{ l.module:id }">
	<MvASSIGN NAME = "l.page:admin"				VALUE = 1>
	<MvASSIGN NAME = "l.page:settings"			VALUE = "">
	<MvASSIGN NAME = "l.page:settings:feed_id"	VALUE = "{ l.feed:id }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Page_LowLevel( l.page, l.source ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.feed:config:page_id"	VALUE = "{ l.page:id }">
	<MvASSIGN NAME = "l.feed:config:page_code"	VALUE = "{ l.page:code }">
	<MvASSIGN NAME = "l.feed:config:template"	VALUE = "{ l.template }">

	<MvIF EXPR = "{ l.assign_item }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'templatefeed' ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Replace_Settings( l.page ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Generate_RowCode" PARAMETERS = "delimiter, records var, record_count" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.char" VALUE = "{ TemplateFeed_Convert_Delimiter_To_Char( l.delimiter ) }">

	<MvCAPTURE VARIABLE = "l.output">
		<MvFOREACH ITERATOR = "l.record" ARRAY = "l.records" INDEX = "l.pos" COUNT = "{ l.record_count }">
			<MvIF EXPR = "{ l.pos GT 1 }">
				<MvEVAL EXPR = "{ l.char }">
			</MvIF>

			<MvEVAL EXPR = "{ l.record }">
		</MvFOREACH>
	</MvCAPTURE>

	<MvFUNCTIONRETURN VALUE = "{ l.output }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Build_URL_From_URI" PARAMETERS = "uri" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.uri }">																	<MvFUNCTIONRETURN VALUE = "">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Feature_URI_DB ].URISettings_Load_Cached( l.urisettings ) }">	<MvFUNCTIONRETURN VALUE = "">
	</MvIF>

	<MvASSIGN NAME = "l.base" VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Base_Secure( l.urisettings ) }">

	<MvIF EXPR = "{ ( '/' IN l.uri ) EQ 1 }">
		<MvASSIGN NAME = "l.uri" VALUE = "{ substring_var( l.uri, 2, len_var( l.uri ) - 1 ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.base $ l.uri }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_MultiSelect_DrawOption" PARAMETERS = "selected, value, text" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.option"		VALUE = "{ '<option value="' $ encodeentities( l.value ) $ '"' }">

	<MvIF EXPR = "{ l.selected }">
		<MvASSIGN NAME = "l.option" VALUE = "{ l.option $ ' selected' }">
	</MvIF>

	<MvASSIGN NAME = "l.option"		VALUE = "{ l.option $ '>' $ l.text $ '</option>' }">

	<MvEVAL EXPR = "{ l.option }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_MultiSelect_To_Fields" PARAMETERS = "type, variable var, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| This function is used in conjunction with the mutliselect input fields
	| for products, categories, and customers.  The multiselect manager sends
	| all selected values, but the unselected values are NOT sent.  This function
	| ultimately builds an output structure containing either a 1 or 0 for ALL
	| default fields.  This solves issues of comparing the previous fields and the
	| current fields when updating the component settings.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.type EQ 'product' }">			<MvEVAL EXPR = "{ TemplateFeed_Product_DefaultFields_Master( l.field_defaults ) }">
	<MvELSEIF EXPR = "{ l.type EQ 'variant' }">		<MvEVAL EXPR = "{ TemplateFeed_Product_DefaultFields_Variant( l.field_defaults ) }">
	<MvELSEIF EXPR = "{ l.type EQ 'category' }">	<MvEVAL EXPR = "{ TemplateFeed_Category_DefaultFields( l.field_defaults ) }">
	<MvELSEIF EXPR = "{ l.type EQ 'customer' }">	<MvEVAL EXPR = "{ TemplateFeed_Customer_DefaultFields( l.field_defaults ) }">
	</MvIF>

	<MvFOREACH ITERATOR = "l.field" ARRAY = "l.fields" COUNT = "{ miva_splitstring( l.variable, ',', l.fields, 'trim,lower' ) }">
		<MvIF EXPR = "{ NOT ISNULL l.field }">
			<MvASSIGN NAME = "l.output" MEMBER = "{ l.field }" VALUE = 1>
		</MvIF>
	</MvFOREACH>

	<MvFOREACH ITERATOR = "l.member" ARRAY = "l.members" COUNT = "{ miva_struct_members( l.field_defaults, l.members ) }">
		<MvIF EXPR = "{ NOT miva_member_exists( l.output, l.member ) }">
			<MvASSIGN NAME = "l.output" MEMBER = "{ l.member }" VALUE = 0>
		</MvIF>
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Convert_Delimiter_To_Char" PARAMETERS = "delimiter" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.delimiter EQ 'pipe' }">		<MvFUNCTIONRETURN VALUE = "{ asciichar( 124 ) }">
	<MvELSEIF EXPR = "{ l.delimiter EQ 'tab' }">	<MvFUNCTIONRETURN VALUE = "{ asciichar( 9 ) }">
	<MvELSE>										<MvFUNCTIONRETURN VALUE = "{ asciichar( 44 ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Normalize_CustomField_Code" PARAMETERS = "code" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ tolower( glosub( trim( l.code ), ' ', '_' ) ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Normalize_CustomField_Name" PARAMETERS = "name" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ toupper( glosub( trim( l.name ), ' ', '_' ) ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_CustomFields_MapBuild" PARAMETERS = "customfields var, customfield_count, plans var, plan_count var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.customfields" COUNT = "{ l.customfield_count }">
		<MvREFERENCEARRAY NAME = "l.cached_module_index" VARIABLE = "l.cached_modules">
			<MvMEMBER NAME = "{ l.customfield:module:code }">
		</MvREFERENCEARRAY>

		<MvIF EXPR = "{ NOT ISNULL l.cached_module_index }">
			<MvASSIGNARRAY NAME = "l.customfield_module_mappings"																		VALUE = "{ TemplateFeed_Normalize_CustomField_Code( l.customfield:code ) }">
				<MvDIMENSION INDEX = "{ l.cached_module_index }">
				<MvMEMBER NAME = "mappings">
				<MvMEMBER NAME = "{ l.customfield:code }">
			</MvASSIGNARRAY>

			<MvASSIGN NAME = "l.customfield_module_mappings" INDEX = "{ l.cached_module_index }"	MEMBER = "mappings_array_count"		VALUE = "{ miva_array_insert_var( l.customfield_module_mappings[ l.cached_module_index ]:mappings_array, l.customfield:code, -1 ) }">
		<MvELSE>
			<MvASSIGN NAME = "l.element"																								VALUE = "">
			<MvASSIGN NAME = "l.element:module"																							VALUE = "{ l.customfield:module }">
			<MvASSIGN NAME = "l.element:mappings"													MEMBER = "{ l.customfield:code }"	VALUE = "{ TemplateFeed_Normalize_CustomField_Code( l.customfield:code ) }">
			<MvASSIGN NAME = "l.element:mappings_array_count"																			VALUE = "{ miva_array_insert_var( l.element:mappings_array, l.customfield:code, -1 ) }">
			<MvASSIGN NAME = "l.customfield_module_mapping_count"																		VALUE = "{ miva_array_insert_var( l.customfield_module_mappings, l.element, -1 ) }">
			<MvASSIGN NAME = "l.cached_module_index"																					VALUE = "{ l.customfield_module_mapping_count }">
		</MvIF>
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Validate_Categories" PARAMETERS = "codes" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.category_code" ARRAY = "l.category_codes" COUNT = "{ miva_splitstring( l.codes, ',', l.category_codes, 'trim' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Category_Load_Code_Cached( l.category_code, l.null ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-TFD-00001', 'Category \'' $ l.category_code $ '\' does not exist' ) }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Validate_PriceGroups" PARAMETERS = "names" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.pricegroup_name" ARRAY = "l.pricegroups_names" COUNT = "{ miva_splitstring( l.names, ',', l.pricegroups_names, 'trim' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_DB ].PriceGroup_Load_Name( l.pricegroup_name, l.null ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-TFD-00002', 'Price Group \'' $ l.pricegroup_name $ '\' does not exist' ) }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Validate_AvailabilityGroups" PARAMETERS = "names" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.availabilitygroup_name" ARRAY = "l.availabilitygroup_names" COUNT = "{ miva_splitstring( l.names, ',', l.availabilitygroup_names, 'trim' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_AGR_DB ].AvailabilityGroup_Load_Name( l.availabilitygroup_name, l.null ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-TFD-00003', 'Availability Group \'' $ l.availabilitygroup_name $ '\' does not exist' ) }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Generate_Or_Update_Template" PARAMETERS = "module var, feed var, template" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.page_code" VALUE = "{ 'TEMPLATEFEED_' $ l.feed:code }">

	<MvIF EXPR = "{ len_var( l.page_code ) GT 50 }">
		<MvASSIGN NAME = "l.page_code" VALUE = "{ substring_var( l.page_code, 1, 50 ) }">

		<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code( l.page_code, l.existing_page ) AND l.existing_page:id NE l.feed:config:page_id }">
			<MvASSIGN NAME = "l.page_code" VALUE = "{ 'TEMPLATEFEED_' $ crypto_md5( l.feed:code ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code( l.page_code, l.existing_page ) AND l.existing_page:id NE l.feed:config:page_id }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-TFD-00004', 'Page code \'' $ l.page_code $ '\' already exists' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT l.feed:config:page_id }">
		<MvIF EXPR = "{ NOT TemplateFeed_Generate_Template( l.module, l.feed, l.template, l.page_code ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	<MvELSEIF EXPR = "{ l.page_code NE l.feed:config:page_code }">
		<MvIF EXPR = "{ NOT ISNULL l.existing_page }">
			<MvREFERENCE NAME = "l.page" VARIABLE = "l.existing_page">
		<MvELSE>
			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_ID_Cached( l.feed:config:page_id, l.page ) }">
				<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
					<MvFUNCTIONRETURN VALUE = 0>
				</MvIF>

				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FEED-TFD-00005', 'Failed to load page' ) }">
			</MvIF>
		</MvIF>

		<MvASSIGN NAME = "l.page:code"				VALUE = "{ l.page_code }">
		<MvASSIGN NAME = "l.feed:config:page_code"	VALUE = "{ l.page_code }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Update_LowLevel( l.page ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Initialize_xxx_CustomField_ModuleList_Load" PARAMETERS = "fields_custom var, field_feature, modules var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.modules"		VALUE = "">
	<MvASSIGN NAME = "l.module_count"	VALUE = 0>

	<MvFOREACH ITERATOR = "l.field_custom_filtered" ARRAY = "l.fields_custom_filtered" INDEX = "l.pos" COUNT = "{ miva_array_filter_ref( l.fields_custom, 1, l.field_custom, 'NOT miva_array_search( l.fields_custom_filtered, 1, l.field_custom_filtered, \'l.field_custom_filtered:module EQ l.field_custom:module\' )', l.fields_custom_filtered ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ModuleAndFeatures_Load_Code_Cached( l.field_custom_filtered:module, l.module ) OR
						NOT l.module:active }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvREFERENCEARRAY NAME = "l.has_customfield_feature" VARIABLE = "l.module:feature_hash">
			<MvMEMBER NAME = "{ l.field_feature }">
		</MvREFERENCEARRAY>

		<MvIF EXPR = "{ NOT l.has_customfield_feature }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.module_count" VALUE = "{ miva_array_insert_var( l.modules, l.module, -1 ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = "{ l.module_count }">
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Initialize_xxx_CustomField_Filter_ModuleFields" PARAMETERS = "module var, fields_custom var, module_fields var, module_field_count, fields var, field_count var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.module_field" ARRAY = "l.module_fields" COUNT = "{ l.module_field_count }">
		<MvIF EXPR = "{ NOT miva_array_search( l.fields_custom, 1, l.field_custom, 'l.field_custom:module EQ l.module:code AND tolower( l.field_custom:code ) EQ tolower( l.module_field:code )' ) }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.field"			VALUE = "">
		<MvASSIGN NAME = "l.field:code"		VALUE = "{ l.module_field:code }">
		<MvASSIGN NAME = "l.field:name"		VALUE = "{ l.module_field:name }">
		<MvASSIGN NAME = "l.field:type"		VALUE = "{ l.module_field:type }">
		<MvASSIGN NAME = "l.field:module"	VALUE = "{ l.module }">
		<MvASSIGN NAME = "l.field_count"	VALUE = "{ miva_array_insert_var( l.fields, l.field, -1 ) }">
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Initialize_xxx_CustomField_Lookup" PARAMETERS = "fields var, field_count, field_map_feature, plans var, plan_count var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| fields[n]
	|	:code		- The custom field code
	|	:name		- The custom field name
	|	:type		- The custom field type (if one exists)
	|	:module		- The custom field module
	|
	| Build an execution plan so that the selected field module version/call logic
	| is not executed for each product/category/customer being initialized.
	|
	| plan[n]				- one entry per field or set of fields, per product/category/customer execution simply iterates over this array
	| 	:module				- reference to cached modules
	|
	| 	:field_map			- causes a call to Module_xxx_Fields_Mapped
	| 			- or -
	|	:field_array		- causes a call to Module_xxx_Field_Value_Array
	|			- or -
	| 	:field_single		- causes a call to Module_xxx_Field_Value
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.plans"		VALUE = "">
	<MvASSIGN NAME = "l.plan_count"	VALUE = 0>

	<MvFOREACH ITERATOR = "l.field" ARRAY = "l.fields" INDEX = "l.pos" COUNT = "{ l.field_count }">
		<MvREFERENCEARRAY NAME = "l.has_mapping_feature" VARIABLE = "l.field:module:feature_hash">
			<MvMEMBER NAME = "{ l.field_map_feature }">
		</MvREFERENCEARRAY>

		<MvCOMMENT>
		|
		| Make l.plan a reference variable into a dummy local array
		| so that when the plan is built it can be added to the master plan
		| by reference using miva_array_insert_ref.
		|
		| We cannot make l.plan a direct reference into the master plan
		| because mapped modules that have multiple fields would increment
		| the master plan array on each iteration of the loop even if a new
		| plan is not created.
		|
		</MvCOMMENT>

		<MvREFERENCEARRAY NAME = "l.plan" VARIABLE = "l.dummy_plan_references">
			<MvDIMENSION INDEX = "{ l.pos }">
		</MvREFERENCEARRAY>

		<MvIF EXPR = "{ l.field:type EQ 'multitext' }">
			<MvCOMMENT>
			|
			| Execution plan must include one entry per multitext field for this module
			|
			</MvCOMMENT>

			<MvASSIGN NAME = "l.plan"					VALUE = "">
			<MvASSIGN NAME = "l.plan:field_single"		VALUE = 0>
			<MvASSIGN NAME = "l.plan:field_array"		VALUE = 1>
			<MvASSIGN NAME = "l.plan:field_map"			VALUE = "">
			<MvASSIGN NAME = "l.plan:field_code"		VALUE = "{ l.field:code }">
			<MvASSIGN NAME = "l.plan:module"			VALUE = "">

			<MvREFERENCE NAME = "l.plan:module"			VARIABLE = "l.field:module">

			<MvASSIGN NAME = "l.plan_count"				VALUE = "{ miva_array_insert_ref( l.plans, l.plan, -1 ) }">
		<MvELSEIF EXPR = "{ NOT l.has_mapping_feature }">
			<MvCOMMENT>
			|
			| Execution plan must include one entry per field for this module
			|
			</MvCOMMENT>

			<MvASSIGN NAME = "l.plan"					VALUE = "">
			<MvASSIGN NAME = "l.plan:field_single"		VALUE = 1>
			<MvASSIGN NAME = "l.plan:field_array"		VALUE = 0>
			<MvASSIGN NAME = "l.plan:field_map"			VALUE = "">
			<MvASSIGN NAME = "l.plan:field_code"		VALUE = "{ l.field:code }">
			<MvASSIGN NAME = "l.plan:module"			VALUE = "">

			<MvREFERENCE NAME = "l.plan:module"			VARIABLE = "l.field:module">

			<MvASSIGN NAME = "l.plan_count"				VALUE = "{ miva_array_insert_ref( l.plans, l.plan, -1 ) }">
		<MvELSE>
			<MvCOMMENT>
			|
			| Execution plan may query all fields from this module at once
			|
			</MvCOMMENT>

			<MvASSIGN NAME = "l.plan_index"				VALUE = "{ miva_array_search( l.plans, 1, l.element, 'NOT ISNULL l.element:field_map AND ( tolower( l.element:module:code ) EQ tolower( l.field:module:code ) )' ) }">

			<MvIF EXPR = "{ NOT l.plan_index }">
				<MvASSIGN NAME = "l.plan"				VALUE = "">
				<MvASSIGN NAME = "l.plan:field_single"	VALUE = 0>
				<MvASSIGN NAME = "l.plan:field_array"	VALUE = 0>
				<MvASSIGN NAME = "l.plan:field_map"		VALUE = "">
				<MvASSIGN NAME = "l.plan:field_code"	VALUE = "">
				<MvASSIGN NAME = "l.plan:module"		VALUE = "">

				<MvREFERENCE NAME = "l.plan:module"		VARIABLE = "l.field:module">

				<MvASSIGN NAME = "l.plan_count"			VALUE = "{ miva_array_insert_ref( l.plans, l.plan, -1 ) }">
				<MvASSIGN NAME = "l.plan_index"			VALUE = "{ l.plan_count }">
			</MvIF>

			<MvREFERENCEARRAY NAME = "l.plans_reference" VARIABLE = "l.plans">
				<MvDIMENSION INDEX = "{ l.plan_index }">
			</MvREFERENCEARRAY>

			<MvASSIGNARRAY NAME = "l.plans_reference:field_map" VALUE = "{ l.field:code }">
				<MvMEMBER NAME = "{ l.field:code }">
			</MvASSIGNARRAY>
		</MvIF>
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_CustomFields_Build_Values" PARAMETERS = "record var, customfields_structure var, encoding" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.module_code" ARRAY = "l.module_codes" COUNT = "{ miva_struct_members( l.customfields_structure, l.module_codes ) }">
		<MvREFERENCEARRAY NAME = "l.customfield_field_codes" VARIABLE = "l.customfields_structure">
			<MvMEMBER NAME = "{ l.module_code }">
		</MvREFERENCEARRAY>

		<MvFOREACH ITERATOR = "l.field_code" ARRAY = "l.field_codes" COUNT = "{ miva_struct_members( l.customfield_field_codes, l.field_codes ) }">
			<MvREFERENCEARRAY NAME = "l.value" VARIABLE = "l.customfield_field_codes">
				<MvMEMBER NAME = "{ l.field_code }">
			</MvREFERENCEARRAY>

			<MvIF EXPR = "{ l.encoding NE 'none' }">
				<MvCOMMENT>
				|
				| All custom field values should be treated as a single value and not as an
				| ARRAY / STRUCTURE when the encoding takes place within TemplateFeed_Encode_Record_CSV
				|
				</MvCOMMENT>

				<MvASSIGN NAME = "l.value" VALUE = "{ l.value $ '' }">
			</MvIF>

			<MvCOMMENT>
			|
			| Backwards compatible custom fields record :record:customfield:<field_code> = value
			|
			</MvCOMMENT>

			<MvASSIGN NAME = "l.record:customfield" MEMBER = "{ TemplateFeed_Normalize_CustomField_Code( l.field_code ) }" VALUE = "{ l.value }">

			<MvCOMMENT>
			|
			| Standard custom fields record :record:customfield_values:<safe_module_code>:<safe_field_code> = value
			|
			</MvCOMMENT>

			<MvASSIGNARRAY NAME = "l.record:customfield_values" VALUE = "{ l.value }">
				<MvMEMBER NAME = "{ [ g.Module_feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.module_code ) }">
				<MvMEMBER NAME = "{ [ g.Module_feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.field_code ) }">
			</MvASSIGNARRAY>
		</MvFOREACH>
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Encode_Record" PARAMETERS = "record var, encoding, delimiter" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.encoding EQ 'none' }">	<MvFUNCTIONRETURN>
	<MvELSEIF EXPR = "{ l.encoding EQ 'csv' }">	<MvFUNCTIONRETURN VALUE = "{ TemplateFeed_Encode_Record_CSV( l.record, l.delimiter ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Encode_Record_CSV" PARAMETERS = "data var, delimiter" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.variable_type" VALUE = "{ miva_variable_type( l.data ) }">

	<MvIF EXPR = "{ l.variable_type EQ 'INTEGER' }">		<MvFUNCTIONRETURN>
	<MvELSEIF EXPR = "{ l.variable_type EQ 'DOUBLE' }">		<MvFUNCTIONRETURN>
	<MvELSEIF EXPR = "{ l.variable_type EQ 'STRING' }">		<MvASSIGN NAME = "l.data" VALUE = "{ miva_csv_encode( l.data, l.delimiter ) }">
	<MvELSEIF EXPR = "{ l.variable_type EQ 'ARRAY' }">
		<MvFOREACH ITERATOR = "l.element" ARRAY = "l.data">
			<MvEVAL EXPR = "{ TemplateFeed_Encode_Record_CSV( l.element, l.delimiter ) }">
		</MvFOREACH>
	<MvELSEIF EXPR = "{ l.variable_type EQ 'STRUCTURE' }">
		<MvFOREACH ITERATOR = "l.member" ARRAY = "l.members" COUNT = "{ miva_struct_members( l.data, l.members ) }">
			<MvREFERENCEARRAY NAME = "l.ref" VARIABLE = "l.data">
				<MvMEMBER NAME = "{ l.member }">
			</MvREFERENCEARRAY>

			<MvEVAL EXPR = "{ TemplateFeed_Encode_Record_CSV( l.ref, l.delimiter ) }">
		</MvFOREACH>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "TemplateFeed_Attribute_Supports_Options" PARAMETERS = "attribute var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ l.attribute:type EQ 'radio' OR l.attribute:type EQ 'select' OR l.attribute:type EQ 'swatch-select' }">
</MvFUNCTION>

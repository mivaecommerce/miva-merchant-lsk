<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-RPT-SUB-PST-
| Next Error Code: 31   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-report_subscription_product_stats">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Subscription Product Statistics">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0901">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.62">
	<MvASSIGN NAME = "l.module:features"	VALUE = "report">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: report
|
</MvCOMMENT>

<MvFUNCTION NAME = "ReportModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:date_range"			VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:date_interval"			VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:display"				VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:output_chart"			VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:output_tabular"		VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:output_custom"			VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:provision_settings"	VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Fields" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.report:id }">
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_Active"						VALUE = "{ l.report:config:active }">
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_New"						VALUE = "{ l.report:config:new }">
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_Cancellations"				VALUE = "{ l.report:config:cancellations }">
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_CurrentChurn"				VALUE = "{ l.report:config:current_churn }">
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_GrossRevenue"				VALUE = "{ l.report:config:gross_revenue }">
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_Age"						VALUE = "{ l.report:config:age }">
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_OrderBy"					VALUE = "{ l.report:config:orderby }">
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_Display_TopN"				VALUE = "{ l.report:config:display_topn }">
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_Export_TopN"				VALUE = "{ l.report:config:export_topn }">
	<MvELSE>
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_Active"						VALUE = 1>
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_New"						VALUE = 1>
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_Cancellations"				VALUE = 1>
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_CurrentChurn"				VALUE = 1>
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_GrossRevenue"				VALUE = 1>
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_Age"						VALUE = 1>
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_OrderBy"					VALUE = 1>
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_Display_TopN"				VALUE = 10>
		<MvASSIGN NAME = "g.Report_SubscriptionProductStats_Export_TopN"				VALUE = 10>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "metrics,orderby,topn">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.field_id EQ 'metrics' }">			<MvFUNCTIONRETURN VALUE = "<b>Metrics:</b>">
	<MvELSEIF EXPR = "{ l.field_id EQ 'orderby' }">		<MvFUNCTIONRETURN VALUE = "<b>Sort By:</b>">
	<MvELSEIF EXPR = "{ l.field_id EQ 'topn' }">		<MvFUNCTIONRETURN VALUE = "<b>Display:</b>">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.field_id EQ 'metrics' }">
		<div><label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Report_SubscriptionProductStats_Active,		'Report_SubscriptionProductStats_Active',			'1', 'Active' ) }"></label></div>
		<div><label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Report_SubscriptionProductStats_New,			'Report_SubscriptionProductStats_New',				'1', 'New' ) }"></label></div>
		<div><label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Report_SubscriptionProductStats_Cancellations,	'Report_SubscriptionProductStats_Cancellations',	'1', 'Cancellations' ) }"></label></div>
		<div><label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Report_SubscriptionProductStats_CurrentChurn,	'Report_SubscriptionProductStats_CurrentChurn',		'1', 'Current Churn' ) }"></label></div>
		<div><label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Report_SubscriptionProductStats_GrossRevenue,	'Report_SubscriptionProductStats_GrossRevenue',		'1', 'Gross Revenue' ) }"></label></div>
		<div><label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Report_SubscriptionProductStats_Age,			'Report_SubscriptionProductStats_Age',				'1', 'Average Age' ) }"></label></div>
	<MvELSEIF EXPR = "{ l.field_id EQ 'orderby' }">
		<select name="Report_SubscriptionProductStats_OrderBy">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'active',			g.Report_SubscriptionProductStats_OrderBy, 'Active' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'new',				g.Report_SubscriptionProductStats_OrderBy, 'New' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'cancellations',	g.Report_SubscriptionProductStats_OrderBy, 'Cancellations' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'gross_revenue',	g.Report_SubscriptionProductStats_OrderBy, 'Gross Revenue' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'age',				g.Report_SubscriptionProductStats_OrderBy, 'Average Age' ) }">
		</select>
	<MvELSEIF EXPR = "{ l.field_id EQ 'topn' }">
		<input type="text" size="5" name="Report_SubscriptionProductStats_Display_TopN" value="{ encodeentities( g.Report_SubscriptionProductStats_Display_TopN ) }"> product(s) on the main screen<br>
		<input type="text" size="5" name="Report_SubscriptionProductStats_Export_TopN" value="{ encodeentities( g.Report_SubscriptionProductStats_Export_TopN ) }"> product(s) in exported data
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Validate" PARAMETERS = "modvar, report var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Report_SubscriptionProductStats_Active"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Report_SubscriptionProductStats_Active ) }">
	<MvASSIGN NAME = "g.Report_SubscriptionProductStats_New"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Report_SubscriptionProductStats_New ) }">
	<MvASSIGN NAME = "g.Report_SubscriptionProductStats_Cancellations"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Report_SubscriptionProductStats_Cancellations ) }">
	<MvASSIGN NAME = "g.Report_SubscriptionProductStats_CurrentChurn"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Report_SubscriptionProductStats_CurrentChurn ) }">
	<MvASSIGN NAME = "g.Report_SubscriptionProductStats_GrossRevenue"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Report_SubscriptionProductStats_GrossRevenue ) }">
	<MvASSIGN NAME = "g.Report_SubscriptionProductStats_Age"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Report_SubscriptionProductStats_Age ) }">
	<MvASSIGN NAME = "g.Report_SubscriptionProductStats_OrderBy"			VALUE = "{ trim( g.Report_SubscriptionProductStats_OrderBy ) }">
	<MvASSIGN NAME = "g.Report_SubscriptionProductStats_Display_TopN"		VALUE = "{ trim( g.Report_SubscriptionProductStats_Display_TopN ) }">
	<MvASSIGN NAME = "g.Report_SubscriptionProductStats_Export_TopN"		VALUE = "{ trim( g.Report_SubscriptionProductStats_Export_TopN ) }">

	<MvIF EXPR = "{ NOT g.Report_SubscriptionProductStats_Active AND
					NOT g.Report_SubscriptionProductStats_New AND
					NOT g.Report_SubscriptionProductStats_Cancellations AND
					NOT g.Report_SubscriptionProductStats_CurrentChurn AND
					NOT g.Report_SubscriptionProductStats_GrossRevenue AND
					NOT g.Report_SubscriptionProductStats_Age }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select at least one metric to display.' ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Report_SubscriptionProductStats_OrderBy NE 'active' AND
					g.Report_SubscriptionProductStats_OrderBy NE 'new' AND
					g.Report_SubscriptionProductStats_OrderBy NE 'cancellations' AND
					g.Report_SubscriptionProductStats_OrderBy NE 'gross_revenue' AND
					g.Report_SubscriptionProductStats_OrderBy NE 'age' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select a sorting metric.' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( g.Report_SubscriptionProductStats_Display_TopN ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Report_SubscriptionProductStats_Display_TopN', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( g.Report_SubscriptionProductStats_Export_TopN ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Report_SubscriptionProductStats_Export_TopN', g.Validation_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Update" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.report:config:active"			VALUE = "{ g.Report_SubscriptionProductStats_Active }">
	<MvASSIGN NAME = "l.report:config:new"				VALUE = "{ g.Report_SubscriptionProductStats_New }">
	<MvASSIGN NAME = "l.report:config:cancellations"	VALUE = "{ g.Report_SubscriptionProductStats_Cancellations }">
	<MvASSIGN NAME = "l.report:config:current_churn"	VALUE = "{ g.Report_SubscriptionProductStats_CurrentChurn }">
	<MvASSIGN NAME = "l.report:config:gross_revenue"	VALUE = "{ g.Report_SubscriptionProductStats_GrossRevenue }">
	<MvASSIGN NAME = "l.report:config:age"				VALUE = "{ g.Report_SubscriptionProductStats_Age }">
	<MvASSIGN NAME = "l.report:config:orderby"			VALUE = "{ g.Report_SubscriptionProductStats_OrderBy }">
	<MvASSIGN NAME = "l.report:config:display_topn"		VALUE = "{ g.Report_SubscriptionProductStats_Display_TopN }">
	<MvASSIGN NAME = "l.report:config:export_topn"		VALUE = "{ g.Report_SubscriptionProductStats_Export_TopN }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Calculate_DateRange_All" PARAMETERS = "module var, report var, time_t_start var, time_t_end var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "SubscriptionDates"
				QUERY	= "{ 'SELECT
								MIN( firstdate ) AS min_subscriptiondate,
								MAX( lastdate ) AS max_subscriptiondate
							  FROM
								' $ g.Store_Table_Prefix $ 'Subscriptions' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00001', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ SubscriptionDates.d.EOF OR ( ( ISNULL SubscriptionDates.d.min_subscriptiondate ) AND ( ISNULL SubscriptionDates.d.max_subscriptiondate ) ) }">
		<MvASSIGN NAME = "l.time_t_start"	VALUE = "{ s.time_t }">
		<MvASSIGN NAME = "l.time_t_end"		VALUE = "{ s.time_t }">
	<MvELSE>
		<MvASSIGN NAME = "l.time_t_start"	VALUE = "{ SubscriptionDates.d.min_subscriptiondate }">
		<MvASSIGN NAME = "l.time_t_end"		VALUE = "{ SubscriptionDates.d.max_subscriptiondate + 1 }">	<MvCOMMENT> Compensate for < end </MvCOMMENT>
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionDates">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Run_DateRange" PARAMETERS = "module var, report var, time_t_start, time_t_end" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_RPT_DB ].ReportData_Delete_Report( l.report:id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.query:topn"						VALUE = "{ max( l.report:config:display_topn, l.report:config:export_topn ) }">
	<MvASSIGN NAME = "l.query:time_t_start"				VALUE = "{ l.time_t_start }">
	<MvASSIGN NAME = "l.query:time_t_end"				VALUE = "{ l.time_t_end }">
	<MvASSIGN NAME = "l.query:set_count"				VALUE = 0>
	<MvASSIGN NAME = "l.query:product_count"			VALUE = 0>

	<MvASSIGN NAME = "l.result"							VALUE = 1>

	<MvIF EXPR = "{ l.result }">
		<MvASSIGN NAME = "l.result"						VALUE = "{ SubscriptionProductStats_Create_Product_TempTable( l.query ) }">
	</MvIF>

	<MvIF EXPR = "{ l.result }">
		<MvIF EXPR = "{ l.report:config:orderby EQ 'active' }">				<MvASSIGN NAME = "l.result" VALUE = "{ SubscriptionProductStats_Populate_Product_TempTable_Active( l.report, l.query ) }">
		<MvELSEIF EXPR = "{ l.report:config:orderby EQ 'new' }">			<MvASSIGN NAME = "l.result" VALUE = "{ SubscriptionProductStats_Populate_Product_TempTable_New( l.report, l.query ) }">
		<MvELSEIF EXPR = "{ l.report:config:orderby EQ 'cancellations' }">	<MvASSIGN NAME = "l.result" VALUE = "{ SubscriptionProductStats_Populate_Product_TempTable_Cancellations( l.report, l.query ) }">
		<MvELSEIF EXPR = "{ l.report:config:orderby EQ 'gross_revenue' }">	<MvASSIGN NAME = "l.result" VALUE = "{ SubscriptionProductStats_Populate_Product_TempTable_GrossRevenue( l.report, l.query ) }">
		<MvELSEIF EXPR = "{ l.report:config:orderby EQ 'age' }">			<MvASSIGN NAME = "l.result" VALUE = "{ SubscriptionProductStats_Populate_Product_TempTable_Age( l.report, l.query ) }">
		<MvELSE>															<MvASSIGN NAME = "l.result" VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00002', 'Invalid orderby' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.result }">
		<MvASSIGN NAME = "l.result"						VALUE = "{ SubscriptionProductStats_Index_Product_TempTable( l.query ) }">
	</MvIF>

	<MvIF EXPR = "{ l.result AND l.report:config:active AND ( l.report:config:orderby NE 'active' ) }">
		<MvASSIGN NAME = "l.result"						VALUE = "{ SubscriptionProductStats_Run_Active( l.report, l.query ) }">
	</MvIF>

	<MvIF EXPR = "{ l.result AND l.report:config:new AND ( l.report:config:orderby NE 'new' ) }">
		<MvASSIGN NAME = "l.result"						VALUE = "{ SubscriptionProductStats_Run_New( l.report, l.query ) }">
	</MvIF>

	<MvIF EXPR = "{ l.result AND l.report:config:cancellations AND ( l.report:config:orderby NE 'cancellations' ) }">
		<MvASSIGN NAME = "l.result"						VALUE = "{ SubscriptionProductStats_Run_Cancellations( l.report, l.query ) }">
	</MvIF>

	<MvIF EXPR = "{ l.result AND l.report:config:current_churn }">
		<MvASSIGN NAME = "l.result"						VALUE = "{ SubscriptionProductStats_Run_Active30Days( l.report, l.query ) }">
	</MvIF>

	<MvIF EXPR = "{ l.result AND l.report:config:current_churn }">
		<MvASSIGN NAME = "l.result"						VALUE = "{ SubscriptionProductStats_Run_Cancellations30Days( l.report, l.query ) }">
	</MvIF>

	<MvIF EXPR = "{ l.result AND l.report:config:gross_revenue AND ( l.report:config:orderby NE 'gross_revenue' ) }">
		<MvASSIGN NAME = "l.result"						VALUE = "{ SubscriptionProductStats_Run_GrossRevenue( l.report, l.query ) }">
	</MvIF>

	<MvIF EXPR = "{ l.result AND l.report:config:age AND ( l.report:config:orderby NE 'age' ) }">
		<MvASSIGN NAME = "l.result"						VALUE = "{ SubscriptionProductStats_Run_Age( l.report, l.query ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.query:product_temp_table }">
		<MvASSIGN NAME = "l.result"						VALUE = "{ l.result AND SubscriptionProductStats_Drop_TempTable( l.query:product_temp_table ) }">
	</MvIF>

	<MvIF EXPR = "{ l.result }">
		<MvASSIGN NAME = "l.report:last_state:products"	VALUE = "{ l.query:products }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.result }">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Display" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<table style="border-collapse: collapse; font-size: small; font-family: Verdana, Arial, Helvetica, sans-serif;" cellspacing="0" cellpadding="0">
		<thead style="font-weight: bold;">
			<tr style="height: 20px; margin-left: 10px;">
				<td>&nbsp;</td>

				<MvIF EXPR = "{ l.report:config:active }">
					<td align="right" style="padding-right: 10px;">Active</td>
				</MvIF>

				<MvIF EXPR = "{ l.report:config:new }">
					<td align="right" style="padding-right: 10px;">New</td>
				</MvIF>

				<MvIF EXPR = "{ l.report:config:cancellations }">
					<td align="right" style="padding-right: 10px;">Cancellations</td>
				</MvIF>

				<MvIF EXPR = "{ l.report:config:current_churn }">
					<td align="center" style="padding-right: 10px;">Current<br />Churn</td>
				</MvIF>

				<MvIF EXPR = "{ l.report:config:gross_revenue }">
					<td align="right" style="padding-right: 10px;">Gross<br />Revenue</td>
				</MvIF>

				<MvIF EXPR = "{ l.report:config:age }">
					<td align="center">Average<br />Age</td>
				</MvIF>
			</tr>
		</thead>

		<tbody>
		<MvASSIGN NAME = "l.product_count"		VALUE = "{ miva_array_elements( l.report:last_state:products ) }">
		<MvIF EXPR = "{ l.product_count GT l.report:config:display_topn }">
			<MvASSIGN NAME = "l.product_count"	VALUE = "{ l.report:config:display_topn }">
		</MvIF>

		<MvFOREACH ITERATOR = "l.product" ARRAY = "l.report:last_state:products" COUNT = "{ l.product_count }">
			<MvIF EXPR = "{ NOT SubscriptionProductStats_ReportData_Load_Product( l.report, l.product, l.metrics ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<tr>
			<td valign="top" style="padding-right: 10px; word-break: break-all; word-wrap: break-word;"><MvEVAL EXPR = "{ encodeentities( l.product:name ) }"></td>

			<MvIF EXPR = "{ l.report:config:active }">
				<td valign="top" align="right" style="padding-right: 10px;"><MvEVAL EXPR = "{ l.metrics:active ROUND 0 }"></td>
			</MvIF>

			<MvIF EXPR = "{ l.report:config:new }">
				<td valign="top" align="right" style="padding-right: 10px;"><MvEVAL EXPR = "{ l.metrics:new ROUND 0 }"></td>
			</MvIF>

			<MvIF EXPR = "{ l.report:config:cancellations }">
				<td valign="top" align="right" style="padding-right: 10px;"><MvEVAL EXPR = "{ l.metrics:cancellations ROUND 0 }"></td>
			</MvIF>

			<MvIF EXPR = "{ l.report:config:current_churn }">
				<td valign="top" align="center" style="padding-right: 10px;">
					<MvIF EXPR = "{ l.metrics:active30days EQ 0 }">
						&nbsp;
					<MvELSE>
						<MvEVAL EXPR = "{ ( 100.0 * ( l.metrics:cancellations30days / l.metrics:active30days ) ) ROUND 1 }">%
					</MvIF>
				</td>
			</MvIF>

			<MvIF EXPR = "{ l.report:config:gross_revenue }">
				<td valign="top" align="right" style="padding-right: 10px;"><MvEVAL EXPR = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.metrics:gross_revenue ) }"></td>
			</MvIF>

			<MvIF EXPR = "{ l.report:config:age }">
				<td valign="top" align="center" nowrap><MvEVAL EXPR = "{ l.metrics:age ROUND 1 }"> Days</td>
			</MvIF>
			</tr>
		</MvFOREACH>
		</tbody>
	</table>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Tabular_Definition" PARAMETERS = "module var, report var, definition var" STANDARDOUTPUTLEVEL = "">
														<MvEVAL EXPR = "{ SubscriptionProductStats_Add_Tabular_Column( l.definition, l.values, 'PRODUCT_CODE',	'product_code' ) }">
														<MvEVAL EXPR = "{ SubscriptionProductStats_Add_Tabular_Column( l.definition, l.values, 'PRODUCT_NAME',	'product_name' ) }">

	<MvIF EXPR = "{ l.report:config:active }">			<MvEVAL EXPR = "{ SubscriptionProductStats_Add_Tabular_Column( l.definition, l.values, 'ACTIVE',		'active' ) }">			</MvIF>
	<MvIF EXPR = "{ l.report:config:new }">				<MvEVAL EXPR = "{ SubscriptionProductStats_Add_Tabular_Column( l.definition, l.values, 'NEW',			'new' ) }">				</MvIF>
	<MvIF EXPR = "{ l.report:config:cancellations }">	<MvEVAL EXPR = "{ SubscriptionProductStats_Add_Tabular_Column( l.definition, l.values, 'CANCELLATIONS',	'cancellations' ) }">	</MvIF>
	<MvIF EXPR = "{ l.report:config:current_churn }">	<MvEVAL EXPR = "{ SubscriptionProductStats_Add_Tabular_Column( l.definition, l.values, 'CURRENT_CHURN',	'current_churn' ) }">	</MvIF>
	<MvIF EXPR = "{ l.report:config:gross_revenue }">	<MvEVAL EXPR = "{ SubscriptionProductStats_Add_Tabular_Column( l.definition, l.values, 'GROSS_REVENUE',	'gross_revenue' ) }">	</MvIF>
	<MvIF EXPR = "{ l.report:config:age }">				<MvEVAL EXPR = "{ SubscriptionProductStats_Add_Tabular_Column( l.definition, l.values, 'AVERAGE_AGE',	'age' ) }">				</MvIF>

	<MvASSIGN NAME = "l.product_count"		VALUE = "{ miva_array_elements( l.report:last_state:products ) }">
	<MvIF EXPR = "{ l.product_count GT l.report:config:export_topn }">
		<MvASSIGN NAME = "l.product_count"	VALUE = "{ l.report:config:export_topn }">
	</MvIF>

	<MvFOREACH ITERATOR = "l.product" ARRAY = "l.report:last_state:products" COUNT = "{ l.product_count }">
		<MvIF EXPR = "{ NOT SubscriptionProductStats_ReportData_Load_Product( l.report, l.product, l.metrics ) }">
			<MvFOREACHCONTINUE>
		</MvIF>

															<MvASSIGN NAME = "l.null"	VALUE = "{ miva_array_insert( l.values:product_code,	l.product:code,																	-1 ) }">
															<MvASSIGN NAME = "l.null"	VALUE = "{ miva_array_insert( l.values:product_name,	l.product:name,																	-1 ) }">

		<MvIF EXPR = "{ l.report:config:active }">			<MvASSIGN NAME = "l.null"	VALUE = "{ miva_array_insert( l.values:active,			l.metrics:active,																-1 ) }">	</MvIF>
		<MvIF EXPR = "{ l.report:config:new }">				<MvASSIGN NAME = "l.null"	VALUE = "{ miva_array_insert( l.values:new,				l.metrics:new,																	-1 ) }">	</MvIF>
		<MvIF EXPR = "{ l.report:config:cancellations }">	<MvASSIGN NAME = "l.null"	VALUE = "{ miva_array_insert( l.values:cancellations,	l.metrics:cancellations,														-1 ) }">	</MvIF>
		<MvIF EXPR = "{ l.report:config:current_churn }">
			<MvIF EXPR = "{ l.metrics:active30days EQ 0 }">	<MvASSIGN NAME = "l.null"	VALUE = "{ miva_array_insert( l.values:current_churn,	'',																				-1 ) }">
			<MvELSE>										<MvASSIGN NAME = "l.null"	VALUE = "{ miva_array_insert( l.values:current_churn,	( 100.0 * ( l.metrics:cancellations30days / l.metrics:active30days ) ) ROUND 1,	-1 ) }">
			</MvIF>
		</MvIF>
		<MvIF EXPR = "{ l.report:config:gross_revenue }">	<MvASSIGN NAME = "l.null"	VALUE = "{ miva_array_insert( l.values:gross_revenue,	l.metrics:gross_revenue,														-1 ) }">	</MvIF>
		<MvIF EXPR = "{ l.report:config:age }">				<MvASSIGN NAME = "l.null"	VALUE = "{ miva_array_insert( l.values:age,				l.metrics:age,																	-1 ) }">	</MvIF>
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Provision_Settings" PARAMETERS = "module var, report var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.report:module_id NE l.module:id }">
		<MvASSIGN NAME = "l.report:config:display_topn"	VALUE = 10>
		<MvASSIGN NAME = "l.report:config:export_topn"	VALUE = 10>
	</MvIF>

	<MvASSIGN NAME = "l.report:config:active"			VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Metrics[ 1 ], 'Active' ) }">
	<MvASSIGN NAME = "l.report:config:new"				VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Metrics[ 1 ], 'New' ) }">
	<MvASSIGN NAME = "l.report:config:cancellations"	VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Metrics[ 1 ], 'Cancellations' ) }">
	<MvASSIGN NAME = "l.report:config:current_churn"	VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Metrics[ 1 ], 'CurrentChurn' ) }">
	<MvASSIGN NAME = "l.report:config:gross_revenue"	VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Metrics[ 1 ], 'GrossRevenue' ) }">
	<MvASSIGN NAME = "l.report:config:age"				VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Metrics[ 1 ], 'Age' ) }">

	<MvIF EXPR = "{ NOT l.report:config:active AND
					NOT l.report:config:new AND
					NOT l.report:config:cancellations AND
					NOT l.report:config:current_churn AND
					NOT l.report:config:gross_revenue AND
					NOT l.report:config:age }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'At least one metric must be specified' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'R', l.provide_xml, 'OrderBy',		l.report:config:orderby, 'Active,New,Cancellations,GrossRevenue,Age', 'active,new,cancellations,gross_revenue,age' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O', l.provide_xml, 'Display_TopN',	l.report:config:display_topn )																							OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O', l.provide_xml, 'Export_TopN',	l.report:config:export_topn ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Report data generation functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "SubscriptionProductStats_Create_Product_TempTable" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.attempt"						VALUE = 1>
	<MvWHILE EXPR = "{ l.attempt LE 5 }">
		<MvASSIGN NAME = "l.query:product_temp_table"	VALUE = "{ g.Store_Table_Prefix $ 'TempSPS' $ l.attempt }">

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE TABLE ' $ l.query:product_temp_table $ '
							  (
								product_id ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER() $ '
							  )' }">
		<MvIF EXPR = "{ NOT g.MvQUERY_Error }">
			<MvWHILESTOP>
		</MvIF>

		<MvASSIGN NAME = "l.attempt"					VALUE = "{ l.attempt + 1 }">
	</MvWHILE>

	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Product_TempTable_Insert" PARAMETERS = "query var, product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ l.query:product_temp_table $ ' ( product_id ) VALUES ( ? )' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Index_Product_TempTable" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE INDEX ' $ l.query:product_temp_table $ '_1 ON ' $ l.query:product_temp_table $ ' ( product_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Drop_TempTable" PARAMETERS = "table_name" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DROP TABLE ' $ l.table_name }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Create_Subtotal_TempTable" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.attempt"						VALUE = 1>
	<MvWHILE EXPR = "{ l.attempt LE 5 }">
		<MvASSIGN NAME = "l.query:subtotal_temp_table"	VALUE = "{ g.Store_Table_Prefix $ 'TempSPS_Subtotal' $ l.attempt }">

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE TABLE ' $ l.query:subtotal_temp_table $ '
							  (
								prod_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
								prod_code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
								prod_name	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 100 )		$ ',
								line_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
								quantity	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
								price		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ '
							  )' }">
		<MvIF EXPR = "{ NOT g.MvQUERY_Error }">
			<MvWHILESTOP>
		</MvIF>

		<MvASSIGN NAME = "l.attempt"					VALUE = "{ l.attempt + 1 }">
	</MvWHILE>

	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00021', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Index_Subtotal_TempTable" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE INDEX ' $ l.query:subtotal_temp_table $ '_1 ON ' $ l.query:subtotal_temp_table $ ' ( prod_id, prod_code, prod_name )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00023', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE INDEX ' $ l.query:subtotal_temp_table $ '_2 ON ' $ l.query:subtotal_temp_table $ ' ( line_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00024', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Drop_Subtotal_TempTable" PARAMETERS = "table_name" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DROP TABLE ' $ l.table_name }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00025', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Add_Product" PARAMETERS = "query var, product_id, product_code, product_name" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| This function exists to allow the same variable to be referenced inside both
	| l.query:product_lookup and l.query:products, which requires a function call in
	| order to force a new temporary variable to be created.
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.product:id"								VALUE = "{ l.product_id }">
	<MvASSIGN NAME = "l.product:code"							VALUE = "{ l.product_code }">
	<MvASSIGN NAME = "l.product:name"							VALUE = "{ l.product_name }">

	<MvASSIGN NAME = "l.product:set_ids:active"					VALUE = "{ ++l.query:set_count }">
	<MvASSIGN NAME = "l.product:set_ids:new"					VALUE = "{ ++l.query:set_count }">
	<MvASSIGN NAME = "l.product:set_ids:cancellations"			VALUE = "{ ++l.query:set_count }">
	<MvASSIGN NAME = "l.product:set_ids:active30days"			VALUE = "{ ++l.query:set_count }">
	<MvASSIGN NAME = "l.product:set_ids:cancellations30days"	VALUE = "{ ++l.query:set_count }">
	<MvASSIGN NAME = "l.product:set_ids:gross_revenue"			VALUE = "{ ++l.query:set_count }">
	<MvASSIGN NAME = "l.product:set_ids:age"					VALUE = "{ ++l.query:set_count }">

	<MvREFERENCE NAME = "l.query:product_lookup"	INDEX = "{ l.product_id }"				VARIABLE = "l.product">
	<MvREFERENCE NAME = "l.query:products"			INDEX = "{ ++l.query:product_count }"	VARIABLE = "l.product">
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Add_Tabular_Column" PARAMETERS = "definition var, values var, label, member" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCEARRAY NAME = "l.column" VARIABLE = "l.definition">
		<MvMEMBER NAME = "columns">
		<MvDIMENSION INDEX = "{ miva_array_elements( l.definition:columns ) + 1 }">
	</MvREFERENCEARRAY>

	<MvASSIGN NAME = "l.column:type"	VALUE = "values">
	<MvASSIGN NAME = "l.column:start"	VALUE = 1>

	<MvREFERENCEARRAY NAME = "l.values_member" VARIABLE = "l.values">
		<MvMEMBER NAME = "{ l.member }">
	</MvREFERENCEARRAY>

	<MvASSIGN NAME = "l.values_member"	VALUE = "">
	<MvASSIGN NAME = "l.null"			VALUE = "{ miva_array_insert( l.values_member, l.label, -1 ) }">

	<MvREFERENCE NAME = "l.column:values"	VARIABLE = "l.values_member">
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Populate_Product_TempTable_Active" PARAMETERS = "report var, query var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Session:cache:subscriptionproductstats:query"	VALUE = "{ l.query }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'SubscriptionProductStats_Active',
																			 'SELECT
																				p.id		AS product_id,
																				p.code		AS product_code,
																				p.name		AS product_name,
																				COUNT( * )	AS active
																			  FROM
																				' $ g.Store_Table_Prefix $ 'Subscriptions s,
																				' $ g.Store_Table_Prefix $ 'Products p
																			  WHERE
																				p.id		= s.product_id	AND
																				s.firstdate	<= ?			AND
																				( ( ( s.status = \'A\' ) AND ( ( s.termrem > 0 ) OR ( s.lastdate >= ? ) ) ) OR
																				  ( ( s.status = \'C\' ) AND ( s.cncldate >= ? ) ) )
																			  GROUP BY
																				p.id, p.code, p.name
																			  ORDER BY
																				active DESC',
																			 'g.Session:cache:subscriptionproductstats:query:time_t_end, g.Session:cache:subscriptionproductstats:query:time_t_start, g.Session:cache:subscriptionproductstats:query:time_t_start',
																			 0, l.query:topn ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00007', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ ( NOT SubscriptionProductStats_Active.d.EOF ) AND ( l.query:product_count LE l.query:topn ) }">
		<MvEVAL EXPR = "{ SubscriptionProductStats_Add_Product( l.query, SubscriptionProductStats_Active.d.product_id, SubscriptionProductStats_Active.d.product_code, SubscriptionProductStats_Active.d.product_name ) }">

		<MvIF EXPR = "{ NOT SubscriptionProductStats_Product_TempTable_Insert( l.query, SubscriptionProductStats_Active.d.product_id ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Active">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT SubscriptionProductStats_Insert_Active( l.report, l.query, SubscriptionProductStats_Active.d.product_id, SubscriptionProductStats_Active.d.active ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Active">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "SubscriptionProductStats_Active" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Active">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Populate_Product_TempTable_New" PARAMETERS = "report var, query var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Session:cache:subscriptionproductstats:query"	VALUE = "{ l.query }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'SubscriptionProductStats_New',
																			 'SELECT
																				p.id		AS product_id,
																				p.code		AS product_code,
																				p.name		AS product_name,
																				COUNT( * )	AS new
																			  FROM
																				' $ g.Store_Table_Prefix $ 'Subscriptions s,
																				' $ g.Store_Table_Prefix $ 'Products p
																			  WHERE
																				p.id		= s.product_id	AND
																				s.firstdate	>= ?			AND
																				s.firstdate	<= ?
																			  GROUP BY
																				p.id, p.code, p.name
																			  ORDER BY
																				new DESC',
																			 'g.Session:cache:subscriptionproductstats:query:time_t_start, g.Session:cache:subscriptionproductstats:query:time_t_end',
																			 0, l.query:topn ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00008', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ ( NOT SubscriptionProductStats_New.d.EOF ) AND ( l.query:product_count LE l.query:topn ) }">
		<MvEVAL EXPR = "{ SubscriptionProductStats_Add_Product( l.query, SubscriptionProductStats_New.d.product_id, SubscriptionProductStats_New.d.product_code, SubscriptionProductStats_New.d.product_name ) }">

		<MvIF EXPR = "{ NOT SubscriptionProductStats_Product_TempTable_Insert( l.query, SubscriptionProductStats_New.d.product_id ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_New">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT SubscriptionProductStats_Insert_New( l.report, l.query, SubscriptionProductStats_New.d.product_id, SubscriptionProductStats_New.d.new ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_New">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "SubscriptionProductStats_New" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_New">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Populate_Product_TempTable_Cancellations" PARAMETERS = "report var, query var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Session:cache:subscriptionproductstats:query"	VALUE = "{ l.query }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'SubscriptionProductStats_Cancellations',
																			 'SELECT
																				p.id		AS product_id,
																				p.code		AS product_code,
																				p.name		AS product_name,
																				COUNT( * )	AS cancellations
																			  FROM
																				' $ g.Store_Table_Prefix $ 'Subscriptions s,
																				' $ g.Store_Table_Prefix $ 'Products p
																			  WHERE
																				p.id		= s.product_id	AND
																				s.status	= \'C\'			AND
																				s.cncldate	>= ?			AND
																				s.cncldate	<= ?
																			  GROUP BY
																				p.id, p.code, p.name
																			  ORDER BY
																				cancellations DESC',
																			 'g.Session:cache:subscriptionproductstats:query:time_t_start, g.Session:cache:subscriptionproductstats:query:time_t_end',
																			 0, l.query:topn ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ ( NOT SubscriptionProductStats_Cancellations.d.EOF ) AND ( l.query:product_count LE l.query:topn ) }">
		<MvEVAL EXPR = "{ SubscriptionProductStats_Add_Product( l.query, SubscriptionProductStats_Cancellations.d.product_id, SubscriptionProductStats_Cancellations.d.product_code, SubscriptionProductStats_Cancellations.d.product_name ) }">

		<MvIF EXPR = "{ NOT SubscriptionProductStats_Product_TempTable_Insert( l.query, SubscriptionProductStats_Cancellations.d.product_id ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Cancellations">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT SubscriptionProductStats_Insert_Cancellations( l.report, l.query, SubscriptionProductStats_Cancellations.d.product_id, SubscriptionProductStats_Cancellations.d.cancellations ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Cancellations">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "SubscriptionProductStats_Cancellations" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Cancellations">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Populate_Product_TempTable_GrossRevenue" PARAMETERS = "report var, query var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT SubscriptionProductStats_Create_Subtotal_TempTable( l.query ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.result" VALUE = "{ SubscriptionProductStats_Populate_Product_TempTable_GrossRevenue_LowLevel( l.report, l.query ) }">

	<MvIF EXPR = "{ NOT ISNULL l.query:subtotal_temp_table }">
		<MvASSIGN NAME = "l.result" VALUE = "{ l.result AND SubscriptionProductStats_Drop_Subtotal_TempTable( l.query:subtotal_temp_table ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.result }">
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Populate_Product_TempTable_GrossRevenue_LowLevel" PARAMETERS = "report var, query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'INSERT INTO ' $ l.query:subtotal_temp_table $ '
						  SELECT
							p.id			AS prod_id,
							p.code			AS prod_code,
							p.name			AS prod_name,
							oi.line_id		AS line_id,
							oi.quantity		AS quantity,
							oi.price		AS price
						  FROM
							' $ g.Store_Table_Prefix $ 'Orders o,
							' $ g.Store_Table_Prefix $ 'OrderItems oi,
							' $ g.Store_Table_Prefix $ 'Subscriptions s,
							' $ g.Store_Table_Prefix $ 'Products p
						  WHERE
							o.source		= \'subscription\'	AND
							o.orderdate		>= ?				AND
							o.orderdate		<= ?				AND
							s.id			= o.source_id		AND
							p.id			= s.product_id 		AND
							oi.order_id		= o.id 				AND
							oi.subscrp_id	= s.id' }"
			 FIELDS = "l.query:time_t_start, l.query:time_t_end">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00026', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'INSERT INTO ' $ l.query:subtotal_temp_table $ '
						  SELECT
							p.id			AS prod_id,
							p.code			AS prod_code,
							p.name			AS prod_name,
							oi.line_id		AS line_id,
							oi.quantity		AS quantity,
							oi.price		AS price
						  FROM
							' $ g.Store_Table_Prefix $ 'Orders o,
							' $ g.Store_Table_Prefix $ 'OrderSources os,
							' $ g.Store_Table_Prefix $ 'OrderItems oi,
							' $ g.Store_Table_Prefix $ 'Subscriptions s,
							' $ g.Store_Table_Prefix $ 'Products p
						  WHERE
							o.source		= \'subscription\'	AND
							o.orderdate		>= ?				AND
							o.orderdate		<= ?				AND
							o.source_id		= 0					AND
							os.order_id		= o.id				AND
							os.source		= \'subscription\'	AND
							os.source_id	= s.id				AND
							p.id			= s.product_id		AND
							oi.order_id		= o.id				AND
							oi.subscrp_id	= s.id' }"
			 FIELDS = "l.query:time_t_start, l.query:time_t_end">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00027', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT SubscriptionProductStats_Index_Subtotal_TempTable( l.query ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE 
			 				' $ l.query:subtotal_temp_table $ '
						  SET
							price = price + ( SELECT ' $ [ g.Module_Library_Native_DBAPI ].DB_IFNULL( 'SUM( price )', '0.00' ) $ ' FROM ' $ g.Store_Table_Prefix $ 'OrderOptions WHERE ' $ g.Store_Table_Prefix $ 'OrderOptions.line_id = ' $ l.query:subtotal_temp_table $ '.line_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00028', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'SubscriptionProductStats_GrossRevenue',
																			 'SELECT
																				prod_id						AS product_id,
																				prod_code					AS product_code,
																				prod_name					AS product_name,
																				SUM( price * quantity )		AS gross_revenue
																			  FROM
																				' $ l.query:subtotal_temp_table $ '
																			  GROUP BY
																				prod_id, prod_code, prod_name
																			  ORDER BY
																				gross_revenue DESC',
																			 '',
																			 0, l.query:topn ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00029', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ ( NOT SubscriptionProductStats_GrossRevenue.d.EOF ) AND ( l.query:product_count LE l.query:topn ) }">
		<MvEVAL EXPR = "{ SubscriptionProductStats_Add_Product( l.query, SubscriptionProductStats_GrossRevenue.d.product_id, SubscriptionProductStats_GrossRevenue.d.product_code, SubscriptionProductStats_GrossRevenue.d.product_name ) }">

		<MvIF EXPR = "{ NOT SubscriptionProductStats_Product_TempTable_Insert( l.query, SubscriptionProductStats_GrossRevenue.d.product_id ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_GrossRevenue">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT SubscriptionProductStats_Insert_GrossRevenue( l.report, l.query, SubscriptionProductStats_GrossRevenue.d.product_id, SubscriptionProductStats_GrossRevenue.d.gross_revenue ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_GrossRevenue">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "SubscriptionProductStats_GrossRevenue" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_GrossRevenue">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Populate_Product_TempTable_Age" PARAMETERS = "report var, query var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Session:cache:subscriptionproductstats:query"	VALUE = "{ l.query }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'SubscriptionProductStats_Age',
																			 'SELECT
																				p.id				AS product_id,
																				p.code				AS product_code,
																				p.name				AS product_name,
																				AVG( s.firstdate )	AS average_firstdate
																			  FROM
																				' $ g.Store_Table_Prefix $ 'Subscriptions s,
																				' $ g.Store_Table_Prefix $ 'Products p
																			  WHERE
																				p.id		= s.product_id	AND
																				s.firstdate	<= ? AND
																				( ( ( s.status = \'A\' ) AND ( ( s.termrem > 0 ) OR ( s.lastdate <= ? ) ) ) OR
																				  ( ( s.status = \'C\' ) AND ( s.cncldate <= ? ) ) )
																			  GROUP BY
																				p.id, p.code, p.name
																			  ORDER BY
																				average_firstdate ASC',
																			 'g.Session:cache:subscriptionproductstats:query:time_t_end, g.Session:cache:subscriptionproductstats:query:time_t_end, g.Session:cache:subscriptionproductstats:query:time_t_end',
																			 0, l.query:topn ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00011', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ ( NOT SubscriptionProductStats_Age.d.EOF ) AND ( l.query:product_count LE l.query:topn ) }">
		<MvEVAL EXPR = "{ SubscriptionProductStats_Add_Product( l.query, SubscriptionProductStats_Age.d.product_id, SubscriptionProductStats_Age.d.product_code, SubscriptionProductStats_Age.d.product_name ) }">

		<MvIF EXPR = "{ NOT SubscriptionProductStats_Product_TempTable_Insert( l.query, SubscriptionProductStats_Age.d.product_id ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Age">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT SubscriptionProductStats_Insert_Age( l.report, l.query, SubscriptionProductStats_Age.d.product_id, ( l.query:time_t_end - SubscriptionProductStats_Age.d.average_firstdate ) / 86400 ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Age">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "SubscriptionProductStats_Age" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Age">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Run_Active" PARAMETERS = "report var, query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "SubscriptionProductStats_Active"
				QUERY	= "{ 'SELECT
								s.product_id	AS product_id,
								COUNT( * )		AS active
							  FROM
								' $ l.query:product_temp_table $ ' p,
								' $ g.Store_Table_Prefix $ 'Subscriptions s
							  WHERE
								s.product_id	= p.product_id	AND
								s.firstdate		<= ?			AND
								( ( ( s.status = \'A\' ) AND ( ( s.termrem > 0 ) OR ( s.lastdate >= ? ) ) ) OR
								  ( ( s.status = \'C\' ) AND ( s.cncldate >= ? ) ) )
							  GROUP BY
								s.product_id' }"
				FIELDS	= "l.query:time_t_end, l.query:time_t_start, l.query:time_t_start">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00012', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT SubscriptionProductStats_Active.d.EOF }">
		<MvIF EXPR = "{ NOT SubscriptionProductStats_Insert_Active( l.report, l.query, SubscriptionProductStats_Active.d.product_id, SubscriptionProductStats_Active.d.active ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Active">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "SubscriptionProductStats_Active" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Active">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Run_New" PARAMETERS = "report var, query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "SubscriptionProductStats_New"
				QUERY	= "{ 'SELECT
								s.product_id	AS product_id,
								COUNT( * )		AS new 
							  FROM
								' $ l.query:product_temp_table $ ' p,
								' $ g.Store_Table_Prefix $ 'Subscriptions s
							  WHERE
								s.product_id	= p.product_id	AND
								s.firstdate		>= ?			AND
								s.firstdate		<= ?
							  GROUP BY
								s.product_id' }"
				FIELDS	= "l.query:time_t_start, l.query:time_t_end">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00013', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT SubscriptionProductStats_New.d.EOF }">
		<MvIF EXPR = "{ NOT SubscriptionProductStats_Insert_New( l.report, l.query, SubscriptionProductStats_New.d.product_id, SubscriptionProductStats_New.d.new ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_New">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "SubscriptionProductStats_New" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_New">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Run_Cancellations" PARAMETERS = "report var, query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "SubscriptionProductStats_Cancellations"
				QUERY	= "{ 'SELECT
								s.product_id	AS product_id,
								COUNT( * )		AS cancellations 
							  FROM
								' $ l.query:product_temp_table $ ' p,
								' $ g.Store_Table_Prefix $ 'Subscriptions s
							  WHERE
								s.product_id	= p.product_id	AND
								s.status		= \'C\'			AND
								s.cncldate		>= ?			AND
								s.cncldate		<= ?
							  GROUP BY
								s.product_id' }"
				FIELDS	= "l.query:time_t_start, l.query:time_t_end">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00014', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT SubscriptionProductStats_Cancellations.d.EOF }">
		<MvIF EXPR = "{ NOT SubscriptionProductStats_Insert_Cancellations( l.report, l.query, SubscriptionProductStats_Cancellations.d.product_id, SubscriptionProductStats_Cancellations.d.cancellations ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Cancellations">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "SubscriptionProductStats_Cancellations" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Cancellations">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Run_Active30Days" PARAMETERS = "report var, query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.asof"	VALUE = "{ l.query:time_t_end - ( 30 * 86400 ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "SubscriptionProductStats_Active30Days"
				QUERY	= "{ 'SELECT
								s.product_id	AS product_id,
								COUNT( * )		AS active
							  FROM
								' $ l.query:product_temp_table $ ' p,
								' $ g.Store_Table_Prefix $ 'Subscriptions s
							  WHERE
								s.product_id	= p.product_id	AND
								s.firstdate		<= ?			AND
								( ( ( s.status = \'A\' ) AND ( ( s.termrem > 0 ) OR ( s.lastdate >= ? ) ) ) OR
								  ( ( s.status = \'C\' ) AND ( s.cncldate >= ? ) ) )
							  GROUP BY
								s.product_id' }"
				FIELDS	= "l.asof, l.asof, l.asof">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00015', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT SubscriptionProductStats_Active30Days.d.EOF }">
		<MvIF EXPR = "{ NOT SubscriptionProductStats_Insert_Active30Days( l.report, l.query, SubscriptionProductStats_Active30Days.d.product_id, SubscriptionProductStats_Active30Days.d.active ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Active30Days">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "SubscriptionProductStats_Active30Days" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Active30Days">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Run_Cancellations30Days" PARAMETERS = "report var, query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.start"	VALUE = "{ l.query:time_t_end - ( 30 * 86400 ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "SubscriptionProductStats_Cancellations30Days"
				QUERY	= "{ 'SELECT
								s.product_id	AS product_id,
								COUNT( * )		AS cancellations 
							  FROM
								' $ l.query:product_temp_table $ ' p,
								' $ g.Store_Table_Prefix $ 'Subscriptions s
							  WHERE
								s.product_id	= p.product_id	AND
								s.status		= \'C\'			AND
								s.cncldate		>= ?			AND
								s.cncldate		<= ?
							  GROUP BY
								s.product_id' }"
				FIELDS	= "l.start, l.query:time_t_end">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00016', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT SubscriptionProductStats_Cancellations30Days.d.EOF }">
		<MvIF EXPR = "{ NOT SubscriptionProductStats_Insert_Cancellations30Days( l.report, l.query, SubscriptionProductStats_Cancellations30Days.d.product_id, SubscriptionProductStats_Cancellations30Days.d.cancellations ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Cancellations30Days">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "SubscriptionProductStats_Cancellations30Days" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Cancellations30Days">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Run_GrossRevenue" PARAMETERS = "report var, query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "SubscriptionProductStats_GrossRevenue"
				QUERY	= "{ 'SELECT
								s.product_id		AS product_id,
								oi.line_id			AS orderitem_line_id,
								oi.price			AS orderitem_price,
								oi.quantity			AS orderitem_quantity,
								oo.price			AS orderoption_price,
								oo.attr_id			AS orderoption_attr_id
							  FROM
								' $ g.Store_Table_Prefix $ 'Orders o,
								' $ g.Store_Table_Prefix $ 'OrderItems oi
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'OrderOptions oo ON oi.line_id = oo.line_id,
								' $ g.Store_Table_Prefix $ 'Subscriptions s,
								' $ l.query:product_temp_table $ ' p
							  WHERE
								o.source			= \'subscription\'	AND
								o.orderdate			>= ?				AND
								o.orderdate			<= ?				AND
								s.id				= o.source_id		AND
								p.product_id		= s.product_id		AND
								oi.order_id			= o.id				AND
								oi.subscrp_id		= s.id
							  UNION ALL
							  SELECT
								s.product_id		AS product_id,
								oi.line_id			AS orderitem_line_id,
								oi.price			AS orderitem_price,
								oi.quantity			AS orderitem_quantity,
								oo.price			AS orderoption_price,
								oo.attr_id			AS orderoption_attr_id
							  FROM
								' $ g.Store_Table_Prefix $ 'Orders o,
								' $ g.Store_Table_Prefix $ 'OrderSources os,
								' $ g.Store_Table_Prefix $ 'OrderItems oi
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'OrderOptions oo ON oi.line_id = oo.line_id,
								' $ g.Store_Table_Prefix $ 'Subscriptions s,
								' $ l.query:product_temp_table $ ' p
							  WHERE
								o.source			= \'subscription\'	AND
								o.orderdate			>= ?				AND
								o.orderdate			<= ?				AND
								o.source_id			= 0					AND
								os.order_id			= o.id				AND
								os.source			= \'subscription\'	AND
								os.source_id		= s.id				AND
								p.product_id		= s.product_id		AND
								oi.order_id			= o.id				AND
								oi.subscrp_id		= s.id
							  ORDER BY
							  	product_id ASC, orderitem_line_id ASC, orderoption_attr_id ASC' }"
				FIELDS	= "l.query:time_t_start, l.query:time_t_end, l.query:time_t_start, l.query:time_t_end">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00030', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.subtotal"					VALUE = 0.00>
	<MvASSIGN NAME = "l.last_line_id"				VALUE = -1>
	<MvASSIGN NAME = "l.last_product_id"			VALUE = -1>

	<MvWHILE EXPR = "{ NOT SubscriptionProductStats_GrossRevenue.d.EOF }">
		<MvIF EXPR = "{ SubscriptionProductStats_GrossRevenue.d.product_id NE l.last_product_id }">
			<MvASSIGN NAME = "l.last_product_id"	VALUE = "{ SubscriptionProductStats_GrossRevenue.d.product_id }">
		</MvIF>

		<MvIF EXPR = "{ SubscriptionProductStats_GrossRevenue.d.orderitem_line_id NE l.last_line_id }">
			<MvASSIGN NAME = "l.subtotal"			VALUE = "{ l.subtotal + SubscriptionProductStats_GrossRevenue.d.orderitem_price * SubscriptionProductStats_GrossRevenue.d.orderitem_quantity }">
			<MvASSIGN NAME = "l.last_line_id"		VALUE = "{ SubscriptionProductStats_GrossRevenue.d.orderitem_line_id }">
		</MvIF>

		<MvIF EXPR = "{ SubscriptionProductStats_GrossRevenue.d.orderoption_price GT 0 }">
			<MvASSIGN NAME = "l.subtotal"			VALUE = "{ l.subtotal + ( SubscriptionProductStats_GrossRevenue.d.orderoption_price * SubscriptionProductStats_GrossRevenue.d.orderitem_quantity ) }">
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "SubscriptionProductStats_GrossRevenue" ROWS = 1>

		<MvIF EXPR = "{ SubscriptionProductStats_GrossRevenue.d.EOF OR SubscriptionProductStats_GrossRevenue.d.product_id NE l.last_product_id }">
			<MvIF EXPR = "{ NOT SubscriptionProductStats_Insert_GrossRevenue( l.report, l.query, l.last_product_id, l.subtotal ) }">
				<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_GrossRevenue">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.last_product_id"	VALUE = "{ SubscriptionProductStats_GrossRevenue.d.product_id }">
			<MvASSIGN NAME = "l.subtotal"			VALUE = 0.00>
		</MvIF>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_GrossRevenue">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Run_Age" PARAMETERS = "report var, query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "SubscriptionProductStats_Age"
				QUERY	= "{ 'SELECT
								s.product_id		AS product_id,
								AVG( s.firstdate )	AS average_firstdate
							  FROM
								' $ l.query:product_temp_table $ ' p,
								' $ g.Store_Table_Prefix $ 'Subscriptions s
							  WHERE
								s.product_id	= p.product_id	AND
								s.firstdate		<= ? AND
								( ( ( s.status = \'A\' ) AND ( ( s.termrem > 0 ) OR ( s.lastdate <= ? ) ) ) OR
								  ( ( s.status = \'C\' ) AND ( s.cncldate <= ? ) ) )
							  GROUP BY
								s.product_id' }"
				FIELDS	= "l.query:time_t_end, l.query:time_t_end, l.query:time_t_end">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00018', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT SubscriptionProductStats_Age.d.EOF }">
		<MvIF EXPR = "{ NOT SubscriptionProductStats_Insert_Age( l.report, l.query, SubscriptionProductStats_Age.d.product_id, ( l.query:time_t_end - SubscriptionProductStats_Age.d.average_firstdate ) / 86400 ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Age">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "SubscriptionProductStats_Age" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SubscriptionProductStats_Age">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Insert_Active" PARAMETERS = "report var, query var, product_id, active" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT miva_element_exists( l.query:product_lookup, l.product_id ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.reportdata:report_id"	VALUE = "{ l.report:id }">
	<MvASSIGN NAME = "l.reportdata:dt_start"	VALUE = "{ l.query:time_t_start }">
	<MvASSIGN NAME = "l.reportdata:dt_end"		VALUE = "{ l.query:time_t_end }">
	<MvASSIGN NAME = "l.reportdata:set_id"		VALUE = "{ l.query:product_lookup[ l.product_id ]:set_ids:active }">
	<MvASSIGN NAME = "l.reportdata:data"		VALUE = "{ l.active ROUND 0 }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_RPT_DB ].ReportData_Insert( l.reportdata ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Insert_New" PARAMETERS = "report var, query var, product_id, new" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT miva_element_exists( l.query:product_lookup, l.product_id ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.reportdata:report_id"	VALUE = "{ l.report:id }">
	<MvASSIGN NAME = "l.reportdata:dt_start"	VALUE = "{ l.query:time_t_start }">
	<MvASSIGN NAME = "l.reportdata:dt_end"		VALUE = "{ l.query:time_t_end }">
	<MvASSIGN NAME = "l.reportdata:set_id"		VALUE = "{ l.query:product_lookup[ l.product_id ]:set_ids:new }">
	<MvASSIGN NAME = "l.reportdata:data"		VALUE = "{ l.new ROUND 0 }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_RPT_DB ].ReportData_Insert( l.reportdata ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Insert_Cancellations" PARAMETERS = "report var, query var, product_id, cancellations" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT miva_element_exists( l.query:product_lookup, l.product_id ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.reportdata:report_id"	VALUE = "{ l.report:id }">
	<MvASSIGN NAME = "l.reportdata:dt_start"	VALUE = "{ l.query:time_t_start }">
	<MvASSIGN NAME = "l.reportdata:dt_end"		VALUE = "{ l.query:time_t_end }">
	<MvASSIGN NAME = "l.reportdata:set_id"		VALUE = "{ l.query:product_lookup[ l.product_id ]:set_ids:cancellations }">
	<MvASSIGN NAME = "l.reportdata:data"		VALUE = "{ l.cancellations ROUND 0 }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_RPT_DB ].ReportData_Insert( l.reportdata ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Insert_Active30Days" PARAMETERS = "report var, query var, product_id, active" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT miva_element_exists( l.query:product_lookup, l.product_id ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.reportdata:report_id"	VALUE = "{ l.report:id }">
	<MvASSIGN NAME = "l.reportdata:dt_start"	VALUE = "{ l.query:time_t_start }">
	<MvASSIGN NAME = "l.reportdata:dt_end"		VALUE = "{ l.query:time_t_end }">
	<MvASSIGN NAME = "l.reportdata:set_id"		VALUE = "{ l.query:product_lookup[ l.product_id ]:set_ids:active30days }">
	<MvASSIGN NAME = "l.reportdata:data"		VALUE = "{ l.active ROUND 0 }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_RPT_DB ].ReportData_Insert( l.reportdata ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Insert_Cancellations30Days" PARAMETERS = "report var, query var, product_id, cancellations" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT miva_element_exists( l.query:product_lookup, l.product_id ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.reportdata:report_id"	VALUE = "{ l.report:id }">
	<MvASSIGN NAME = "l.reportdata:dt_start"	VALUE = "{ l.query:time_t_start }">
	<MvASSIGN NAME = "l.reportdata:dt_end"		VALUE = "{ l.query:time_t_end }">
	<MvASSIGN NAME = "l.reportdata:set_id"		VALUE = "{ l.query:product_lookup[ l.product_id ]:set_ids:cancellations30days }">
	<MvASSIGN NAME = "l.reportdata:data"		VALUE = "{ l.cancellations ROUND 0 }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_RPT_DB ].ReportData_Insert( l.reportdata ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Insert_GrossRevenue" PARAMETERS = "report var, query var, product_id, gross_revenue" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT miva_element_exists( l.query:product_lookup, l.product_id ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.reportdata:report_id"	VALUE = "{ l.report:id }">
	<MvASSIGN NAME = "l.reportdata:dt_start"	VALUE = "{ l.query:time_t_start }">
	<MvASSIGN NAME = "l.reportdata:dt_end"		VALUE = "{ l.query:time_t_end }">
	<MvASSIGN NAME = "l.reportdata:set_id"		VALUE = "{ l.query:product_lookup[ l.product_id ]:set_ids:gross_revenue }">
	<MvASSIGN NAME = "l.reportdata:data"		VALUE = "{ l.gross_revenue ROUND 2 }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_RPT_DB ].ReportData_Insert( l.reportdata ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_Insert_Age" PARAMETERS = "report var, query var, product_id, age" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT miva_element_exists( l.query:product_lookup, l.product_id ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.reportdata:report_id"	VALUE = "{ l.report:id }">
	<MvASSIGN NAME = "l.reportdata:dt_start"	VALUE = "{ l.query:time_t_start }">
	<MvASSIGN NAME = "l.reportdata:dt_end"		VALUE = "{ l.query:time_t_end }">
	<MvASSIGN NAME = "l.reportdata:set_id"		VALUE = "{ l.query:product_lookup[ l.product_id ]:set_ids:age }">
	<MvASSIGN NAME = "l.reportdata:data"		VALUE = "{ l.age ROUND 1 }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_RPT_DB ].ReportData_Insert( l.reportdata ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "SubscriptionProductStats_ReportData_Load_Product" PARAMETERS = "report var, product var, metrics var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.report:config:orderby EQ 'active' }">
		<MvASSIGN NAME = "l.primary_correlation"	VALUE = "rd_active">

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query, g.Store_Table_Prefix $ 'ReportData', 'rd_active' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'rd_active.report_id = ? AND rd_active.set_id = ?', 'l.report:id, l.product:set_ids:active' ) }">
	<MvELSEIF EXPR = "{ l.report:config:orderby EQ 'new' }">
		<MvASSIGN NAME = "l.primary_correlation"	VALUE = "rd_new">

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query, g.Store_Table_Prefix $ 'ReportData', 'rd_new' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'rd_new.report_id = ? AND rd_new.set_id = ?', 'l.report:id, l.product:set_ids:new' ) }">
	<MvELSEIF EXPR = "{ l.report:config:orderby EQ 'cancellations' }">
		<MvASSIGN NAME = "l.primary_correlation"	VALUE = "rd_cancellations">

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query, g.Store_Table_Prefix $ 'ReportData', 'rd_cancellations' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'rd_cancellations.report_id = ? AND rd_cancellations.set_id = ?', 'l.report:id, l.product:set_ids:cancellations' ) }">
	<MvELSEIF EXPR = "{ l.report:config:orderby EQ 'gross_revenue' }">
		<MvASSIGN NAME = "l.primary_correlation"	VALUE = "rd_gross_revenue">

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query, g.Store_Table_Prefix $ 'ReportData', 'rd_gross_revenue' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'rd_gross_revenue.report_id = ? AND rd_gross_revenue.set_id = ?', 'l.report:id, l.product:set_ids:gross_revenue' ) }">
	<MvELSEIF EXPR = "{ l.report:config:orderby EQ 'age' }">
		<MvASSIGN NAME = "l.primary_correlation"	VALUE = "rd_age">

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query, g.Store_Table_Prefix $ 'ReportData', 'rd_age' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'rd_age.report_id = ? AND rd_age.set_id = ?', 'l.report:id, l.product:set_ids:age' ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.result" VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00019', 'Invalid orderby' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.report:config:active }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.query, 'rd_active.data AS active' ) }">

		<MvIF EXPR = "{ l.report:config:orderby NE 'active' }">
			<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, l.primary_correlation, g.Store_Table_Prefix $ 'ReportData', 'rd_active',
																				 'rd_active.report_id = ? AND rd_active.set_id = ?', 'l.report:id, l.product:set_ids:active' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.report:config:new }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.query, 'rd_new.data AS new' ) }">

		<MvIF EXPR = "{ l.report:config:orderby NE 'new' }">
			<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, l.primary_correlation, g.Store_Table_Prefix $ 'ReportData', 'rd_new',
																				 'rd_new.report_id = ? AND rd_new.set_id = ?', 'l.report:id, l.product:set_ids:new' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.report:config:cancellations }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.query, 'rd_cancellations.data AS cancellations' ) }">

		<MvIF EXPR = "{ l.report:config:orderby NE 'cancellations' }">
			<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, l.primary_correlation, g.Store_Table_Prefix $ 'ReportData', 'rd_cancellations',
																				 'rd_cancellations.report_id = ? AND rd_cancellations.set_id = ?', 'l.report:id, l.product:set_ids:cancellations' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.report:config:current_churn }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.query, 'rd_active30days.data			AS active30days,
																			  rd_cancellations30days.data	AS cancellations30days' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, l.primary_correlation, g.Store_Table_Prefix $ 'ReportData', 'rd_active30days',
																			 'rd_active30days.report_id = ? AND rd_active30days.set_id = ?', 'l.report:id, l.product:set_ids:active30days' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, l.primary_correlation, g.Store_Table_Prefix $ 'ReportData', 'rd_cancellations30days',
																			 'rd_cancellations30days.report_id = ? AND rd_cancellations30days.set_id = ?', 'l.report:id, l.product:set_ids:cancellations30days' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.report:config:gross_revenue }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.query, 'rd_gross_revenue.data AS gross_revenue' ) }">

		<MvIF EXPR = "{ l.report:config:orderby NE 'gross_revenue' }">
			<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, l.primary_correlation, g.Store_Table_Prefix $ 'ReportData', 'rd_gross_revenue',
																				 'rd_gross_revenue.report_id = ? AND rd_gross_revenue.set_id = ?', 'l.report:id, l.product:set_ids:gross_revenue' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.report:config:age }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.query, 'rd_age.data AS age' ) }">

		<MvIF EXPR = "{ l.report:config:orderby NE 'age' }">
			<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, l.primary_correlation, g.Store_Table_Prefix $ 'ReportData', 'rd_age',
																				 'rd_age.report_id = ? AND rd_age.set_id = ?', 'l.report:id, l.product:set_ids:age' ) }">
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.query_sql"	VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.query, l.query_fields ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ReportData"
				QUERY	= "{ l.query_sql }"
				FIELDS	= "{ l.query_fields }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SUB-PST-00020', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ ReportData.d.EOF }">
		<MvASSIGN NAME = "l.metrics:active"					VALUE = 0>
		<MvASSIGN NAME = "l.metrics:new"					VALUE = 0>
		<MvASSIGN NAME = "l.metrics:cancellations"			VALUE = 0>
		<MvASSIGN NAME = "l.metrics:active30days"			VALUE = 0>
		<MvASSIGN NAME = "l.metrics:cancellations30days"	VALUE = 0>
		<MvASSIGN NAME = "l.metrics:gross_revenue"			VALUE = 0.00>
		<MvASSIGN NAME = "l.metrics:age"					VALUE = 0.0>
	<MvELSE>
		<MvASSIGN NAME = "l.metrics:active"					VALUE = "{ int( ReportData.d.active ) }">
		<MvASSIGN NAME = "l.metrics:new"					VALUE = "{ int( ReportData.d.new ) }">
		<MvASSIGN NAME = "l.metrics:cancellations"			VALUE = "{ int( ReportData.d.cancellations ) }">
		<MvASSIGN NAME = "l.metrics:active30days"			VALUE = "{ int( ReportData.d.active30days ) }">
		<MvASSIGN NAME = "l.metrics:cancellations30days"	VALUE = "{ int( ReportData.d.cancellations30days ) }">
		<MvASSIGN NAME = "l.metrics:gross_revenue"			VALUE = "{ ReportData.d.gross_revenue ROUND 2 }">
		<MvASSIGN NAME = "l.metrics:age"					VALUE = "{ ReportData.d.age ROUND 1 }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ReportData">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

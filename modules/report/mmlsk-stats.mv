<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2020 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-RPT-SALES-
| Next Error Code: 1   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-report_stats">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Statistics Display">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.62">
	<MvASSIGN NAME = "l.module:features"	VALUE = "report">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:date_range"		VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:date_interval"		VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:display"			VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:output_chart"		VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:output_tabular"	VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:output_custom"		VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Fields" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.report:id }">
		<MvASSIGN NAME = "g.Report_Stats_Display"		VALUE = "{ l.report:config:display }">
	<MvELSE>
		<MvASSIGN NAME = "g.Report_Stats_Display"		VALUE = "statistics">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "display">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.field_id EQ 'display' }">		<MvFUNCTIONRETURN VALUE = "Display:">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.field_id EQ 'display' }">
		<select name="Report_Stats_Display">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'statistics',	g.Report_Stats_Display, 'Statistics' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'bestsellers',	g.Report_Stats_Display, 'Best Sellers' ) }">
		</select>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Validate" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Report_Stats_Display"	VALUE = "{ trim( g.Report_Stats_Display ) }">

	<MvIF EXPR = "{ ( g.Report_Stats_Display NE 'statistics' ) AND
					( g.Report_Stats_Display NE 'bestsellers' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select a statistic to display' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Update" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.report:config:display"	VALUE = "{ g.Report_Stats_Display }">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Run" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Display" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.report:config:display EQ 'statistics' }">			<MvFUNCTIONRETURN VALUE = "{ Stats_Display_Statistics() }">
	<MvELSEIF EXPR = "{ l.report:config:display EQ 'bestsellers' }">	<MvFUNCTIONRETURN VALUE = "{ Stats_Display_BestSellers() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Stats_Display_Statistics" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_STA_DB ].Stats_Load( l.stats ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<style type="text/css">
		.mm_report_stats_container
		{
			position: relative;
			display: block;
		}

		.mm_report_stats_header
		{
			position: relative;
			display: flex;
			margin-bottom: 32px;
			align-items: center;
			overflow: hidden;
		}

		.mm_report_stats_header_prompt
		{
			position: relative;
			display: inline-block;
			margin-right: 20px;
			font-size: 16px;
			font-weight: normal;
			color: #6a6e79;
			white-space: nowrap;
			flex-grow: 1;
		}

		.mm_report_stats_header_field
		{
			position: relative;
			display: inline-block;
			font-size: 18px;
			font-weight: 500;
			color: #5b5c67;
			flex-shrink: 0;
		}

		.mm_report_stats_entry
		{
			position: relative;
			display: flex;
			align-items: center;
			overflow: hidden;
		}

		.mm_report_stats_entry + .mm_report_stats_entry
		{
			margin-top: 18px;
		}

		.mm_report_stats_entry_prompt
		{
			position: relative;
			display: inline-block;
			margin-right: 20px;
			font-size: 13px;
			font-weight: normal;
			color: #6a6e79;
			white-space: nowrap;
			flex-grow: 1;
		}

		.mm_report_stats_entry_field
		{
			position: relative;
			display: inline-block;
			font-size: 13px;
			font-weight: normal;
			color: #5b5c67;
			flex-shrink: 0;
		}
	</style>

	<span class="mm_report_stats_container">
		<span class="mm_report_stats_header">
			<span class="mm_report_stats_header_prompt">Orders</span>
			<span class="mm_report_stats_header_field"><MvEVAL EXPR = "{ int( l.stats:orders ) }"></span>
		</span>

		<span class="mm_report_stats_entry">
			<span class="mm_report_stats_entry_prompt">Revenue</span>
			<span class="mm_report_stats_entry_field"><MvEVAL EXPR = "{ l.stats:revenue ROUND 2 }"></span>
		</span>

		<span class="mm_report_stats_entry">
			<span class="mm_report_stats_entry_prompt">Products Sold</span>
			<span class="mm_report_stats_entry_field"><MvEVAL EXPR = "{ int( l.stats:products ) }"></span>
		</span>

		<MvIF EXPR = "{ g.Store:trackhits }">
			<span class="mm_report_stats_entry">
				<span class="mm_report_stats_entry_prompt">Hits</span>
				<span class="mm_report_stats_entry_field"><MvEVAL EXPR = "{ int( l.stats:hits ) }"></span>
			</span>

			<span class="mm_report_stats_entry">
				<span class="mm_report_stats_entry_prompt">Visits</span>
				<span class="mm_report_stats_entry_field"><MvEVAL EXPR = "{ int( l.stats:visits ) }"></span>
			</span>
		</MvIF>

		<MvIF EXPR = "{ g.Admin_User_Administrator }">
			<a class="mm_main_box_title_link" href="{ 'JavaScript:PopupStatReset( \'' $ [ g.Module_Admin ].JavaScriptEncode( g.Store:code ) $ '\' );' }"><MvEVAL EXPR = "Reset Statistics"></a>
			<span class="mm_main_box_content_action_custom">Last Reset on: <MvEVAL EXPR = "{ padl( time_t_month( l.stats:resetdate, g.Merchant_Local_Timezone ), 2, '0' ) }">/<MvEVAL EXPR = "{ padl( time_t_dayofmonth( l.stats:resetdate, g.Merchant_Local_Timezone ), 2, '0' ) }">/<MvEVAL EXPR = "{ padl( time_t_year( l.stats:resetdate, g.Merchant_Local_Timezone ), 2, '0' ) }"></span>
		</MvIF>
	</span>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Stats_Display_BestSellers" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvASSIGN NAME = "l.bestseller_pos"		VALUE = 1>
	<MvASSIGN NAME = "l.bestseller_count"	VALUE = "{ [ g.Module_Feature_STA_DB ].ProdStatsList_Load_Max( 5, l.prodstats ) }">

	<MvIF EXPR = "{ l.bestseller_count EQ 0 }">
		<i>No Best Sellers Currently Available</i>
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<table border="0" cellpadding="0" cellspacing="0">
	<MvWHILE EXPR = "{ l.bestseller_pos LE l.bestseller_count }">
		<tr><td nowrap>
			<MvEVAL EXPR = "{ encodeentities( l.prodstats[ l.bestseller_pos ]:prod_name ) }">:
		</td><td align="right">
			<MvEVAL EXPR = "{ int( l.prodstats[ l.bestseller_pos ]:counter ) }">
		</td></tr>

		<MvASSIGN NAME = "l.bestseller_pos"	VALUE = "{ l.bestseller_pos + 1 }">
	</MvWHILE>
	</table>
	
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

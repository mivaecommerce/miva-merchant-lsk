<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-RPT-COUPON-
| Next Error Code: 18   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-report_coupon_usage">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Coupon Usage Report">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.1101">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.72">
	<MvASSIGN NAME = "l.module:description" VALUE = "Use this module to generate reports of shopper coupon usage">
	<MvASSIGN NAME = "l.module:features"	VALUE = "report">
</MvFUNCTION>

<MvCOMMENT>
|
| Report Feature (report)
|
</MvCOMMENT>

<MvFUNCTION NAME = "ReportModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:date_range"			VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:date_interval"			VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:display"				VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:output_chart"			VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:output_tabular"		VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:output_custom"			VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:provision_settings"	VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Chart_Type" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "canvas_chart">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Display" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ miva_array_elements( l.report:last_state:coupons ) EQ 0 }">
		<i>Not Available</i>
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<table style="border-collapse: collapse; font-size: small; font-family: Verdana, Arial, Helvetica, sans-serif;" cellspacing="0" cellpadding="0">
		<thead style="font-weight: bold;">
			<tr style="height: 20px;">
				<td>&nbsp;</td>
				<td align="center">
					<MvEVAL EXPR = "{ encodeentities( l.report:last_state:setsize ) }">

					<MvIF EXPR = "{ l.report:date_group EQ 'H' }">		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Plural( l.report:last_state:setsize, 'Hour',	'Hours' ) }">
					<MvELSEIF EXPR = "{ l.report:date_group EQ 'D' }">	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Plural( l.report:last_state:setsize, 'Day',	'Days' ) }">
					<MvELSEIF EXPR = "{ l.report:date_group EQ 'W' }">	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Plural( l.report:last_state:setsize, 'Week',	'Weeks' ) }">
					<MvELSEIF EXPR = "{ l.report:date_group EQ 'M' }">	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Plural( l.report:last_state:setsize, 'Month',	'Months' ) }">
					<MvELSEIF EXPR = "{ l.report:date_group EQ 'Y' }">	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Plural( l.report:last_state:setsize, 'Year',	'Years' ) }">
					</MvIF>
				</td>
				<td align="right">
					<MvIF EXPR = "{ l.report:config:metric EQ 'total_discount' }">			Total Discount
					<MvELSEIF EXPR = "{ l.report:config:metric EQ 'average_discount' }">	Average Discount
					<MvELSEIF EXPR = "{ l.report:config:metric EQ 'num_uses' }">			Number of Uses
					</MvIF>
				</td>
			</tr>
		</thead>

		<tbody>
			<MvASSIGN NAME = "l.color_pos"			VALUE = 0>
			<MvASSIGN NAME = "l.color_count" 		VALUE = "{ [ g.Module_Feature_RPT_UT ].Chart_Color_Palette( l.colors ) }">

			<MvFOREACH INDEX = "l.coupon_pos" ITERATOR = "l.coupon" ARRAY = "l.report:last_state:coupons">
				<MvIF EXPR = "{ l.coupon_pos GT l.report:config:display_topn }">
					<MvFOREACHSTOP>
				</MvIF>

				<MvIF EXPR = "{ ++l.color_pos GT l.color_count }">
					<MvASSIGN NAME = "l.color_pos"	VALUE = 1>
				</MvIF>

				<MvASSIGN NAME = "l.rgb_color"		VALUE = "{ [ g.Module_Feature_RPT_UT ].Chart_Color_HexToRGB( l.colors[ l.color_pos ] ) }">
				<MvASSIGN NAME = "l.image_source"	VALUE = "{ g.json_nosessionurl										$
															   'ST=admin'												$
															   '&Store_Code='	$ encodeattribute( g.Store:code )		$
															   '&Function=ReportData_Graph_Line'						$
															   '&Report_ID='	$ encodeattribute( l.report:id )		$
															   '&Smoothing=0'											$
															   '&Width=100'												$ 
															   '&Height=20'												$
															   '&Set_IDs='		$ encodeattribute( l.coupon:set_id )	$
															   '&Colors='		$ encodeattribute( l.rgb_color )		$
															   '&Background=255,255,255' }">

				<tr style="height: 20px;">
					<td><MvEVAL EXPR = "{ encodeentities( l.coupon:code ) }"></td>
					<td align="center" style="padding-left: 5px; padding-right: 5px;">
						<a href="#" onclick="{ 'Report_Display_Chart( \'' $ [ g.Module_Admin ].JavaScriptEncode( g.Store:code ) $ '\', ' $ int( l.report:id ) $ ', true, ' $ int( l.coupon:set_id ) $ ' ); return false' }">
							<MvEVAL EXPR = "{ [ g.Module_Feature_RPT_UT ].Report_Draw_DelayedImage( l.image_source, 100, 20, l.rgb_color ) }">
						</a>
					</td>
					<td align="right">
						<MvEVAL EXPR = "{ ReportModule_Format_Vertical_Label( l.module, l.report, l.coupon:set_id, l.coupon:metric ) }">
					</td>
				</tr>
			</MvFOREACH>
		</tbody>
	</table>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.field_id EQ 'metric' }">
		<select name="Report_CouponUsage_Metric">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'total_discount',		g.Report_CouponUsage_Metric, 'Total Discount' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'average_discount',	g.Report_CouponUsage_Metric, 'Average Discount' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'num_uses',			g.Report_CouponUsage_Metric, 'Number of Uses' ) }">
		</select>
	<MvELSEIF EXPR = "{ l.field_id EQ 'topn' }">
		<table>
			<tr>
				<td>
					<input type="text" id="Report_CouponUsage_Display_TopN" name="Report_CouponUsage_Display_TopN" size="5" value="{ encodeentities( g.Report_CouponUsage_Display_TopN ) }" /> coupon(s) on the main screen
				</td>
			</tr>
			<tr>
				<td>
					<input type="text" id="Report_CouponUsage_Chart_TopN" name="Report_CouponUsage_Chart_TopN" size="5" value="{ encodeentities( g.Report_CouponUsage_Chart_TopN ) }" /> coupon(s) on the expanded chart
				</td>
			</tr>
			<tr>
				<td>
					<input type="text" id="Report_CouponUsage_Export_TopN" name="Report_CouponUsage_Export_TopN" size="5" value="{ encodeentities( g.Report_CouponUsage_Export_TopN ) }" /> coupon(s) in exported data
				</td>
			</tr>
		</table>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Fields" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.report:id }">
		<MvASSIGN NAME = "g.Report_CouponUsage_Metric"			VALUE = "{ l.report:config:metric }">
		<MvASSIGN NAME = "g.Report_CouponUsage_Display_TopN"	VALUE = "{ l.report:config:display_topn }">
		<MvASSIGN NAME = "g.Report_CouponUsage_Chart_TopN"		VALUE = "{ l.report:config:chart_topn }">
		<MvASSIGN NAME = "g.Report_CouponUsage_Export_TopN"		VALUE = "{ l.report:config:export_topn }">
	<MvELSE>
		<MvASSIGN NAME = "g.Report_CouponUsage_Metric"			VALUE = "total_discount">
		<MvASSIGN NAME = "g.Report_CouponUsage_Display_TopN"	VALUE = 10>
		<MvASSIGN NAME = "g.Report_CouponUsage_Chart_TopN"		VALUE = 10>
		<MvASSIGN NAME = "g.Report_CouponUsage_Export_TopN"		VALUE = 10>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "metric,topn">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.field_id EQ 'metric' }">			<MvFUNCTIONRETURN VALUE = "<b>Compare Using:</b>">
	<MvELSEIF EXPR = "{ l.field_id EQ 'topn' }">		<MvFUNCTIONRETURN VALUE = "Display:">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Validate" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Report_CouponUsage_Metric" VALUE = "{ trim( g.Report_CouponUsage_Metric ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Value_In_List( g.Report_CouponUsage_Metric, 'total_discount,average_discount,num_uses' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Report_CouponUsage_Metric', 'Please select a valid comparison metric' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( g.Report_CouponUsage_Display_TopN ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Report_CouponUsage_Display_TopN', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( g.Report_CouponUsage_Chart_TopN ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Report_CouponUsage_Chart_TopN', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( g.Report_CouponUsage_Export_TopN ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Report_CouponUsage_Export_TopN', g.Validation_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Update" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.report:config:metric"		VALUE = "{ g.Report_CouponUsage_Metric }">
	<MvASSIGN NAME = "l.report:config:display_topn"	VALUE = "{ g.Report_CouponUsage_Display_TopN }">
	<MvASSIGN NAME = "l.report:config:chart_topn"	VALUE = "{ g.Report_CouponUsage_Chart_TopN }">
	<MvASSIGN NAME = "l.report:config:export_topn"	VALUE = "{ g.Report_CouponUsage_Export_TopN }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Calculate_DateRange_All" PARAMETERS = "module var, report var, time_t_start var, time_t_end var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderDates"
				QUERY	= "{ 'SELECT
								MIN( orderdate ) AS min_orderdate,
								MAX( orderdate ) AS max_orderdate
							  FROM
								' $ g.Store_Table_Prefix $ 'Orders' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-COUPON-00008', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ OrderDates.d.EOF OR ( ( ISNULL OrderDates.d.min_orderdate ) AND ( ISNULL OrderDates.d.max_orderdate ) ) }">
		<MvASSIGN NAME = "l.time_t_start"	VALUE = "{ s.time_t }">
		<MvASSIGN NAME = "l.time_t_end"		VALUE = "{ s.time_t }">
	<MvELSE>
		<MvASSIGN NAME = "l.time_t_start"	VALUE = "{ OrderDates.d.min_orderdate }">
		<MvASSIGN NAME = "l.time_t_end"		VALUE = "{ OrderDates.d.max_orderdate + 1 }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderDates">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Run_Intervals" PARAMETERS = "module var, report var, time_t_start, time_t_end, intervals var, interval_count" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_RPT_DB ].ReportData_Delete_Report( l.report:id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.query:topn"				VALUE = "{ max( l.report:config:display_topn, max( l.report:config:chart_topn, l.report:config:export_topn ) ) }">
	<MvASSIGN NAME = "l.query:time_t_start"		VALUE = "{ l.time_t_start }">
	<MvASSIGN NAME = "l.query:time_t_end"		VALUE = "{ l.time_t_end }">
	<MvASSIGN NAME = "l.query:coupons"			VALUE = "">

	<MvASSIGN NAME = "l.result"					VALUE = 1>

	<MvIF EXPR = "{ l.result }">
		<MvASSIGN NAME = "l.result"				VALUE = "{ CouponUsage_Create_OrderCoupon_TempTable( l.query ) }">
	</MvIF>

	<MvIF EXPR = "{ l.result }">
		<MvASSIGN NAME = "l.result"				VALUE = "{ CouponUsage_Create_TopNCoupon_TempTable( l.query ) }">
	</MvIF>

	<MvIF EXPR = "{ l.result }">
		<MvASSIGN NAME = "l.result"				VALUE = "{ CouponUsage_Run_Intervals( l.report, l.query, l.intervals, l.interval_count ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.query:ordercoupon_temp_table }">
		<MvASSIGN NAME = "l.result"				VALUE = "{ l.result AND CouponUsage_Drop_TempTable( l.query:ordercoupon_temp_table ) }">	
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.query:topncoupon_temp_table }">
		<MvASSIGN NAME = "l.result"				VALUE = "{ l.result AND CouponUsage_Drop_TempTable( l.query:topncoupon_temp_table ) }">	
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.result }">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Format_Vertical_Label" PARAMETERS = "module var, report var, set_id, label" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.report:config:metric EQ 'total_discount' OR
					l.report:config:metric EQ 'average_discount' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.label ROUND 2 ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.label ROUND 0 }">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Canvas_Chart_Definition" PARAMETERS = "module var, report var, chart var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.color_pos" 					VALUE = 0>
	<MvASSIGN NAME = "l.color_count" 				VALUE = "{ [ g.Module_Feature_RPT_UT ].Chart_Color_Palette( l.colors ) }">

	<MvFOREACH INDEX = "l.coupon_pos" ITERATOR = "l.coupon" ARRAY = "l.report:last_state:coupons">
		<MvIF EXPR = "{ l.coupon_pos GT l.report:config:chart_topn }">
			<MvFOREACHSTOP>
		</MvIF>

		<MvIF EXPR = "{ ++l.color_pos GT l.color_count }">
			<MvASSIGN NAME = "l.color_pos" 			VALUE = 1>
		</MvIF>

		<MvASSIGN NAME = "l.dataset:name" 			VALUE = "{ l.coupon:code }">
		<MvASSIGN NAME = "l.dataset:set_id" 		VALUE = "{ l.coupon:set_id }">
		<MvASSIGN NAME = "l.dataset:color" 			VALUE = "{ l.colors[ l.color_pos ] }">
		<MvASSIGN NAME = "l.dataset:force_visible" 	VALUE = "{ g.Report_Chart_Param EQ l.coupon:set_id }">

		<MvIF EXPR = "{ l.report:config:metric EQ 'num_uses' }">	<MvASSIGN NAME = "l.dataset:data_type" VALUE = "numeric">
		<MvELSE>													<MvASSIGN NAME = "l.dataset:data_type" VALUE = "currency">
		</MvIF>

		<MvASSIGN NAME = "l.chart:dataset_count" 	VALUE = "{ miva_array_insert_var( l.chart:datasets, l.dataset, -1 ) }">
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Canvas_Output_LabelFormat_JavaScript" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Tabular_Definition" PARAMETERS = "module var, report var, definition var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.definition:rows"			VALUE = "">
	<MvASSIGN NAME = "l.definition:columns"			VALUE = "">
	<MvASSIGN NAME = "l.coupon_count"				VALUE = "{ miva_array_elements( l.report:last_state:coupons ) }">

	<MvCOMMENT>
	|
	| Labels
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.column"						VALUE = "">
	<MvASSIGN NAME = "l.column:type"				VALUE = "values">
	<MvASSIGN NAME = "l.column:start"				VALUE = 1>
	<MvASSIGN NAME = "l.column:values"				VALUE = "">
	<MvASSIGN NAME = "l.column:value_count"			VALUE = "{ miva_array_insert( l.column:values, 'COUPON_CODE', -1 ) }">

	<MvFOREACH INDEX = "l.coupon_pos" ITERATOR = "l.coupon" ARRAY = "l.report:last_state:coupons" COUNT = "{ l.coupon_count }">
		<MvIF EXPR = "{ l.coupon_pos GT l.report:config:export_topn }">
			<MvFOREACHSTOP>
		</MvIF>

		<MvASSIGN NAME = "l.column:value_count"		VALUE = "{ miva_array_insert_var( l.column:values, l.coupon:code, -1 ) }">
	</MvFOREACH>

	<MvASSIGN NAME = "l.definition:column_count"	VALUE = "{ miva_array_insert_var( l.definition:columns, l.column, -1 ) }">

	<MvCOMMENT>
	|
	| Data
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.coupon_count GE 1 }">
		<MvASSIGN NAME = "l.row"						VALUE = "">
		<MvASSIGN NAME = "l.row:type"					VALUE = "reportdata_date">
		<MvASSIGN NAME = "l.row:start"					VALUE = 2>
		<MvASSIGN NAME = "l.row:set_id"					VALUE = "{ l.report:last_state:coupons[ 1 ]:set_id }">

		<MvASSIGN NAME = "l.definition:row_count"		VALUE = "{ miva_array_insert_var( l.definition:rows, l.row, -1 ) }">

		<MvFOREACH INDEX = "l.coupon_pos" ITERATOR = "l.coupon" ARRAY = "l.report:last_state:coupons" COUNT = "{ l.coupon_count }">
			<MvIF EXPR = "{ l.coupon_pos GT l.report:config:export_topn }">
				<MvFOREACHSTOP>
			</MvIF>

			<MvASSIGN NAME = "l.row"					VALUE = "">
			<MvASSIGN NAME = "l.row:type"				VALUE = "reportdata">
			<MvASSIGN NAME = "l.row:start"				VALUE = 2>
			<MvASSIGN NAME = "l.row:set_id"				VALUE = "{ l.coupon:set_id }">

			<MvASSIGN NAME = "l.definition:row_count"	VALUE = "{ miva_array_insert_var( l.definition:rows, l.row, -1 ) }">
		</MvFOREACH>
	</MvIF>

	<MvCOMMENT>
	|
	| If there are more than 256 datapoints, rotate the output to be vertical to avoid
	| issues with Excel
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.report:last_state:setsize GT 256 }">
		<MvASSIGN NAME = "l.temp"											VALUE = "{ l.definition:rows }">
		<MvASSIGN NAME = "l.definition:rows"								VALUE = "{ l.definition:columns }">
		<MvASSIGN NAME = "l.definition:columns"								VALUE = "{ l.temp }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Provision_Settings" PARAMETERS = "module var, report var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.report:module_id NE l.module:id }">
		<MvASSIGN NAME = "l.report:config:display_topn"		VALUE = 10>
		<MvASSIGN NAME = "l.report:config:chart_topn"		VALUE = 10>
		<MvASSIGN NAME = "l.report:config:export_topn"		VALUE = 10>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'R', l.provide_xml, 'Metric',		l.report:config:metric,		'total_discount,average_discount,num_uses',
																																		'total_discount,average_discount,num_uses' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O', l.provide_xml, 'Display_TopN',	l.report:config:display_topn )												OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O', l.provide_xml, 'Chart_TopN',	l.report:config:chart_topn )												OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O', l.provide_xml, 'Export_TopN',	l.report:config:export_topn ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Delete" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Temporary Table Functions
|
| This report uses the following temporary tables:
|
|	Order Coupon: sNN_TempCPNOCNN ( code, orderdate, total ): query:ordercoupon_temp_table (always present)
|		Contains the coupon code, date, and coupon total of every order coupon within the overall date range
|
|	TopN Coupon: sNN_TempCPNTCNN ( code, set_id ): query:topncoupon_temp_table (always present)
|		Contains the code of coupons meeting the Top-N restriction within the overall date range, and the set_id value, which is generated based on TopN order
|
| There is some overlap between the temporary tables because the whittling-down of entries from one temporary table to the next is
| done progressively in an attempt to increase efficiency.  For example, the Order temporary table is created before the Coupon
| temporary table to make calculating the final set of coupons (subject to Top-N) more efficient, and the two temporary tables are
| then used in conjunction when calculating the per-interval data.
|
</MvCOMMENT>

<MvFUNCTION NAME = "CouponUsage_Create_OrderCoupon_TempTable" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvFOR INDEX = "l.attempt" COUNT = 5>
		<MvASSIGN NAME = "l.query:ordercoupon_temp_table" VALUE = "{ g.Store_Table_Prefix $ 'TempCPNOC' $ l.attempt }">

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE TABLE ' $ l.query:ordercoupon_temp_table $ '
							  (
								code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
								orderdate	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
								total		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 16, 8 )	$ '
							  )' }">
		<MvIF EXPR = "{ NOT g.MvQUERY_Error }">
			<MvFORSTOP>
		</MvIF>
	</MvFOR>

	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-COUPON-00009', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CouponUsage_Populate_OrderCoupon_TempTable" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.select_query, 'oc.code, o.orderdate, oc.total' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.select_query, g.Store_Table_Prefix $ 'Orders',			'o' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.select_query, g.Store_Table_Prefix $ 'OrderCoupons',	'oc' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.select_query, 'oc.order_id	= o.id',	'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.select_query, 'o.orderdate	>= ?',		[ g.Module_Library_DB ].SQL_Query_Field( l.query:time_t_start ) ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.select_query, 'o.orderdate	<= ?',		[ g.Module_Library_DB ].SQL_Query_Field( l.query:time_t_end ) ) }">
	
	<MvASSIGN NAME = "l.select_sql"	VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.select_query, l.select_fields ) }">

	<MvQUERY NAME	= "Merchant"	
			 QUERY	= "{ 'INSERT INTO ' $ l.query:ordercoupon_temp_table $ ' ' $ l.select_sql }"
			 FIELDS	= "{ l.select_fields }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-COUPON-00010', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CouponUsage_Index_OrderCoupon_TempTable" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE INDEX ' $ l.query:ordercoupon_temp_table $ '_1 ON ' $ l.query:ordercoupon_temp_table $ ' ( code, orderdate )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-COUPON-00011', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CouponUsage_Create_TopNCoupon_TempTable" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvFOR INDEX = "l.attempt" COUNT = 5>
		<MvASSIGN NAME = "l.query:topncoupon_temp_table" VALUE = "{ g.Store_Table_Prefix $ 'TempCPNTC' $ l.attempt }">

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE TABLE ' $ l.query:topncoupon_temp_table $ '
							  (
								code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )	$ ',
								set_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ '
							  )' }">
		<MvIF EXPR = "{ NOT g.MvQUERY_Error }">
			<MvFORSTOP>
		</MvIF>
	</MvFOR>

	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-COUPON-00012', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CouponUsage_Populate_TopNCoupon_TempTable" PARAMETERS = "report var, query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.select_query, 'code' ) }">

	<MvIF EXPR = "{ l.report:config:metric EQ 'average_discount' }">	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.select_query, 'AVG( total )	AS metric' ) }">
	<MvELSEIF EXPR = "{ l.report:config:metric EQ 'num_uses' }">		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.select_query, 'COUNT( code )	AS metric' ) }">
	<MvELSE>															<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.select_query, 'SUM( total )	AS metric' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.select_query, l.query:ordercoupon_temp_table, '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_GROUP_BY( l.select_query, 'code' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY( l.select_query, 'metric', 'DESC' ) }">

	<MvASSIGN NAME = "l.select_sql"	VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.select_query, l.select_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI].DB_OPENVIEW_Range( 'Merchant', 'CouponUsage_Orders',
																		    l.select_sql, l.select_fields,
																		    0, l.query:topn ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-COUPON-00014', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ ( NOT CouponUsage_Orders.d.EOF ) AND ( ++l.count LE l.query:topn ) }">
		<MvCOMMENT>
		|
		| Register this coupon in the persistent query coupon information, so that it can be persisted for report display
		|
		</MvCOMMENT>

		<MvASSIGN NAME = "l.coupon:code"		VALUE = "{ CouponUsage_Orders.d.code }">
		<MvASSIGN NAME = "l.coupon:set_id"		VALUE = "{ l.count }">
		<MvASSIGN NAME = "l.coupon:metric"		VALUE = "{ CouponUsage_Orders.d.metric }">

		<MvASSIGN NAME = "l.null"				VALUE = "{ miva_array_insert_var( l.query:coupons, l.coupon, -1 ) }">

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'INSERT INTO ' $ l.query:topncoupon_temp_table $ ' ( code, set_id ) VALUES ( ?, ? )' }"
				 FIELDS	= "l.coupon:code, l.coupon:set_id">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "CouponUsage_Orders">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-COUPON-00013', g.MvQUERY_Error ) }">
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "CouponUsage_Orders" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CouponUsage_Orders">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CouponUsage_Index_TopNCoupon_TempTable" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ l.query:topncoupon_temp_table $ '_1 ON ' $ l.query:topncoupon_temp_table $ ' ( code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-COUPON-00015', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CouponUsage_Drop_TempTable" PARAMETERS = "table_name" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DROP TABLE ' $ l.table_name }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-COUPON-00016', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Report Data Generation Helper Functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "CouponUsage_Run_Intervals" PARAMETERS = "report var, query var, intervals var, interval_count" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| Populate the Order temporary table, satisfying any product or price group restrictions
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT CouponUsage_Populate_OrderCoupon_TempTable( l.query ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT CouponUsage_Index_OrderCoupon_TempTable( l.query ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| Populate the Coupon temporary table
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT CouponUsage_Populate_TopNCoupon_TempTable( l.report, l.query ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT CouponUsage_Index_TopNCoupon_TempTable( l.query ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| Generate data for each interval
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT CouponUsage_Run_Interval( l.report, l.query, l.intervals, l.interval_count ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.report:last_state:coupons" VALUE = "{ l.query:coupons }">
	<MvASSIGN NAME = "l.report:last_state:setsize" VALUE = "{ l.interval_count }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CouponUsage_Run_Interval" PARAMETERS = "report var, query var, intervals var, interval_count" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.select_query, 'c.set_id				AS set_id,
																				 o.code					AS code,
																				 o.orderdate			AS orderdate' ) }">

	<MvIF EXPR = "{ l.report:config:metric EQ 'average_discount' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.select_query, 'o.total			AS metric' ) }">
	<MvELSEIF EXPR = "{ l.report:config:metric EQ 'num_uses' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.select_query, 'COUNT( o.code )	AS metric' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_GROUP_BY( l.select_query, 'c.set_id, o.code, o.orderdate' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.select_query, 'SUM( o.total )		AS metric' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_GROUP_BY( l.select_query, 'c.set_id, o.code, o.orderdate' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.select_query, l.query:ordercoupon_temp_table,	'o' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.select_query, l.query:topncoupon_temp_table,	'c' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.select_query, 'c.code = o.code', '' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY( l.select_query, 'code',		'ASC' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY( l.select_query, 'orderdate',	'ASC' ) }">

	<MvASSIGN NAME = "l.units_sql" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.select_query, l.units_fields ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CouponUsage_Units"
				QUERY	= "{ l.units_sql }"
				FIELDS	= "{ l.units_fields }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-COUPON-00017', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.reportdata:report_id"			VALUE = "{ l.report:id }">

	<MvCOMMENT>
	|
	| The query is sorted by coupon code and then order date, so the outer WHILE loop here iterates over coupons, and the inner loop
	| handles each interval for a single coupon.
	|
	</MvCOMMENT>

	<MvWHILE EXPR = "{ NOT CouponUsage_Units.d.EOF }">
		<MvASSIGN NAME = "l.interval_pos"				VALUE = 1>
		<MvASSIGN NAME = "l.interval_data"				VALUE = "">

		<MvASSIGN NAME = "l.reportdata:set_id"			VALUE = "{ CouponUsage_Units.d.set_id }">

		<MvWHILE EXPR = "{ ( NOT CouponUsage_Units.d.EOF ) AND ( l.interval_pos LE l.interval_count ) }">
			<MvASSIGN NAME = "l.reportdata:data"		VALUE = 0>
			<MvASSIGN NAME = "l.reportdata:dt_start"	VALUE = "{ l.intervals[ l.interval_pos ]:time_t_start }">
			<MvASSIGN NAME = "l.reportdata:dt_end"		VALUE = "{ l.intervals[ l.interval_pos ]:time_t_end }">

			<MvCOMMENT>
			|
			| We have moved on to the next coupon
			|
			</MvCOMMENT>

			<MvIF EXPR = "{ CouponUsage_Units.d.set_id NE l.reportdata:set_id }">
				<MvWHILESTOP>
			</MvIF>

			<MvCOMMENT>
			|
			| While we are within this interval, accumulate the units amount for each involved orderitem
			|
			</MvCOMMENT>

			<MvASSIGN NAME = "l.data_count"				VALUE = 0>

			<MvWHILE EXPR = "{ ( NOT CouponUsage_Units.d.EOF ) AND ( CouponUsage_Units.d.set_id EQ l.reportdata:set_id ) AND ( CouponUsage_Units.d.orderdate LT l.intervals[ l.interval_pos ]:time_t_end ) }">
				<MvASSIGN NAME = "l.reportdata:data"	VALUE = "{ l.reportdata:data + CouponUsage_Units.d.metric }">
				<MvASSIGN NAME = "l.data_count"			VALUE = "{ l.data_count + 1 }">

				<MvSKIP NAME = "Merchant" VIEW = "CouponUsage_Units" ROWS = 1>
			</MvWHILE>

			<MvIF EXPR = "{ ( l.report:config:metric EQ 'average_discount' ) AND ( l.reportdata:data NE 0 ) }">
				<MvASSIGN NAME = "l.reportdata:data"	VALUE = "{ l.reportdata:data / l.data_count }">
			</MvIF>

			<MvCOMMENT>
			|
			| Insert data for this interval, if needed, and skip to the next one
			|
			</MvCOMMENT>

			<MvIF EXPR = "{ l.reportdata:data NE 0 }">
				<MvIF EXPR = "{ NOT [ g.Module_Feature_RPT_DB ].ReportData_Insert( l.reportdata ) }">
					<MvCLOSEVIEW NAME = "Merchant" VIEW = "CouponUsage_Units">
					<MvFUNCTIONRETURN VALUE = 0>
				</MvIF>
			</MvIF>

			<MvASSIGN NAME = "l.interval_pos"			VALUE = "{ l.interval_pos + 1 }">
		</MvWHILE>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CouponUsage_Units">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-RPT-SALES-
| Next Error Code: 27   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-report_sales">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Sales Report">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.1100">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.62">
	<MvASSIGN NAME = "l.module:features"	VALUE = "report">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:date_range"			VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:date_interval"			VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:display"				VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:output_chart"			VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:output_tabular"		VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:output_custom"			VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:provision_settings"	VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Fields" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.report:id }">
		<MvASSIGN NAME = "g.Report_Sales_Customers_In"			VALUE = "{ l.report:config:customers_in }">
		<MvASSIGN NAME = "g.Report_Sales_Customers_PriceGroup"	VALUE = "{ l.report:config:customers_pricegroup }">
		<MvASSIGN NAME = "g.Report_Sales_Category"				VALUE = "{ l.report:config:category }">
		<MvASSIGN NAME = "g.Report_Sales_Product"				VALUE = "{ l.report:config:product }">
		<MvASSIGN NAME = "g.Report_Sales_Gross"					VALUE = "{ l.report:config:gross }">
		<MvASSIGN NAME = "g.Report_Sales_Units"					VALUE = "{ l.report:config:units }">
		<MvASSIGN NAME = "g.Report_Sales_Orders"				VALUE = "{ l.report:config:orders }">
		<MvASSIGN NAME = "g.Report_Sales_Average"				VALUE = "{ l.report:config:average }">
		<MvASSIGN NAME = "g.Report_Sales_Tax"					VALUE = "{ l.report:config:tax }">
		<MvASSIGN NAME = "g.Report_Sales_Shipping"				VALUE = "{ l.report:config:shipping }">
	<MvELSE>
		<MvASSIGN NAME = "g.Report_Sales_Customers_In"			VALUE = 1>
		<MvASSIGN NAME = "g.Report_Sales_Customers_PriceGroup"	VALUE = "">
		<MvASSIGN NAME = "g.Report_Sales_Category"				VALUE = "">
		<MvASSIGN NAME = "g.Report_Sales_Gross"					VALUE = 1>
		<MvASSIGN NAME = "g.Report_Sales_Units"					VALUE = 1>
		<MvASSIGN NAME = "g.Report_Sales_Orders"				VALUE = 1>
		<MvASSIGN NAME = "g.Report_Sales_Average"				VALUE = 1>
		<MvASSIGN NAME = "g.Report_Sales_Tax"					VALUE = 1>
		<MvASSIGN NAME = "g.Report_Sales_Shipping"				VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "customers,category,product,metrics">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.field_id EQ 'customers' }">		<MvFUNCTIONRETURN VALUE = "Customers:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'category' }">	<MvFUNCTIONRETURN VALUE = "Category:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'product' }">		<MvFUNCTIONRETURN VALUE = "Product:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'metrics' }">		<MvFUNCTIONRETURN VALUE = "<b>Display:</b>">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.field_id EQ 'customers' }">
		<span style="white-space: nowrap;">
			<select name="Report_Sales_Customers_In">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 1, g.Report_Sales_Customers_In, 'In' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 0, g.Report_Sales_Customers_In, 'Not In' ) }">
			</select>
			<span style="position: relative; vertical-align: middle;">
				Price Group:
				<input type="text" size="20" id="Report_Sales_Customers_PriceGroup" name="Report_Sales_Customers_PriceGroup" value="{ encodeentities( g.Report_Sales_Customers_PriceGroup ) }">
				<a href="JavaScript:PriceGroupLookupDialog( 'Report_Sales_Customers_PriceGroup' );"><MvEVAL EXPR = "{ [ g.Module_Admin ].DrawImgButton_Lookup( 0 ) }"></a>
			</span>
		</span>
	<MvELSEIF EXPR = "{ l.field_id EQ 'category' }">
		<input type="text" size="20" id="Report_Sales_Category" name="Report_Sales_Category" value="{ encodeentities( g.Report_Sales_Category ) }">
		<a href="JavaScript:CategoryLookupDialog( 'Report_Sales_Category' );"><MvEVAL EXPR = "{ [ g.Module_Admin ].DrawImgButton_Lookup( 0 ) }"></a>
	<MvELSEIF EXPR = "{ l.field_id EQ 'product' }">
		<input type="text" size="20" id="Report_Sales_Product" name="Report_Sales_Product" value="{ encodeentities( g.Report_Sales_Product ) }">
		<a href="JavaScript:ProductLookupDialog( 'Report_Sales_Product' );"><MvEVAL EXPR = "{ [ g.Module_Admin ].DrawImgButton_Lookup( 0 ) }"></a>
	<MvELSEIF EXPR = "{ l.field_id EQ 'metrics' }">
		<div><label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Report_Sales_Gross,	'Report_Sales_Gross',		'1', 'Gross Sales' ) }"></label></div>
		<div><label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Report_Sales_Units,	'Report_Sales_Units',		'1', 'Total Units' ) }"></label></div>
		<div><label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Report_Sales_Orders,	'Report_Sales_Orders',		'1', 'Number of Orders' ) }"></label></div>
		<div><label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Report_Sales_Average,	'Report_Sales_Average',		'1', 'Average Order Total' ) }"></label></div>
		<div><label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Report_Sales_Tax,		'Report_Sales_Tax',			'1', 'Sales Tax*' ) }"></label></div>
		<div><label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Report_Sales_Shipping,	'Report_Sales_Shipping',	'1', 'Shipping Charges*' ) }"></label></div>
		<div style="margin-top: 2px; margin-left: 25px; font-size: .8em; font-style: italic;">* Not available when a product or category is specified</div>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Validate" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Report_Sales_Customers_In"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Report_Sales_Customers_In ) }">
	<MvASSIGN NAME = "g.Report_Sales_Customers_PriceGroup"	VALUE = "{ trim( g.Report_Sales_Customers_PriceGroup ) }">
	<MvASSIGN NAME = "g.Report_Sales_Category"				VALUE = "{ trim( g.Report_Sales_Category ) }">
	<MvASSIGN NAME = "g.Report_Sales_Product"				VALUE = "{ trim( g.Report_Sales_Product ) }">
	<MvASSIGN NAME = "g.Report_Sales_Gross"					VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Report_Sales_Gross ) }">
	<MvASSIGN NAME = "g.Report_Sales_Units"					VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Report_Sales_Units ) }">
	<MvASSIGN NAME = "g.Report_Sales_Orders"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Report_Sales_Orders ) }">
	<MvASSIGN NAME = "g.Report_Sales_Average"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Report_Sales_Average ) }">
	<MvASSIGN NAME = "g.Report_Sales_Tax"					VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Report_Sales_Tax ) }">
	<MvASSIGN NAME = "g.Report_Sales_Shipping"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Report_Sales_Shipping ) }">

	<MvIF EXPR = "{ len( g.Report_Sales_Customers_PriceGroup ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_DB ].PriceGroup_Load_Name( g.Report_Sales_Customers_PriceGroup, l.pricegroup ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Report_Sales_Customers_PriceGroup', 'Price Group \'' $ g.Report_Sales_Customers_PriceGroup $ '\' not found' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ len( g.Report_Sales_Category ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Category_Load_Code( g.Report_Sales_Category, l.category ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Report_Sales_Category', 'Category \'' $ g.Report_Sales_Category $ '\' not found' ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT Sales_Validate_CategoryHeirarchy( l.category:id ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Report_GeoSales_Category', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ len( g.Report_Sales_Product ) OR len( g.Report_Sales_Category ) }">
		<MvASSIGN NAME = "g.Report_Sales_Tax"					VALUE = 0>
		<MvASSIGN NAME = "g.Report_Sales_Shipping"				VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT g.Report_Sales_Gross AND
					NOT g.Report_Sales_Units AND
					NOT g.Report_Sales_Orders AND
					NOT g.Report_Sales_Average AND
					NOT g.Report_Sales_Tax AND
					NOT g.Report_Sales_Shipping }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select at least one metric to display.  Note that when a Category or Product is specified, Sales Tax and Shipping Charges may not be displayed.' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Update" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.report:config:customers_in"			VALUE = "{ g.Report_Sales_Customers_In }">
	<MvASSIGN NAME = "l.report:config:customers_pricegroup"	VALUE = "{ g.Report_Sales_Customers_PriceGroup }">
	<MvASSIGN NAME = "l.report:config:category"				VALUE = "{ g.Report_Sales_Category }">
	<MvASSIGN NAME = "l.report:config:product"				VALUE = "{ g.Report_Sales_Product }">
	<MvASSIGN NAME = "l.report:config:gross"				VALUE = "{ g.Report_Sales_Gross }">
	<MvASSIGN NAME = "l.report:config:units"				VALUE = "{ g.Report_Sales_Units }">
	<MvASSIGN NAME = "l.report:config:orders"				VALUE = "{ g.Report_Sales_Orders }">
	<MvASSIGN NAME = "l.report:config:average"				VALUE = "{ g.Report_Sales_Average }">
	<MvASSIGN NAME = "l.report:config:tax"					VALUE = "{ g.Report_Sales_Tax }">
	<MvASSIGN NAME = "l.report:config:shipping"				VALUE = "{ g.Report_Sales_Shipping }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Calculate_DateRange_All" PARAMETERS = "module var, report var, time_t_start var, time_t_end var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderDates"
				QUERY	= "{ 'SELECT
								MIN( orderdate ) AS min_orderdate,
								MAX( orderdate ) AS max_orderdate
							  FROM
								' $ g.Store_Table_Prefix $ 'Orders' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00001', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| Work around engine bug 5880: All ExternalDatabaseVariables ISNULL until they have been read
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.null" VALUE = "{ OrderDates.d.min_orderdate }">
	<MvASSIGN NAME = "l.null" VALUE = "{ OrderDates.d.max_orderdate }">

	<MvIF EXPR = "{ OrderDates.d.EOF OR ( ( ISNULL OrderDates.d.min_orderdate ) AND ( ISNULL OrderDates.d.max_orderdate ) ) }">
		<MvASSIGN NAME = "l.time_t_start"	VALUE = "{ s.time_t }">
		<MvASSIGN NAME = "l.time_t_end"		VALUE = "{ s.time_t }">
	<MvELSE>
		<MvASSIGN NAME = "l.time_t_start"	VALUE = "{ OrderDates.d.min_orderdate }">
		<MvASSIGN NAME = "l.time_t_end"		VALUE = "{ OrderDates.d.max_orderdate + 1 }">	<MvCOMMENT> Compensate for < end </MvCOMMENT>
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderDates">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Run_Intervals" PARAMETERS = "module var, report var, time_t_start, time_t_end, intervals var, interval_count" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_RPT_DB ].ReportData_Delete_Report( l.report:id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.query:time_t_start"			VALUE = "{ l.time_t_start }">
	<MvASSIGN NAME = "l.query:time_t_end"			VALUE = "{ l.time_t_end }">

	<MvIF EXPR = "{ ISNULL l.report:config:customers_pricegroup }">
		<MvASSIGN NAME = "l.query:pricegroup_id"	VALUE = 0>
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_DB ].PriceGroup_Load_Name( l.report:config:customers_pricegroup, l.pricegroup ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00002', 'Price Group \'' $ l.report:config:customers_pricegroup $ '\' not found' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.query:pricegroup_id"	VALUE = "{ l.pricegroup:id }">
	</MvIF>

	<MvIF EXPR = "{ ( NOT ISNULL l.report:config:product ) OR
					( ISNULL l.report:config:category ) }">
		<MvASSIGN NAME = "l.query:category_id"		VALUE = 0>
		<MvASSIGN NAME = "l.query:product_code"		VALUE = "{ l.report:config:product }">
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Category_Load_Code( l.report:config:category, l.category ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00003', 'Category \'' $ l.report:config:category $ '\' not found' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.query:category_id"		VALUE = "{ l.category:id }">

		<MvIF EXPR = "{ NOT Sales_Build_Category_TempTable( l.query ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.result"			VALUE = 1>

	<MvIF EXPR = "{ l.result }">
		<MvASSIGN NAME = "l.result"		VALUE = "{ Sales_Create_Order_TempTable( l.query ) }">
	</MvIF>

	<MvIF EXPR = "{ l.result }">
		<MvASSIGN NAME = "l.result"		VALUE = "{ Sales_Run_Intervals( l.report, l.query, l.intervals, l.interval_count ) }">
	</MvIF>
	
	<MvIF EXPR = "{ NOT ISNULL l.query:category_temp_table }">
		<MvASSIGN NAME = "l.result"		VALUE = "{ l.result AND Sales_Drop_TempTable( l.query:category_temp_table ) }">
	</MvIF>

	<MvASSIGN NAME = "l.result"			VALUE = "{ l.result AND Sales_Drop_TempTable( l.query:order_temp_table ) }">	

	<MvFUNCTIONRETURN VALUE = "{ l.result }">
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Run_Intervals" PARAMETERS = "report var, query var, intervals var, interval_count" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT ISNULL l.query:product_code }">
		<MvIF EXPR = "{ NOT l.query:pricegroup_id }">
			<MvIF EXPR = "{ NOT Sales_Populate_Order_TempTable_Product( l.query ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		<MvELSEIF EXPR = "{ l.report:config:customers_in }">
			<MvIF EXPR = "{ NOT Sales_Populate_Order_TempTable_Product_PriceGroup_In( l.query ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ NOT Sales_Populate_Order_TempTable_Product_PriceGroup_Out( l.query ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ NOT Sales_Index_Order_TempTable( l.query ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ l.report:config:gross }">
			<MvIF EXPR = "{ NOT Sales_Run_Gross_Product( l.report, l.query, l.intervals, l.interval_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.report:config:orders OR l.report:config:average }">
			<MvIF EXPR = "{ NOT Sales_Run_OrdersAverage( l.report, l.query, l.intervals, l.interval_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.report:config:units }">
			<MvIF EXPR = "{ NOT Sales_Run_Units_Product( l.report, l.query, l.intervals, l.interval_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	<MvELSEIF EXPR = "{ l.query:category_id }">
		<MvIF EXPR = "{ NOT l.query:pricegroup_id }">
			<MvIF EXPR = "{ NOT Sales_Populate_Order_TempTable_Category( l.query ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		<MvELSEIF EXPR = "{ l.report:config:customers_in }">
			<MvIF EXPR = "{ NOT Sales_Populate_Order_TempTable_Category_PriceGroup_In( l.query ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ NOT Sales_Populate_Order_TempTable_Category_PriceGroup_Out( l.query ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ NOT Sales_Index_Order_TempTable( l.query ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ l.report:config:gross }">
			<MvIF EXPR = "{ NOT Sales_Run_Gross_Category( l.report, l.query, l.intervals, l.interval_count ) }">	
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.report:config:orders OR l.report:config:average }">
			<MvIF EXPR = "{ NOT Sales_Run_OrdersAverage( l.report, l.query, l.intervals, l.interval_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.report:config:units }">
			<MvIF EXPR = "{ NOT Sales_Run_Units_Category( l.report, l.query, l.intervals, l.interval_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	<MvELSE>
		<MvIF EXPR = "{ NOT l.query:pricegroup_id }">
			<MvIF EXPR = "{ NOT Sales_Populate_Order_TempTable( l.query ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		<MvELSEIF EXPR = "{ l.report:config:customers_in }">
			<MvIF EXPR = "{ NOT Sales_Populate_Order_TempTable_PriceGroup_In( l.query ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ NOT Sales_Populate_Order_TempTable_PriceGroup_Out( l.query ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ NOT Sales_Index_Order_TempTable( l.query ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ l.report:config:gross OR l.report:config:orders OR l.report:config:average }">
			<MvIF EXPR = "{ NOT Sales_Run_GrossOrdersAverage( l.report, l.query, l.intervals, l.interval_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.report:config:units }">
			<MvIF EXPR = "{ NOT Sales_Run_Units( l.report, l.query, l.intervals, l.interval_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.report:config:tax OR l.report:config:shipping }">
			<MvIF EXPR = "{ NOT Sales_Run_TaxShipping( l.report, l.query, l.intervals, l.interval_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.report:last_state:setsize"		VALUE = "{ l.interval_count }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Format_Vertical_Label" PARAMETERS = "module var, report var, set_id, label" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( l.set_id EQ 1 ) OR
					( l.set_id EQ 4 ) OR
					( l.set_id EQ 5 ) OR
					( l.set_id EQ 6 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.label ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.label ROUND 0 }">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Display" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvASSIGN NAME = "l.color_count" VALUE = "{ [ g.Module_Feature_RPT_UT ].Chart_Color_Palette( l.colors ) }">

	<table style="border-collapse: collapse; font-size: small; font-family: Verdana, Arial, Helvetica, sans-serif;" cellspacing="0" cellpadding="0">
		<thead style="font-weight: bold;">
			<tr style="height: 20px;">
				<td>&nbsp;</td>
				<td align="center">
					<MvIF EXPR = "{ l.report:last_state:setsize EQ 1 }">
						<MvIF EXPR = "{ l.report:date_group EQ 'H' }">		1 Hour
						<MvELSEIF EXPR = "{ l.report:date_group EQ 'D' }">	1 Day
						<MvELSEIF EXPR = "{ l.report:date_group EQ 'W' }">	1 Week
						<MvELSEIF EXPR = "{ l.report:date_group EQ 'M' }">	1 Month
						<MvELSEIF EXPR = "{ l.report:date_group EQ 'Y' }">	1 Year
						</MvIF>
					<MvELSE>
						<MvEVAL EXPR = "{ encodeentities( l.report:last_state:setsize ) }">
						
						<MvIF EXPR = "{ l.report:date_group EQ 'H' }">		Hours
						<MvELSEIF EXPR = "{ l.report:date_group EQ 'D' }">	Days
						<MvELSEIF EXPR = "{ l.report:date_group EQ 'W' }">	Weeks
						<MvELSEIF EXPR = "{ l.report:date_group EQ 'M' }">	Months
						<MvELSEIF EXPR = "{ l.report:date_group EQ 'Y' }">	Years
						</MvIF>
					</MvIF>
				</td>
				<td align="right">
					Total
				</td>
			</tr>
		</thead>

		<tbody>
		<MvIF EXPR = "{ l.report:config:gross }">
			<MvASSIGN NAME = "l.rgb_color"		VALUE = "{ [ g.Module_Feature_RPT_UT ].Chart_Color_HexToRGB( l.colors[ 1 ] ) }">
			<MvASSIGN NAME = "l.image_source"	VALUE = "{ g.json_nosessionurl									$
														   'ST=admin'											$
														   '&Store_Code='	$ encodeattribute( g.Store:code )	$
														   '&Function=ReportData_Graph_Line'					$
														   '&Report_ID='	$ encodeattribute( l.report:id )	$
														   '&Smoothing=0'										$
														   '&Width=100'											$ 
														   '&Height=20'											$
														   '&Set_IDs=1'											$
														   '&Colors='		$ encodeattribute( l.rgb_color )	$
														   '&Background=255,255,255' }">

			<tr style="height: 20px;">
				<td>Gross Sales</td>
				<td align="center" style="padding-left: 5px; padding-right: 5px;">
					<a href="#" onclick="{ 'Report_Display_Chart( \'' $ [ g.Module_Admin ].JavaScriptEncode( g.Store:code ) $ '\', ' $ int( l.report:id ) $ ', true, 1 ); return false' }">
						<MvEVAL EXPR = "{ [ g.Module_Feature_RPT_UT ].Report_Draw_DelayedImage( l.image_source, 100, 20, l.rgb_color ) }">
					</a>
				</td>
				<td align="right">
					<MvIF EXPR = "{ [ g.Module_Feature_RPT_DB ].ReportData_Sum_Set( l.report:id, 1, l.total ) }">
						<MvEVAL EXPR = "{ ReportModule_Format_Vertical_Label( l.module, l.report, 1, l.total ) }">
					</MvIF>
				</td>
			</tr>
		</MvIF>

		<MvIF EXPR = "{ l.report:config:units }">
			<MvASSIGN NAME = "l.rgb_color"		VALUE = "{ [ g.Module_Feature_RPT_UT ].Chart_Color_HexToRGB( l.colors[ 2 ] ) }">
			<MvASSIGN NAME = "l.image_source"	VALUE = "{ g.json_nosessionurl									$
														   'ST=admin'											$
														   '&Store_Code='	$ encodeattribute( g.Store:code )	$
														   '&Function=ReportData_Graph_Line'					$
														   '&Report_ID='	$ encodeattribute( l.report:id )	$
														   '&Smoothing=0'										$
														   '&Width=100'											$ 
														   '&Height=20'											$
														   '&Set_IDs=2'											$
														   '&Colors='		$ encodeattribute( l.rgb_color )	$
														   '&Background=255,255,255' }">

			<tr style="height: 20px;">
				<td>Total Units</td>
				<td align="center" style="padding-left: 5px; padding-right: 5px;">
					<a href="#" onclick="{ 'Report_Display_Chart( \'' $ [ g.Module_Admin ].JavaScriptEncode( g.Store:code ) $ '\', ' $ int( l.report:id ) $ ', true, 2 ); return false' }">
						<MvEVAL EXPR = "{ [ g.Module_Feature_RPT_UT ].Report_Draw_DelayedImage( l.image_source, 100, 20, l.rgb_color ) }">
					</a>
				</td>
				<td align="right">
					<MvIF EXPR = "{ [ g.Module_Feature_RPT_DB ].ReportData_Sum_Set( l.report:id, 2, l.total ) }">
						<MvEVAL EXPR = "{ ReportModule_Format_Vertical_Label( l.module, l.report, 2, l.total ) }">
					</MvIF>
				</td>
			</tr>
		</MvIF>

		<MvIF EXPR = "{ l.report:config:orders }">
			<MvASSIGN NAME = "l.rgb_color"		VALUE = "{ [ g.Module_Feature_RPT_UT ].Chart_Color_HexToRGB( l.colors[ 3 ] ) }">
			<MvASSIGN NAME = "l.image_source"	VALUE = "{ g.json_nosessionurl									$
														   'ST=admin'											$
														   '&Store_Code='	$ encodeattribute( g.Store:code )	$
														   '&Function=ReportData_Graph_Line'					$
														   '&Report_ID='	$ encodeattribute( l.report:id )	$
														   '&Smoothing=0'										$
														   '&Width=100'											$ 
														   '&Height=20'											$
														   '&Set_IDs=3'											$
														   '&Colors='		$ encodeattribute( l.rgb_color )	$
														   '&Background=255,255,255' }">

			<tr style="height: 20px;">
				<td>Number of Orders</td>
				<td align="center" style="padding-left: 5px; padding-right: 5px;">
					<a href="#" onclick="{ 'Report_Display_Chart( \'' $ [ g.Module_Admin ].JavaScriptEncode( g.Store:code ) $ '\', ' $ int( l.report:id ) $ ', true, 3 ); return false' }">
						<MvEVAL EXPR = "{ [ g.Module_Feature_RPT_UT ].Report_Draw_DelayedImage( l.image_source, 100, 20, l.rgb_color ) }">
					</a>
				</td>
				<td align="right">
					<MvIF EXPR = "{ [ g.Module_Feature_RPT_DB ].ReportData_Sum_Set( l.report:id, 3, l.total ) }">
						<MvEVAL EXPR = "{ ReportModule_Format_Vertical_Label( l.module, l.report, 3, l.total ) }">
					</MvIF>
				</td>
			</tr>
		</MvIF>

		<MvIF EXPR = "{ l.report:config:average }">
			<MvASSIGN NAME = "l.rgb_color"		VALUE = "{ [ g.Module_Feature_RPT_UT ].Chart_Color_HexToRGB( l.colors[ 4 ] ) }">
			<MvASSIGN NAME = "l.image_source"	VALUE = "{ g.json_nosessionurl									$
														   'ST=admin'											$
														   '&Store_Code='	$ encodeattribute( g.Store:code )	$
														   '&Function=ReportData_Graph_Line'					$
														   '&Report_ID='	$ encodeattribute( l.report:id )	$
														   '&Smoothing=0'										$
														   '&Width=100'											$ 
														   '&Height=20'											$
														   '&Set_IDs=4'											$
														   '&Colors='		$ encodeattribute( l.rgb_color )	$
														   '&Background=255,255,255' }">

			<tr style="height: 20px;">
				<td>Average Order Total</td>
				<td align="center" style="padding-left: 5px; padding-right: 5px;">
					<a href="#" onclick="{ 'Report_Display_Chart( \'' $ [ g.Module_Admin ].JavaScriptEncode( g.Store:code ) $ '\', ' $ int( l.report:id ) $ ', true, 4 ); return false' }">
						<MvEVAL EXPR = "{ [ g.Module_Feature_RPT_UT ].Report_Draw_DelayedImage( l.image_source, 100, 20, l.rgb_color ) }">
					</a>
				</td>
				<td align="right">
					<MvEVAL EXPR = "{ ReportModule_Format_Vertical_Label( l.module, l.report, 4, l.report:last_state:setorderaverage ) }">
				</td>
			</tr>
		</MvIF>

		<MvIF EXPR = "{ l.report:config:tax }">
			<MvASSIGN NAME = "l.rgb_color"		VALUE = "{ [ g.Module_Feature_RPT_UT ].Chart_Color_HexToRGB( l.colors[ 5 ] ) }">
			<MvASSIGN NAME = "l.image_source"	VALUE = "{ g.json_nosessionurl									$
														   'ST=admin'											$
														   '&Store_Code='	$ encodeattribute( g.Store:code )	$
														   '&Function=ReportData_Graph_Line'					$
														   '&Report_ID='	$ encodeattribute( l.report:id )	$
														   '&Smoothing=0'										$
														   '&Width=100'											$ 
														   '&Height=20'											$
														   '&Set_IDs=5'											$
														   '&Colors='		$ encodeattribute( l.rgb_color )	$
														   '&Background=255,255,255' }">

			<tr style="height: 20px;">
				<td>Sales Tax</td>
				<td align="center" style="padding-left: 5px; padding-right: 5px;">
					<a href="#" onclick="{ 'Report_Display_Chart( \'' $ [ g.Module_Admin ].JavaScriptEncode( g.Store:code ) $ '\', ' $ int( l.report:id ) $ ', true, 5 ); return false' }">
						<MvEVAL EXPR = "{ [ g.Module_Feature_RPT_UT ].Report_Draw_DelayedImage( l.image_source, 100, 20, l.rgb_color ) }">
					</a>
				</td>
				<td align="right">
					<MvIF EXPR = "{ [ g.Module_Feature_RPT_DB ].ReportData_Sum_Set( l.report:id, 5, l.total ) }">
						<MvEVAL EXPR = "{ ReportModule_Format_Vertical_Label( l.module, l.report, 5, l.total ) }">
					</MvIF>
				</td>
			</tr>
		</MvIF>

		<MvIF EXPR = "{ l.report:config:shipping }">
			<MvASSIGN NAME = "l.rgb_color"		VALUE = "{ [ g.Module_Feature_RPT_UT ].Chart_Color_HexToRGB( l.colors[ 6 ] ) }">
			<MvASSIGN NAME = "l.image_source"	VALUE = "{ g.json_nosessionurl									$
														   'ST=admin'											$
														   '&Store_Code='	$ encodeattribute( g.Store:code )	$
														   '&Function=ReportData_Graph_Line'					$
														   '&Report_ID='	$ encodeattribute( l.report:id )	$
														   '&Smoothing=0'										$
														   '&Width=100'											$ 
														   '&Height=20'											$
														   '&Set_IDs=6'											$
														   '&Colors='		$ encodeattribute( l.rgb_color )	$
														   '&Background=255,255,255' }">

			<tr style="height: 20px;">
				<td>Shipping Charges</td>
				<td align="center" style="padding-left: 5px; padding-right: 5px;">
					<a href="#" onclick="{ 'Report_Display_Chart( \'' $ [ g.Module_Admin ].JavaScriptEncode( g.Store:code ) $ '\', ' $ int( l.report:id ) $ ', true, 6 ); return false' }">
						<MvEVAL EXPR = "{ [ g.Module_Feature_RPT_UT ].Report_Draw_DelayedImage( l.image_source, 100, 20, l.rgb_color ) }">
					</a>
				</td>
				<td align="right">
					<MvIF EXPR = "{ [ g.Module_Feature_RPT_DB ].ReportData_Sum_Set( l.report:id, 6, l.total ) }">
						<MvEVAL EXPR = "{ ReportModule_Format_Vertical_Label( l.module, l.report, 6, l.total ) }">
					</MvIF>
				</td>
			</tr>
		</MvIF>
		</tbody>
	</table>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Chart_Type" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "canvas_chart">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Canvas_Chart_Definition" PARAMETERS = "module var, report var, chart var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.color_count"											VALUE = "{ [ g.Module_Feature_RPT_UT ].Chart_Color_Palette( l.colors ) }">

	<MvIF EXPR = "{ l.report:config:gross }">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 1 MEMBER = "set_id"			VALUE = 1>
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 1 MEMBER = "color"			VALUE = "{ l.colors[ 1 ] }">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 1 MEMBER = "name"			VALUE = "Gross Sales">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 1 MEMBER = "data_type"		VALUE = "currency">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 1 MEMBER = "force_visible"	VALUE = "{ g.Report_Chart_Param EQ 1 }">
	</MvIF>

	<MvIF EXPR = "{ l.report:config:units }">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 2 MEMBER = "set_id"			VALUE = 2>
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 2 MEMBER = "color"			VALUE = "{ l.colors[ 2 ] }">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 2 MEMBER = "name"			VALUE = "Total Units">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 2 MEMBER = "data_type"		VALUE = "numeric">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 2 MEMBER = "force_visible"	VALUE = "{ g.Report_Chart_Param EQ 2 }">
	</MvIF>

	<MvIF EXPR = "{ l.report:config:orders }">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 3 MEMBER = "set_id"			VALUE = 3>
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 3 MEMBER = "color"			VALUE = "{ l.colors[ 3 ] }">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 3 MEMBER = "name"			VALUE = "Number of Orders">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 3 MEMBER = "data_type"		VALUE = "numeric">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 3 MEMBER = "force_visible"	VALUE = "{ g.Report_Chart_Param EQ 3 }">
	</MvIF>

	<MvIF EXPR = "{ l.report:config:average }">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 4 MEMBER = "set_id"			VALUE = 4>
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 4 MEMBER = "color"			VALUE = "{ l.colors[ 4 ] }">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 4 MEMBER = "name"			VALUE = "Average Order Total">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 4 MEMBER = "data_type"		VALUE = "currency">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 4 MEMBER = "force_visible"	VALUE = "{ g.Report_Chart_Param EQ 4 }">
	</MvIF>

	<MvIF EXPR = "{ l.report:config:tax }">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 5 MEMBER = "set_id"			VALUE = 5>
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 5 MEMBER = "color"			VALUE = "{ l.colors[ 5 ] }">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 5 MEMBER = "name"			VALUE = "Sales Tax">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 5 MEMBER = "data_type"		VALUE = "currency">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 5 MEMBER = "force_visible"	VALUE = "{ g.Report_Chart_Param EQ 5 }">
	</MvIF>

	<MvIF EXPR = "{ l.report:config:shipping }">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 6 MEMBER = "set_id"			VALUE = 6>
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 6 MEMBER = "color"			VALUE = "{ l.colors[ 6 ] }">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 6 MEMBER = "name"			VALUE = "Shipping Charges">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 6 MEMBER = "data_type"		VALUE = "currency">
		<MvASSIGN NAME = "l.chart:datasets" INDEX = 6 MEMBER = "force_visible"	VALUE = "{ g.Report_Chart_Param EQ 6 }">
	</MvIF>

	<MvASSIGN NAME = "l.null"													VALUE = "{ miva_array_collapse( l.chart:datasets ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Canvas_Output_LabelFormat_JavaScript" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Tabular_Definition" PARAMETERS = "module var, report var, definition var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.report:config:gross }">		<MvASSIGN NAME = "l.labels" INDEX = 1 VALUE = "Gross Sales">			</MvIF>
	<MvIF EXPR = "{ l.report:config:units }">		<MvASSIGN NAME = "l.labels" INDEX = 2 VALUE = "Total Units">			</MvIF>
	<MvIF EXPR = "{ l.report:config:orders }">		<MvASSIGN NAME = "l.labels" INDEX = 3 VALUE = "Number of Orders">		</MvIF>
	<MvIF EXPR = "{ l.report:config:average }">		<MvASSIGN NAME = "l.labels" INDEX = 4 VALUE = "Average Order Total">	</MvIF>
	<MvIF EXPR = "{ l.report:config:tax }">			<MvASSIGN NAME = "l.labels" INDEX = 5 VALUE = "Sales Tax">				</MvIF>
	<MvIF EXPR = "{ l.report:config:shipping }">	<MvASSIGN NAME = "l.labels" INDEX = 6 VALUE = "Shipping Charges">		</MvIF>

	<MvASSIGN NAME = "l.null"												VALUE = "{ miva_array_collapse( l.labels ) }">

	<MvASSIGN NAME = "l.definition:columns"		INDEX = 1 MEMBER = "type"	VALUE = "values">
	<MvASSIGN NAME = "l.definition:columns"		INDEX = 1 MEMBER = "start"	VALUE = 2>
	<MvASSIGN NAME = "l.definition:columns"		INDEX = 1 MEMBER = "values"	VALUE = "{ l.labels }">

	<MvASSIGN NAME = "l.definition:rows"		INDEX = 1 MEMBER = "type"	VALUE = "reportdata_date">
	<MvASSIGN NAME = "l.definition:rows"		INDEX = 1 MEMBER = "start"	VALUE = 2>
	<MvASSIGN NAME = "l.definition:rows"		INDEX = 1 MEMBER = "set_id"	VALUE = 1>

	<MvIF EXPR = "{ l.report:config:gross }">
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 2 MEMBER = "type"	VALUE = "reportdata">
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 2 MEMBER = "start"	VALUE = 2>
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 2 MEMBER = "set_id"	VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.report:config:units }">
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 3 MEMBER = "type"	VALUE = "reportdata">
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 3 MEMBER = "start"	VALUE = 2>
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 3 MEMBER = "set_id"	VALUE = 2>
	</MvIF>

	<MvIF EXPR = "{ l.report:config:orders }">
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 4 MEMBER = "type"	VALUE = "reportdata">
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 4 MEMBER = "start"	VALUE = 2>
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 4 MEMBER = "set_id"	VALUE = 3>
	</MvIF>

	<MvIF EXPR = "{ l.report:config:average }">
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 5 MEMBER = "type"	VALUE = "reportdata">
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 5 MEMBER = "start"	VALUE = 2>
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 5 MEMBER = "set_id"	VALUE = 4>
	</MvIF>

	<MvIF EXPR = "{ l.report:config:tax }">
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 6 MEMBER = "type"	VALUE = "reportdata">
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 6 MEMBER = "start"	VALUE = 2>
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 6 MEMBER = "set_id"	VALUE = 5>
	</MvIF>

	<MvIF EXPR = "{ l.report:config:shipping }">
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 7 MEMBER = "type"	VALUE = "reportdata">
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 7 MEMBER = "start"	VALUE = 2>
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 7 MEMBER = "set_id"	VALUE = 6>
	</MvIF>

	<MvASSIGN NAME = "l.null"												VALUE = "{ miva_array_collapse( l.definition:rows ) }">

	<MvIF EXPR = "{ l.definition:rows[ 2 ]:set_id }">
		<MvASSIGN NAME = "l.definition:rows"	INDEX = 1 MEMBER = "set_id"	VALUE = "{ l.definition:rows[ 2 ]:set_id }">
	</MvIF>

	<MvCOMMENT>
	|
	| If there are more than 256 datapoints, rotate the output to be vertical to avoid
	| issues with Excel
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.report:last_state:setsize GT 256 }">
		<MvASSIGN NAME = "l.temp"											VALUE = "{ l.definition:rows }">
		<MvASSIGN NAME = "l.definition:rows"								VALUE = "{ l.definition:columns }">
		<MvASSIGN NAME = "l.definition:columns"								VALUE = "{ l.temp }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Provision_Settings" PARAMETERS = "module var, report var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'PriceGroup' ) }">
		<MvASSIGN NAME = "l.report:config:customers_in"			VALUE = 0>
		<MvASSIGN NAME = "l.report:config:customers_pricegroup"	VALUE = "">
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_List(			l.provide_xml:tags:PriceGroup[ 1 ],	'customers',	l.report:config:customers_in, 'in,out', '1,0' ) OR
						NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'O',	l.provide_xml,						'PriceGroup',	l.report:config:customers_pricegroup ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml,	'Category',			l.report:config:category )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml,	'Product',			l.report:config:product ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.report:config:gross"					VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Metrics[ 1 ], 'Gross' ) }">
	<MvASSIGN NAME = "l.report:config:units"					VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Metrics[ 1 ], 'Units' ) }">
	<MvASSIGN NAME = "l.report:config:orders"					VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Metrics[ 1 ], 'Orders' ) }">
	<MvASSIGN NAME = "l.report:config:average"					VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Metrics[ 1 ], 'Average' ) }">
	<MvASSIGN NAME = "l.report:config:tax"						VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Metrics[ 1 ], 'Tax' ) }">
	<MvASSIGN NAME = "l.report:config:shipping"					VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml:tags:Metrics[ 1 ], 'Shipping' ) }">

	<MvIF EXPR = "{ len( l.report:config:customers_pricegroup ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_DB ].PriceGroup_Load_Name( l.report:config:customers_pricegroup, l.pricegroup ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Price Group \'' $ l.report:config:customers_pricegroup $ '\' does not exist' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ len( l.report:config:category ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Category_Load_Code( l.report:config:category, l.category ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Category \'' $ l.report:config:category $ '\' does not exist' ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT Sales_Validate_CategoryHeirarchy( l.category:id ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ( len( l.report:config:product ) OR len( l.report:config:category ) ) AND
					( l.report:config:tax OR l.report:config:shipping ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'The Tax and Shipping metrics cannot be enabled when limiting results to a category or product' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT l.report:config:gross AND
					NOT l.report:config:units AND
					NOT l.report:config:orders AND
					NOT l.report:config:average AND
					NOT l.report:config:tax AND
					NOT l.report:config:shipping }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'At least one metric must be specified' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Build_Category_TempTable" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.attempt"						VALUE = 1>
	<MvWHILE EXPR = "{ l.attempt LE 5 }">
		<MvASSIGN NAME = "l.query:category_temp_table"	VALUE = "{ g.Store_Table_Prefix $ 'TempSRC' $ l.attempt }">

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE TABLE ' $ l.query:category_temp_table $ '
							  (
								code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 ) $ '
							  )' }">
		<MvIF EXPR = "{ NOT g.MvQUERY_Error }">
			<MvWHILESTOP>
		</MvIF>

		<MvASSIGN NAME = "l.attempt"					VALUE = "{ l.attempt + 1 }">
	</MvWHILE>

	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Sales_CategoryHeirarchy_SQL( l.query:category_id, l.category_sql ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ l.query:category_temp_table $ '
						  SELECT DISTINCT
							p.code
						  FROM
							' $ g.Store_Table_Prefix $ 'CategoryXProduct cxp,
							' $ g.Store_Table_Prefix $ 'Products p
						  WHERE
							p.id = cxp.product_id AND
							' $ l.category_sql }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ l.query:category_temp_table $ '_1 ON ' $ l.query:category_temp_table $ ' ( code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Drop_TempTable" PARAMETERS = "table_name" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DROP TABLE ' $ l.table_name }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Validate_CategoryHeirarchy" PARAMETERS = "category_id" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.count"	VALUE = 1>
	<MvFUNCTIONRETURN VALUE = "{ Sales_Validate_Category_Children( l.category_id, l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Validate_Category_Children" PARAMETERS = "category_id, count var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.child" ARRAY = "l.children" COUNT = "{ [ g.Module_Library_DB ].CategoryList_Load_Parent( l.category_id, l.children ) }">
		<MvASSIGN NAME = "l.count"	VALUE = "{ l.count + 1 }">

		<MvIF EXPR = "{ l.count GT 100 }">
			<MvASSIGN NAME = "g.Validation_Message" VALUE = "The specified category hierarchy contains more than 100 categories.  Please select a hierarchy with fewer categories.">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT Sales_Validate_Category_Children( l.child:id, l.count ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_CategoryHeirarchy_SQL" PARAMETERS = "category_id, sql var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.id_count"				VALUE = 1>
	<MvASSIGN NAME = "l.ids"		INDEX = 1	VALUE = "{ l.category_id }">

	<MvIF EXPR = "{ NOT Sales_Add_Category_Children( l.category_id, l.ids, l.id_count ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.sql"					VALUE = "{ 'cxp.cat_id IN ( ' $ int( l.ids[ 1 ] ) }">
	<MvASSIGN NAME = "l.id_pos"					VALUE = 2>
	<MvWHILE EXPR = "{ l.id_pos LE l.id_count }">
		<MvASSIGN NAME = "l.sql"				VALUE = "{ l.sql $ ', ' $ int( l.ids[ l.id_pos ] ) }">
		<MvASSIGN NAME = "l.id_pos"				VALUE = "{ l.id_pos + 1 }">
	</MvWHILE>

	<MvASSIGN NAME = "l.sql"					VALUE = "{ l.sql $ ' )' }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Add_Category_Children" PARAMETERS = "category_id, ids var, id_count var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.child_pos"			VALUE = 1>
	<MvASSIGN NAME = "l.child_count"		VALUE = "{ [ g.Module_Library_DB ].CategoryList_Load_Parent( l.category_id, l.children ) }">

	<MvWHILE EXPR = "{ l.child_pos LE l.child_count }">
		<MvASSIGN NAME = "l.id_count"								VALUE = "{ l.id_count + 1 }">
		<MvASSIGN NAME = "l.ids"		INDEX = "{ l.id_count }"	VALUE = "{ l.children[ l.child_pos ]:id }">

		<MvIF EXPR = "{ l.id_count GT 100 }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00008', 'The specified category hierarchy contains more than 100 categories.  Please select a hierarchy with fewer categories.' ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT Sales_Add_Category_Children( l.children[ l.child_pos ]:id, l.ids, l.id_count ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.child_pos"		VALUE = "{ l.child_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Create_Order_TempTable" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.attempt"					VALUE = 1>
	<MvWHILE EXPR = "{ l.attempt LE 5 }">
		<MvASSIGN NAME = "l.query:order_temp_table"	VALUE = "{ g.Store_Table_Prefix $ 'TempSRO' $ l.attempt }">

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE TABLE ' $ l.query:order_temp_table $ '
							  (
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
								total		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
								orderdate	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ '
							  )' }">
		<MvIF EXPR = "{ NOT g.MvQUERY_Error }">
			<MvWHILESTOP>
		</MvIF>

		<MvASSIGN NAME = "l.attempt"	VALUE = "{ l.attempt + 1 }">
	</MvWHILE>

	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00009', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Index_Order_TempTable" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE INDEX ' $ l.query:order_temp_table $ '_1 ON ' $ l.query:order_temp_table $ ' ( orderdate )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00010', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Populate_Order_TempTable_Category" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ l.query:order_temp_table $ '
						  SELECT DISTINCT
							o.id, o.total, o.orderdate
						  FROM
							' $ g.Store_Table_Prefix $ 'Orders o,
							' $ g.Store_Table_Prefix $ 'OrderItems oi,
							' $ l.query:category_temp_table $ ' p
						  WHERE
							orderdate	>= ?	AND
							orderdate	< ?		AND
							oi.order_id = o.id	AND
							' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'p.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'oi.code' ) }"
			 FIELDS	= "l.query:time_t_start, l.query:time_t_end">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00011', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Populate_Order_TempTable_Category_PriceGroup_In" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ l.query:order_temp_table $ '
						  SELECT DISTINCT
							o.id, o.total, o.orderdate
						  FROM
							' $ g.Store_Table_Prefix $ 'PriceGroupXCustomer pxc,
							' $ g.Store_Table_Prefix $ 'Orders o,
							' $ g.Store_Table_Prefix $ 'OrderItems oi,
							' $ l.query:category_temp_table $ ' p
						  WHERE
							pxc.pgrp_id	= ?				AND
							o.cust_id	= pxc.cust_id	AND
							orderdate	>= ?			AND
							orderdate	< ?				AND
							oi.order_id	= o.id			AND
							' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'p.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'oi.code' ) }"
			 FIELDS	= "l.query:pricegroup_id, l.query:time_t_start, l.query:time_t_end">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00012', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Populate_Order_TempTable_Category_PriceGroup_Out" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ l.query:order_temp_table $ '
						  SELECT DISTINCT
							o.id, o.total, o.orderdate
						  FROM
							' $ g.Store_Table_Prefix $ 'Orders o
							LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'PriceGroupXCustomer pxc ON pxc.pgrp_id = ? AND pxc.cust_id = o.cust_id,
							' $ g.Store_Table_Prefix $ 'OrderItems oi,
							' $ l.query:category_temp_table $ ' p
						  WHERE
							orderdate	>= ?	AND
							orderdate	< ?		AND
							oi.order_id	= o.id	AND
							pxc.cust_id IS NULL	AND
							' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'p.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'oi.code' ) }"
			 FIELDS	= "l.query:pricegroup_id, l.query:time_t_start, l.query:time_t_end">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00013', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Populate_Order_TempTable_Product" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ l.query:order_temp_table $ '
						  SELECT DISTINCT
							o.id, o.total, o.orderdate
						  FROM
							' $ g.Store_Table_Prefix $ 'Orders o,
							' $ g.Store_Table_Prefix $ 'OrderItems oi
						  WHERE
							orderdate	>= ?	AND
							orderdate	< ?		AND
							oi.order_id	= o.id	AND
							' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'oi.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( '?' ) }"
			 FIELDS	= "l.query:time_t_start, l.query:time_t_end, l.query:product_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00014', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Populate_Order_TempTable_Product_PriceGroup_In" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ l.query:order_temp_table $ '
						  SELECT DISTINCT
							o.id, o.total, o.orderdate
						  FROM
							' $ g.Store_Table_Prefix $ 'PriceGroupXCustomer pxc,
							' $ g.Store_Table_Prefix $ 'Orders o,
							' $ g.Store_Table_Prefix $ 'OrderItems oi
						  WHERE
							pxc.pgrp_id	= ?				AND
							o.cust_id	= pxc.cust_id	AND
							orderdate	>= ?			AND
							orderdate	< ?				AND
							oi.order_id	= o.id			AND
							' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'oi.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( '?' ) }"
			 FIELDS	= "l.query:pricegroup_id, l.query:time_t_start, l.query:time_t_end, l.query:product_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00015', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Populate_Order_TempTable_Product_PriceGroup_Out" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ l.query:order_temp_table $ '
						  SELECT DISTINCT
							o.id, o.total, o.orderdate
						  FROM
							' $ g.Store_Table_Prefix $ 'Orders o
							LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'PriceGroupXCustomer pxc ON pxc.pgrp_id = ? AND pxc.cust_id = o.cust_id,
							' $ g.Store_Table_Prefix $ 'OrderItems oi
						  WHERE
							orderdate	>= ?	AND
							orderdate	< ?		AND
							oi.order_id	= o.id	AND
							pxc.cust_id IS NULL	AND
							' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'oi.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( '?' ) }"
			 FIELDS	= "l.query:pricegroup_id, l.query:time_t_start, l.query:time_t_end, l.query:product_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00016', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Populate_Order_TempTable" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ l.query:order_temp_table $ '
						  SELECT DISTINCT
							id, total, orderdate
						  FROM
							' $ g.Store_Table_Prefix $ 'Orders
						  WHERE
							orderdate >= ? AND
							orderdate < ?' }"
			 FIELDS	= "l.query:time_t_start, l.query:time_t_end">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00017', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Populate_Order_TempTable_PriceGroup_In" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ l.query:order_temp_table $ '
						  SELECT DISTINCT
							o.id, o.total, o.orderdate
						  FROM
							' $ g.Store_Table_Prefix $ 'PriceGroupXCustomer pxc,
							' $ g.Store_Table_Prefix $ 'Orders o
						  WHERE
							pxc.pgrp_id	= ?				AND
							o.cust_id	= pxc.cust_id	AND
							orderdate	>= ?			AND
							orderdate	< ?' }"
			 FIELDS	= "l.query:pricegroup_id, l.query:time_t_start, l.query:time_t_end">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00018', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Populate_Order_TempTable_PriceGroup_Out" PARAMETERS = "query var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ l.query:order_temp_table $ '
						  SELECT DISTINCT
							o.id, o.total, o.orderdate
						  FROM
							' $ g.Store_Table_Prefix $ 'Orders o
							LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'PriceGroupXCustomer pxc ON pxc.pgrp_id = ? AND pxc.cust_id = o.cust_id
						  WHERE
							orderdate >= ?	AND
							orderdate < ?	AND
							pxc.cust_id IS NULL' }"
			 FIELDS	= "l.query:pricegroup_id, l.query:time_t_start, l.query:time_t_end">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00019', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Run_Gross_Category" PARAMETERS = "report var, query var, intervals var, interval_count" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Sales_Gross"
				QUERY	= "{ 'SELECT
								o.orderdate	AS orderdate,
								oi.total	AS total
							  FROM
								' $ l.query:order_temp_table $ ' o, 
								' $ l.query:category_temp_table $ ' p,
								' $ g.Store_Table_Prefix $ 'OrderItems oi
							  WHERE
								oi.order_id = o.id AND
								' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'p.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'oi.code' ) $ '
							  ORDER BY
								orderdate' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00020', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.result" VALUE = "{ Sales_Run_Gross_CategoryProduct_LowLevel( l.report, l.query, l.intervals, l.interval_count ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Sales_Gross">

	<MvFUNCTIONRETURN VALUE = "{ l.result }">
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Run_Gross_Product" PARAMETERS = "report var, query var, intervals var, interval_count" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Sales_Gross"
				QUERY	= "{ 'SELECT
								o.orderdate	AS orderdate,
								oi.total	AS total
							  FROM
								' $ l.query:order_temp_table $ ' o,
								' $ g.Store_Table_Prefix $ 'OrderItems oi
							  WHERE
								oi.order_id = o.id AND
								' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'oi.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( '?' ) $ '
							  ORDER BY
								orderdate' }"
				FIELDS	= "l.query:product_code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00021', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.result" VALUE = "{ Sales_Run_Gross_CategoryProduct_LowLevel( l.report, l.query, l.intervals, l.interval_count ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Sales_Gross">

	<MvFUNCTIONRETURN VALUE = "{ l.result }">
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Run_Gross_CategoryProduct_LowLevel" PARAMETERS = "report var, query var, intervals var, interval_count var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.interval_gross"			VALUE = 0.00>
	<MvASSIGN NAME = "l.interval_pos"			VALUE = 1>

	<MvWHILE EXPR = "{ NOT Sales_Gross.d.EOF }">
		<MvWHILE EXPR = "{ Sales_Gross.d.orderdate GE l.intervals[ l.interval_pos ]:time_t_end }">
			<MvIF EXPR = "{ NOT Sales_Insert_Gross( l.report, l.intervals[ l.interval_pos ], l.interval_gross ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.interval_gross"	VALUE = 0.00>
			<MvASSIGN NAME = "l.interval_pos"	VALUE = "{ l.interval_pos + 1 }">
		</MvWHILE>

		<MvASSIGN NAME = "l.interval_gross"		VALUE = "{ l.interval_gross + Sales_Gross.d.total }">

		<MvSKIP NAME = "Merchant" VIEW = "Sales_Gross" ROWS = 1>
	</MvWHILE>

	<MvIF EXPR = "{ NOT Sales_Insert_Gross( l.report, l.intervals[ l.interval_pos ], l.interval_gross ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.interval_pos"			VALUE = "{ l.interval_pos + 1 }">

	<MvWHILE EXPR = "{ l.interval_pos LE l.interval_count }">
		<MvIF EXPR = "{ NOT Sales_Insert_Gross( l.report, l.intervals[ l.interval_pos ], 0.00 ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.interval_pos"		VALUE = "{ l.interval_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Run_OrdersAverage" PARAMETERS = "report var, query var, intervals var, interval_count" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Sales_Calculate_OrderCountAndTotals( l.report, l.query, l.intervals, l.interval_count, l.data ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.sum_total"							VALUE = 0.00>
	<MvASSIGN NAME = "l.sum_count"							VALUE = 0>

	<MvASSIGN NAME = "l.interval_pos"						VALUE = 1>
	<MvWHILE EXPR = "{ l.interval_pos LE l.interval_count }">
		<MvASSIGN NAME = "l.sum_total"						VALUE = "{ l.sum_total + l.data[ l.interval_pos ]:total }">
		<MvASSIGN NAME = "l.sum_count"						VALUE = "{ l.sum_count + l.data[ l.interval_pos ]:count }">

		<MvIF EXPR = "{ NOT Sales_Insert_OrdersAverage( l.report, l.intervals[ l.interval_pos ], l.data[ l.interval_pos ]:total, l.data[ l.interval_pos ]:count ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.interval_pos"					VALUE = "{ l.interval_pos + 1 }">
	</MvWHILE>

	<MvASSIGN NAME = "l.report:last_state:setorderaverage"	VALUE = "{ l.sum_total / l.sum_count }">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Run_GrossOrdersAverage" PARAMETERS = "report var, query var, intervals var, interval_count" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Sales_Calculate_OrderCountAndTotals( l.report, l.query, l.intervals, l.interval_count, l.data ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.sum_total"							VALUE = 0.00>
	<MvASSIGN NAME = "l.sum_count"							VALUE = 0>

	<MvASSIGN NAME = "l.interval_pos"						VALUE = 1>
	<MvWHILE EXPR = "{ l.interval_pos LE l.interval_count }">
		<MvASSIGN NAME = "l.sum_total"						VALUE = "{ l.sum_total + l.data[ l.interval_pos ]:total }">
		<MvASSIGN NAME = "l.sum_count"						VALUE = "{ l.sum_count + l.data[ l.interval_pos ]:count }">

		<MvIF EXPR = "{ NOT Sales_Insert_GrossOrdersAverage( l.report, l.intervals[ l.interval_pos ], l.data[ l.interval_pos ]:total, l.data[ l.interval_pos ]:count ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.interval_pos"					VALUE = "{ l.interval_pos + 1 }">
	</MvWHILE>

	<MvASSIGN NAME = "l.report:last_state:setorderaverage"	VALUE = "{ l.sum_total / l.sum_count }">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Calculate_OrderCountAndTotals" PARAMETERS = "report var, query var, intervals var, interval_count, data var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Sales_Orders"
				QUERY	= "{ 'SELECT
								total,
								orderdate
							  FROM
								' $ l.query:order_temp_table $ '
							  ORDER BY
								orderdate' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00022', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.interval_pos"				VALUE = 1>
	<MvASSIGN NAME = "l.interval_total"				VALUE = 0.00>
	<MvASSIGN NAME = "l.interval_count"				VALUE = 0>

	<MvWHILE EXPR = "{ NOT Sales_Orders.d.EOF }">
		<MvWHILE EXPR = "{ Sales_Orders.d.orderdate GE l.intervals[ l.interval_pos ]:time_t_end }">
			<MvASSIGN NAME = "l.data" INDEX = "{ l.interval_pos }" MEMBER = "total"		VALUE = "{ l.interval_total }">
			<MvASSIGN NAME = "l.data" INDEX = "{ l.interval_pos }" MEMBER = "count"		VALUE = "{ l.interval_count }">

			<MvASSIGN NAME = "l.interval_total"		VALUE = 0.00>
			<MvASSIGN NAME = "l.interval_count"		VALUE = 0>

			<MvASSIGN NAME = "l.interval_pos"		VALUE = "{ l.interval_pos + 1 }">
		</MvWHILE>

		<MvASSIGN NAME = "l.interval_total"			VALUE = "{ l.interval_total + Sales_Orders.d.total }">
		<MvASSIGN NAME = "l.interval_count"			VALUE = "{ l.interval_count + 1 }">

		<MvSKIP NAME = "Merchant" VIEW = "Sales_Orders" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Sales_Orders">

	<MvASSIGN NAME = "l.data" INDEX = "{ l.interval_pos }" MEMBER = "total"				VALUE = "{ l.interval_total }">
	<MvASSIGN NAME = "l.data" INDEX = "{ l.interval_pos }" MEMBER = "count"				VALUE = "{ l.interval_count }">

	<MvASSIGN NAME = "l.interval_pos"				VALUE = "{ l.interval_pos + 1 }">

	<MvWHILE EXPR = "{ l.interval_pos LE l.interval_count }">
		<MvASSIGN NAME = "l.data" INDEX = "{ l.interval_pos }" MEMBER = "total"			VALUE = 0.00>
		<MvASSIGN NAME = "l.data" INDEX = "{ l.interval_pos }" MEMBER = "count"			VALUE = 0>

		<MvASSIGN NAME = "l.interval_pos"			VALUE = "{ l.interval_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Run_Units_Category" PARAMETERS = "report var, query var, intervals var, interval_count" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Sales_Units"
				QUERY	= "{ 'SELECT
								o.orderdate			AS orderdate,
								SUM( oi.quantity )	AS units
							  FROM
								' $ l.query:order_temp_table $ ' o,
								' $ g.Store_Table_Prefix $ 'OrderItems oi,
								' $ l.query:category_temp_table $ ' p
							  WHERE
								oi.order_id = o.id	AND
								' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'p.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'oi.code' ) $ '
							  GROUP BY
								o.orderdate
							  ORDER BY
								orderdate' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00023', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.result"	VALUE = "{ Sales_Run_Units_LowLevel( l.report, l.query, l.intervals, l.interval_count ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Sales_Units">
	
	<MvFUNCTIONRETURN VALUE = "{ l.result }">
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Run_Units_Product" PARAMETERS = "report var, query var, intervals var, interval_count" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Sales_Units"
				QUERY	= "{ 'SELECT
								o.orderdate			AS orderdate,
								SUM( oi.quantity )	AS units
							  FROM
								' $ l.query:order_temp_table $ ' o,
								' $ g.Store_Table_Prefix $ 'OrderItems oi
							  WHERE
								oi.order_id = o.id	AND
								' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'oi.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( '?' ) $ '
							  GROUP BY
								o.orderdate
							  ORDER BY
								orderdate' }"
				FIELDS	= "l.query:product_code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00024', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.result"	VALUE = "{ Sales_Run_Units_LowLevel( l.report, l.query, l.intervals, l.interval_count ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Sales_Units">
	
	<MvFUNCTIONRETURN VALUE = "{ l.result }">
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Run_Units" PARAMETERS = "report var, query var, intervals var, interval_count" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Sales_Units"
				QUERY	= "{ 'SELECT
								o.orderdate			AS orderdate,
								SUM( oi.quantity )	AS units
							  FROM
								' $ l.query:order_temp_table $ ' o,
								' $ g.Store_Table_Prefix $ 'OrderItems oi
							  WHERE
								oi.order_id = o.id
							  GROUP BY
								o.orderdate
							  ORDER BY
								orderdate' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00025', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.result"	VALUE = "{ Sales_Run_Units_LowLevel( l.report, l.query, l.intervals, l.interval_count ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Sales_Units">
	
	<MvFUNCTIONRETURN VALUE = "{ l.result }">
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Run_Units_LowLevel" PARAMETERS = "report var, query var, intervals var, interval_count" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.interval_pos"				VALUE = 1>
	<MvASSIGN NAME = "l.interval_units"				VALUE = 0.00>

	<MvWHILE EXPR = "{ NOT Sales_Units.d.EOF }">
		<MvWHILE EXPR = "{ Sales_Units.d.orderdate GE l.intervals[ l.interval_pos ]:time_t_end }">
			<MvIF EXPR = "{ NOT Sales_Insert_Units( l.report, l.intervals[ l.interval_pos ], l.interval_units ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.interval_units"		VALUE = 0>
			<MvASSIGN NAME = "l.interval_pos"		VALUE = "{ l.interval_pos + 1 }">
		</MvWHILE>

		<MvASSIGN NAME = "l.interval_units"			VALUE = "{ l.interval_units + Sales_Units.d.units }">

		<MvSKIP NAME = "Merchant" VIEW = "Sales_Units" ROWS = 1>
	</MvWHILE>

	<MvIF EXPR = "{ NOT Sales_Insert_Units( l.report, l.intervals[ l.interval_pos ], l.interval_units ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.interval_pos"				VALUE = "{ l.interval_pos + 1 }">

	<MvWHILE EXPR = "{ l.interval_pos LE l.interval_count }">
		<MvIF EXPR = "{ NOT Sales_Insert_Units( l.report, l.intervals[ l.interval_pos ], 0 ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.interval_pos"			VALUE = "{ l.interval_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Run_TaxShipping" PARAMETERS = "report var, query var, intervals var, interval_count" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Sales_OrderCharges"
				QUERY	= "{ 'SELECT
								c.type			AS type,
								c.amount		AS amount,
								o.orderdate		AS orderdate
							  FROM
								' $ l.query:order_temp_table $ ' o,
								' $ g.Store_Table_Prefix $ 'OrderCharges c
							  WHERE
								c.order_id	= o.id
							  ORDER BY
								orderdate' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SALES-00026', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.interval_pos"				VALUE = 1>
	<MvASSIGN NAME = "l.interval_tax"				VALUE = 0.00>
	<MvASSIGN NAME = "l.interval_shipping"			VALUE = 0.00>

	<MvWHILE EXPR = "{ NOT Sales_OrderCharges.d.EOF }">
		<MvWHILE EXPR = "{ Sales_OrderCharges.d.orderdate GE l.intervals[ l.interval_pos ]:time_t_end }">
			<MvIF EXPR = "{ NOT Sales_Insert_TaxShipping( l.report, l.intervals[ l.interval_pos ], l.interval_tax, l.interval_shipping ) }">
				<MvCLOSEVIEW NAME = "Merchant" VIEW = "Sales_OrderCharges">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.interval_tax"		VALUE = 0.00>
			<MvASSIGN NAME = "l.interval_shipping"	VALUE = 0.00>

			<MvASSIGN NAME = "l.interval_pos"		VALUE = "{ l.interval_pos + 1 }">
		</MvWHILE>

		<MvIF EXPR = "{ Sales_OrderCharges.d.type EQ 'TAX' }">
			<MvASSIGN NAME = "l.interval_tax"		VALUE = "{ l.interval_tax + Sales_OrderCharges.d.amount }">
		<MvELSEIF EXPR = "{ Sales_OrderCharges.d.type EQ 'SHIPPING' }">
			<MvASSIGN NAME = "l.interval_shipping"	VALUE = "{ l.interval_shipping + Sales_OrderCharges.d.amount }">
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "Sales_OrderCharges" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Sales_OrderCharges">

	<MvIF EXPR = "{ NOT Sales_Insert_TaxShipping( l.report, l.intervals[ l.interval_pos ], l.interval_tax, l.interval_shipping ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.interval_pos"				VALUE = "{ l.interval_pos + 1 }">

	<MvWHILE EXPR = "{ l.interval_pos LE l.interval_count }">
		<MvIF EXPR = "{ NOT Sales_Insert_TaxShipping( l.report, l.intervals[ l.interval_pos ], 0.00, 0.00 ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.interval_pos"			VALUE = "{ l.interval_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Insert_Gross" PARAMETERS = "report var, interval var, interval_gross" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.interval_gross EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.reportdata:report_id"	VALUE = "{ l.report:id }">
	<MvASSIGN NAME = "l.reportdata:dt_start"	VALUE = "{ l.interval:time_t_start }">
	<MvASSIGN NAME = "l.reportdata:dt_end"		VALUE = "{ l.interval:time_t_end }">
	<MvASSIGN NAME = "l.reportdata:set_id"		VALUE = 1>
	<MvASSIGN NAME = "l.reportdata:data"		VALUE = "{ l.interval_gross ROUND 2 }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_RPT_DB ].ReportData_Insert( l.reportdata ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Insert_OrdersAverage" PARAMETERS = "report var, interval var, interval_order_total, interval_orders" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.reportdata:report_id"	VALUE = "{ l.report:id }">
	<MvASSIGN NAME = "l.reportdata:dt_start"	VALUE = "{ l.interval:time_t_start }">
	<MvASSIGN NAME = "l.reportdata:dt_end"		VALUE = "{ l.interval:time_t_end }">

	<MvIF EXPR = "{ l.interval_orders EQ 0 }">
		<MvASSIGN NAME = "l.interval_average"	VALUE = 0.00>
	<MvELSE>
		<MvASSIGN NAME = "l.interval_average"	VALUE = "{ l.interval_order_total / l.interval_orders }">
	</MvIF>

	<MvIF EXPR = "{ l.report:config:orders }">
		<MvASSIGN NAME = "l.reportdata:set_id"		VALUE = 3>
		<MvASSIGN NAME = "l.reportdata:data"		VALUE = "{ l.interval_orders ROUND 0 }">

		<MvIF EXPR = "{ l.reportdata:data NE 0 }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_RPT_DB ].ReportData_Insert( l.reportdata ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.report:config:average }">
		<MvASSIGN NAME = "l.reportdata:set_id"		VALUE = 4>
		<MvASSIGN NAME = "l.reportdata:data"		VALUE = "{ l.interval_average ROUND 2 }">

		<MvIF EXPR = "{ l.reportdata:data NE 0 }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_RPT_DB ].ReportData_Insert( l.reportdata ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Insert_GrossOrdersAverage" PARAMETERS = "report var, interval var, interval_gross, interval_orders" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.interval_orders EQ 0 }">
		<MvASSIGN NAME = "l.interval_average"	VALUE = 0.00>
	<MvELSE>
		<MvASSIGN NAME = "l.interval_average"	VALUE = "{ l.interval_gross / l.interval_orders }">
	</MvIF>

	<MvASSIGN NAME = "l.reportdata:report_id"	VALUE = "{ l.report:id }">
	<MvASSIGN NAME = "l.reportdata:dt_start"	VALUE = "{ l.interval:time_t_start }">
	<MvASSIGN NAME = "l.reportdata:dt_end"		VALUE = "{ l.interval:time_t_end }">

	<MvIF EXPR = "{ l.report:config:gross }">
		<MvASSIGN NAME = "l.reportdata:set_id"	VALUE = 1>
		<MvASSIGN NAME = "l.reportdata:data"	VALUE = "{ l.interval_gross ROUND 2 }">

		<MvIF EXPR = "{ l.reportdata:data NE 0 }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_RPT_DB ].ReportData_Insert( l.reportdata ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.report:config:orders }">
		<MvASSIGN NAME = "l.reportdata:set_id"	VALUE = 3>
		<MvASSIGN NAME = "l.reportdata:data"	VALUE = "{ l.interval_orders ROUND 0 }">

		<MvIF EXPR = "{ l.reportdata:data NE 0 }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_RPT_DB ].ReportData_Insert( l.reportdata ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.report:config:average }">
		<MvASSIGN NAME = "l.reportdata:set_id"	VALUE = 4>
		<MvASSIGN NAME = "l.reportdata:data"	VALUE = "{ l.interval_average ROUND 2 }">

		<MvIF EXPR = "{ l.reportdata:data NE 0 }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_RPT_DB ].ReportData_Insert( l.reportdata ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Insert_Units" PARAMETERS = "report var, interval var, interval_units" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.interval_units EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.reportdata:report_id"	VALUE = "{ l.report:id }">
	<MvASSIGN NAME = "l.reportdata:dt_start"	VALUE = "{ l.interval:time_t_start }">
	<MvASSIGN NAME = "l.reportdata:dt_end"		VALUE = "{ l.interval:time_t_end }">

	<MvCOMMENT> Units </MvCOMMENT>
	<MvASSIGN NAME = "l.reportdata:set_id"		VALUE = 2>
	<MvASSIGN NAME = "l.reportdata:data"		VALUE = "{ l.interval_units ROUND 0 }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_RPT_DB ].ReportData_Insert( l.reportdata ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Sales_Insert_TaxShipping" PARAMETERS = "report var, interval var, interval_tax, interval_shipping" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.reportdata:report_id"	VALUE = "{ l.report:id }">
	<MvASSIGN NAME = "l.reportdata:dt_start"	VALUE = "{ l.interval:time_t_start }">
	<MvASSIGN NAME = "l.reportdata:dt_end"		VALUE = "{ l.interval:time_t_end }">

	<MvIF EXPR = "{ l.report:config:tax }">
		<MvASSIGN NAME = "l.reportdata:set_id"		VALUE = 5>
		<MvASSIGN NAME = "l.reportdata:data"		VALUE = "{ l.interval_tax ROUND 2 }">

		<MvIF EXPR = "{ l.reportdata:data NE 0 }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_RPT_DB ].ReportData_Insert( l.reportdata ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.report:config:shipping }">
		<MvASSIGN NAME = "l.reportdata:set_id"		VALUE = 6>
		<MvASSIGN NAME = "l.reportdata:data"		VALUE = "{ l.interval_shipping ROUND 2 }">

		<MvIF EXPR = "{ l.reportdata:data NE 0 }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_RPT_DB ].ReportData_Insert( l.reportdata ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

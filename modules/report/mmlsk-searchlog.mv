<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-RPT-SLG-
| Next Error Code: 3   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-report_searchlog">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Search Log">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0900">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.62">
	<MvASSIGN NAME = "l.module:description" VALUE = "Use this module to generate reports of runtime searches">
	<MvASSIGN NAME = "l.module:features"	VALUE = "report">
</MvFUNCTION>

<MvCOMMENT>
|
| Report Feature (report)
|
</MvCOMMENT>

<MvFUNCTION NAME = "ReportModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:date_range"			VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:date_interval"			VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:display"				VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:output_chart"			VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:output_tabular"		VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:output_custom"			VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:provision_settings"	VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Fields" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.report:id }">
		<MvASSIGN NAME = "g.Report_SearchLog_SearchType"		VALUE = "{ l.report:config:searchtype }">
		<MvASSIGN NAME = "g.Report_SearchLog_MinResults"		VALUE = "{ l.report:config:minresults }">
		<MvASSIGN NAME = "g.Report_SearchLog_MaxResults"		VALUE = "{ l.report:config:maxresults }">
		<MvASSIGN NAME = "g.Report_SearchLog_Display_TopN" 		VALUE = "{ l.report:config:display_topn }">
		<MvASSIGN NAME = "g.Report_SearchLog_Export_TopN"		VALUE = "{ l.report:config:export_topn }">
	<MvELSE>
		<MvASSIGN NAME = "g.Report_SearchLog_SearchType"		VALUE = "all">
		<MvASSIGN NAME = "g.Report_SearchLog_MinResults"		VALUE = 0>
		<MvASSIGN NAME = "g.Report_SearchLog_MaxResults"		VALUE = 0>
		<MvASSIGN NAME = "g.Report_SearchLog_Display_TopN" 		VALUE = 10>
		<MvASSIGN NAME = "g.Report_SearchLog_Export_TopN"		VALUE = 10>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "searchtype,searchlimit,topn">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.field_id EQ 'searchtype' }">		<MvFUNCTIONRETURN VALUE = "<b>Search Type:<b>">
	<MvELSEIF EXPR = "{ l.field_id EQ 'searchlimit' }">	<MvFUNCTIONRETURN VALUE = "<b>Limit to Searches Returning:<b>">
	<MvELSEIF EXPR = "{ l.field_id EQ 'topn' }">		<MvFUNCTIONRETURN VALUE = "<b>Display:<b>">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.field_id EQ 'searchtype' }">
		<span style="white-space: nowrap;">
			<select name="Report_SearchLog_SearchType">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'all',			g.Report_SearchLog_SearchType, 'All' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'customer',	g.Report_SearchLog_SearchType, 'Logged in Customers' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'order',		g.Report_SearchLog_SearchType, 'Sessions that Placed an Order' ) }">
			</select>
		</span>
	<MvELSEIF EXPR = "{ l.field_id EQ 'searchlimit' }">
		<div style="margin-bottom: 3px;">Less than <input type="text" size="5" name="Report_SearchLog_MinResults" value="{ encodeentities( g.Report_SearchLog_MinResults ) }"> results (average, 0 = no restriction)</div>
		<div style="margin-bottom: 3px;">More than <input type="text" size="5" name="Report_SearchLog_MaxResults" value="{ encodeentities( g.Report_SearchLog_MaxResults ) }"> results (average, 0 = no restriction)</div>
	<MvELSEIF EXPR = "{ l.field_id EQ 'topn' }">
		<div style="margin-bottom: 3px;"><input type="text" size="5" name="Report_SearchLog_Display_TopN" value="{ encodeentities( g.Report_SearchLog_Display_TopN ) }"> record(s) on the dashboard</div>
		<div style="margin-bottom: 3px;"><input type="text" size="5" name="Report_SearchLog_Export_TopN" value="{ encodeentities( g.Report_SearchLog_Export_TopN ) }"> record(s) in exported data</div>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Validate" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Report_SearchLog_SearchType" VALUE = "{ trim( g.Report_SearchLog_SearchType ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Value_In_List( g.Report_SearchLog_SearchType, 'all,customer,order' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Report_SearchLog_SearchType', 'Please select a valid Search Type' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( g.Report_SearchLog_MinResults ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Report_SearchLog_MinResults', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( g.Report_SearchLog_MaxResults ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Report_SearchLog_MaxResults', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( g.Report_SearchLog_Display_TopN ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Report_SearchLog_Display_TopN', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( g.Report_SearchLog_Export_TopN ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Report_SearchLog_Export_TopN', g.Validation_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Update" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.report:config:searchtype" 	VALUE = "{ g.Report_SearchLog_SearchType }">
	<MvASSIGN NAME = "l.report:config:minresults"	VALUE = "{ g.Report_SearchLog_MinResults }">
	<MvASSIGN NAME = "l.report:config:maxresults"	VALUE = "{ g.Report_SearchLog_MaxResults }">
	<MvASSIGN NAME = "l.report:config:display_topn"	VALUE = "{ g.Report_SearchLog_Display_TopN }">
	<MvASSIGN NAME = "l.report:config:export_topn" 	VALUE = "{ g.Report_SearchLog_Export_TopN }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Calculate_DateRange_All" PARAMETERS = "module var, report var, time_t_start var, time_t_end var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "SearchLogDates"
				QUERY	= "{ 'SELECT
								MIN( dtstamp ) AS min_searchdate,
								MAX( dtstamp ) AS max_searchdate
							  FROM
								' $ g.Store_Table_Prefix $ 'SearchLogEntries' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SLG-00001', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ SearchLogDates.d.EOF OR ( ( ISNULL SearchLogDates.d.min_searchdate ) AND ( ISNULL SearchLogDates.d.max_searchdate ) ) }">
		<MvASSIGN NAME = "l.time_t_start"	VALUE = "{ s.time_t }">
		<MvASSIGN NAME = "l.time_t_end"		VALUE = "{ s.time_t }">
	<MvELSE>
		<MvASSIGN NAME = "l.time_t_start"	VALUE = "{ SearchLogDates.d.min_searchdate }">
		<MvASSIGN NAME = "l.time_t_end"		VALUE = "{ SearchLogDates.d.max_searchdate }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SearchLogDates">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Run_DateRange" PARAMETERS = "module var, report var, time_t_start, time_t_end" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_RPT_DB ].ReportData_Delete_Report( l.report:id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ SearchLog_Run( l.report, l.time_t_start, l.time_t_end ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Display" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.report:last_state:search_count EQ 0 }">
		<i>Not Available</i>
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<table style="border-collapse: collapse; font-size: small; font-family: Verdana, Arial, Helvetica, sans-serif;" cellspacing="0" cellpadding="0">
		<thead style="font-weight: bold;">
			<tr style="height: 20px; margin-left: 10px;">
				<td align="left" style="padding-right: 10px;">Search</td>
				<td align="center" style="padding-right: 10px;">Session Count</td>
				<td align="center" style="padding-right: 10px;">Average Results</td>
			</tr>
		</thead>

		<tbody>
		<MvFOREACH ITERATOR = "l.search" ARRAY = "l.report:last_state:searches" COUNT = "{ min( l.report:last_state:search_count, l.report:config:display_topn ) }">
			<tr>
			<td valign="top" align="left" style="padding-right: 10px;"><MvEVAL EXPR = "{ encodeentities( l.search:search ) }"></td>
			<td valign="top" align="center" style="padding-right: 10px;"><MvEVAL EXPR = "{ int( l.search:session_count ) }"></td>
			<td valign="top" align="center" style="padding-right: 10px;"><MvEVAL EXPR = "{ l.search:average_results ROUND 2 }"></td>
			</tr>
		</MvFOREACH>
		</tbody>
	</table>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Tabular_Definition" PARAMETERS = "module var, report var, definition var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ SearchLog_Add_Tabular_Column( l.definition, l.values, 'SEARCH',			'search' ) }">
	<MvEVAL EXPR = "{ SearchLog_Add_Tabular_Column( l.definition, l.values, 'SESSION_COUNT',	'session_count' ) }">
	<MvEVAL EXPR = "{ SearchLog_Add_Tabular_Column( l.definition, l.values, 'AVERAGE_RESULTS',	'average_results' ) }">

	<MvFOREACH ITERATOR = "l.search" ARRAY = "l.report:last_state:searches" COUNT = "{ min( l.report:last_state:search_count, l.report:config:export_topn ) }">
		<MvASSIGN NAME = "l.null"	VALUE = "{ miva_array_insert_var( l.values:search,			l.search:search,			-1 ) }">
		<MvASSIGN NAME = "l.null"	VALUE = "{ miva_array_insert_var( l.values:session_count,	l.search:session_count,		-1 ) }">
		<MvASSIGN NAME = "l.null"	VALUE = "{ miva_array_insert_var( l.values:average_results, l.search:average_results,	-1 ) }">
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Provision_Settings" PARAMETERS = "module var, report var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.report:module_id NE l.module:id }">
		<MvASSIGN NAME = "l.report:config:minresults"		VALUE = 0>
		<MvASSIGN NAME = "l.report:config:maxresults"		VALUE = 0>
		<MvASSIGN NAME = "l.report:config:display_topn"		VALUE = 10>
		<MvASSIGN NAME = "l.report:config:export_topn"		VALUE = 10>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'R',		l.provide_xml, 'SearchType',				l.report:config:searchtype, 'all,customer,order', 'all,customer,order' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O:nonneg', l.provide_xml, 'MinimumAverageResults',		l.report:config:minresults )												OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O:nonneg', l.provide_xml, 'MaximumAverageResults',		l.report:config:maxresults )												OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O:nonneg', l.provide_xml, 'Display_TopN',				l.report:config:display_topn )												OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O:nonneg', l.provide_xml, 'Export_TopN',				l.report:config:export_topn ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReportModule_Delete" PARAMETERS = "module var, report var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Helper Functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "SearchLog_Run" PARAMETERS = "report var, time_t_start, time_t_end" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.select_query, 'search,
																				 COUNT( DISTINCT srchses_id ) AS session_count,
																				 AVG( prodcount ) AS average_results' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.select_query, g.Store_Table_Prefix $ 'SearchLogEntries', '' ) }">

	<MvIF EXPR = "{ l.report:config:searchtype EQ 'order' }">			<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.select_query, 'order_id <> 0',	'' ) }">
	<MvELSEIF EXPR = "{ l.report:config:searchtype EQ 'customer' }">	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.select_query, 'cust_id <> 0',	'' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.select_query, 'dtstamp >= ? AND dtstamp <= ?', [ g.Module_Library_DB ].SQL_Query_Field( l.time_t_start ) $ ',' $ [ g.Module_Library_DB ].SQL_Query_Field( l.time_t_end ) ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_GROUP_BY( l.select_query, 'search' ) }">

	<MvASSIGN NAME = "l.search_sql" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.select_query, l.search_fields ) }">

	<MvIF EXPR = "{ l.report:config:minresults GT 0 }">
		<MvASSIGN NAME = "l.search_sql"		VALUE = "{ l.search_sql $ ' HAVING AVG( prodcount ) >= ?' }">
		<MvASSIGN NAME = "l.search_fields"	VALUE = "{ l.search_fields $ ',' $ [ g.Module_Library_DB ].SQL_Query_Field( l.report:config:minresults ) }">
	</MvIF>

	<MvIF EXPR = "{ l.report:config:maxresults GT 0 }">
		<MvIF EXPR = "{ l.report:config:minresults GT 0 }">
			<MvASSIGN NAME = "l.search_sql"	VALUE = "{ l.search_sql $ ' AND AVG( prodcount ) <= ?' }">
		<MvELSE>
			<MvASSIGN NAME = "l.search_sql"	VALUE = "{ l.search_sql $ ' HAVING AVG( prodcount ) <= ?' }">
		</MvIF>

		<MvASSIGN NAME = "l.search_fields"	VALUE = "{ l.search_fields $ ',' $ [ g.Module_Library_DB ].SQL_Query_Field( l.report:config:maxresults ) }">
	</MvIF>

	<MvASSIGN NAME = "l.search_sql" VALUE = "{ l.search_sql $ ' ORDER BY session_count DESC' }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'SearchLogEntries', l.search_sql, l.search_fields, 0, max( l.report:config:display_topn, l.report:config:export_topn ) ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-RPT-SLG-00002', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.report:last_state"					VALUE = "">
	<MvASSIGN NAME = "l.report:last_state:searches"			VALUE = "">
	<MvASSIGN NAME = "l.report:last_state:search_count"		VALUE = 0>

	<MvWHILE EXPR = "{ NOT SearchLogEntries.d.EOF }">
		<MvASSIGN NAME = "l.search" 						VALUE = "">
		<MvASSIGN NAME = "l.search:search" 					VALUE = "{ SearchLogEntries.d.search }">
		<MvASSIGN NAME = "l.search:session_count" 			VALUE = "{ SearchLogEntries.d.session_count }">
		<MvASSIGN NAME = "l.search:prodcount" 				VALUE = "{ SearchLogEntries.d.prodcount }">
		<MvASSIGN NAME = "l.search:average_results" 		VALUE = "{ SearchLogEntries.d.average_results }">

		<MvASSIGN NAME = "l.report:last_state:search_count"	VALUE = "{ miva_array_insert_var( l.report:last_state:searches, l.search, -1 ) }">

		<MvSKIP NAME = "Merchant" VIEW = "SearchLogEntries" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SearchLogEntries">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchLog_Add_Tabular_Column" PARAMETERS = "definition var, values var, label, member" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCEARRAY NAME = "l.column" VARIABLE = "l.definition">
		<MvMEMBER NAME = "columns">
		<MvDIMENSION INDEX = "{ miva_array_elements( l.definition:columns ) + 1 }">
	</MvREFERENCEARRAY>

	<MvASSIGN NAME = "l.column:type"	VALUE = "values">
	<MvASSIGN NAME = "l.column:start"	VALUE = 1>

	<MvREFERENCEARRAY NAME = "l.values_member" VARIABLE = "l.values">
		<MvMEMBER NAME = "{ l.member }">
	</MvREFERENCEARRAY>

	<MvASSIGN NAME = "l.values_member"	VALUE = "">
	<MvASSIGN NAME = "l.null"			VALUE = "{ miva_array_insert( l.values_member, l.label, -1 ) }">

	<MvREFERENCE NAME = "l.column:values"	VARIABLE = "l.values_member">
</MvFUNCTION>

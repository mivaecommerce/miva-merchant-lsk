<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-SSEL-
| Next Error Code: 1    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-stateselect">
	<MvASSIGN NAME = "l.module:name"		VALUE = "State Selector">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0902">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.00">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StateList_Load_All( l.settings ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ State_Select( l.param, miva_variable_value( 'g.' $ l.param ), l.settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "State_Select" PARAMETERS = "name, default, states var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvASSIGN NAME = "l.selected" 			VALUE = 0>
	<MvASSIGN NAME = "l.pos" 				VALUE = 1>

	<MvASSIGN NAME = "l.state_count"		VALUE = "{ miva_array_elements( l.states ) }">

	<MvASSIGN NAME = "l.first_state_name"	VALUE = "{ l.states[ l.pos ]:name }">
	<MvASSIGN NAME = "l.first_state_code"	VALUE = "{ l.states[ l.pos ]:code }">

	<MvIF EXPR = "{ l.state_count EQ 1 }">
		<select name="{ encodeentities( l.name ) }" id="{ encodeentities( l.name ) }" disabled><option value="" selected><MvEVAL EXPR = "{ encodeentities( l.first_state_name ) }"></option></select>
		<input type="hidden" name="{ encodeentities( l.name ) }" value="{ encodeentities( l.first_state_code ) }" />
		<MvASSIGN NAME = "l.selected" 			VALUE = 1>
	<MvELSE>
		<select name="{ encodeentities( l.name ) }" id="{ encodeentities( l.name ) }">

		<MvWHILE EXPR = "{ l.pos LE l.state_count }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( l.states[ l.pos ]:code, l.default, encodeentities( l.states[ l.pos ]:name ) ) }">
			
			<MvIF EXPR = "{ l.states[ l.pos ]:code EQ l.default }">
				<MvASSIGN NAME = "l.selected"	VALUE = 1>
			</MvIF>

			<MvASSIGN NAME = "l.pos"			VALUE = "{ l.pos + 1 }">
		</MvWHILE>

		</select>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.selected }">
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2025 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-CTTL-
| Next Error Code: 16   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-cattitle">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI Category Title">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.1301">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.60">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, vis_category, provision_store, fields_cat, skins">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: vis_category
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Category_Tabs" PARAMETERS = "module var, category var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "GT_CTGY/CMP-CSSUI-CATTITLE:Category Title">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Category_Content" PARAMETERS = "module var, tab, load_fields, category var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvASSIGN NAME = "l.can_modify"	VALUE = "{ [ g.Module_Admin ].CanI( 'CTGY', 0, 0, 1, 0 ) }">
	<MvASSIGN NAME = "l.can_add"	VALUE = "{ [ g.Module_Admin ].CanI( 'CTGY', 0, 1, 0, 0 ) }">

	<MvIF EXPR = "{ l.category:id }">
		<MvIF EXPR = "{ l.load_fields }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedPropertyAndVersion_Load_Category_Current( 'cssui_cattitle', l.category:id, l.managedproperty ) }">
				<MvASSIGN NAME = "g.Category_CSSUI_CategoryTitle_Image"	VALUE = "">
			<MvELSE>
				<MvASSIGN NAME = "g.Category_CSSUI_CategoryTitle_Image"	VALUE = "{ CSSUI_CatTitle_ManagedPropertyAndVersion_To_ImagePath( l.managedproperty ) }">
			</MvIF>
		</MvIF>
	<MvELSE>
		<MvIF EXPR = "{ l.load_fields }">
			<MvASSIGN NAME = "g.Category_CSSUI_CategoryTitle_Image"		VALUE = "">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, 'CMP-CSSUI-CATTITLE' ) }">
		<MvHIDE FIELDS = "g.Category_CSSUI_CategoryTitle_Image">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_CTGY', 'CMP-CSSUI-CATTITLE' ) }">
		<mm-grid columns="2">
			<mm-grid-row>
				<mm-grid-column>
					<mm-flex-item prompt="Title Image">
						<MvIF EXPR = "{ ( l.can_modify ) OR ( l.can_add AND NOT len( g.Category_CSSUI_CategoryTitle_Image ) ) }">
							<mm-input type="text" name="Category_CSSUI_CategoryTitle_Image" value="{ encodeentities( g.Category_CSSUI_CategoryTitle_Image ) }" input-class="mm_input_common whole_width">
								<mm-button text="Upload" button-class="mm10_button_style_alternative_1 small" onclick="MMImagePicker_SingleSelect( 'Category_CSSUI_CategoryTitle_Image', false );"></mm-button>
							</mm-input>
						<MvELSE>
							<mm-input type="text" name="Category_CSSUI_CategoryTitle_Image" value="{ encodeentities( g.Category_CSSUI_CategoryTitle_Image ) }" input-class="mm_input_common whole_width" disabled></mm-input>
						</MvIF>
					</mm-flex-item>
				</mm-grid-column>
			</mm-grid-row>
		</mm-grid>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Category_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Category_CSSUI_CategoryTitle_Image"	VALUE = "{ trim( g.Category_CSSUI_CategoryTitle_Image ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Category_Insert" PARAMETERS = "module var, category var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ CSSUI_CatTitle_InsertOrUpdate( l.category:id, g.Category_CSSUI_CategoryTitle_Image ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Category_Update" PARAMETERS = "module var, category var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ CSSUI_CatTitle_InsertOrUpdate( l.category:id, g.Category_CSSUI_CategoryTitle_Image ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Category_Delete" PARAMETERS = "module var, category var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: component
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Runtime_ManagedProperty_Load_Category( 'cssui_cattitle', l.all_settings:category:id, l.managedproperty ) }">
		<MvASSIGN NAME = "l.settings" VALUE = "{ l.managedproperty:settings }">

		<MvIF EXPR = "{ NOT ISNULL l.managedproperty:image:image }">	<MvASSIGN NAME = "l.settings:image" VALUE = "{ l.managedproperty:image:image }">
		<MvELSE>														<MvASSIGN NAME = "l.settings:image" VALUE = "{ l.managedproperty:settings:image }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: provision_store
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Provision_Store" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.child_xml" ARRAY = "l.provide_xml:children">
		<MvASSIGN NAME = "l.name"		VALUE = "{ tolower( l.child_xml:name ) }">

		<MvIF EXPR = "{ l.name EQ 'image' }">		<MvEVAL EXPR = "{ Module_Provision_Store_Image( l.module, l.child_xml ) }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Store_Image" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml, 'category_code', l.category_code ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Category_Load_Code( l.category_code, l.category ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load category \'' $ l.category_code $ '\'' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT CSSUI_CatTitle_InsertOrUpdate( l.category:id, trim( l.provide_xml:value ) ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: skins
|
</MvCOMMENT>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "Exports all available Category Title Images for categories">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<Module code="{ encodeentities( l.module:code ) }" feature="component">
			<MvFOREACH ITERATOR = "l.managedproperty" ARRAY = "l.managedproperties" COUNT = "{ [ g.Module_Feature_TUI_DB ].ManagedPropertyAndVersionList_Load_Type_Current( 'cssui_cattitle', l.managedproperties ) }">
				<MvIF EXPR = "{ [ g.Module_Library_DB ].Category_Load_ID( l.managedproperty:cat_id, l.category ) }">
					<Image category_code="{ encodeentities( l.category:code ) }"><MvEVAL EXPR = "{ miva_cdata_encode( CSSUI_CatTitle_ManagedPropertyAndVersion_To_ImagePath( l.managedproperty ) ) }"></Image>
				</MvIF>
			</MvFOREACH>
		</Module>
	</MvCAPTURE>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: fields_cat
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Category_Fields" PARAMETERS = "module var, fields var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.fields" INDEX = 1 MEMBER = "code"	VALUE = "category_title_image">
	<MvASSIGN NAME = "l.fields" INDEX = 1 MEMBER = "name"	VALUE = "Category Title Image">
	<MvASSIGN NAME = "l.fields" INDEX = 1 MEMBER = "public"	VALUE = 1>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Category_Field_Name" PARAMETERS = "module var, code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.code EQ 'category_title_image' }">
		<MvFUNCTIONRETURN VALUE = "Category Title Image">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Category_Field_Value" PARAMETERS = "module var, cat_id, code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.code EQ 'category_title_image' }">
		<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedPropertyAndVersion_Load_Category_Current( 'cssui_cattitle', l.cat_id, l.managedproperty ) }">
			<MvFUNCTIONRETURN VALUE = "{ CSSUI_CatTitle_ManagedPropertyAndVersion_To_ImagePath( l.managedproperty ) }">		
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Category_Set_Field" PARAMETERS = "module var, cat_id, code, value" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.code EQ 'category_title_image' }">
		<MvIF EXPR = "{ NOT CSSUI_CatTitle_InsertOrUpdate( l.cat_id, l.value ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Helper functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "CSSUI_CatTitle_Load" PARAMETERS = "cat_id, cssui_category var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedPropertyAndVersion_Load_Category_Current( 'cssui_cattitle', l.cat_id, l.managedproperty ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.cssui_category:cat_id"	VALUE = "{ l.managedproperty:cat_id }">
	<MvASSIGN NAME = "l.cssui_category:image"	VALUE = "{ CSSUI_CatTitle_ManagedPropertyAndVersion_To_ImagePath( l.managedproperty ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUI_CatTitle_InsertOrUpdate" PARAMETERS = "cat_id, image" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.image_id"	VALUE = 0>
	<MvASSIGN NAME = "l.settings"	VALUE = "">

	<MvIF EXPR = "{ NOT ISNULL l.image }">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].Image_FindOrInsert_ExistingFile( l.image, l.loaded_image ) }">	<MvASSIGN NAME = "l.image_id"		VALUE = "{ l.loaded_image:id }">
		<MvELSE>																								<MvASSIGN NAME = "l.settings:image"	VALUE = "{ l.image }">
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.settings"	VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_ManagedProperty_SettingsAndImage( l.settings, l.image_id ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedPropertyAndVersion_Load_Category_Current( 'cssui_cattitle', l.cat_id, l.managedproperty ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvCOMMENT>
		|
		| Do not create a property if the settings are empty
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ ( l.settings:image_id EQ 0 ) AND ( ISNULL l.settings:settings:image ) }">
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Category_ManagedProperty( 'cssui_cattitle', l.cat_id, 'nosync,notemplate', l.null, l.settings, l.managedproperty ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-CTTL-00014', 'CSSUI category title image for category ' $ l.cat_id $ ' created' ) }">

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvCOMMENT>
	|
	| Do not create a new version if none of the settings have changed
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ( l.managedproperty:version:image_id EQ l.settings:image_id ) AND ( l.managedproperty:version:settings:image EQ l.settings:settings:image ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedPropertyVersion( l.managedproperty, l.settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-CTTL-00015', 'CSSUI category title image for category ' $ l.cat_id $ ' updated' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUI_CatTitle_ManagedPropertyAndVersion_To_ImagePath" PARAMETERS = "managedproperty var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.managedproperty:version:image_id }">	<MvFUNCTIONRETURN VALUE = "{ l.managedproperty:version:image:image }">
	<MvELSE>												<MvFUNCTIONRETURN VALUE = "{ l.managedproperty:version:settings:image }">
	</MvIF>
</MvFUNCTION>

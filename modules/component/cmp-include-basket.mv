<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CMP-INC-BASK-
| Next Error Code: 6
|
</MvCOMMENT>

<MvCOMMENT>
|
| Feature component
|
</MvCOMMENT>

<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ BasketComponentModule_Tabs( l.module, l.item, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "BasketComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.tabs"		VALUE = "{ 'GT_PAGE/' $ l.item $ ':Basket Contents' }">

	<MvIF EXPR = "{ miva_array_elements( l.settings:imagetypes ) GT 0 }">
		<MvASSIGN NAME = "l.tabs"	VALUE = "{ l.tabs $ ',GT_PAGE/' $ l.item $ '_dimensions:Basket Contents Image Dimensions' }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.tabs }">
</MvFUNCTION>

<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ BasketComponentModule_Validate( l.module, l.item, l.field_prefix, l.fields, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "BasketComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.fields:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.fields:b_width"				VALUE = "{ trim( l.fields:b_width ) }">
	<MvASSIGN NAME = "l.fields:b_height"			VALUE = "{ trim( l.fields:b_height ) }">
	<MvASSIGN NAME = "l.fields:groups"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:groups ) }">
	<MvASSIGN NAME = "l.fields:lineitemprice"		VALUE = "{ trim( l.fields:lineitemprice ) }">
	<MvASSIGN NAME = "l.fields:lineitemdiscounts"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:lineitemdiscounts ) }">
	<MvASSIGN NAME = "l.fields:lineitemcoupons"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:lineitemcoupons ) }">
	<MvASSIGN NAME = "l.fields:applycoupons"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:applycoupons ) }">
	<MvASSIGN NAME = "l.fields:redeemgiftcerts"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:redeemgiftcerts ) }">
	<MvASSIGN NAME = "l.fields:button_wish"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:button_wish ) }">
	<MvASSIGN NAME = "l.fields:shipping"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:shipping ) }">
	<MvASSIGN NAME = "l.fields:handling"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:handling ) }">
	<MvASSIGN NAME = "l.fields:salestax"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:salestax ) }">
	<MvASSIGN NAME = "l.fields:discounts"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:discounts ) }">
	<MvASSIGN NAME = "l.fields:splitpayments"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:splitpayments ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:b_width ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item $ '_dimensions', l.field_prefix $ 'b_width', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:b_height ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item $ '_dimensions', l.field_prefix $ 'b_height', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ l.fields:advanced }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.fields:template_code ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ BasketComponentModule_Update( l.module, l.item, l.field_prefix, l.fields, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "BasketComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.settings:advanced"				VALUE = "{ l.fields:advanced }">

	<MvASSIGN NAME = "l.selectedfield_count"			VALUE = "{ [ g.Module_Library_Utilities ].CustomFieldSelect_Selected( l.fields:fields_custom, l.settings:fields_custom ) }">

	<MvASSIGN NAME = "l.settings:constrain"				VALUE = "{ l.fields:constrain }">
	<MvASSIGN NAME = "l.settings:b_width"				VALUE = "{ l.fields:b_width }">
	<MvASSIGN NAME = "l.settings:b_height"				VALUE = "{ l.fields:b_height }">

	<MvIF EXPR = "{ l.fields:advanced }">
		<MvASSIGN NAME = "l.imagetype_count"			VALUE = "{ [ g.Module_Library_Utilities ].ImageTypeSelect_Selected( l.fields:imagetypes, l.settings:imagetypes ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:imagetypes"		VALUE = "">
		<MvIF EXPR = "{ NOT ISNULL l.fields:image }">
			<MvIF EXPR = "{ [ g.Module_Library_DB ].ImageType_Load_Code( l.fields:image, l.imagetype ) }">
				<MvASSIGNARRAY NAME = "l.settings"		VALUE = "{ l.imagetype:code }">
					<MvMEMBER NAME = "imagetypes">
					<MvDIMENSION INDEX = 1>
				</MvASSIGNARRAY>
			</MvIF>
		</MvIF>

		<MvASSIGN NAME = "l.settings:groups"			VALUE = "{ l.fields:groups }">
		<MvASSIGN NAME = "l.settings:lineitemprice"		VALUE = "{ l.fields:lineitemprice }">
		<MvASSIGN NAME = "l.settings:lineitemdiscounts"	VALUE = "{ l.fields:lineitemdiscounts }">
		<MvASSIGN NAME = "l.settings:lineitemcoupons"	VALUE = "{ l.fields:lineitemcoupons }">
		<MvASSIGN NAME = "l.settings:applycoupons"		VALUE = "{ l.fields:applycoupons }">
		<MvASSIGN NAME = "l.settings:redeemgiftcerts"	VALUE = "{ l.fields:redeemgiftcerts }">
		<MvASSIGN NAME = "l.settings:button_wish"		VALUE = "{ l.fields:button_wish }">
	</MvIF>

	<MvASSIGN NAME = "l.settings:shipping"				VALUE = "{ l.fields:shipping }">
	<MvASSIGN NAME = "l.settings:handling"				VALUE = "{ l.fields:handling }">
	<MvASSIGN NAME = "l.settings:salestax"				VALUE = "{ l.fields:salestax }">
	<MvASSIGN NAME = "l.settings:discounts"				VALUE = "{ l.fields:discounts }">
	<MvASSIGN NAME = "l.settings:splitpayments"			VALUE = "{ l.fields:splitpayments }">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CMP-INC-BASK-00001', l.module:name $ ' \'' $ l.item $ '\' settings updated' ) }">

	<MvIF EXPR = "{ NOT l.fields:advanced }">
		<MvEVAL EXPR = "{ BasketComponentModule_Generate_Code( l.settings, l.fields:template_code ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, '', l.fields:template_code, l.settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Update_Requires_Version" PARAMETERS = "module var, page var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>
</MvIFNDEF>

<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_PostUpdate" PARAMETERS = "module var, page var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>
</MvIFNDEF>

<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ BasketComponentModule_Content( l.module, l.item, l.tab, l.load_fields, l.field_prefix, l.fields, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "BasketComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvCOMMENT>
	|
	| Load all image types
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.imagetype_count"	VALUE = "{ [ g.Module_Library_DB ].ImageTypeList_Load_All( l.imagetypes ) }">

	<MvIF EXPR = "{ l.load_fields }">
		<MvIF EXPR = "{ ISNULL l.settings:constrain }">
			<MvASSIGN NAME = "l.fields:constrain"				VALUE = "B">
		<MvELSE>
			<MvASSIGN NAME = "l.fields:constrain"				VALUE = "{ l.settings:constrain }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:b_width }">
			<MvASSIGN NAME = "l.fields:b_width"					VALUE = 75>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:b_width"					VALUE = "{ l.settings:b_width }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:b_height }">
			<MvASSIGN NAME = "l.fields:b_height"				VALUE = 75>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:b_height"				VALUE = "{ l.settings:b_height }">
		</MvIF>

		<MvASSIGN NAME = "l.fields:groups"						VALUE = "{ l.settings:groups }">
		<MvASSIGN NAME = "l.fields:lineitemprice"				VALUE = "{ l.settings:lineitemprice }">
		<MvASSIGN NAME = "l.fields:lineitemdiscounts"			VALUE = "{ l.settings:lineitemdiscounts }">

		<MvASSIGN NAME = "l.fields:lineitemcoupons"				VALUE = "{ l.settings:lineitemcoupons }">
		<MvASSIGN NAME = "l.fields:applycoupons"				VALUE = "{ l.settings:applycoupons }">
		<MvASSIGN NAME = "l.fields:redeemgiftcerts"				VALUE = "{ l.settings:redeemgiftcerts }">
		<MvASSIGN NAME = "l.fields:button_wish"					VALUE = "{ l.settings:button_wish }">

		<MvASSIGN NAME = "l.fields:shipping"					VALUE = "{ l.settings:shipping }">
		<MvASSIGN NAME = "l.fields:handling"					VALUE = "{ l.settings:handling }">
		<MvASSIGN NAME = "l.fields:salestax"					VALUE = "{ l.settings:salestax }">
		<MvASSIGN NAME = "l.fields:discounts"					VALUE = "{ ( ISNULL l.settings:discounts ) OR l.settings:discounts }">
		<MvASSIGN NAME = "l.fields:splitpayments"				VALUE = "{ l.settings:splitpayments }">

		<MvASSIGN NAME = "l.fields:advanced"					VALUE = "{ ( ISNULL l.settings:advanced ) OR l.settings:advanced }">
		<MvASSIGN NAME = "l.fields:template_code"				VALUE = "{ l.templateversion:source }">
		<MvASSIGN NAME = "l.fields:template_filename"			VALUE = "{ l.settings:template_filename }">

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].InitializeCustomFieldSelect( l.settings:fields_custom, l.fields:fields_custom ) }">

		<MvASSIGN NAME = "l.fields:imagetypes"					VALUE = "{ l.settings:imagetypes }">
		<MvASSIGN NAME = "l.fields:image"						VALUE = "{ l.settings:imagetypes[ 1 ] }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'advanced,template_code,template_filename,fields_custom,imagetypes,image,groups,lineitemprice,lineitemdiscounts,lineitemcoupons,applycoupons,redeemgiftcerts,button_wish,shipping,handling,salestax,sales,coupons,discounts,splitpayments' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_filename' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<MvIF EXPR = "{ NOT l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_code' ) }">
		<MvELSE>
			<tr><td colspan="2" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( l.field_prefix $ 'template_code', l.fields:template_code, 20, 100, l.templateversion:templ_id ) }">
			</td></tr>

			<tr><td colspan="2">
				&nbsp;
			</td></tr>
		</MvIF>

		<MvASSIGN NAME = "l.customfield_count"					VALUE = "{ [ g.Module_Library_Utilities ].ProductCustomFieldList_Load( l.customfields ) }">

		<MvIF EXPR = "{ l.customfield_count }">
			<tr><td align="left" valign="top" nowrap>
				<b>Custom Fields:</b>
			</td><td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCustomFieldSelect( l.field_prefix $ 'fields_custom', l.fields:fields_custom, l.customfields, l.customfield_count ) }">
			</td></tr>
		</MvIF>

		<MvIF EXPR = "{ l.imagetype_count EQ 0 }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'image,imagetypes' ) }">
		<MvELSE>
			<MvIF EXPR = "{ l.fields:advanced }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'image' ) }">

				<tr><td align="left" valign="top" nowrap>
					<b>Product Images:</b>
				</td><td align="left" width="100%">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawImageTypeSelect( l.field_prefix $ 'imagetypes', l.fields:imagetypes, l.imagetypes, l.imagetype_count ) }">
				</td></tr>
			<MvELSE>
				<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'imagetypes' ) }">

				<tr><td align="left" valign="top" nowrap>
					<b>Image:</b>
				</td><td align="left" width="100%">
					<select name="{ encodeentities( l.field_prefix ) $ 'image' }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '', l.fields:image, 'None' ) }">

						<MvFOREACH ITERATOR = "l.imagetype" ARRAY = "l.imagetypes" COUNT = "{ l.imagetype_count }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( l.imagetype:code, l.fields:image, encodeentities( l.imagetype:descrip ) ) }">
						</MvFOREACH>
					</select>
				</td></tr>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'groups,lineitemdiscounts,lineitemprice,lineitemcoupons,applycoupons,redeemgiftcerts,button_wish' ) }">
		<MvELSE>
			<tr><td align="left" valign="top" nowrap>
				<b>Line Item Discounts:</b>
			</td><td align="left" width="100%">
				<select name="{ encodeentities( l.field_prefix ) $ 'groups' }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0', l.fields:groups, 'Display Discounted Line Items Separately' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '1', l.fields:groups, 'Group Discounted Line Items by Product/Attributes' ) }">
				</select>

				<br /><label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:lineitemdiscounts, l.field_prefix $ 'lineitemdiscounts',	1, 'Display Line Item Discount Details' ) }"></label>
			</td></tr>

			<tr><td align="left" valign="top" nowrap>
				<b>Additional Price Display:</b>
			</td><td align="left" width="100%">
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'lineitemprice', '',		l.fields:lineitemprice,	'None' ) }"></label><br />
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'lineitemprice', 'retail',	l.fields:lineitemprice, 'Retail Price' ) }"></label><br />
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'lineitemprice', 'base',	l.fields:lineitemprice, 'Base Price (includes any legacy price group discounts but does not reflect sales, coupons or discounts applied in the basket)' ) }"></label>
			</td></tr>

			<tr><td align="left" valign="top" nowrap>
				<b>Coupons:</b>
			</td><td align="left" width="100%">
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:lineitemcoupons,	l.field_prefix $ 'lineitemcoupons',	1, 'Display Line Items for Applied Coupons' ) }"></label><br />
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:applycoupons,	l.field_prefix $ 'applycoupons',	1, 'Allow Coupons to be Applied' ) }"></label><br />
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:redeemgiftcerts,	l.field_prefix $ 'redeemgiftcerts',	1, 'Allow Gift Certificates to be Redeemed' ) }"></label>
			</td></tr>

			<tr><td align="left" valign="top" nowrap>
				<b>Buttons:</b>
			</td><td align="left" width="100%">
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:button_wish, l.field_prefix $ 'button_wish', 1, 'Add To Wish List' ) }"></label><br />
			</td></tr>
		</MvIF>

		<tr><td align="left" valign="top" nowrap>
			<b>Basket Charges/Discounts:</b>
		</td><td align="left" width="100%">
			<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:shipping,	l.field_prefix $ 'shipping',	1, 'Display Shipping Charges' ) }"></label><br />
			<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:handling,	l.field_prefix $ 'handling',	1, 'Display Handling Charge' ) }"></label><br />
			<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:salestax,	l.field_prefix $ 'salestax',	1, 'Display Sales Tax' ) }"></label><br />
			<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:discounts,	l.field_prefix $ 'discounts',	1, 'Display Other Basket Level Discounts' ) }"></label>
		</td></tr>

		<MvIF EXPR = "{ l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'splitpayments' ) }">
		<MvELSE>
			<tr><td align="left" valign="top" nowrap>
				<b>Payments:</b>
			</td><td align="left" width="100%">
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:splitpayments,	l.field_prefix $ 'splitpayments',	1, 'Display Split Payment Transactions and Remaining Total' ) }"></label>
			</td></tr>
		</MvIF>

		<tr><td colspan="2">&nbsp;</td></tr>

		<tr><td>
			&nbsp;
		</td><td width="100%">
			<input type="hidden" name="{ encodeentities( l.field_prefix ) $ 'advanced' }" value="{ encodeentities( l.fields:advanced ) }">
			<MvIF EXPR = "{ l.fields:advanced }">
				<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 0 ); return false;' }">Point + Click Mode</a>
			<MvELSE>
				<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 1 ); return false;' }">Advanced Mode</a>
			</MvIF>
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, l.item $ '_dimensions' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'constrain,b_width,b_height' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item $ '_dimensions' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<tr>
		<td align="left" valign="top" nowrap><b>Product Image:</b></td>
		<td align="left" width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'constrain', 'N', l.fields:constrain, 'No constraints' ) }"><br>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'constrain', 'B', l.fields:constrain, 'Resize to fit within bounding box:' ) }">
			<input type="text" name="{ encodeentities( l.field_prefix ) $ 'b_width' }" value="{ encodeentities( l.fields:b_width ) }" style="text-align:right;" size="5" /> x 
			<input type="text" name="{ encodeentities( l.field_prefix ) $ 'b_height' }" value="{ encodeentities( l.fields:b_height ) }" style="text-align:right;" size="5" /> pixels
		</td>
		</tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFDEF NAME = "BASKET_COMPONENT_DEFAULTS">
<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ BasketComponentModule_Defaults( l.module, l.item, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "BasketComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:advanced"			VALUE = 0>
	<MvASSIGN NAME = "l.settings:fields_custom"		VALUE = "">
	<MvASSIGN NAME = "l.settings:imagetypes"		VALUE = "">
	<MvASSIGN NAME = "l.settings:constrain"			VALUE = "B">
	<MvASSIGN NAME = "l.settings:b_width"			VALUE = 75>
	<MvASSIGN NAME = "l.settings:b_height"			VALUE = 75>
	<MvASSIGN NAME = "l.settings:groups"			VALUE = 1>
	<MvASSIGN NAME = "l.settings:lineitemprice"		VALUE = "base">
	<MvASSIGN NAME = "l.settings:lineitemdiscounts"	VALUE = 1>
	<MvASSIGN NAME = "l.settings:lineitemcoupons"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:applycoupons"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:redeemgiftcerts"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:button_wish"		VALUE = 1>
	<MvASSIGN NAME = "l.settings:shipping"			VALUE = 0>
	<MvASSIGN NAME = "l.settings:handling"			VALUE = 0>
	<MvASSIGN NAME = "l.settings:salestax"			VALUE = 0>
	<MvASSIGN NAME = "l.settings:discounts"			VALUE = 1>
	<MvASSIGN NAME = "l.settings:splitpayments"		VALUE = 0>
</MvFUNCTION>
</MvIFDEF>

<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ BasketComponentModule_Page_Assign( l.module, l.page, l.item, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "BasketComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ BasketComponentModule_Generate_Code( l.settings, l.template_source ) }">

	<MvASSIGN NAME = "l.settings:template_filename"	VALUE = "{ tolower( l.page:code ) $ '-' $ tolower( l.item ) $ '.mvc' }">

	<MvIF EXPR = "{ l.page:admin }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Unsynced_ManagedTemplate_NoDuplicates( l.template_source, l.settings, l.settings:template_filename ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.template_source, l.settings, l.settings:template_filename ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'buttons' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ BasketComponentModule_Page_Unassign( l.module, l.page, l.item, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "BasketComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:template_filename ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ BasketComponentModule_Page_Copy( l.module, l.item, l.source_branch, l.source_page, l.source_settings, l.dest_branch, l.dest_page, l.dest_settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "BasketComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:template_filename, l.source_templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:template_filename, l.dest_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.dest_settings"						VALUE = "{ l.source_settings }">
	<MvASSIGN NAME = "l.dest_settings:template_filename"	VALUE = "{ l.dest_managedtemplate:version:settings:template_filename }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_managedtemplate, '', l.source_templateversion:source, l.dest_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.source_branch:id EQ l.dest_branch:id }">	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CMP-INC-BASK-00004', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' on branch \'' $ l.dest_branch:name $ '\'' ) }">
	<MvELSE>													<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CMP-INC-BASK-00005', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' from branch \'' $ l.source_branch:name $ '\' to branch \'' $ l.dest_branch:name $ '\'' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "BASKET_COMPONENT_NO_INITIALIZE">
<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ BasketComponentModule_Initialize( l.module, l.item, l.all_settings, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "BasketComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:splitpayments }">
		<MvASSIGN NAME = "l.null"						VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Initialize_Item( l.all_settings, 'splitpayment' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.all_settings:basket:total"		VALUE = 0.00>
	<MvASSIGN NAME = "l.basketitem_count"				VALUE = "{ [ g.Module_Library_DB ].BasketItemList_Load_Basket( g.Basket:basket_id, l.all_settings:basket:items ) }">

	<MvIF EXPR = "{ l.basketitem_count EQ 0 }">
		<MvASSIGN NAME = "l.all_settings:basket:empty"	VALUE = 1>
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.coupon_count"					VALUE = "{ [ g.Module_Feature_PGR_DB ].CouponList_Load_Basket( g.Basket:basket_id, l.all_settings:basket:coupons ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_CustomField_Lookup( l.all_settings, l.settings:fields_custom ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.basketitem" ARRAY = "l.all_settings:basket:items" COUNT = "{ l.basketitem_count }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_BasketItem_WithParts( l.basketitem ) OR
						NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_CustomFields( l.basketitem:product_id, l.basketitem, l.all_settings, l.settings:fields_custom ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_ImageTypes( l.basketitem:product_id, l.basketitem:variant_id, l.basketitem, l.all_settings, l.settings:imagetypes, l.settings:constrain, l.settings:b_width, l.settings:b_height ) }">

		<MvIF EXPR = "{ NOT l.basketitem:product:id }">	<MvASSIGN NAME = "l.basketitem:link" VALUE = "">
		<MvELSE>										<MvASSIGN NAME = "l.basketitem:link" VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Product_URL( l.basketitem:product, l.null ) }">
		</MvIF>

		<MvASSIGN NAME = "l.all_settings:basket:total"	VALUE = "{ l.all_settings:basket:total + l.basketitem:total }">
	</MvFOREACH>

	<MvASSIGN NAME = "l.basketgroup_count"				VALUE = "{ [ g.Module_Feature_TUI_UT ].CommonComponentFields_Merge_BasketOrOrderItem_Groups( l.all_settings:basket:items, l.basketitem_count, l.all_settings:basket:groups ) }">
	<MvASSIGN NAME = "l.output_charge_count"			VALUE = 0>

	<MvFOREACH ITERATOR = "l.basketcharge" ARRAY = "l.basketcharges" COUNT = "{ [ g.Module_Library_DB ].BasketChargeList_Load_Basket( g.Basket:basket_id, l.basketcharges ) }">
		<MvIF EXPR = "{ ( l.basketcharge:type EQ 'SHIPPING' ) AND ( NOT l.settings:shipping ) }">												<MvFOREACHCONTINUE>
		<MvELSEIF EXPR = "{ ( l.basketcharge:type EQ 'HANDLING' ) AND ( NOT l.settings:handling ) }">											<MvFOREACHCONTINUE>
		<MvELSEIF EXPR = "{ ( l.basketcharge:type EQ 'TAX' ) AND ( NOT l.settings:salestax ) }">												<MvFOREACHCONTINUE>
		<MvELSEIF EXPR = "{ ( l.basketcharge:type EQ 'DISCOUNT' ) AND ( NOT ISNULL l.settings:discounts ) AND ( NOT l.settings:discounts ) }">	<MvFOREACHCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.output_charge_count"												VALUE = "{ l.output_charge_count + 1 }">
		<MvASSIGN NAME = "l.all_settings:basket:charges" INDEX = "{ l.output_charge_count }"	VALUE = "{ l.basketcharge }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_BasketCharge( l.all_settings:basket:charges[ l.output_charge_count ] ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvCOMMENT>
		|
		| This version of the function rounds of each basket charge to 2 decimal places, before adding it to the total in order to
		| avoid discrpeancies between the invoice line items and the invoice total.
		| See GEM#M5-756.
		|
		</MvCOMMENT>

		<MvASSIGN NAME = "l.all_settings:basket:total"				VALUE = "{ l.all_settings:basket:total + ( l.basketcharge:amount ROUND 2 ) }">
	</MvFOREACH>

	<MvASSIGN NAME = "l.all_settings:basket:formatted_total"		VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.all_settings:basket:total ) }">
	<MvASSIGN NAME = "l.all_settings:basket:total_weight" 			VALUE = "{ [ g.Module_Library_DB ].Basket_Weight( g.Basket:basket_id ) }">
	<MvASSIGN NAME = "l.all_settings:basket:formatted_total_weight"	VALUE = "{ [ g.Module_Library_Utilities ].Format_Weight( l.all_settings:basket:total_weight ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>
</MvIFNDEF>

<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ BasketComponentModule_Prerender( l.module, l.item, l.all_settings, l.settings, l.param ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "BasketComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ BasketComponentModule_Render_Head( l.module, l.item, l.all_settings, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "BasketComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ BasketComponentModule_Render_Start( l.module, l.item, l.all_settings, l.settings, l.param ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "BasketComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:template_filename, l.all_settings ) }">
</MvFUNCTION>

<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ BasketComponentModule_Render_End( l.module, l.item, l.all_settings, l.settings, l.param ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "BasketComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: component_prov
|
</MvCOMMENT>

<MvIFNDEF NAME = "BASKET_COMPONENT_NO_PROVISION">
<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ BasketComponentModule_Provision( l.module, l.provide_xml, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>
</MvIFNDEF>

<MvFUNCTION NAME = "BasketComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean( 'O', l.provide_xml, 'DisplayShipping',			l.settings:shipping )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean( 'O', l.provide_xml, 'DisplayHandling',			l.settings:handling )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean( 'O', l.provide_xml, 'DisplaySalesTax',			l.settings:salestax )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean( 'O', l.provide_xml, 'DisplayDiscounts',		l.settings:discounts )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean( 'O', l.provide_xml, 'DisplaySplitPayments',	l.settings:splitpayments ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Fields' ) }">
		<MvIF EXPR = "{ NOT BasketComponentModule_Provision_Fields( l.module, l.provide_xml:tags:Fields[ 1 ], l.settings ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'ImageTypes' ) }">
		<MvIF EXPR = "{ NOT BasketComponentModule_Provision_ImageTypes( l.module, l.provide_xml:tags:ImageTypes[ 1 ], l.settings ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to provision multiple image types' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'ImageDimensions' ) }">
		<MvASSIGN NAME = "l.dimensions" VALUE = "{ l.provide_xml:tags:ImageDimensions[ 1 ] }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Boolean( l.dimensions, 'constrain', l.constrain ) }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvIF EXPR = "{ NOT l.constrain }">
			<MvASSIGN NAME = "l.settings:constrain" VALUE = "N">
		<MvELSE>
			<MvASSIGN NAME = "l.settings:constrain" VALUE = "B">

			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer( 'R', l.dimensions, 'Width',	l.settings:b_width ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer( 'R', l.dimensions, 'Height',	l.settings:b_height ) }">
				<MvFUNCTIONRETURN>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml, 'LineItemDiscounts',				l.settings:groups,				'Grouped,Separate',	'1,0' )				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml, 'DisplayLineItemDiscountDetails',	l.settings:lineitemdiscounts )											OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml, 'AdditionalPriceDisplay',			l.settings:lineitemprice,		'Retail,Base,None',	'retail,base,' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml, 'DisplayCouponLineItems',			l.settings:lineitemcoupons )											OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml, 'AllowCouponApplication',			l.settings:applycoupons )												OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml, 'AllowGiftCertificateRedemption',	l.settings:redeemgiftcerts ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Buttons' ) }">
		<MvEVAL EXPR = "{ BasketComponentModule_Provision_Buttons( l.module, l.provide_xml:tags:Buttons[ 1 ], l.settings ) }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'R', l.provide_xml, 'Template', l.template ) }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvASSIGN NAME = "l.settings:advanced"		VALUE = 1>
	<MvELSE>
		<MvASSIGN NAME = "l.settings:advanced"		VALUE = 0>

		<MvEVAL EXPR = "{ BasketComponentModule_Generate_Code( l.settings, l.template ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml, 'Notes', l.notes ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.template, l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketComponentModule_Provision_Buttons" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:button_wish" VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'AddToWishList' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketComponentModule_Provision_Fields" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:fields_custom"	VALUE = "">
	<MvASSIGN NAME = "l.customfield_count"		VALUE = 0>

	<MvASSIGN NAME = "l.child_pos"		VALUE = 1>
	<MvASSIGN NAME = "l.child_count"	VALUE = "{ miva_array_elements( l.provide_xml:children ) }">
	<MvWHILE EXPR = "{ l.child_pos LE l.child_count }">
		<MvASSIGN NAME = "l.name"		VALUE = "{ tolower( l.provide_xml:children[ l.child_pos ]:name ) }">

		<MvIF EXPR = "{ l.name EQ 'custom' }">
			<MvASSIGN NAME = "l.customfield_count"															VALUE = "{ l.customfield_count + 1 }">

			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml:children[ l.child_pos ], 'module',	l.settings:fields_custom[ l.customfield_count ]:module ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml:children[ l.child_pos ], 'code',		l.settings:fields_custom[ l.customfield_count ]:code ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvASSIGN NAME = "l.child_pos"	VALUE = "{ l.child_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketComponentModule_Provision_ImageTypes" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:imagetypes" VALUE = "">

	<MvASSIGN NAME = "l.type_pos"		VALUE = 1>
	<MvASSIGN NAME = "l.type_found"		VALUE = 1>
	<MvASSIGN NAME = "l.type_count"		VALUE = "{ miva_array_elements( l.provide_xml:children ) }">

	<MvWHILE EXPR = "{ l.type_pos LE l.type_count }">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].ImageType_Load_Code( l.provide_xml:children[ l.type_pos ]:value, l.imagetype ) }">
			<MvASSIGN NAME = "l.settings:imagetypes" INDEX = "{ l.type_found }" VALUE = "{ l.provide_xml:children[ l.type_pos ]:value }">
			<MvASSIGN NAME = "l.type_found"										VALUE = "{ l.type_found + 1 }">
		</MvIF>

		<MvASSIGN NAME = "l.type_pos" VALUE = "{ l.type_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: skins
|
</MvCOMMENT>

<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ BasketSkinsComponentModule_Description( l.module, l.item ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "BasketSkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pages" VALUE = "{ [ g.Module_Feature_TUI_DB ].Item_Load_PageList( l.item ) }">

	<MvFUNCTIONRETURN VALUE = "{ 'Exports Basket Contents template from pages: ' $ l.pages }">
</MvFUNCTION>

<MvIFNDEF NAME = "BASKET_COMPONENT_NO_EXPORT">
<MvIFNDEF NAME = "BASKET_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ BasketSkinsComponentModule_Export_Item( l.module, l.item, l.output ) }">
</MvFUNCTION>
</MvIFNDEF>
</MvIFNDEF>

<MvFUNCTION NAME = "BasketSkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item_Runtime( l.item:id, l.pages ) }">
			<MvREFERENCEARRAY NAME = "l.settings" VARIABLE = "l.page:settings">
				<MvMEMBER NAME = "{ l.item:code }">
			</MvREFERENCEARRAY>

			<Page_Update code="{ l.page:code }">
				<Item code="{ l.item:code }">
					<MvEVAL EXPR = "{ BasketSkinsComponentModule_Export_ItemPageSettings( l.module, l.item, l.page, l.settings, l.page_output ) }">
					<MvEVAL EXPR = "{ l.page_output }">
				</Item>
			</Page_Update>
		</MvFOREACH>
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketSkinsComponentModule_Export_ItemPageSettings" PARAMETERS = "module var, item, page var, settings var, output var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<DisplayShipping><MvIF EXPR = "{ l.settings:shipping }">Yes<MvELSE>No</MvIF></DisplayShipping>
		<DisplayHandling><MvIF EXPR = "{ l.settings:handling }">Yes<MvELSE>No</MvIF></DisplayHandling>
		<DisplaySalesTax><MvIF EXPR = "{ l.settings:salestax }">Yes<MvELSE>No</MvIF></DisplaySalesTax>
		<DisplayDiscounts><MvIF EXPR = "{ l.settings:discounts }">Yes<MvELSE>No</MvIF></DisplayDiscounts>
		<DisplaySplitPayments><MvIF EXPR = "{ l.settings:splitpayments }">Yes<MvELSE>No</MvIF></DisplaySplitPayments>

		<Fields>
			<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:fields_custom">
				<Custom module="{ l.customfield:module }" code="{ l.customfield:code }" />
			</MvFOREACH>
		</Fields>

		<ImageTypes>
			<MvFOREACH ITERATOR = "l.imagetype" ARRAY = "l.settings:imagetypes">
				<ImageType><MvEVAL EXPR = "{ miva_cdata_encode( l.imagetype ) }"></ImageType>
			</MvFOREACH>
		</ImageTypes>

		<MvIF EXPR = "{ l.settings:constrain NE 'B' }">
			<ImageDimensions constrain="No" />
		<MvELSE>
			<ImageDimensions constrain="Yes">
				<Width><MvEVAL EXPR = "{ int( l.settings:b_width ) }"></Width>
				<Height><MvEVAL EXPR = "{ int( l.settings:b_height ) }"></Height>
			</ImageDimensions>
		</MvIF>

		<MvIF EXPR = "{ l.settings:groups }">
			<LineItemDiscounts>Grouped</LineItemDiscounts>
		<MvELSE>
			<LineItemDiscounts>Separate</LineItemDiscounts>
		</MvIF>

		<DisplayLineItemDiscountDetails><MvIF EXPR = "{ l.settings:lineitemdiscounts }">Yes<MvELSE>No</MvIF></DisplayLineItemDiscountDetails>

		<MvIF EXPR = "{ l.settings:lineitemprice EQ 'retail' }">
			<AdditionalPriceDisplay>Retail</AdditionalPriceDisplay>
		<MvELSEIF EXPR = "{ l.settings:lineitemprice EQ 'base' }">
			<AdditionalPriceDisplay>Base</AdditionalPriceDisplay>
		<MvELSE>
			<AdditionalPriceDisplay>None</AdditionalPriceDisplay>
		</MvIF>

		<DisplayCouponLineItems><MvIF EXPR = "{ l.settings:lineitemcoupons }">Yes<MvELSE>No</MvIF></DisplayCouponLineItems>
		<AllowCouponApplication><MvIF EXPR = "{ l.settings:applycoupons }">Yes<MvELSE>No</MvIF></AllowCouponApplication>
		<AllowGiftCertificateRedemption><MvIF EXPR = "{ l.settings:redeemgiftcerts }">Yes<MvELSE>No</MvIF></AllowGiftCertificateRedemption>

		<Buttons>
			<MvIF EXPR = "{ l.settings:button_wish }"><AddToWishList /></MvIF>
		</Buttons>

		<MvIF EXPR = "{ l.settings:advanced }">
			<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
				<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.templateversion:source ) }"></Template>
			</MvIF>
		</MvIF>

		<Notes>#Set_Current_Time#</Notes>
	</MvCAPTURE>
</MvFUNCTION>

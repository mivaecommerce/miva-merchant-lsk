<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2020 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CMP-SOF-
| Next Error Code: 1    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-mv-adminorderfields">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Administrative Order Fields">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.00">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT l.all_settings:page:admin }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.orderorder_count"		VALUE = "{ miva_array_elements( l.settings:orders ) }">

	<MvIF EXPR = "{ l.orderorder_count GT 0 }">
		<MvASSIGN NAME = "l.orderorder_pos"		VALUE = 1>
		<MvWHILE EXPR = "{ l.orderorder_pos LE l.orderorder_count }">
			<MvIF EXPR = "{ NOT AdminOrderFields_Initialize_Order( l.all_settings:page, l.settings:orders[ l.orderorder_pos ] ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.orderorder_pos"	VALUE = "{ l.orderorder_pos + 1 }">
		</MvWHILE>
	<MvELSEIF EXPR = "{ g.Order_ID }">
		<MvFUNCTIONRETURN VALUE = "{ AdminOrderFields_Load_Order( l.all_settings:page, g.Order_ID, l.settings ) }">
	<MvELSEIF EXPR = "{ len( g.Order_IDs ) }">
		<MvASSIGN NAME = "l.order_pos"				VALUE = 1>
		<MvASSIGN NAME = "l.order_id"				VALUE = "{ gettoken( g.Order_IDs, '|', l.order_pos ) }">

		<MvWHILE EXPR = "{ l.order_id }">
			<MvIF EXPR = "{ NOT AdminOrderFields_Load_Order( l.all_settings:page, l.order_id, l.settings:orders[ l.order_pos ] ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.order_pos"			VALUE = "{ l.order_pos + 1 }">
			<MvASSIGN NAME = "l.order_id"			VALUE = "{ gettoken( g.Order_IDs, '|', l.order_pos ) }">
		</MvWHILE>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "AdminOrderFields_Load_Order" PARAMETERS = "page var, order_id, order var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Order_Load_ID( l.order_id, l.order ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ AdminOrderFields_Initialize_Order( l.page, l.order ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "AdminOrderFields_Initialize_Order" PARAMETERS = "page var, order var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Order( l.order ) }">	<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ NOT AdminOrderFields_Initialize_Shipping( l.page, l.order ) }">							<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ NOT AdminOrderFields_Initialize_Payments( l.page, l.order ) }">							<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ NOT AdminOrderFields_Initialize_OrderItems( l.page, l.order ) }">						<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ NOT AdminOrderFields_Initialize_OrderCharges( l.page, l.order ) }">						<MvFUNCTIONRETURN VALUE = 0>	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AdminOrderFields_Initialize_Shipping" PARAMETERS = "page var, order var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.order:ship_id EQ 0 }">
		<MvASSIGN NAME = "l.order:ship_method"	VALUE = "Unknown">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.module" VARIABLE = "g.Session:cache:cmp_mv_adminorderfields:modules">
		<MvDIMENSION INDEX = "{ l.order:ship_id }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ l.module:loaded EQ 0 }">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].Module_Load_ID( l.order:ship_id, l.module ) }">
			<MvASSIGN NAME = "l.module:loaded"	VALUE = 1>
		<MvELSE>
			<MvASSIGN NAME = "l.module:loaded"	VALUE = -1>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.module:loaded EQ -1 }">
		<MvASSIGN NAME = "l.order:ship_method"	VALUE = "Unknown">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.order:ship_method"		VALUE = "{ [ g.Module_Root $ l.module:module ].ShippingModule_Description( l.module, l.order:ship_data ) }">

	<MvASSIGN NAME = "l.fields"					VALUE = "{ [ g.Module_Root $ l.module:module ].ShippingModule_Report_Fields( l.module ) }">
	<MvASSIGN NAME = "l.field_pos"				VALUE = 1>
	<MvASSIGN NAME = "l.field"					VALUE = "{ trim( gettoken( l.fields, ',', l.field_pos ) ) }">

	<MvWHILE EXPR = "{ len( l.field ) }">
		<MvASSIGN NAME = "l.label"				VALUE = "{ [ g.Module_Root $ l.module:module ].ShippingModule_Report_Label( l.module, l.field ) }">
		<MvASSIGN NAME = "l.value"				VALUE = "{ [ g.Module_Root $ l.module:module ].ShippingModule_Report_Value( l.module, l.field, l.order:ship_data ) }">

		<MvASSIGNARRAY NAME = "l.order" VALUE = "{ l.value }">
			<MvMEMBER NAME = "ship_field">
			<MvMEMBER NAME = "{ AdminOrderFields_Sanitize_FieldCode( l.field ) }">
		</MvASSIGNARRAY>
	
		<MvASSIGN NAME = "l.order:ship_fields" INDEX = "{ l.field_pos }" MEMBER = "code"	VALUE = "{ l.field }">
		<MvASSIGN NAME = "l.order:ship_fields" INDEX = "{ l.field_pos }" MEMBER = "label"	VALUE = "{ l.label }">
		<MvASSIGN NAME = "l.order:ship_fields" INDEX = "{ l.field_pos }" MEMBER = "value"	VALUE = "{ l.value }">

		<MvASSIGN NAME = "l.field_pos"			VALUE = "{ l.field_pos + 1 }">
		<MvASSIGN NAME = "l.field"				VALUE = "{ trim( gettoken( l.fields, ',', l.field_pos ) ) }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AdminOrderFields_Initialize_Payments" PARAMETERS = "page var, order var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.orderpayment_pos"		VALUE = 1>
	<MvASSIGN NAME = "l.orderpayment_count"		VALUE = "{ [ g.Module_Library_DB ].OrderPaymentList_Load_Order( l.order:id, l.order:payments ) }">

	<MvWHILE EXPR = "{ l.orderpayment_pos LE l.orderpayment_count }">
		<MvIF EXPR = "{ NOT AdminOrderFields_Initialize_Payment( l.page, l.order:payments[ l.orderpayment_pos ] ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.orderpayment_pos"	VALUE = "{ l.orderpayment_pos + 1 }">
	</MvWHILE>

	<MvIF EXPR = "{ l.orderpayment_count }">
		<MvREFERENCEARRAY NAME = "l.order:payment" VARIABLE = "l.order:payments">
			<MvDIMENSION INDEX = "{ l.orderpayment_count }">
		</MvREFERENCEARRAY>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AdminOrderFields_Initialize_Payment" PARAMETERS = "page var, orderpayment var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.orderpayment:pay_id EQ 0 }">
		<MvASSIGN NAME = "l.orderpayment:descrip"		VALUE = "Unknown">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.module" VARIABLE = "g.Session:cache:cmp_mv_adminorderfields:modules">
		<MvDIMENSION INDEX = "{ l.orderpayment:pay_id }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ l.module:loaded EQ 0 }">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].Module_Load_ID( l.orderpayment:pay_id, l.module ) }">
			<MvASSIGN NAME = "l.module:loaded"			VALUE = 1>
		<MvELSE>
			<MvASSIGN NAME = "l.module:loaded"			VALUE = -1>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.module:loaded EQ -1 }">
		<MvASSIGN NAME = "l.orderpayment:descrip"		VALUE = "Unknown">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.pay_data"						VALUE = "{ miva_array_deserialize( l.orderpayment:pay_data ) }">

	<MvIF EXPR = "{ l.orderpayment:pay_secid EQ 0 }">
		<MvASSIGN NAME = "l.pay_secdat"					VALUE = "{ miva_array_deserialize( l.orderpayment:pay_secdat ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.null"						VALUE = "{ [ g.Module_Library_Crypto ].Decrypt_OrderPayment( 'Administrative Order Fields: ' $ l.page:name,
																													 l.orderpayment,
																													 g.passphrase[ l.orderpayment:pay_secid ],
																													 l.pay_secdat ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderpayment:descrip"			VALUE = "{ [ g.Module_Root $ l.module:module ].PaymentModule_Report_Description( l.module, l.pay_data ) }">
	<MvASSIGN NAME = "l.orderpayment:formatted_amount"	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.orderpayment:amount ) }">

	<MvASSIGN NAME = "l.fields"							VALUE = "{ [ g.Module_Root $ l.module:module ].PaymentModule_Report_Fields( l.module, l.pay_data, l.pay_secdat ) }">
	<MvASSIGN NAME = "l.field_pos"						VALUE = 1>
	<MvASSIGN NAME = "l.field"							VALUE = "{ trim( gettoken( l.fields, ',', l.field_pos ) ) }">

	<MvWHILE EXPR = "{ len( l.field ) }">
		<MvASSIGN NAME = "l.label"						VALUE = "{ [ g.Module_Root $ l.module:module ].PaymentModule_Report_Label( l.module, l.field ) }">
		<MvASSIGN NAME = "l.value"						VALUE = "{ [ g.Module_Root $ l.module:module ].PaymentModule_Report_Value( l.module, l.field, l.pay_data, l.pay_secdat ) }">

		<MvASSIGNARRAY NAME = "l.orderpayment" VALUE = "{ l.value }">
			<MvMEMBER NAME = "field">
			<MvMEMBER NAME = "{ AdminOrderFields_Sanitize_FieldCode( l.field ) }">
		</MvASSIGNARRAY>
	
		<MvASSIGN NAME = "l.orderpayment:fields" INDEX = "{ l.field_pos }" MEMBER = "code"	VALUE = "{ l.field }">
		<MvASSIGN NAME = "l.orderpayment:fields" INDEX = "{ l.field_pos }" MEMBER = "label"	VALUE = "{ l.label }">
		<MvASSIGN NAME = "l.orderpayment:fields" INDEX = "{ l.field_pos }" MEMBER = "value"	VALUE = "{ l.value }">

		<MvASSIGN NAME = "l.field_pos"					VALUE = "{ l.field_pos + 1 }">
		<MvASSIGN NAME = "l.field"						VALUE = "{ trim( gettoken( l.fields, ',', l.field_pos ) ) }">
	</MvWHILE>

	<MvASSIGN NAME = "l.pay_data"						VALUE = "">
	<MvASSIGN NAME = "l.pay_secdat"						VALUE = "">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AdminOrderFields_Initialize_OrderItems" PARAMETERS = "page var, order var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.orderitem_count" VALUE = "{ [ g.Module_Library_DB ].OrderItemList_Load_Order( l.order:id, l.order:items ) }">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_OrderItems_WithParts( l.order:items, l.orderitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "AdminOrderFields_Initialize_OrderCharges" PARAMETERS = "page var, order var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.ordercharge_pos"		VALUE = 1>
	<MvASSIGN NAME = "l.ordercharge_count"		VALUE = "{ [ g.Module_Library_DB ].OrderChargeList_Load_Order( l.order:id, l.order:charges ) }">
	
	<MvWHILE EXPR = "{ l.ordercharge_pos LE l.ordercharge_count }">
		<MvASSIGN NAME = "l.order:charges" INDEX = "{ l.ordercharge_pos }" MEMBER = "formatted_disp_amt"	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.order:charges[ l.ordercharge_pos ]:disp_amt ) }">
		<MvASSIGN NAME = "l.ordercharge_pos"	VALUE = "{ l.ordercharge_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AdminOrderFields_Sanitize_FieldCode" PARAMETERS = "field_id" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.search" INDEX = 1 VALUE = ":">	<MvASSIGN NAME = "l.replace" INDEX = 1 VALUE = "_">
	<MvASSIGN NAME = "l.search" INDEX = 2 VALUE = "-">	<MvASSIGN NAME = "l.replace" INDEX = 2 VALUE = "_">

	<MvFUNCTIONRETURN VALUE = "{ glosub_array( l.field_id, l.search, l.replace ) }">
</MvFUNCTION>

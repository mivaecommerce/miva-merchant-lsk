<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-PLST-
| Next Error Code: 15
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-prodlist">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI Product List">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0900">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "10.08">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, component_prov, skins, data_store, vis_store, provision_store">
</MvFUNCTION>

<MvCOMMENT>
|
|	Feature data_store
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'cssui-prodlist-facets.mvc', l.null_managedtemplate ) }">
		<MvEVAL EXPR = "{ Generate_Facets_Code( l.facet_settings, l.template_code ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.template_code, l.facet_settings, 'cssui-prodlist-facets.mvc' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>
	
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.module:version EQ l.version }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CSSUI-CMP-PLST-00011', 'Module \'' $ l.module:name $ '\' does not support manual upgrade.  New versions may only be obtained through the streaming update system.' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_GlobalDelete_ManagedTemplate_Filename( 'cssui-prodlist-facets.mvc' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	Feature vis_store
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Store_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.CSSUI_ProdList_FacetSettings:template_code" VALUE = "{ trim( g.CSSUI_ProdList_FacetSettings:template_code ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( g.CSSUI_ProdList_FacetSettings:template_code ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'CSSUI-PRODLIST-FACETS', 'CSSUI_ProdList_FacetSettings:template_code', g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'cssui-prodlist-facets.mvc', l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CSSUI-CMP-PLST-00012', 'Facets template not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, '', g.CSSUI_ProdList_FacetSettings:template_code, l.null_settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( 'CSSUI-PRODLIST-FACETS', 'CSSUI_ProdList_FacetSettings:template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Tabs" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "GT_UISETTINGS/CSSUI-PRODLIST-FACETS:Facets Layout">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Head" PARAMETERS = "module var, tab" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( 'cssui-prodlist-facets.mvc', l.templateversion ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.load_fields }">
		<MvASSIGN NAME = "g.CSSUI_ProdList_FacetSettings:template_code" VALUE = "{ l.templateversion:source }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( g.Tab, 'CSSUI-PRODLIST-FACETS' ) }">
		<MvHIDE FIELDS = "g.CSSUI_ProdList_FacetSettings">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_UISETTINGS', 'CSSUI-PRODLIST-FACETS' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
			<tr><td valign="top" nowrap>
				Facets Layout Template:
			</td><td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( 'CSSUI_ProdList_FacetSettings:template_code', g.CSSUI_ProdList_FacetSettings:template_code, 20, 100, l.templateversion:templ_id ) }">
			</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature component
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item EQ 'facets' }">
		<MvFUNCTIONRETURN VALUE = "">
	</MvIF>

	<MvASSIGN NAME = "l.settings:imagemachine_enabled"	VALUE = "{ CSSUIProductList_AssociatedImageMachine_IsCurrentlyEnabled( g.Edit_Page, l.item ) }">

	<MvIF EXPR = "{ l.item EQ 'category_listing' }">		<MvASSIGN NAME = "l.tab_title" VALUE = "Category Product List">
	<MvELSEIF EXPR = "{ l.item EQ 'all_products' }">		<MvASSIGN NAME = "l.tab_title" VALUE = "Product List">
	<MvELSEIF EXPR = "{ l.item EQ 'related_products' }">	<MvASSIGN NAME = "l.tab_title" VALUE = "Related Product List">
	<MvELSEIF EXPR = "{ l.item EQ 'search_results' }">		<MvASSIGN NAME = "l.tab_title" VALUE = "Search Results">
	<MvELSE>												<MvASSIGN NAME = "l.tab_title" VALUE = "{ 'Product List (' $ l.item $ ')' }">
	</MvIF>

	<MvASSIGN NAME = "l.tabs"							VALUE = "{ 'GT_PAGE/' $ l.item $ ':' $ l.tab_title $ ' Layout' }">
	<MvIF EXPR = "{ ( NOT l.settings:imagemachine_enabled ) AND ( miva_array_elements( l.settings:imagetypes ) GT 0 ) }">
		<MvASSIGN NAME = "l.tabs"						VALUE = "{ l.tabs $ ',GT_PAGE/' $ l.item $ '_dimensions:' $ l.tab_title $ ' Image Dimensions' }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.tabs }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModuleValidate_SortMethod" PARAMETERS = "item, default_sort, value, sort_by, sorting_option, sorting_method" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( l.default_sort EQ l.value ) AND ( NOT l.sort_by ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, '', 'Sorting option \'' $ l.sorting_option $ '\' must be enabled to use the Default Sort Method of ' $ l.sorting_method ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item EQ 'facets' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.fields:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.fields:price"						VALUE = "{ trim( l.fields:price ) }">
	<MvASSIGN NAME = "l.fields:additionalprice"				VALUE = "{ trim( l.fields:additionalprice ) }">
	<MvASSIGN NAME = "l.fields:displaydiscounts"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:displaydiscounts ) }">
	<MvASSIGN NAME = "l.fields:predictdiscounts"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:predictdiscounts ) }">
	<MvASSIGN NAME = "l.fields:button_wish"					VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:button_wish ) }">
	<MvASSIGN NAME = "l.fields:per_page"					VALUE = "{ trim( l.fields:per_page ) }">
	<MvASSIGN NAME = "l.fields:page_disp_count"				VALUE = "{ trim( l.fields:page_disp_count ) }">
	<MvASSIGN NAME = "l.fields:linkexcludecat"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:linkexcludecat ) }">
	<MvASSIGN NAME = "l.fields:reverse_default_disp_order"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:reverse_default_disp_order ) }">

	<MvIF EXPR = "{ NOT l.fields:advanced }">
		<MvIF EXPR = "{ l.fields:format EQ 'X' }">
			<MvASSIGN NAME = "l.fields:col_num"	VALUE = "{ trim( l.fields:col_num ) }">

			<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:col_num ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'col_num', g.Validation_Message ) }">
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.fields:paginate }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:per_page ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'per_page', g.Validation_Message ) }">
		</MvIF>
	</MvIF>
	
	<MvIF EXPR = "{ l.fields:paginate_count }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:page_disp_count ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'page_disp_count', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:b_width ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item $ '_dimensions', l.field_prefix $ 'b_width', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:b_height ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item $ '_dimensions', l.field_prefix $ 'b_height', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT l.fields:advanced AND l.fields:sort_by:switch }">
		<MvIF EXPR = "{ ( ( 'customfield:' IN l.fields:default_sort ) EQ 1 ) OR
						( ( 'customfield_desc:' IN l.fields:default_sort ) EQ 1 ) }">
			<MvASSIGN NAME = "l.module_code"				VALUE = "{ gettoken( l.fields:default_sort, ':', 2 ) }">
			<MvASSIGN NAME = "l.field_code"					VALUE = "{ gettoken( l.fields:default_sort, ':', 3 ) }">

			<MvASSIGN NAME = "l.sortable_customfield_count" VALUE = "{ [ g.Module_Library_Utilities ].CustomFieldSelect_Selected( l.fields:sort_custom, l.sortable_customfields ) }">

			<MvIF EXPR = "{ NOT miva_array_search( l.sortable_customfields, 1, l.sortable_customfield, '( l.sortable_customfield:module EQ l.module_code ) AND ( l.sortable_customfield:code EQ l.field_code )' ) }">
				<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Module_Load_Code_Cached( l.module_code, l.customfield_module ) }">	<MvASSIGN NAME = "l.field_name" VALUE = "Unknown">
				<MvELSE>																										<MvASSIGN NAME = "l.field_name" VALUE = "{ [ g.Module_Root $ l.customfield_module:module ].Module_Product_Field_Name( l.customfield_module, l.field_code ) }">
				</MvIF>

				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, '', 'Sorting by custom field \'' $ l.field_name $ '\' must be enabled to use it as the Default Sort Method' ) }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( 	l.item, l.fields:default_sort, 'name_asc', 		l.fields:sort_by:name,			'Name', 		'Name Ascending' ) }">	<MvFUNCTIONRETURN VALUE = 0>
			<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.fields:default_sort, 'name_desc', 	l.fields:sort_by:name,			'Name', 		'Name Descending' ) }">	<MvFUNCTIONRETURN VALUE = 0>
			<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.fields:default_sort, 'code_asc',		l.fields:sort_by:code,			'Product Code', 'Code Ascending' ) }">	<MvFUNCTIONRETURN VALUE = 0>
			<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.fields:default_sort, 'code_desc', 	l.fields:sort_by:code,			'Product Code', 'Code Descending' ) }">	<MvFUNCTIONRETURN VALUE = 0>
			<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.fields:default_sort, 'bestsellers', 	l.fields:sort_by:best,			'Best Selling', 'Best Selling' ) }">	<MvFUNCTIONRETURN VALUE = 0>
			<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.fields:default_sort, 'price_asc', 	l.fields:sort_by:price,			'Price', 		'Lowest Price' ) }">	<MvFUNCTIONRETURN VALUE = 0>
			<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.fields:default_sort, 'price_desc', 	l.fields:sort_by:price,			'Price', 		'Highest Price' ) }">	<MvFUNCTIONRETURN VALUE = 0>
			<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.fields:default_sort, 'newest', 		l.fields:sort_by:new,			'Newest', 		'Newest' ) }">			<MvFUNCTIONRETURN VALUE = 0>
			<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.fields:default_sort, 'oldest', 		l.fields:sort_by:old,			'Oldest', 		'Oldest' ) }">			<MvFUNCTIONRETURN VALUE = 0>
			<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.fields:default_sort, 'updated', 		l.fields:sort_by:updated,		'Updated', 		'Updated' ) }">			<MvFUNCTIONRETURN VALUE = 0>
			<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.fields:default_sort, 'relevance',		l.fields:sort_by:relevance, 	'Relevance', 	'Relevance' ) }">		<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.fields:advanced }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.fields:template_code ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item EQ 'facets' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.settings:advanced"						VALUE = "{ l.fields:advanced }">
	<MvASSIGN NAME = "l.settings:mode"							VALUE = "{ CSSUIProductList_Mode( l.item, l.fields:mode ) }">
	<MvASSIGN NAME = "l.settings:imagemachine_enabled"			VALUE = "{ CSSUIProductList_AssociatedImageMachine_WillBeEnabled( g.Edit_Page, l.item ) }">
	<MvASSIGN NAME = "l.settings:reverse_default_disp_order" 	VALUE = "{ l.fields:reverse_default_disp_order }">

	<MvIF EXPR = "{ l.fields:advanced }">
		<MvASSIGN NAME = "l.settings:predictdiscounts"	VALUE = "{ l.fields:predictdiscounts }">
	<MvELSE>
		<MvIF EXPR = "{ l.fields:format EQ 'X' }">
			<MvASSIGN NAME = "l.settings:col_num"		VALUE = "{ l.fields:col_num }">
		</MvIF>

		<MvASSIGN NAME = "l.settings:format"			VALUE = "{ l.fields:format }">
		<MvASSIGN NAME = "l.settings:field_name"		VALUE = "{ l.fields:field_name }">
		<MvASSIGN NAME = "l.settings:field_code"		VALUE = "{ l.fields:field_code }">
		<MvASSIGN NAME = "l.settings:field_sku"			VALUE = "{ l.fields:field_sku }">
		<MvASSIGN NAME = "l.settings:field_price"		VALUE = "{ l.fields:field_price }">
		<MvASSIGN NAME = "l.settings:field_weight"		VALUE = "{ l.fields:field_weight }">
		<MvASSIGN NAME = "l.settings:field_desc"		VALUE = "{ l.fields:field_desc }">
		<MvASSIGN NAME = "l.settings:button_add"		VALUE = "{ l.fields:button_add }">
		<MvASSIGN NAME = "l.settings:button_buy"		VALUE = "{ l.fields:button_buy }">
		<MvASSIGN NAME = "l.settings:button_wish"		VALUE = "{ l.fields:button_wish }">
		<MvASSIGN NAME = "l.settings:price"				VALUE = "{ l.fields:price }">
		<MvASSIGN NAME = "l.settings:additionalprice"	VALUE = "{ l.fields:additionalprice }">
		<MvASSIGN NAME = "l.settings:displaydiscounts"	VALUE = "{ l.fields:displaydiscounts }">
		<MvASSIGN NAME = "l.settings:predictdiscounts"	VALUE = "{ l.settings:displaydiscounts }">
		<MvASSIGN NAME = "l.settings:image"				VALUE = "{ l.fields:image }">
		<MvASSIGN NAME = "l.settings:invmsg"			VALUE = "{ l.fields:invmsg }">
	</MvIF>

	<MvASSIGN NAME = "l.null"							VALUE = "{ [ g.Module_Library_Utilities ].CustomFieldSelect_Selected( l.fields:fields_custom,	l.settings:fields_custom ) }">
	<MvASSIGN NAME = "l.null"							VALUE = "{ [ g.Module_Library_Utilities ].CustomFieldSelect_Selected( l.fields:sort_custom,		l.settings:sort_custom ) }">

	<MvCOMMENT>
	|
	| If Point + Click, add the single selected image type (if any) to the list
	| of image types to be initialized.  Otherwise, add all selected image types.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.fields:advanced }">
		<MvASSIGN NAME = "l.imagetype_count"					VALUE = "{ [ g.Module_Library_Utilities ].ImageTypeSelect_Selected( l.fields:imagetypes, l.settings:imagetypes ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:imagetypes"				VALUE = "">
		
		<MvIF EXPR = "{ ( 'type:' IN l.fields:image ) EQ 1 }">	
			<MvASSIGN NAME = "l.settings:imagetypes" INDEX = 1	VALUE = "{ substring( l.fields:image, 6, len( l.fields:image ) - 5 ) }">
		</MvIF>
	</MvIF>

	<MvCOMMENT>
	|
	| These settings are common between advanced and point + click mode
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.settings:linkexcludecat"			VALUE = "{ l.fields:linkexcludecat }">
	
	<MvIF EXPR = "{ l.fields:paginate }">
		<MvASSIGN NAME = "l.settings:per_page"				VALUE = "{ l.fields:per_page }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:per_page"				VALUE = 0>
	</MvIF>
	
	<MvIF EXPR = "{ l.fields:paginate_count }">
		<MvASSIGN NAME = "l.settings:page_disp_count"		VALUE = "{ l.fields:page_disp_count }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:page_disp_count"		VALUE = 0>
	</MvIF>
	
	<MvASSIGN NAME = "l.settings:items_per_page_filter"		VALUE = "{ l.fields:items_per_page_filter }">
	
	<MvASSIGN NAME = "l.settings:sort_by:switch"			VALUE = "{ l.fields:sort_by:switch }">
	<MvASSIGN NAME = "l.settings:sort_by:name"				VALUE = "{ l.fields:sort_by:name }">
	<MvASSIGN NAME = "l.settings:sort_by:code"				VALUE = "{ l.fields:sort_by:code }">
	<MvASSIGN NAME = "l.settings:sort_by:best"				VALUE = "{ l.fields:sort_by:best }">
	<MvASSIGN NAME = "l.settings:sort_by:price"				VALUE = "{ l.fields:sort_by:price }">
	<MvASSIGN NAME = "l.settings:sort_by:new"				VALUE = "{ l.fields:sort_by:new }">
	<MvASSIGN NAME = "l.settings:sort_by:old"				VALUE = "{ l.fields:sort_by:old }">
	<MvASSIGN NAME = "l.settings:sort_by:updated"			VALUE = "{ l.fields:sort_by:updated }">
	<MvASSIGN NAME = "l.settings:sort_by:relevance"			VALUE = "{ l.fields:sort_by:relevance }">

	<MvIF EXPR = "{ l.fields:default_sort EQ '' }">
		<MvASSIGN NAME = "l.settings:default_sort"			VALUE = "disp_order">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:default_sort"			VALUE = "{ l.fields:default_sort }">
	</MvIF>

	<MvASSIGN NAME = "l.settings:constrain"					VALUE = "{ l.fields:constrain }">
	<MvASSIGN NAME = "l.settings:b_width"					VALUE = "{ l.fields:b_width }">
	<MvASSIGN NAME = "l.settings:b_height"					VALUE = "{ l.fields:b_height }">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-PLST-00003', l.module:name $ ' \'' $ l.item $ '\' settings updated' ) }">

	<MvIF EXPR = "{ NOT l.fields:advanced }">
		<MvEVAL EXPR = "{ Generate_Code( l.module, l.settings, l.fields:template_code, l.item ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, '', l.fields:template_code, l.settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update_Requires_Version" PARAMETERS = "module var, page var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_PostUpdate" PARAMETERS = "module var, page var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.item EQ 'facets' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvCOMMENT>
	|
	| Load all image types
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.imagetype_count"	VALUE = "{ [ g.Module_Library_DB ].ImageTypeList_Load_All( l.imagetypes ) }">

	<MvIF EXPR = "{ l.load_fields }">
		<MvASSIGN NAME = "l.fields:mode"								VALUE = "{ CSSUIProductList_Mode( l.item, l.settings:mode ) }">
		<MvASSIGN NAME = "l.fields:linkexcludecat"						VALUE = "{ l.settings:linkexcludecat }">
		<MvASSIGN NAME = "l.fields:format"								VALUE = "{ l.settings:format }">
		<MvASSIGN NAME = "l.fields:field_name"							VALUE = "{ l.settings:field_name }">
		<MvASSIGN NAME = "l.fields:field_code"							VALUE = "{ l.settings:field_code }">
		<MvASSIGN NAME = "l.fields:field_sku"							VALUE = "{ l.settings:field_sku }">
		<MvASSIGN NAME = "l.fields:field_price"							VALUE = "{ l.settings:field_price }">
		<MvASSIGN NAME = "l.fields:field_weight"						VALUE = "{ l.settings:field_weight }">
		<MvASSIGN NAME = "l.fields:field_desc"							VALUE = "{ l.settings:field_desc }">
		<MvASSIGN NAME = "l.fields:button_add"							VALUE = "{ l.settings:button_add }">
		<MvASSIGN NAME = "l.fields:button_buy"							VALUE = "{ l.settings:button_buy }">
		<MvASSIGN NAME = "l.fields:button_wish"							VALUE = "{ l.settings:button_wish }">

		<MvIF EXPR = "{ ISNULL l.settings:price }">
			<MvASSIGN NAME = "l.fields:price"							VALUE = "base">
		<MvELSE>
			<MvASSIGN NAME = "l.fields:price"							VALUE = "{ l.settings:price }">
		</MvIF>

		<MvASSIGN NAME = "l.fields:additionalprice"						VALUE = "{ l.settings:additionalprice }">
		<MvASSIGN NAME = "l.fields:displaydiscounts"					VALUE = "{ l.settings:displaydiscounts }">
		<MvASSIGN NAME = "l.fields:predictdiscounts"					VALUE = "{ l.settings:predictdiscounts }">
		<MvASSIGN NAME = "l.fields:image"								VALUE = "{ l.settings:image }">
		<MvASSIGN NAME = "l.fields:invmsg"								VALUE = "{ l.settings:invmsg }">

		<MvIF EXPR = "{ ISNULL l.settings:constrain }">
			<MvASSIGN NAME = "l.fields:constrain"						VALUE = "B">
		<MvELSE>
			<MvASSIGN NAME = "l.fields:constrain"						VALUE = "{ l.settings:constrain }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:b_width }">
			<MvASSIGN NAME = "l.fields:b_width"							VALUE = 234>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:b_width"							VALUE = "{ l.settings:b_width }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:b_height }">
			<MvASSIGN NAME = "l.fields:b_height"						VALUE = 234>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:b_height"						VALUE = "{ l.settings:b_height }">
		</MvIF>

		<MvASSIGN NAME = "l.fields:advanced"							VALUE = "{ l.settings:advanced }">
		<MvASSIGN NAME = "l.fields:template_code"						VALUE = "{ l.templateversion:source }">
		<MvASSIGN NAME = "l.fields:template_filename"					VALUE = "{ l.settings:template_filename }">

		<MvASSIGN NAME = "l.fields:col_num"								VALUE = "{ l.settings:col_num }">

		<MvIF EXPR = "{ l.settings:per_page EQ 0 }">
			<MvASSIGN NAME = "l.fields:paginate"						VALUE = 0>
			<MvASSIGN NAME = "l.fields:per_page"						VALUE = 10>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:paginate"						VALUE = 1>
			<MvASSIGN NAME = "l.fields:per_page"						VALUE = "{ l.settings:per_page }">
		</MvIF>

		<MvIF EXPR = "{ l.settings:page_disp_count EQ 0 }">
			<MvASSIGN NAME = "l.fields:paginate_count"					VALUE = 0>
			<MvASSIGN NAME = "l.fields:page_disp_count"					VALUE = 3>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:paginate_count"					VALUE = 1>
			<MvASSIGN NAME = "l.fields:page_disp_count"					VALUE = "{ l.settings:page_disp_count }">
		</MvIF>

		<MvIF EXPR = "{ l.settings:items_per_page_filter EQ 0 }">
			<MvASSIGN NAME = "l.fields:items_per_page_filter"			VALUE = 0>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:items_per_page_filter"			VALUE = "{ l.settings:items_per_page_filter }">
		</MvIF>
		
		<MvASSIGN NAME = "l.fields:sort_by:switch"						VALUE = "{ l.settings:sort_by:switch }">
		<MvASSIGN NAME = "l.fields:sort_by:name"						VALUE = "{ l.settings:sort_by:name }">
		<MvASSIGN NAME = "l.fields:sort_by:code"						VALUE = "{ l.settings:sort_by:code }">
		<MvASSIGN NAME = "l.fields:sort_by:best"						VALUE = "{ l.settings:sort_by:best }">
		<MvASSIGN NAME = "l.fields:sort_by:price"						VALUE = "{ l.settings:sort_by:price }">
		<MvASSIGN NAME = "l.fields:sort_by:new"							VALUE = "{ l.settings:sort_by:new }">
		<MvASSIGN NAME = "l.fields:sort_by:old"							VALUE = "{ l.settings:sort_by:old }">
		<MvASSIGN NAME = "l.fields:sort_by:updated"						VALUE = "{ l.settings:sort_by:updated }">
		<MvASSIGN NAME = "l.fields:sort_by:relevance"					VALUE = "{ l.settings:sort_by:relevance }">

		<MvIF EXPR = "{ l.settings:default_sort EQ '' }">
			<MvASSIGN NAME = "l.fields:default_sort"					VALUE = "disp_order">
		<MvELSE>
			<MvASSIGN NAME = "l.fields:default_sort"					VALUE = "{ l.settings:default_sort }">
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].InitializeCustomFieldSelect( l.settings:fields_custom,	l.fields:fields_custom ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].InitializeCustomFieldSelect( l.settings:sort_custom,	l.fields:sort_custom ) }">

		<MvASSIGN NAME = "l.fields:imagetypes"							VALUE = "{ l.settings:imagetypes }">
		<MvASSIGN NAME = "l.fields:reverse_default_disp_order" 			VALUE = "{ l.settings:reverse_default_disp_order }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'mode,linkexcludecat,format,field_name,field_code,field_sku,field_price,field_weight,field_desc,fields_custom,imagetypes,button_add,button_buy,button_wish,price,additionalprice,displaydiscounts,predictdiscounts,image,invmsg,col_num,paginate,per_page,paginate_count,items_per_page_filter,page_disp_count,default_sort,sort_by:switch,sort_by:name,sort_by:code,sort_by:best,sort_by:price,sort_by:new,sort_by:old,sort_by:updated,sort_by:relevance,sort_custom,reverse_default_disp_order,template_code,template_filename,advanced' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_filename' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">

		<MvIF EXPR = "{ ( l.item EQ 'category_listing' ) 	OR
						( l.item EQ 'all_products' ) 		OR
						( l.item EQ 'search_results' ) 		OR
						( l.item EQ 'related_products' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'mode' ) }">
		<MvELSE>
			<tr><td align="left" nowrap valign="top">
				<b>Layout:</b>
			</td><td align="left" nowrap width="100%">
				<select name="{ encodeentities( l.field_prefix ) $ 'mode' }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'ctgy', l.fields:mode, 'Category Product List Layout' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'plst', l.fields:mode, 'Product List Layout' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'srch', l.fields:mode, 'Search Results Layout' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'relp', l.fields:mode, 'Related Product List Layout' ) }">
				</select>
			</td></tr>
		</MvIF>
		
		<MvIF EXPR = "{ NOT l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_code' ) }">
		<MvELSE>
			<tr><td colspan="2" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( l.field_prefix $ 'template_code', l.fields:template_code, 20, 100, l.templateversion:templ_id ) }">
			</td></tr>

			<MvIF EXPR = "{ l.customfield_count }">
				<tr><td colspan="2">&nbsp;</td></tr>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'format,field_name,field_code,field_sku,field_price,field_weight,field_desc' ) }">
		</MvIF>

		<MvIF EXPR = "{ l.fields:mode EQ 'ctgy' }">
			<tr><td align="left" nowrap valign="top">
				<b>Product Links:</b>
			</td><td align="left" nowrap width="100%">
				<select name="{ encodeentities( l.field_prefix ) $ 'linkexcludecat' }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '1', l.fields:linkexcludecat, 'Do Not Include Category Code' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0', l.fields:linkexcludecat, 'Include Category Code' ) }">
				</select>
			</td></tr>
		</MvIF>

		<MvIF EXPR = "{ NOT l.fields:advanced }">
			<tr><td align="left" nowrap valign="top">
				<b>Format:</b>
			</td><td align="left" nowrap width="100%">
				<select name="{ encodeentities( l.field_prefix ) $ 'format' }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'X', l.fields:format, 'Expanded' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'L', l.fields:format, 'Line Item' ) }">
				</select>
			</td></tr>

			<tr><td align="left" valign="top" nowrap>
				<b>Product Fields:</b>
			</td><td align="left" width="100%">
				<table border="0" cellpadding="0" cellspacing="0">
				<tr><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_name, l.field_prefix $ 'field_name', 1, 'Product Name' ) }">
				</td><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_code, l.field_prefix $ 'field_code', 1, 'Product Code' ) }">
				</td></tr>

				<tr><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_sku, l.field_prefix $ 'field_sku', 1, 'Product SKU' ) }">
				</td><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_price, l.field_prefix $ 'field_price', 1, 'Price' ) }">
				</td></tr>

				<tr><td align="left" nowrap>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_weight, l.field_prefix $ 'field_weight', 1, 'Weight' ) }">
				</td><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_desc, l.field_prefix $ 'field_desc', 1, 'Description' ) }">
				</td></tr>
				</table>
			</td></tr>
		</MvIF>

		<MvASSIGN NAME = "l.customfield_count"			VALUE = "{ [ g.Module_Library_Utilities ].ProductCustomFieldList_Load( l.customfields ) }">
		<MvASSIGN NAME = "l.sortable_customfield_count"	VALUE = "{ miva_array_filter_ref( l.customfields, 1, l.customfield, 'l.customfield:capabilities:orderby', l.sortable_customfields ) }">

		<MvIF EXPR = "{ l.customfield_count }">
			<tr><td align="left" valign="top" nowrap>
				<b>Custom Fields:</b>
			</td><td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCustomFieldSelect( l.field_prefix $ 'fields_custom', l.fields:fields_custom, l.customfields, l.customfield_count ) }">
			</td></tr>
		</MvIF>

		<MvIF EXPR = "{ l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'price,additionalprice,displaydiscounts' ) }">

			<tr><td>
				&nbsp;
			</td><td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:predictdiscounts, l.field_prefix $ 'predictdiscounts', 1, 'Calculate Predicted Discounts' ) }">
			</td></tr>
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'predictdiscounts' ) }">

			<tr><td colspan="2">&nbsp;</td></tr>

			<tr><td align="left" nowrap valign="top">
				<b>Displayed Price:</b>
			</td><td align="left" nowrap width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'price', 'retail',	l.fields:price, 'Retail Price' ) }"><br />
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'price', 'base',	l.fields:price, 'Base Price (includes any legacy price group discounts but does not reflect sales, coupons or discounts applied in the basket)' ) }"><br />
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'price', 'sale',	l.fields:price, 'Sale Price (includes legacy price groups and predicted discounts)' ) }"><br />
			</td></tr>

			<tr><td align="left" nowrap valign="top">
				<b>Additional Price Display:</b>
			</td><td align="left" nowrap width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'additionalprice', '',			l.fields:additionalprice, 'None' ) }"><br />
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'additionalprice', 'retail',	l.fields:additionalprice, 'Retail Price' ) }"><br />
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'additionalprice', 'base',		l.fields:additionalprice, 'Base Price (includes any legacy price group discounts but does not reflect sales, coupons or discounts applied in the basket)' ) }"><br />
			</td></tr>

			<tr><td>
				&nbsp;
			</td><td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:displaydiscounts, l.field_prefix $ 'displaydiscounts', 1, 'Display Predicted Discounts' ) }">
			</td></tr>

			<tr><td colspan="2">&nbsp;</td></tr>
		</MvIF>

		<MvIF EXPR = "{ NOT l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'imagetypes' ) }">
		<MvELSE>
			<MvIF EXPR = "{ ( NOT l.settings:imagemachine_enabled ) AND ( l.imagetype_count GT 0 ) }">
				<tr><td align="left" valign="top" nowrap>
					<b>Product Images:</b>
				</td><td align="left" width="100%">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawImageTypeSelect( l.field_prefix $ 'imagetypes', l.fields:imagetypes, l.imagetypes, l.imagetype_count ) }">
				</td></tr>
			</MvIF>

			<tr><td colspan="2">&nbsp;</td></tr>
		</MvIF>

		<MvIF EXPR = "{ l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'button_add,button_buy,button_wish,image,invmsg,col_num,sort_by:name,sort_by:code,sort_by:best,sort_by:price,sort_by:new,sort_by:old,sort_by:updated,sort_by:relevance' ) }">
			<tr>
				<td align="left" valign="top" nowrap>
					<b>Sorting Options:</b>
				</td>
				<td valign="top" width="100%">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'sort_by:switch', 0, l.fields:sort_by:switch, 'Off' ) }"><br />
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'sort_by:switch', 1, l.fields:sort_by:switch, 'On' ) }">
				</td>
			</tr>
			<tr>
				<td align="left" style="padding-right: 15px;" nowrap>
					<b>Default Sort Method:</b>
				</td>
				<td width="100%">
					<table width="100%">
						<tr>
							<td align="left" style="padding-right: 15px;" nowrap>
								<select name="{ encodeentities( l.field_prefix ) $ 'default_sort' }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'disp_order', 	l.fields:default_sort, 'Default' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'name_asc', 	l.fields:default_sort, 'Name Ascending' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'name_desc', 	l.fields:default_sort, 'Name Descending' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'code_asc', 	l.fields:default_sort, 'Code Ascending' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'code_desc', 	l.fields:default_sort, 'Code Descending' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'bestsellers', l.fields:default_sort, 'Best Selling' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'price_asc', 	l.fields:default_sort, 'Lowest Price' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'price_desc', 	l.fields:default_sort, 'Highest Price' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'newest', 		l.fields:default_sort, 'Newest' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'oldest', 		l.fields:default_sort, 'Oldest' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'updated', 	l.fields:default_sort, 'Updated' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'relevance',	l.fields:default_sort, 'Relevance' ) }">

									<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.sortable_customfields" COUNT = "{ l.sortable_customfield_count }">
										<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption_Encode( 'customfield:' $ l.customfield:module:code $ ':' $ l.customfield:code,		l.fields:default_sort, l.customfield:name $ ' Ascending' ) }">
										<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption_Encode( 'customfield_desc:' $ l.customfield:module:code $ ':' $ l.customfield:code,	l.fields:default_sort, l.customfield:name $ ' Descending' ) }">
									</MvFOREACH>
								</select>
							</td>
							<td width="100%">
								<font color="red"><b>WARNING:</b></font>&nbsp;&nbsp;Changing the default sorting option to any value other than "default" will increase page load times and increase the load on your server. Be fully aware of the effect this will have on your store before changing this option!
							</td>
						</tr>
					</table>
				</td>
			</tr>
			<tr>
				<td align="left" nowrap>
					&nbsp;
				</td>
				<td width="100%">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:reverse_default_disp_order, l.field_prefix $ 'reverse_default_disp_order', '1', 'Reverse Default Display Order' ) }">
				</td>
			</tr>
		<MvELSE>
			<tr><td align="left" valign="top" nowrap>
				<b>Buttons:</b>
			</td><td nowrap width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:button_add, l.field_prefix $ 'button_add', 1, 'Add To Basket' ) }"><br />
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:button_buy, l.field_prefix $ 'button_buy', 1, 'Buy Now' ) }"><br />
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:button_wish, l.field_prefix $ 'button_wish', 1, 'Add To Wish List' ) }">
			</td></tr>

			<MvIF EXPR = "{ l.settings:imagemachine_enabled }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'image' ) }">
			<MvELSE>
				<tr><td align="left" nowrap>
					<b>Image:</b>
				</td><td align="left" width="100%">
					<select name="{ encodeentities( l.field_prefix ) $ 'image' }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'N', l.fields:image, 'None' ) }">

						<MvFOREACH ITERATOR = "l.imagetype" ARRAY = "l.imagetypes" COUNT = "{ l.imagetype_count }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'type:' $ l.imagetype:code, l.fields:image, encodeentities( l.imagetype:descrip ) ) }">
						</MvFOREACH>

						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'T', l.fields:image, 'Legacy Thumbnail' ) }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'F', l.fields:image, 'Legacy Full-Sized' ) }">
					</select>
				</td></tr>
			</MvIF>

			<tr><td align="left" nowrap>
				<b>Inventory Level Message:</b>
			</td><td width="100%">
				<select name="{ encodeentities( l.field_prefix ) $ 'invmsg' }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'N', l.fields:invmsg, 'None' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'S', l.fields:invmsg, 'Short' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'L', l.fields:invmsg, 'Long' ) }">
				</select>
			</td></tr>

			<tr><td align="left" valign="top" nowrap>
				<b>Sorting Options:</b>
			</td><td width="100%">
				<table width="100%">
					<tr>
						<td align="left" style="padding-right: 15px;" valign="top" nowrap>
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'sort_by:switch', 0, l.fields:sort_by:switch, 'Off' ) }"><br />
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'sort_by:switch', 1, l.fields:sort_by:switch, 'On' ) }">
						</td>
						<td width="100%">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:name, 		l.field_prefix $ 'sort_by:name',		'1', 	'Name' ) }"><br />
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:code, 		l.field_prefix $ 'sort_by:code',		'1', 	'Product Code' ) }"><br />
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:best, 		l.field_prefix $ 'sort_by:best',		'1', 	'Best Selling' ) }"><br />
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:price, 		l.field_prefix $ 'sort_by:price',		'1', 	'Price' ) }"><br />
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:new, 		l.field_prefix $ 'sort_by:new',			'1', 	'Newest' ) }"><br />
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:old, 		l.field_prefix $ 'sort_by:old',			'1', 	'Oldest' ) }"><br />
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:updated, 	l.field_prefix $ 'sort_by:updated',		'1', 	'Updated' ) }"><br />
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:relevance, 	l.field_prefix $ 'sort_by:relevance',	'1', 	'Relevance' ) }">
						</td>
					</tr>
				</table>
			</td></tr>

			<tr><td align="left" valign="top" nowrap>
				<b>Sort By Custom Field:</b>
			</td><td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCustomFieldSelect( l.field_prefix $ 'sort_custom', l.fields:sort_custom, l.sortable_customfields, l.sortable_customfield_count ) }">
			</td></tr>
			
			<tr>
				<td align="left" style="padding-right: 15px;" nowrap>
					<b>Default Sort Method:</b>
				</td>
				<td width="100%">
					<table width="100%">
						<tr>
							<td align="left" style="padding-right: 15px;" nowrap>
								<select name="{ encodeentities( l.field_prefix ) $ 'default_sort' }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'disp_order', 	l.fields:default_sort, 'Default' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'name_asc', 	l.fields:default_sort, 'Name Ascending' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'name_desc', 	l.fields:default_sort, 'Name Descending' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'code_asc', 	l.fields:default_sort, 'Code Ascending' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'code_desc', 	l.fields:default_sort, 'Code Descending' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'bestsellers', l.fields:default_sort, 'Best Selling' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'price_asc', 	l.fields:default_sort, 'Lowest Price' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'price_desc', 	l.fields:default_sort, 'Highest Price' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'new', 		l.fields:default_sort, 'Newest' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'old', 		l.fields:default_sort, 'Oldest' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'updated', 	l.fields:default_sort, 'Updated' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'relevance',	l.fields:default_sort, 'Relevance' ) }">

									<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.sortable_customfields" COUNT = "{ l.sortable_customfield_count }">
										<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption_Encode( 'customfield:' $ l.customfield:module:code $ ':' $ l.customfield:code,		l.fields:default_sort, l.customfield:name $ ' Ascending' ) }">
										<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption_Encode( 'customfield_desc:' $ l.customfield:module:code $ ':' $ l.customfield:code,	l.fields:default_sort, l.customfield:name $ ' Descending' ) }">
									</MvFOREACH>
								</select>
							</td>
							<td width="100%">
								<font color="red"><b>WARNING:</b></font>&nbsp;&nbsp;Changing the default sorting option to any value other than "default" will increase page load times and increase the load on your server. Be fully aware of the effect this will have on your store before changing this option!
							</td>
						</tr>
					</table>
				</td>
			</tr>
			<tr>
				<td align="left" nowrap>
					&nbsp;
				</td>
				<td width="100%">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:reverse_default_disp_order, l.field_prefix $ 'reverse_default_disp_order', '1', 'Reverse Default Display Order' ) }">
				</td>
			</tr>
			<tr><td colspan="2">&nbsp;</td></tr>

			<MvIF EXPR = "{ l.fields:format EQ 'L' }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'col_num' ) }">
			<MvELSE>
				<tr><td align="left" nowrap valign="top">
					<b>Product Columns:</b>
				</td><td align="left" nowrap width="100%">
					<input type="text" name="{ encodeentities( l.field_prefix ) $ 'col_num' }" value="{ encodeentities( l.fields:col_num ) }" size="5">
				</td></tr>
			</MvIF>
		</MvIF>
		
		<tr><td align="left" nowrap valign="top">
			<b>Pagination:</b>
		</td><td align="left" nowrap width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate', 0, l.fields:paginate, 'Do not paginate products' ) }"><br>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate', 1, l.fields:paginate, 'Display' ) }"> <input type="text" name="{ encodeentities( l.field_prefix ) $ 'per_page' }" value="{ encodeentities( l.fields:per_page ) }" size="5"> products per page<br />
		</td></tr>
		
		<tr><td align="left" nowrap valign="top">
			<b>Page Numbers:</b>
		</td><td align="left" nowrap width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate_count', 0, l.fields:paginate_count, 'Do not show page count' ) }"><br>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate_count', 1, l.fields:paginate_count, 'Display' ) }"> <input type="text" name="{ encodeentities( l.field_prefix ) $ 'page_disp_count' }" value="{ encodeentities( l.fields:page_disp_count ) }" size="5"> pages at a time
		</td></tr>
		
		<tr><td align="left" nowrap valign="top">
			<b>Items Per Page Filter:</b>
		</td><td align="left" nowrap width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'items_per_page_filter', 0, l.fields:items_per_page_filter, 'Off' ) }"><br>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'items_per_page_filter', 1, l.fields:items_per_page_filter, 'On' ) }">
		</td></tr>

		<tr><td colspan="2">&nbsp;</td></tr>

		<tr><td>
			&nbsp;
		</td><td width="100%">
			<input type="hidden" name="{ encodeentities( l.field_prefix ) $ 'advanced' }" value="{ encodeentities( l.fields:advanced ) }">
			<MvIF EXPR = "{ l.fields:advanced }">
				<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 0 ); return false;' }">Point + Click Mode</a>
			<MvELSE>
				<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 1 ); return false;' }">Advanced Mode</a>
			</MvIF>
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, ( l.item $ '_dimensions' ) ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'constrain,b_width,b_height' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item $ '_dimensions' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<tr>
		<td align="left" valign="top" nowrap><b>Product Image:</b></td>
		<td align="left" width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'constrain', 'N', l.fields:constrain, 'No constraints' ) }"><br>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'constrain', 'B', l.fields:constrain, 'Resize to fit within bounding box:' ) }"> 
			<input type="text" name="{ encodeentities( l.field_prefix ) $ 'b_width' }" value="{ encodeentities( l.fields:b_width ) }" style="text-align:right;" size="5" /> x 
			<input type="text" name="{ encodeentities( l.field_prefix ) $ 'b_height' }" value="{ encodeentities( l.fields:b_height ) }" style="text-align:right;" size="5" /> pixels
		</td>
		</tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:mode"							VALUE = "ctgy">
	<MvASSIGN NAME = "l.settings:linkexcludecat"				VALUE = "1">
	<MvASSIGN NAME = "l.settings:format"						VALUE = "X">
	<MvASSIGN NAME = "l.settings:field_name"					VALUE = "1">
	<MvASSIGN NAME = "l.settings:field_code"					VALUE = "1">
	<MvASSIGN NAME = "l.settings:field_sku"						VALUE = "0">
	<MvASSIGN NAME = "l.settings:field_price"					VALUE = "1">
	<MvASSIGN NAME = "l.settings:field_desc"					VALUE = "0">
	<MvASSIGN NAME = "l.settings:button_add"					VALUE = "1">
	<MvASSIGN NAME = "l.settings:button_buy"					VALUE = "1">
	<MvASSIGN NAME = "l.settings:button_wish"					VALUE = "0">
	<MvASSIGN NAME = "l.settings:price"							VALUE = "base">
	<MvASSIGN NAME = "l.settings:additionalprice"				VALUE = "">
	<MvASSIGN NAME = "l.settings:displaydiscounts"				VALUE = 0>
	<MvASSIGN NAME = "l.settings:predictdiscounts"				VALUE = 0>
	<MvASSIGN NAME = "l.settings:image"							VALUE = "T">
	<MvASSIGN NAME = "l.settings:invmsg"						VALUE = "L">
	<MvASSIGN NAME = "l.settings:col_num"						VALUE = "2">
	<MvASSIGN NAME = "l.settings:per_page"						VALUE = "10">
	<MvASSIGN NAME = "l.settings:page_disp_count"				VALUE = "3">
	<MvASSIGN NAME = "l.settings:items_per_page_filter"			VALUE = "1">
	<MvASSIGN NAME = "l.settings:constrain"						VALUE = "B">
	<MvASSIGN NAME = "l.settings:b_width"						VALUE = "234">
	<MvASSIGN NAME = "l.settings:b_height"						VALUE = "234">
	<MvASSIGN NAME = "l.settings:sort_by:switch"				VALUE = "1">
	<MvASSIGN NAME = "l.settings:sort_by:name"					VALUE = "0">
	<MvASSIGN NAME = "l.settings:sort_by:code"					VALUE = "0">
	<MvASSIGN NAME = "l.settings:sort_by:best"					VALUE = "1">
	<MvASSIGN NAME = "l.settings:sort_by:price"					VALUE = "1">
	<MvASSIGN NAME = "l.settings:sort_by:new"					VALUE = "1">
	<MvASSIGN NAME = "l.settings:sort_by:old"					VALUE = "1">
	<MvASSIGN NAME = "l.settings:sort_by:updated"				VALUE = "0">
	<MvASSIGN NAME = "l.settings:sort_by:relevance"				VALUE = "0">
	<MvASSIGN NAME = "l.settings:default_sort"					VALUE = "disp_order">
	<MvASSIGN NAME = "l.settings:reverse_default_disp_order" 	VALUE = "0">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item EQ 'facets' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.settings:imagemachine_enabled"		VALUE = "{ CSSUIProductList_AssociatedImageMachine_EnabledAtAssign( l.page, l.item, l.settings ) }">

	<MvEVAL EXPR = "{ Generate_Code( l.module, l.settings, l.template_source, l.item ) }">

	<MvASSIGN NAME = "l.settings:template_filename"			VALUE = "{ tolower( l.page:code ) $ '-' $ tolower( l.item ) $ '.mvc' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.template_source, l.settings, l.settings:template_filename ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'urls' ) OR
					NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'buttons' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item EQ 'facets' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:template_filename ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item EQ 'facets' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:template_filename, l.source_templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:template_filename, l.dest_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.dest_settings"						VALUE = "{ l.source_settings }">
	<MvASSIGN NAME = "l.dest_settings:template_filename"	VALUE = "{ l.dest_managedtemplate:version:settings:template_filename }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_managedtemplate, '', l.source_templateversion:source, l.dest_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.source_branch:id EQ l.dest_branch:id }">	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-PLST-00013', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' on branch \'' $ l.dest_branch:name $ '\'' ) }">
	<MvELSE>													<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-PLST-00014', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' from branch \'' $ l.source_branch:name $ '\' to branch \'' $ l.dest_branch:name $ '\'' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Create_Pagination" PARAMETERS = "mode, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:page_disp_count EQ 0 OR l.settings:per_page EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.url" 				VALUE = "">
	<MvASSIGN NAME = "l.additional_params" 	VALUE = "">
	<MvASSIGN NAME = "l.screen"				VALUE = "{ toupper( g.Screen ) }">

	<MvIF EXPR = "{ ISNULL g.Session:uri:uri }">
		<MvASSIGN NAME = "l.flags:sep" 		VALUE = 1>
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Feature_URI_DB ].URISettings_Load_Cached( l.urisettings ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ g.Secure }"> 		<MvASSIGN NAME = "l.flags" VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Flags( 'secure,sep' ) }">
		<MvELSE> 							<MvASSIGN NAME = "l.flags" VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Flags( 'sep' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.url"			VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.urisettings, l.flags, g.Session:uri:uri, '' ) }">
	</MvIF>

	<MvIF EXPR = "{ tolower( l.mode ) EQ 'ctgy' }">		
		<MvASSIGN NAME = "l.offset_name"	VALUE = "CatListing">
		<MvASSIGN NAME = "l.next_offset"	VALUE = "{ g.CatListingNextOffset }">

		<MvIF EXPR = "{ ( ISNULL l.url ) AND ( NOT ISNULL g.Category_Code ) }">
			<MvASSIGN NAME = "l.url"		VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Category_Code_URL( g.Category_Code, l.flags ) }">
		</MvIF>

		<MvIF EXPR = "{ l.screen NE 'CTGY' }">
			<MvASSIGN NAME = "l.additional_params_link_params" 	VALUE = "{ l.additional_params_link_params $ 'Category_Code=' $ encodeattribute( g.Category_Code ) $ '&' }">
		</MvIF>
	<MvELSEIF EXPR = "{ tolower( l.mode ) EQ 'plst' }">	
		<MvASSIGN NAME = "l.offset_name"	VALUE = "All">
		<MvASSIGN NAME = "l.next_offset"	VALUE = "{ g.AllNextOffset }">
	<MvELSEIF EXPR = "{ tolower( l.mode ) EQ 'relp' }">	
		<MvASSIGN NAME = "l.offset_name"	VALUE = "Related">
		<MvASSIGN NAME = "l.next_offset"	VALUE = "{ g.RelatedNextOffset }">

		<MvIF EXPR = "{ ( ISNULL l.url ) AND ( NOT ISNULL g.Product_Code ) }">
			<MvASSIGN NAME = "l.url"		VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Product_Code_URL( g.Product_Code, l.flags ) }">
		</MvIF>

		<MvIF EXPR = "{ l.screen NE 'PROD' }">
			<MvASSIGN NAME = "l.additional_params_link_params" 	VALUE = "{ l.additional_params_link_params $ 'Product_Code=' $ encodeattribute( g.Product_Code ) $ '&' }">
		</MvIF>
	<MvELSEIF EXPR = "{ tolower( l.mode ) EQ 'srch' }">	
		<MvASSIGN NAME = "l.offset_name"	VALUE = "Search">
		<MvASSIGN NAME = "l.next_offset"	VALUE = "{ g.SearchNextOffset }">
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.url }">
		<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code_Cached( l.screen, l.page ) }">
			<MvASSIGN NAME = "l.url"		VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Page_URL( l.page, l.flags ) }">
		<MvELSE>
			<MvASSIGN NAME = "l.url"		VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Screen_URL( l.screen, l.flags ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.Search }">																<MvASSIGN NAME = "l.additional_params" VALUE = "{ l.additional_params $ '&Search=' 				$ encodeattribute( g.Search ) }">			</MvIF>
	<MvIF EXPR = "{ ( l.offset_name NE 'Search' ) 		AND NOT ISNULL g.SearchOffset }">		<MvASSIGN NAME = "l.additional_params" VALUE = "{ l.additional_params $ '&SearchOffset=' 		$ encodeattribute( g.SearchOffset ) }">		</MvIF>
	<MvIF EXPR = "{ ( l.offset_name NE 'Related' ) 		AND NOT ISNULL g.RelatedOffset }">		<MvASSIGN NAME = "l.additional_params" VALUE = "{ l.additional_params $ '&RelatedOffset=' 		$ encodeattribute( g.RelatedOffset ) }">	</MvIF>
	<MvIF EXPR = "{ ( l.offset_name NE 'CatListing' ) 	AND NOT ISNULL g.CatListingOffset }">	<MvASSIGN NAME = "l.additional_params" VALUE = "{ l.additional_params $ '&CatListingOffset=' 	$ encodeattribute( g.CatListingOffset ) }">	</MvIF>
	<MvIF EXPR = "{ ( l.offset_name NE 'All' )			AND NOT ISNULL g.AllOffset }">			<MvASSIGN NAME = "l.additional_params" VALUE = "{ l.additional_params $ '&AllOffset=' 			$ encodeattribute( g.AllOffset ) }">		</MvIF>
	<MvIF EXPR = "{ NOT ISNULL g.Per_Page }">													<MvASSIGN NAME = "l.additional_params" VALUE = "{ l.additional_params $ '&Per_Page=' 			$ encodeattribute( g.Per_Page ) }">			</MvIF>
	<MvIF EXPR = "{ NOT ISNULL g.Sort_By }">													<MvASSIGN NAME = "l.additional_params" VALUE = "{ l.additional_params $ '&Sort_By=' 			$ encodeattribute( g.Sort_By ) }">			</MvIF>

	<MvFOREACH ITERATOR = "l.applied_facet" ARRAY = "l.all_settings:facets:applied_facets">
		<MvFOREACH ITERATOR = "l.value" ARRAY = "l.applied_facet:input_values" COUNT = "{ l.applied_facet:input_value_count }">
			<MvASSIGN NAME = "l.additional_params" 		VALUE = "{ l.additional_params $ '&' $ encodeattribute( l.applied_facet:code ) $ '=' $ encodeattribute( l.value ) }">
		</MvFOREACH>
	</MvFOREACH>

	<MvASSIGN NAME = "l.page_count"						VALUE = "{ ceil( l.settings:total_prod_count / l.settings:per_page ) }">
	<MvASSIGN NAME = "l.settings:page_links:last_page"	VALUE = "{ l.page_count }">
	
	<MvIF EXPR = "{ l.next_offset }">	<MvASSIGN NAME = "l.settings:page_links:current_page" VALUE = "{ ceil( l.next_offset / l.settings:per_page ) }">
	<MvELSE>							<MvASSIGN NAME = "l.settings:page_links:current_page" VALUE = "{ ceil( l.settings:total_prod_count / l.settings:per_page ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| Keeping xxx_link values for backward compatibility
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.params" 									VALUE = "{ l.offset_name $ 'Offset=' $ encodeattribute( ( l.settings:page_links:current_page ) * l.settings:per_page ) $ '&Offset=' $ encodeattribute( ( l.settings:page_links:current_page ) * l.settings:per_page ) $ l.additional_params }">
	<MvASSIGN NAME = "l.settings:page_links:next_link_params"		VALUE = "{ l.additional_params_link_params $ l.params }">
	<MvASSIGN NAME = "l.settings:page_links:next_link"				VALUE = "{ l.url $ l.params $ l.additional_params_link }">

	<MvASSIGN NAME = "l.params" 									VALUE = "{ l.offset_name $ 'Offset=' $ encodeattribute( ( l.settings:page_links:current_page - 2 ) * l.settings:per_page ) $ '&Offset=' $ encodeattribute( ( l.settings:page_links:current_page - 2 ) * l.settings:per_page ) $ l.additional_params  }">
	<MvASSIGN NAME = "l.settings:page_links:prev_link_params"		VALUE = "{ l.additional_params_link_params $ l.params }">
	<MvASSIGN NAME = "l.settings:page_links:prev_link"				VALUE = "{ l.url $ l.params $ l.additional_params_link }">

	<MvASSIGN NAME = "l.params" 									VALUE = "{ l.offset_name $ 'Offset=0&Offset=0' $ l.additional_params }">
	<MvASSIGN NAME = "l.settings:page_links:first_link_params"		VALUE = "{ l.additional_params_link_params $ l.params }">
	<MvASSIGN NAME = "l.settings:page_links:first_link"				VALUE = "{ l.url $ l.params $ l.additional_params_link }">

	<MvIF EXPR = "{ floor( l.settings:total_prod_count / l.settings:per_page ) * l.settings:per_page EQ l.settings:total_prod_count }">
		<MvASSIGN NAME = "l.last_page_offset"						VALUE = "{ floor( ( l.settings:total_prod_count - 1 ) / l.settings:per_page ) * l.settings:per_page }">
		<MvASSIGN NAME = "l.params" 								VALUE = "{ l.offset_name $ 'Offset=' $ encodeattribute( floor( ( l.settings:total_prod_count - 1 ) / l.settings:per_page ) * l.settings:per_page ) $ '&Offset=' $ encodeattribute( floor( ( l.settings:total_prod_count - 1 ) / l.settings:per_page ) * l.settings:per_page ) $ l.additional_params }">
	<MvELSE>
		<MvASSIGN NAME = "l.last_page_offset"						VALUE = "{ floor( ( l.settings:total_prod_count ) / l.settings:per_page ) * l.settings:per_page }">
		<MvASSIGN NAME = "l.params" 								VALUE = "{ l.offset_name $ 'Offset=' $ encodeattribute( floor( l.settings:total_prod_count / l.settings:per_page ) * l.settings:per_page ) $ '&Offset=' $ encodeattribute( floor( l.settings:total_prod_count / l.settings:per_page ) * l.settings:per_page ) $ l.additional_params }">
	</MvIF>

	<MvASSIGN NAME = "l.settings:page_links:last_link_params"		VALUE = "{ l.additional_params_link_params $ l.params }">
	<MvASSIGN NAME = "l.settings:page_links:last_link"				VALUE = "{ l.url $ l.params $ l.additional_params_link }">

	<MvIF EXPR = "{ l.page_count LT l.settings:page_disp_count }">
		<MvASSIGN NAME = "l.left"	VALUE = "{ floor( ( l.page_count - 1 ) / 2 ) }">
		<MvASSIGN NAME = "l.right"	VALUE = "{ ceil( ( l.page_count - 1 ) / 2 ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.left"	VALUE = "{ floor( ( l.settings:page_disp_count - 1 ) / 2 ) }">
		<MvASSIGN NAME = "l.right"	VALUE = "{ ceil( ( l.settings:page_disp_count - 1 ) / 2 ) }">
	</MvIF>

	<MvASSIGN NAME = "l.counter"	VALUE = 0>
	<MvASSIGN NAME = "l.left_final" VALUE = 0>
	
	<MvWHILE EXPR = "{ l.counter LT l.left }">
		<MvIF EXPR = "{ ( ( l.settings:page_links:current_page - l.left + 1 ) + l.counter ) GT 1 }">
			<MvASSIGN NAME = "l.left_final"	VALUE = "{ l.left_final + 1 }">
		<MvELSE>
			<MvASSIGN NAME = "l.right" VALUE = "{ l.right + 1 }">
		</MvIF>
		
		<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
	</MvWHILE>
	
	<MvIF EXPR = "{ l.left_final EQ l.left }">
		<MvASSIGN NAME = "l.counter"	 VALUE = 0>
		<MvASSIGN NAME = "l.right_final" VALUE = 0>
		
		<MvWHILE EXPR = "{ l.counter LT l.right }">
			<MvIF EXPR = "{ ( ( l.settings:page_links:current_page + l.right - 1 ) - l.counter ) LT l.page_count }">
				<MvASSIGN NAME = "l.right_final" VALUE = "{ l.right_final + 1 }">
			<MvELSE>
				<MvASSIGN NAME = "l.left_final" VALUE = "{ l.left_final + 1 }">
			</MvIF>
			
			<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
		</MvWHILE>
		
		<MvASSIGN NAME = "l.pages" VALUE = "{ l.left_final + l.right_final + 1 }">
	<MvELSE>
		<MvASSIGN NAME = "l.pages" VALUE = "{ l.left_final + l.right + 1 }">
	</MvIF>

	<MvASSIGN NAME = "l.counter" VALUE = 0>
	
	<MvWHILE EXPR = "{ l.counter LT l.pages }">
		<MvASSIGN NAME = "l.params" 																		VALUE = "{ l.offset_name $ 'Offset=' $ encodeattribute( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) $ '&Offset=' $ encodeattribute( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) $ l.additional_params }">
		<MvASSIGN NAME = "l.settings:page_links:pages" INDEX = "{ l.counter + 1 }" MEMBER = "page_num" 		VALUE = "{ ( l.settings:page_links:current_page - l.left_final ) + l.counter }">
		<MvASSIGN NAME = "l.settings:page_links:pages" INDEX = "{ l.counter + 1 }" MEMBER = "link_params" 	VALUE = "{ l.additional_params_link_params $ l.params }">
		<MvASSIGN NAME = "l.settings:page_links:pages" INDEX = "{ l.counter + 1 }" MEMBER = "link" 			VALUE = "{ l.url $ l.params $ l.additional_params_link }">

		<MvIF EXPR = "{ ( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) EQ 0 }">					<MvASSIGN NAME = "l.settings:page_links:contains_first"	VALUE = "YES">	</MvIF>
		<MvIF EXPR = "{ ( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) EQ l.last_page_offset }">	<MvASSIGN NAME = "l.settings:page_links:contains_last"	VALUE = "YES">	</MvIF>
		
		<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
	</MvWHILE>
	
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item EQ 'facets' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.mode"											VALUE = "{ CSSUIProductList_Mode( l.item, l.settings:mode ) }">
	<MvASSIGN NAME = "l.facets_item_assigned"							VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Initialize_Item( l.all_settings, 'facets' ) }">

	<MvIF EXPR = "{ l.facets_item_assigned }">
		<MvASSIGN NAME = "l.all_settings:facets:applied_facet_count"	VALUE = "{ CSSUIProductList_AppliedFacets( l.all_settings, l.mode, l.all_settings:facets:applied_facets ) }">
	</MvIF>

    <MvIF EXPR = "{ l.settings:items_per_page_filter }">
		<MvIF EXPR = "{ ISNULL g.Per_Page OR NOT [ g.Module_Admin ].Validate_WholeNumber_Required( g.Per_Page ) }">
			<MvIF EXPR = "{ l.settings:per_page EQ 0 }">
				<MvASSIGN NAME = "g.Per_Page"							VALUE = -1>
			<MvELSE>
				<MvASSIGN NAME = "g.Per_Page"							VALUE = "{ l.settings:per_page }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ g.Per_Page GE 0 }">
				<MvASSIGN NAME = "l.settings:per_page"					VALUE = "{ g.Per_Page }">
			<MvELSE>
				<MvASSIGN NAME = "g.Per_Page"							VALUE = -1>
				<MvASSIGN NAME = "l.settings:per_page"					VALUE = 0>
			</MvIF>
		</MvIF>
    </MvIF>

	<MvIF EXPR = "{ g.Sort_By }">
		<MvASSIGN NAME = "l.sort_by"									VALUE = "{ g.Sort_By }">
	<MvELSE>
		<MvASSIGN NAME = "l.sort_by"									VALUE = "{ l.settings:default_sort }">
		<MvASSIGN NAME = "g.Sort_By"									VALUE = "{ l.settings:default_sort }">
	</MvIF>

	<MvIF EXPR = "{ l.sort_by EQ 'disp_order' AND 
	 				l.settings:reverse_default_disp_order }">
		<MvASSIGN NAME = "l.sort_by" 									VALUE = "disp_order_desc">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query( l.query ) }">

	<MvIF EXPR = "{ ( l.settings:price EQ 'sale' ) OR l.settings:advanced }">
		<MvIF EXPR = "{ [ g.Module_Feature_PGR_UT ].DiscountSignature_Calculate_Cached( g.Basket:cust_id, l.discount_signature ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query_Set_DiscountSignature( l.query, l.discount_signature ) }">
		</MvIF>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query_OutputCustomFields( l.query, l.settings:fields_custom, miva_array_elements( l.settings:fields_custom ), l.all_settings:customfield_names ) }">

	<MvIF EXPR = "{ l.mode EQ 'ctgy' }">
		<MvREFERENCE NAME = "l.settings:products_on_page_count"			VARIABLE = "l.all_settings:category_product_count">

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query_Category( l.query, l.all_settings:category ) }">
	<MvELSEIF EXPR = "{ l.mode EQ 'plst' }">
		<MvREFERENCE NAME = "l.settings:products_on_page_count"			VARIABLE = "l.all_settings:all_product_count">
	<MvELSEIF EXPR = "{ l.mode EQ 'relp' }">
		<MvREFERENCE NAME = "l.settings:products_on_page_count"			VARIABLE = "l.all_settings:related_product_count">

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query_RelatedProducts( l.query, l.all_settings:product ) }">
	<MvELSEIF EXPR = "{ l.mode EQ 'srch' }">
		<MvREFERENCE NAME = "l.settings:products_on_page_count"			VARIABLE = "l.all_settings:search_product_count">

		<MvASSIGN NAME = "g.Search"										VALUE = "{ trim( g.Search ) }">
		<MvIF EXPR = "{ ( ISNULL g.Search ) AND ( ( NOT l.facet_items_assigned ) OR ( l.all_settings:facets:applied_facet_count EQ 0 ) ) }">
			<MvASSIGN NAME = "l.product_count"							VALUE = 0>
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>
		
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query_Search( l.query, trim( g.Search ) ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query_Search_Origin( l.query, 'Page: ' $ l.all_settings:page:code ) }">
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query_ORDER_BY( l.query, l.sort_by ) }">

	<MvIF EXPR = "{ l.facets_item_assigned }">
		<MvASSIGN NAME = "l.all_settings:facets:facet_count"			VALUE = "{ CSSUIProductList_PossibleFacets( l.query, l.all_settings, l.mode, l.all_settings:facets:applied_facets, l.all_settings:facets:applied_facet_count, l.all_settings:facets:facets ) }">

		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query_Facets( l.query, l.all_settings:facets:applied_facets, l.all_settings:facets:applied_facet_count ) }">
	</MvIF>

	<MvREFERENCE NAME = "l.offset"										VARIABLE = "{ 'g.' $ Generate_Template_Prefix( l.item, l.settings ) $ 'Offset' }">
	<MvREFERENCE NAME = "l.next_offset"									VARIABLE = "{ 'g.' $ Generate_Template_Prefix( l.item, l.settings ) $ 'NextOffset' }">
	<MvREFERENCE NAME = "l.prev_offset"									VARIABLE = "{ 'g.' $ Generate_Template_Prefix( l.item, l.settings ) $ 'PrevOffset' }">

	<MvASSIGN NAME = "l.prev_offset"									VALUE = "{ l.offset - l.settings:per_page }">
	<MvIF EXPR = "{ l.prev_offset LT 0 }">
		<MvASSIGN NAME = "l.prev_offset"								VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.settings:products_on_page_count"				VALUE = "{ [ g.Module_Library_DB ].Runtime_ProductList_Load( l.query, l.offset, l.settings:per_page, l.next_offset, l.settings:total_prod_count, l.settings:products ) }">

	<MvASSIGN NAME = "l.null"											VALUE = "{ Create_Pagination( l.mode, l.all_settings, l.settings ) }">

	<MvIF EXPR = "{ ( l.mode EQ 'ctgy' ) AND ( NOT l.settings:linkexcludecat ) }">
		<MvASSIGN NAME = "l.link_flags:category_code"					VALUE = "{ l.all_settings:category:code }">
	</MvIF>

	<MvASSIGN NAME = "l.have_unprecalculated_products"					VALUE = 0>

	<MvFOREACH ITERATOR = "l.product" ARRAY = "l.settings:products" COUNT = "{ l.settings:products_on_page_count }">
		<MvIF EXPR = "{ ISNULL l.product:sale_price }">
			<MvASSIGN NAME = "l.have_unprecalculated_products"			VALUE = 1>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_Runtime( l.product ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.null"										VALUE = "{ [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_ImageTypes( l.product:id, 0, l.product, l.all_settings, l.settings:imagetypes, l.settings:constrain, l.settings:b_width, l.settings:b_height ) }">
		<MvASSIGN NAME = "l.product:link"								VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Product_URL( l.product, l.link_flags ) }">
	</MvFOREACH>

	<MvCOMMENT>
	|
	| We have to create a discount state if we are displaying specific discount totals (predictdiscounts) or if we
	| are displaying sale price or in advanced mode and at least one of our products did not have a sale price
	| already calculated
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.settings:predictdiscounts OR ( ( ( l.settings:price EQ 'sale' ) OR l.settings:advanced ) AND l.have_unprecalculated_products ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_CreateEmpty( l.discount_state ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_UT ].DiscountState_SetCustomerInformationFromBasket( g.Basket, l.discount_state ) }">

		<MvASSIGN NAME = "l.pricegroup_count" VALUE = "{ [ g.Module_Feature_PGR_UT ].ResolvedPriceGroupAndModuleList_Load_Customer_Cached( g.Basket:cust_id, l.pricegroups ) }">
		
		<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Set_PriceGroups( l.discount_state, l.pricegroups, l.pricegroup_count ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_Disable_Unused_Item_PriceGroups( l.discount_state ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_Predict_Baseline_Discounts( l.discount_state ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFOREACH ITERATOR = "l.product" ARRAY = "l.settings:products" COUNT = "{ l.settings:products_on_page_count }">
			<MvIF EXPR = "{ l.product:id LE 0 }">																<MvFOREACHCONTINUE>
			<MvELSEIF EXPR = "{ ( NOT l.settings:predictdiscounts ) AND ( NOT ISNULL l.product:sale_price ) }">	<MvFOREACHCONTINUE>
			</MvIF>

			<MvIF EXPR = "{ NOT ISNULL l.product:base_price }">
				<MvASSIGN NAME = "l.product:price"					VALUE = "{ l.product:base_price }">
			</MvIF>

			<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_Predict_Product_Discounts_WithProductSubscriptionTerm( l.discount_state, l.product, 0, 0, 1, l.product:price, l.product:discounts, l.product:discount_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.product:formatted_price"			VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.product:price ) }">

			<MvFOREACH ITERATOR = "l.discount" ARRAY = "l.product:discounts" COUNT = "{ l.product:discount_count }">
				<MvASSIGN NAME = "l.discount:formatted_discount"	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.discount:discount ) }">
			</MvFOREACH>
		</MvFOREACH>
	</MvIF>

	<MvIF EXPR = "{ l.facets_item_assigned }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_Facets( l.all_settings:facets:facets, l.all_settings:facets:facet_count ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUIProductList_AppliedFacets" PARAMETERS = "all_settings var, mode, applied_facets var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.applied_facets"			VALUE = "">
	<MvASSIGN NAME = "l.applied_facet_count"	VALUE = 0>

	<MvIF EXPR = "{ l.mode EQ 'ctgy' }">	<MvASSIGN NAME = "l.facet_count" VALUE = "{ [ g.Module_Feature_FCT_UT ].FacetList_Load_Category_Cached( l.all_settings:category, l.facets ) }">
	<MvELSE>								<MvASSIGN NAME = "l.facet_count" VALUE = "{ [ g.Module_Feature_FCT_UT ].FacetList_Load_All_Cached( l.facets ) }">
	</MvIF>

	<MvFOREACH ITERATOR = "l.facet" ARRAY = "l.enabled_facets" COUNT = "{ miva_array_filter_ref( l.facets, 1, l.facet, 'l.facet:handle EQ \'show\' OR l.facet:handle EQ \'hide\'', l.enabled_facets ) }">
		<MvREFERENCE NAME = "l.facet_value" VARIABLE = "{ 'g.' $ l.facet:code }">

		<MvIF EXPR = "{ ISNULL l.facet_value }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.applied_facet"								VALUE = "{ l.facet }">

		<MvIF EXPR = "{ miva_array_elements( l.facet_value ) EQ 0 }">
			<MvASSIGN NAME = "l.applied_facet:input_value_count"		VALUE = "{ miva_array_insert_var( l.applied_facet:input_values, l.facet_value, -1 ) }">
		<MvELSE>
			<MvFOREACH ITERATOR = "l.individual_facet_value" ARRAY = "l.facet_value">
				<MvASSIGN NAME = "l.applied_facet:input_value_count"	VALUE = "{ miva_array_insert_var( l.applied_facet:input_values, l.individual_facet_value, -1 ) }">
			</MvFOREACH>
		</MvIF>

		<MvASSIGN NAME = "l.applied_facet_count"						VALUE = "{ miva_array_insert_var( l.applied_facets, l.applied_facet, -1 ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = "{ l.applied_facet_count }">
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUIProductList_EnabledFacets" PARAMETERS = "query var, all_settings var, mode, enabled_facets var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.mode EQ 'ctgy' }">	<MvASSIGN NAME = "l.facet_count" VALUE = "{ [ g.Module_Feature_FCT_UT ].FacetList_Load_Category_Query_Cached( l.all_settings:category, l.query, l.facets ) }">
	<MvELSE>								<MvASSIGN NAME = "l.facet_count" VALUE = "{ [ g.Module_Feature_FCT_UT ].FacetList_Load_Query_Cached( l.query, l.facets ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ miva_array_filter_ref( l.facets, 1, l.facet, 'l.facet:handle EQ \'show\'', l.enabled_facets ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUIProductList_PossibleFacets" PARAMETERS = "query var, all_settings var, mode, applied_facets var, applied_facet_count, possible_facets var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.enabled_facet_count"	VALUE = "{ CSSUIProductList_EnabledFacets( l.query, l.all_settings, l.mode, l.enabled_facets ) }">
	<MvIF EXPR = "{ l.enabled_facet_count EQ 0 }">
		<MvASSIGN NAME = "l.possible_facets"	VALUE = "">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.enabled_facet" ARRAY = "l.enabled_facets" COUNT = "{ l.enabled_facet_count }">
		<MvIF EXPR = "{ miva_array_search( l.applied_facets, 1, l.applied_facet, '( l.applied_facet:module:code EQ l.enabled_facet:module:code ) AND ( l.applied_facet:code EQ l.enabled_facet:code )' ) }">
			<MvASSIGN NAME = "l.enabled_facet:input_values"			VALUE = "{ l.applied_facet:input_values }">
			<MvASSIGN NAME = "l.enabled_facet:input_value_count"	VALUE = "{ l.applied_facet:input_value_count }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_FCT_UT ].FacetList_Load_Values_Query( l.query, l.enabled_facets, l.enabled_facet_count, l.possible_facets ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUIProductList_Mode" PARAMETERS = "item_code, mode" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item_code EQ 'category_listing' }">		<MvFUNCTIONRETURN VALUE = "ctgy">
	<MvELSEIF EXPR = "{ l.item_code EQ 'all_products' }">		<MvFUNCTIONRETURN VALUE = "plst">
	<MvELSEIF EXPR = "{ l.item_code EQ 'search_results' }">		<MvFUNCTIONRETURN VALUE = "srch">
	<MvELSEIF EXPR = "{ l.item_code EQ 'related_products' }">	<MvFUNCTIONRETURN VALUE = "relp">
	<MvELSEIF EXPR = "{ ISNULL l.mode }">						<MvFUNCTIONRETURN VALUE = "ctgy">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.mode }">
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUIProductList_AssociatedImageMachine_IsCurrentlyEnabled" PARAMETERS = "page_code, item_code" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.imagemachine_item_code"	VALUE = "{ l.item_code $ '_imagemachine' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Item_Load_Code( l.imagemachine_item_code, l.item ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_Code( l.page_code, l.page ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].PageXItem_Load( l.page:id, l.item:id, l.pagexitem ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.imagemachine_settings" VARIABLE = "l.page">
		<MvMEMBER NAME = "settings">
		<MvMEMBER NAME = "{ l.imagemachine_item_code }">
	</MvREFERENCEARRAY>

	<MvFUNCTIONRETURN VALUE = "{ l.imagemachine_settings:enabled }">
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUIProductList_AssociatedImageMachine_EnabledAtAssign" PARAMETERS = "page var, item_code, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:imagemachine_enabled }">
		<MvFUNCTIONRETURN VALUE = "{ l.settings:imagemachine_enabled }">
	</MvIF>

	<MvASSIGN NAME = "l.imagemachine_item_code"	VALUE = "{ l.item_code $ '_imagemachine' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Item_Load_Code( l.imagemachine_item_code, l.item ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].PageXItem_Load( l.page:id, l.item:id, l.pagexitem ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.imagemachine_settings" VARIABLE = "l.page">
		<MvMEMBER NAME = "settings">
		<MvMEMBER NAME = "{ l.imagemachine_item_code }">
	</MvREFERENCEARRAY>

	<MvFUNCTIONRETURN VALUE = "{ l.imagemachine_settings:enabled }">
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUIProductList_AssociatedImageMachine_WillBeEnabled" PARAMETERS = "page_code, item_code" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.imagemachine_item_code"	VALUE = "{ l.item_code $ '_imagemachine' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Item_Load_Code( l.imagemachine_item_code, l.item ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_Code( l.page_code, l.page ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| Because removes are processed before this function will be called, if the imagemachine was unassigned on
	| this update, PageXItem_Load will return 0, which simplifies this function (slightly).
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].PageXItem_Load( l.page:id, l.item:id, l.pagexitem ) }">
		<MvCOMMENT>
		|
		| If the imagemachine item is already assigned, then look at its global "fields" variable
		| to see if it will be enabled after this update
		|
		</MvCOMMENT>

		<MvREFERENCEARRAY NAME = "l.imagemachine_settings" VARIABLE = "g.Page_Settings">
			<MvMEMBER NAME = "{ l.imagemachine_item_code }">
		</MvREFERENCEARRAY>

		<MvFUNCTIONRETURN VALUE = "{ l.imagemachine_settings:enabled }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| If the imagemachine item is not currently assigned, then we have to see if it IS being assigned as
	| part of this update, and, if so, if it has existing settings that say it is enabled
	|
	</MvCOMMENT>

	<MvFOREACH INDEX = "l.item_pos" ITERATOR = "l.item" ARRAY = "g.Page_Item">
		<MvIF EXPR = "{ l.item EQ l.imagemachine_item_code }">
			<MvIF EXPR = "{ g.Page_Item_Assigned[ l.item_pos ] }">
				<MvCOMMENT>
				|
				| The item is being assigned, so the enabled status depends on whether
				| there are existing settings on the page already, and the enabled flag
				| is set to 1.
				|
				</MvCOMMENT>
			
				<MvIF EXPR = "{ NOT miva_member_exists( l.page:settings, l.imagemachine_item_code ) }">
					<MvFUNCTIONRETURN VALUE = 0>
				</MvIF>

				<MvREFERENCEARRAY NAME = "l.imagemachine_settings" VARIABLE = "l.page">
					<MvMEMBER NAME = "settings">
					<MvMEMBER NAME = "{ l.imagemachine_item_code }">
				</MvREFERENCEARRAY>

				<MvFUNCTIONRETURN VALUE = "{ l.imagemachine_settings:enabled }">
			</MvIF>

			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvCOMMENT>
	|
	| The item is not assigned, and is not being assigned
	|
	</MvCOMMENT>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Template_Prefix" PARAMETERS = "item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.mode" VALUE = "{ CSSUIProductList_Mode( l.item, l.settings:mode ) }">

	<MvIF EXPR = "{ l.mode EQ 'ctgy' }">		<MvFUNCTIONRETURN VALUE = "CatListing">
	<MvELSEIF EXPR = "{ l.mode EQ 'plst' }">	<MvFUNCTIONRETURN VALUE = "All">
	<MvELSEIF EXPR = "{ l.mode EQ 'relp' }">	<MvFUNCTIONRETURN VALUE = "Related">
	<MvELSEIF EXPR = "{ l.mode EQ 'srch' }">	<MvFUNCTIONRETURN VALUE = "Search">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_NextPrev_Offset_Fields" PARAMETERS = "item, settings var, position" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.mode" VALUE = "{ CSSUIProductList_Mode( l.item, l.settings:mode ) }">

	<MvCAPTURE VARIABLE = "l.code"><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.mode EQ 'plst' }">
				<MIVA STANDARDOUTPUTLEVEL = "text,html"><input type="hidden" name="Offset" value = "{ '&mvte:global:All' $ l.position $ 'Offset;' }" />
				<input type="hidden" name="AllOffset" value = "{ '&mvte:global:All' $ l.position $ 'Offset;' }" />
				<input type="hidden" name="CatListingOffset" value = "&mvte:global:CatListingOffset;" />
				<input type="hidden" name="RelatedOffset" value = "&mvte:global:RelatedOffset;" />
				<input type="hidden" name="SearchOffset" value = "&mvte:global:SearchOffset;" /><MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSEIF EXPR = "{ l.mode EQ 'ctgy' }">
				<MIVA STANDARDOUTPUTLEVEL = "text,html"><input type="hidden" name="Offset" value = "{ '&mvte:global:CatListing' $ l.position $ 'Offset;' }" />
				<input type="hidden" name="AllOffset" value = "&mvte:global:AllOffset;" />
				<input type="hidden" name="CatListingOffset" value = "{ '&mvte:global:CatListing' $ l.position $ 'Offset;' }" />
				<input type="hidden" name="RelatedOffset" value = "&mvte:global:RelatedOffset;" />
				<input type="hidden" name="SearchOffset" value = "&mvte:global:SearchOffset;" /><MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSEIF EXPR = "{ l.mode EQ 'relp' }">
				<MIVA STANDARDOUTPUTLEVEL = "text,html"><input type="hidden" name="Offset" value = "{ '&mvte:global:Related' $ l.position $ 'Offset;' }" />
				<input type="hidden" name="AllOffset" value = "&mvte:global:AllOffset;" />
				<input type="hidden" name="CatListingOffset" value = "&mvte:global:CatListingOffset;" />
				<input type="hidden" name="RelatedOffset" value = "{ '&mvte:global:Related' $ l.position $ 'Offset;' }" />
				<input type="hidden" name="SearchOffset" value = "&mvte:global:SearchOffset;" /><MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSEIF EXPR = "{ l.mode EQ 'srch' }">
				<MIVA STANDARDOUTPUTLEVEL = "text,html"><input type="hidden" name="Offset" value = "{ '&mvte:global:Search' $ l.position $ 'Offset;' }" />
				<input type="hidden" name="AllOffset" value = "&mvte:global:AllOffset;" />
				<input type="hidden" name="CatListingOffset" value = "&mvte:global:CatListingOffset;" />
				<input type="hidden" name="RelatedOffset" value = "&mvte:global:RelatedOffset;" />
				<input type="hidden" name="SearchOffset" value = "{ '&mvte:global:Search' $ l.position $ 'Offset;' }" /><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MIVA STANDARDOUTPUTLEVEL = "">
	</MvCAPTURE>

	<MvFUNCTIONRETURN VALUE = "{ l.code }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside_Deferred( l.all_settings, 'runtime_ui.js' ) }">

	<MvIF EXPR = "{ l.item NE 'facets' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.slider_facets"		VALUE = "">
	<MvASSIGN NAME = "l.slider_facet_count"	VALUE = 0>

	<MvFOREACH ITERATOR = "l.facet" ARRAY = "l.all_settings:facets:facets">
		<MvIF EXPR = "{ l.facet:type EQ 'rangeslider' }">
			<MvASSIGN NAME = "l.slider_facet_count" VALUE = "{ miva_array_insert( l.slider_facets, l.facet, -1 ) }">
		</MvIF>
	</MvFOREACH>

	<MvIF EXPR = "{ NOT l.slider_facet_count }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<script type="text/javascript">
		if ( !window.mm_facet_rangeslider_formatter )
		{
			window.mm_facet_rangeslider_formatter = new Object();
		}

		if ( !window.MMFacet_RangeSlider_FormatValue_AddHook )
		{
			window.MMFacet_RangeSlider_FormatValue_AddHook = function( module_code, fn )
			{
				window.mm_facet_rangeslider_formatter[ module_code ] = fn;
			}
		}

		if ( !window.MMFacet_RangeSlider_FormatValue_RemoveHook )
		{
			window.MMFacet_RangeSlider_FormatValue_RemoveHook = function( module_code )
			{
				delete window.mm_facet_rangeslider_formatter[ module_code ];
			}
		}

		if ( !window.MMFacet_RangeSlider_FormatValue )
		{
			window.MMFacet_RangeSlider_FormatValue = function( module_code, facet_code, value )
			{
				if ( window.mm_facet_rangeslider_formatter && window.mm_facet_rangeslider_formatter[ module_code ] && typeof window.mm_facet_rangeslider_formatter[ module_code ] === 'function' )
				{
					return window.mm_facet_rangeslider_formatter[ module_code ]( facet_code, value );
				}

				return stod_def( value, 0 );
			}
		}

		(function( obj, eventType, fn )
		{
			if ( obj.addEventListener )
			{
				obj.addEventListener( eventType, fn, false );
			}
			else if ( obj.attachEvent )
			{
				obj.attachEvent( 'on' + eventType, fn );
			}
		})( window, 'load', function() { MMFacet_RangeSlider_Initialize(); } );
	</script>

	<MvFOREACH ITERATOR = "l.facet" ARRAY = "l.all_settings:facets:facets">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.facet:module:module ].Module_Product_Facet_Value_Prompt_JavaScript( l.facet:module ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.product_backup"	VALUE = "{ l.all_settings:product }">

	<MvIF EXPR = "{ l.item EQ 'facets' }">	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( 'cssui-prodlist-facets.mvc', l.all_settings ) }">
	<MvELSE>								<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:template_filename, l.all_settings ) }">
	</MvIF>

	<MvREFERENCE NAME = "l.all_settings:product" VARIABLE = "l.product_backup">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Code" PARAMETERS = "module var, settings var, code var, item" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.mode" VALUE = "{ CSSUIProductList_Mode( l.item, l.settings:mode ) }">

	<MvIF EXPR = "{ l.settings:format EQ 'X' }">		
		<MvASSIGN NAME = "l.class"			VALUE = "expanded">
		<MvASSIGN NAME = "l.layout_code"	VALUE = "{ Generate_Code_Expanded( l.settings, l.item ) }">
	<MvELSEIF EXPR = "{ l.settings:format EQ 'L' }">
		<MvASSIGN NAME = "l.class" VALUE = "line-item">
		<MvASSIGN NAME = "l.layout_code"	VALUE = "{ Generate_Code_LineItem( l.settings, l.item ) }">
	</MvIF>

	<MvCAPTURE VARIABLE = "l.code">
	<MIVA STANDARDOUTPUTLEVEL = "text, html"><div class="{ l.class }"><div id="filter-items-container"><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:sort_by:switch }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<mvt:if expr="{ 'l.settings:' $ l.item $ ':total_prod_count GT 1' }">
		<div class="sorting">
			<form method="get" action="&mvte:urls:_self:auto_noparams;">	
				<mvt:if expr="{ 'tolower( l.settings:' $ l.item $ ':mode ) EQ \'ctgy\' AND toupper( g.Screen ) NE \'CTGY\'' }">
					<input type="hidden" name="Category_Code" value="&mvte:global:Category_Code;" />
				<mvt:elseif expr="{ 'tolower( l.settings:' $ l.item $ ':mode ) EQ \'relp\' AND toupper( g.Screen ) NE \'PROD\'' }">
					<input type="hidden" name="Product_Code" value="&mvte:global:Product_Code;" />
				</mvt:if>

				<mvt:item name="urls" param="hidden_params:_self:auto" />
				<input type="hidden" name="Search" value="&mvte:global:Search;" />
				<input type="hidden" name="Per_Page" value="&mvte:global:Per_Page;" />
				<mvt:foreach iterator="facet" array="facets:applied_facets">
					<mvt:foreach iterator="facet_value" array="facet:input_values">
						<input type="hidden" name="&mvte:facet:code;" value="&mvte:facet_value;" />
					</mvt:foreach>
				</mvt:foreach>

				<label for="Sort_By">Sort By:</label>
				<select id="Sort_By" name="Sort_By" onchange="MMProdList_UpdateQuery( this ); return true;">
<MIVA STANDARDOUTPUTLEVEL = "">

				<MvIF EXPR = "{ l.settings:default_sort EQ 'disp_order' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="{ 'ISNULL g.Sort_By' }">
						<option value="{ 'disp_order' }" selected="selected">Default</option>
					<mvt:else>
						<option value="{ 'disp_order' }">Default</option>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF>
				
				<MvIF EXPR = "{ l.settings:sort_by:name }">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea" 		INDEX = "1" VALUE = "Name Ascending">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea_code" 	INDEX = "1" VALUE = "name_asc">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_named" 		INDEX = "1" VALUE = "Name Descending">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_named_code" 	INDEX = "1" VALUE = "name_desc">
				</MvIF>
				
				<MvIF EXPR = "{ l.settings:sort_by:code }">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea" 		INDEX = "2" VALUE = "Code Ascending">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea_code" 	INDEX = "2" VALUE = "code_asc">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_named" 		INDEX = "2" VALUE = "Code Descending">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_named_code" 	INDEX = "2" VALUE = "code_desc">
				</MvIF>
				
				<MvIF EXPR = "{ l.settings:sort_by:best }">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea" 		INDEX = "3" VALUE = "Best Selling">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea_code" 	INDEX = "3" VALUE = "bestsellers">
				</MvIF>
				
				<MvIF EXPR = "{ l.settings:sort_by:price }">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea" 		INDEX = "4" VALUE = "Lowest Price">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea_code" 	INDEX = "4" VALUE = "price_asc">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_named" 		INDEX = "4" VALUE = "Highest Price">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_named_code" 	INDEX = "4" VALUE = "price_desc">
				</MvIF>
				
				<MvIF EXPR = "{ l.settings:sort_by:new }">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea" 		INDEX = "5" VALUE = "Newest Items">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea_code" 	INDEX = "5" VALUE = "newest">
				</MvIF>
				
				<MvIF EXPR = "{ l.settings:sort_by:old }">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea" 		INDEX = "6" VALUE = "Oldest Items">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea_code" 	INDEX = "6" VALUE = "oldest">
				</MvIF>
				
				<MvIF EXPR = "{ l.settings:sort_by:updated }">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea" 		INDEX = "7" VALUE = "Updated Items">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea_code" 	INDEX = "7" VALUE = "updated">
				</MvIF>
				
				<MvIF EXPR = "{ l.settings:sort_by:relevance }">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea" 		INDEX = "8" VALUE = "Relevance">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea_code" 	INDEX = "8" VALUE = "relevance">
				</MvIF>
				
				<MvASSIGN NAME = "l.null" VALUE = "{ miva_array_collapse( l.sorts ) }">

				<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:sort_custom">
					<MvIF EXPR = "{ [ g.Module_Library_DB ].Module_Load_Code_Cached( l.customfield:module, l.customfield_module ) }">
						<MvASSIGN NAME = "l.name"					VALUE = "{ [ g.Module_Root $ l.customfield_module:module ].Module_Product_Field_Name( l.customfield_module, l.customfield:code ) }">

						<MvASSIGN NAME = "l.sort:disp_namea"		VALUE = "{ l.name $ ' Ascending' }">
						<MvASSIGN NAME = "l.sort:disp_namea_code"	VALUE = "{ 'customfield:' $ l.customfield:module $ ':' $ l.customfield:code }">
						<MvASSIGN NAME = "l.sort:disp_named"		VALUE = "{ l.name $ ' Descending' }">
						<MvASSIGN NAME = "l.sort:disp_named_code"	VALUE = "{ 'customfield_desc:' $ l.customfield:module $ ':' $ l.customfield:code }">

						<MvASSIGN NAME = "l.null"					VALUE = "{ miva_array_insert_var( l.sorts, l.sort, -1 ) }">
					</MvIF>
				</MvFOREACH>
				
				<MvFOREACH ITERATOR = "l.sort" ARRAY = "l.sorts">
					<MvIF EXPR = "{ NOT ISNULL l.sort:disp_namea }"><MIVA STANDARDOUTPUTLEVEL = "text,html">
					<mvt:if expr="{ 'g.Sort_By EQ \'' $ encodeentities( l.sort:disp_namea_code ) $ '\'' }">
						<option value="{ encodeentities( l.sort:disp_namea_code ) }" selected="selected"><MvEVAL EXPR = "{ encodeentities( l.sort:disp_namea ) }"></option>
					<mvt:else>
						<option value="{ encodeentities( l.sort:disp_namea_code ) }"><MvEVAL EXPR = "{ encodeentities( l.sort:disp_namea ) }"></option>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
					</MvIF>
					
					<MvIF EXPR = "{ NOT ISNULL l.sort:disp_named }"><MIVA STANDARDOUTPUTLEVEL = "text,html">
					<mvt:if expr="{ 'g.Sort_By EQ \'' $ encodeentities( l.sort:disp_named_code ) $ '\'' }">
						<option value="{ encodeentities( l.sort:disp_named_code ) }" selected="selected"><MvEVAL EXPR = "{ encodeentities( l.sort:disp_named ) }"></option>
					<mvt:else>
						<option value="{ encodeentities( l.sort:disp_named_code ) }"><MvEVAL EXPR = "{ encodeentities( l.sort:disp_named ) }"></option>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
					</MvIF>
				</MvFOREACH>
				
				<MIVA STANDARDOUTPUTLEVEL = "text,html">
				</select>
			</form>
		</div></mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	
	<MvIF EXPR = "{ l.settings:items_per_page_filter }">
		<MvIF EXPR = "{ l.settings:per_page EQ 0 OR ISNULL l.settings:per_page }">
			<MvASSIGN NAME = "l.per_page" VALUE = 10>
		<MvELSE>
			<MvASSIGN NAME = "l.per_page" VALUE = "{ l.settings:per_page }">
		</MvIF>
	
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="{ 'l.settings:' $ l.item $ ':total_prod_count GT 1' }">
			<div class="per-page">
				<form method="get" action="&mvte:urls:_self:auto_noparams;">	
					<mvt:if expr="{ 'tolower( l.settings:' $ l.item $ ':mode ) EQ \'ctgy\' AND toupper( g.Screen ) NE \'CTGY\'' }">
						<input type="hidden" name="Category_Code" value="&mvte:global:Category_Code;" />
					<mvt:elseif expr="{ 'tolower( l.settings:' $ l.item $ ':mode ) EQ \'relp\' AND toupper( g.Screen ) NE \'PROD\'' }">
						<input type="hidden" name="Product_Code" value="&mvte:global:Product_Code;" />
					</mvt:if>

					<mvt:item name="urls" param="hidden_params:_self:auto" />
					<input type="hidden" name="Search" value="&mvte:global:Search;" />
					<input type="hidden" name="Sort_By" value="&mvte:global:Sort_By;" />
					<mvt:foreach iterator="facet" array="facets:applied_facets">
						<mvt:foreach iterator="facet_value" array="facet:input_values">
							<input type="hidden" name="&mvte:facet:code;" value="&mvte:facet_value;" />
						</mvt:foreach>
					</mvt:foreach>

					<label for="Per_Page">View:</label>
					<select id="Per_Page" name="Per_Page" onchange="MMProdList_UpdateQuery( this ); return true;">
<MIVA STANDARDOUTPUTLEVEL = "">

					<MvASSIGN NAME = "l.multiplier" VALUE = 1>
					<MvASSIGN NAME = "l.count" VALUE = 1>
						
					<MvWHILE EXPR = "{ l.count LE 3 }">
						<MvASSIGN NAME = "l.items_per" VALUE = "{ l.per_page * l.multiplier }">
						<MvASSIGN NAME = "l.multiplier" VALUE = "{ l.multiplier + l.count }"><MIVA STANDARDOUTPUTLEVEL = "text,html">
						<mvt:if expr="{ 'g.Per_Page EQ ' $ encodeentities( l.items_per ) }">
							<option value="{ encodeentities( l.items_per ) }" selected="selected"><MvEVAL EXPR = "{ encodeentities( l.items_per ) }"></option>
						<mvt:else>
							<option value="{ encodeentities( l.items_per ) }"><MvEVAL EXPR = "{ encodeentities( l.items_per ) }"></option>
						</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
						<MvASSIGN NAME = "l.count" VALUE = "{ l.count + 1 }">
					</MvWHILE>
						
						<MIVA STANDARDOUTPUTLEVEL = "text,html">
						<mvt:if expr="g.Per_Page EQ -1">
							<option value="-1" selected="selected">All</option>
						<mvt:else>
							<option value="-1">All</option>
						</mvt:if>
					</select>
				</form>
			</div>
		</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	
	<MIVA STANDARDOUTPUTLEVEL = "text, html">
	<mvt:if expr="{ 'l.settings:' $ l.item $ ':page_links:last_page GT 1' }">
		<div class="page-links">
			<span class="page-links-title">Page(s):</span>
			<span class="page-links-container">
				<mvt:if expr="{ 'l.settings:' $ l.item $ ':page_links:current_page NE 1' }">
					<a href="{ '&mvte:urls:_self:auto_sep;&mvte:' $ l.item $ ':page_links:prev_link_params;' }" class="page-links-previous">&lt;</a>
				<mvt:else>
					<span class="page-links-previous page-links-deactivated">&lt;</span>
				</mvt:if>
				<mvt:if expr="{ 'l.settings:' $ l.item $ ':page_links:current_page NE l.settings:' $ l.item $ ':page_links:last_page' }">
					<a href="{ '&mvte:urls:_self:auto_sep;&mvte:' $ l.item $ ':page_links:next_link_params;' }" class="page-links-next">&gt;</a>
				<mvt:else>
					<span class="page-links-next page-links-deactivated">&gt;</span>
				</mvt:if>
				<span class="page-disp">
					<mvt:if expr="{ 'NOT l.settings:' $ l.item $ ':page_links:contains_first' }">
						<a href="{ '&mvte:urls:_self:auto_sep;&mvte:' $ l.item $ ':page_links:first_link_params;' }" class="page-links-inactive">1</a>...
					</mvt:if>
					<mvt:foreach iterator="pages" array="{ l.item $ ':page_links:pages' }">
						<mvt:if expr="{ 'l.settings:' $ l.item $ ':page_links:current_page EQ l.settings:pages:page_num' }">
							<span class="page-links-active">&mvte:pages:page_num;</span>
						<mvt:else>
							<a href="&mvte:urls:_self:auto_sep;&mvte:pages:link_params;" class="page-links-inactive">&mvte:pages:page_num;</a>
						</mvt:if>
					</mvt:foreach>
					<mvt:if expr="{ 'NOT l.settings:' $ l.item $ ':page_links:contains_last' }">
						...<a href="{ '&mvte:urls:_self:auto_sep;&mvte:' $ l.item $ ':page_links:last_link_params;' }" class="page-links-inactive"><MvEVAL EXPR = "{ '&mvte:' $ l.item $ ':page_links:last_page;' }"></a>
					</mvt:if>
				</span>
			</span>
		</div>
		
	</mvt:if>
	</div><div class="clear"></div>
	<MvEVAL EXPR="{ l.layout_code }">
	<mvt:if expr="{ 'NOT l.settings:' $ l.item $ ':page_disp_count GT 0' }">
		<mvt:if expr="{ 'g.' $ Generate_Template_Prefix( l.item, l.settings ) $ 'Offset OR g.' $ Generate_Template_Prefix( l.item, l.settings ) $ 'NextOffset' }">
		<div class="next-previous">
			<mvt:if expr="{ 'g.' $ Generate_Template_Prefix( l.item, l.settings ) $ 'Offset GT 0' }">
			<div class="previous-button">
				<form method="get" action="&mvte:urls:_self:auto_noparams;">
					<mvt:if expr="{ 'tolower( l.settings:' $ l.item $ ':mode ) EQ \'ctgy\' AND toupper( g.Screen ) NE \'CTGY\'' }">
						<input type="hidden" name="Category_Code" value="&mvte:global:Category_Code;" />
					<mvt:elseif expr="{ 'tolower( l.settings:' $ l.item $ ':mode ) EQ \'relp\' AND toupper( g.Screen ) NE \'PROD\'' }">
						<input type="hidden" name="Product_Code" value="&mvte:global:Product_Code;" />
					</mvt:if>

					<mvt:item name="urls" param="hidden_params:_self:auto" />
					<input type="hidden" name="Search" value="&mvte:global:Search;" />
					<input type="hidden" name="Per_Page" value="&mvte:global:Per_Page;" />
					<input type="hidden" name="Sort_By" value="&mvte:global:Sort_By;" />
					<mvt:foreach iterator="facet" array="facets:applied_facets">
						<mvt:foreach iterator="facet_value" array="facet:input_values">
							<input type="hidden" name="&mvte:facet:code;" value="&mvte:facet_value;" />
						</mvt:foreach>
					</mvt:foreach>
					<MvEVAL EXPR = "{ Generate_NextPrev_Offset_Fields( l.item, l.settings, 'Prev' ) }">
					<mvt:item name="buttons" param="Previous" />
				</form>
			</div>
			</mvt:if>

			<mvt:if expr="{ 'g.' $ Generate_Template_Prefix( l.item, l.settings ) $ 'NextOffset GT 0' }">
			<div class="next-button">
				<form method="get" action="&mvte:urls:_self:auto_noparams;">
					<mvt:if expr="{ 'tolower( l.settings:' $ l.item $ ':mode ) EQ \'ctgy\' AND toupper( g.Screen ) NE \'CTGY\'' }">
						<input type="hidden" name="Category_Code" value="&mvte:global:Category_Code;" />
					<mvt:elseif expr="{ 'tolower( l.settings:' $ l.item $ ':mode ) EQ \'relp\' AND toupper( g.Screen ) NE \'PROD\'' }">
						<input type="hidden" name="Product_Code" value="&mvte:global:Product_Code;" />
					</mvt:if>

					<mvt:item name="urls" param="hidden_params:_self:auto" />
					<input type="hidden" name="Search" value="&mvte:global:Search;" />
					<input type="hidden" name="Per_Page" value="&mvte:global:Per_Page;" />
					<input type="hidden" name="Sort_By" value="&mvte:global:Sort_By;" />
					<mvt:foreach iterator="facet" array="facets:applied_facets">
						<mvt:foreach iterator="facet_value" array="facet:input_values">
							<input type="hidden" name="&mvte:facet:code;" value="&mvte:facet_value;" />
						</mvt:foreach>
					</mvt:foreach>
					<MvEVAL EXPR = "{ Generate_NextPrev_Offset_Fields( l.item, l.settings, 'Next' ) }">
					<mvt:item name="buttons" param="Next" />
				</form>
			</div>
			</mvt:if>
		</div>
		</mvt:if>
	</mvt:if>
</div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Code_Expanded" PARAMETERS = "settings var, item" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:price EQ 'retail' }">
		<MvASSIGN NAME = "l.price_variable"	VALUE = "l.settings:product:retail">
		<MvASSIGN NAME = "l.price_token"	VALUE = "&mvt:product:formatted_retail;">
	<MvELSEIF EXPR = "{ l.settings:price EQ 'sale' }">
		<MvASSIGN NAME = "l.price_variable"	VALUE = "l.settings:product:price">
		<MvASSIGN NAME = "l.price_token"	VALUE = "&mvt:product:formatted_price;">
	<MvELSE>
		<MvASSIGN NAME = "l.price_variable"	VALUE = "l.settings:product:base_price">
		<MvASSIGN NAME = "l.price_token"	VALUE = "&mvt:product:formatted_base_price;">
	</MvIF>

	<MvCAPTURE VARIABLE = "l.expanded_code">
	<MIVA STANDARDOUTPUTLEVEL = "text, html"><mvt:foreach iterator="product" array="{ l.item $ ':products' }">
	<div class="product-item" style="{ 'width:' $ floor( ( 100 / l.settings:col_num ) ROUND 1 ) $ '%' }">
		<div class="padding">
			<div class="product-details"><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:imagemachine_enabled }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				<div class="product-image">
					<img id="main_image_&mvte:product:id;" src="graphics/en-US/cssui/blank.gif" alt="&mvte:product:name;" />
				</div>
				<ul id="thumbnails_&mvte:product:id;" class="thumbnails"></ul>
				<div id="closeup_div_&mvte:product:id;" class="closeup"><img id="closeup_image_&mvte:product:id;" src="graphics/en-US/cssui/blank.gif" alt="&mvte:product:name;"><div><a id="closeup_close_&mvte:product:id;">close</a></div></div>
				<mvt:item name="{ l.item $ '_imagemachine' }" param="body:product:id" />
				<div class="clear product-image-margin"></div><MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSEIF EXPR = "{ l.settings:image EQ 'T' }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				<div class="product-thumbnail">
					<mvt:if expr="NOT ISNULL l.settings:product:thumbnail"><a data-mm-linktype="product-list-link" href="&mvte:product:link;"><img src="&mvte:product:thumbnail;" alt="&mvte:product:name;" /><mvt:else><a data-mm-linktype="product-list-link" class="thumbnail-not-available" href="&mvte:product:link;" title="&mvte:product:name;"></mvt:if></a>
				</div><MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSEIF EXPR = "{ l.settings:image EQ 'F' }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				<div class="product-image">
					<mvt:if expr="NOT ISNULL l.settings:product:image"><a data-mm-linktype="product-list-link" href="&mvte:product:link;"><img src="&mvte:product:image;" alt="&mvte:product:name;" /><mvt:else><a data-mm-linktype="product-list-link" class="image-not-available" href="&mvte:product:link;" title="&mvte:product:name;"></mvt:if></a>
				</div><MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSEIF EXPR = "{ ( 'type:' IN l.settings:image ) EQ 1 }">
				<MvASSIGN NAME = "l.imagetype"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( substring( l.settings:image, 6, len( l.settings:image ) - 5 ) ) }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				<div class="product-image">
					<mvt:if expr="{ 'NOT ISNULL l.settings:product:imagetypes:' $ l.imagetype }"><a data-mm-linktype="product-list-link" href="&mvte:product:link;"><img src="{ '&mvte:product:imagetypes:' $ l.imagetype $ ';' }" alt="&mvte:product:name;" /><mvt:else><a data-mm-linktype="product-list-link" class="image-not-available" href="&mvte:product:link;" title="&mvte:product:name;"></mvt:if></a>
				</div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MvIF EXPR = "{ l.settings:field_name }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				<div class="product-name"><a data-mm-linktype="product-list-link" href="&mvte:product:link;">&mvt:product:name;</a></div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MvIF EXPR = "{ l.settings:field_code }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				<div class="product-code">Code: <span class="bold">&mvt:product:code;</span></div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MvIF EXPR = "{ l.settings:field_sku }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				<div class="product-code">SKU: <span class="bold">&mvt:product:sku;</span></div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MvIF EXPR = "{ l.settings:field_price }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				<div class="product-price">
					Price: <MIVA STANDARDOUTPUTLEVEL = "">
					<MvIF EXPR = "{ ( l.settings:additionalprice EQ 'retail' ) AND ( l.settings:price NE 'retail' ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="{ 'l.settings:product:retail GT ' $ l.price_variable }">
						<span style="text-decoration: line-through">&mvt:product:formatted_retail;</span>
					</mvt:if>

					<MIVA STANDARDOUTPUTLEVEL = "">
					<MvELSEIF EXPR = "{ ( l.settings:additionalprice EQ 'base' ) AND ( l.settings:price NE 'base' ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="{ 'l.settings:product:base_price GT ' $ l.price_variable }">
						<span style="text-decoration: line-through">&mvt:product:formatted_base_price;</span>
					</mvt:if>

					<MIVA STANDARDOUTPUTLEVEL = "">
					</MvIF>

					<MIVA STANDARDOUTPUTLEVEL = "text, html"><span class="bold"><MvEVAL EXPR = "{ l.price_token }"></span><MIVA STANDARDOUTPUTLEVEL = "">

					<MvIF EXPR = "{ l.settings:displaydiscounts }"><MIVA STANDARDOUTPUTLEVEL = "text, html">

					<mvt:foreach iterator="discount" array="product:discounts">
						<div class="product-discount">&mvt:discount:descrip;: &mvt:discount:formatted_discount;</div>
					</mvt:foreach><MIVA STANDARDOUTPUTLEVEL = "">
					</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
				</div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MvIF EXPR = "{ l.settings:field_weight }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				<mvt:if expr="l.settings:product:weight NE 0">
				<div class="product-weight">Weight: <span class="bold">&mvt:product:weight;</span></div>
				</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
				
	<MvASSIGN NAME = "l.customfield_pos"		VALUE = 1>
	<MvASSIGN NAME = "l.customfield_count"		VALUE = "{ miva_array_elements( l.settings:fields_custom ) }">

	<MvWHILE EXPR = "{ l.customfield_pos LE l.customfield_count }">
		<MvASSIGN NAME = "l.safe_module_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.settings:fields_custom[ l.customfield_pos ]:module ) }">
		<MvASSIGN NAME = "l.safe_field_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.settings:fields_custom[ l.customfield_pos ]:code ) }">
		
				<MIVA STANDARDOUTPUTLEVEL = "text,html">
				<mvt:if expr="{ 'NOT ISNULL l.settings:product:customfield_values:' $ l.safe_module_code $ ':' $ l.safe_field_code }">
				<div class="custom-field" id="{ l.safe_module_code $ '-' $ l.safe_field_code }">&mvte:customfield_names:<MvEVAL EXPR = "{ l.safe_module_code $ ':' $ l.safe_field_code }">;: <span class="bold">&mvt:product:customfield_values:<MvEVAL EXPR = "{ l.safe_module_code $ ':' $ l.safe_field_code }">;</span></div>
				</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
	
		<MvASSIGN NAME = "l.customfield_pos"	VALUE = "{ l.customfield_pos + 1 }">
	</MvWHILE>
				<MIVA STANDARDOUTPUTLEVEL = "text,html">
				<mvt:if expr="l.settings:product:inv_active"><MIVA STANDARDOUTPUTLEVEL = "">
					<MvIF EXPR = "{ l.settings:invmsg EQ 'S' }">
						<MIVA STANDARDOUTPUTLEVEL = "text,html">&mvt:product:inv_short;<br><MIVA STANDARDOUTPUTLEVEL = "">
					<MvELSEIF EXPR = "{ l.settings:invmsg EQ 'L' }">
						<MIVA STANDARDOUTPUTLEVEL = "text,html">&mvt:product:inv_long;<br><MIVA STANDARDOUTPUTLEVEL = "">
					</MvIF>
				<MIVA STANDARDOUTPUTLEVEL = "text,html"></mvt:if>
				<div class="product-quantity">Quantity in Basket:
					<mvt:if expr="l.settings:product:quantity EQ 0">
					<span class="italic">none</span>
					<mvt:else>
					<span class="italic">&mvt:product:quantity;</span>
					</mvt:if>
				</div><MIVA STANDARDOUTPUTLEVEL = "">
				<MvIF EXPR = "{ l.settings:field_desc }">
					<MIVA STANDARDOUTPUTLEVEL = "text,html">
				<div class="product-descrip">&mvt:product:descrip;</div><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF>
				<MIVA STANDARDOUTPUTLEVEL = "text,html">
			</div><MIVA STANDARDOUTPUTLEVEL = "">

			<MvIF EXPR = "{ l.settings:button_wish }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:product:inv_level EQ 'out'">
			<div class="purchase-buttons">
				<form method="post" action="&mvte:urls:WISH:secure;">
				<input type="hidden" name="Old_Screen" value="&mvte:global:Screen;" />
				<input type="hidden" name="Old_Search" value="&mvte:global:Search;" />
				<input type="hidden" name="Action" value="ATWL" />
				<input type="hidden" name="Quantity" value="1" />
				<input type="hidden" name="Attributes" value="Yes" />
				<input type="hidden" name="Product_Code" value="&mvte:product:code;" />
				<input type="hidden" name="Current_Product_Code" value="&mvte:global:Product_Code;" />
				<input type="hidden" name="Category_Code" value="&mvte:global:Category_Code;" />
				<input type="hidden" name="Offset" value="{ '&mvte:global:' $ Generate_Template_Prefix( l.item, l.settings ) $ 'Offset;' }" />
				<input type="hidden" name="AllOffset" value="&mvte:global:AllOffset;" />
				<input type="hidden" name="CatListingOffset" value="&mvte:global:CatListingOffset;" />
				<input type="hidden" name="RelatedOffset" value="&mvte:global:RelatedOffset;" />
				<input type="hidden" name="SearchOffset" value="&mvte:global:SearchOffset;" />
				<mvt:foreach iterator="facet" array="facets:applied_facets">
					<mvt:foreach iterator="facet_value" array="facet:input_values">
						<input type="hidden" name="&mvte:facet:code;" value="&mvte:facet_value;" />
					</mvt:foreach>
				</mvt:foreach>
				<mvt:item name="buttons" param="AddToWishListE" />
				</form>
			</div>
			<mvt:else><MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:product:inv_level NE 'out'"><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<div class="purchase-buttons"><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:button_add }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				<form method="post" action="&mvte:urls:BASK:rr;">
				<input type="hidden" name="Old_Screen" value="&mvte:global:Screen;" />
				<input type="hidden" name="Old_Search" value="&mvte:global:Search;" />
				<input type="hidden" name="Action" value="ADPR" />
				<input type="hidden" name="Quantity" value="1" />
				<input type="hidden" name="Attributes" value="Yes" />
				<input type="hidden" name="Product_Code" value="&mvte:product:code;" />
				<input type="hidden" name="Current_Product_Code" value="&mvte:global:Product_Code;" />
				<input type="hidden" name="Category_Code" value="&mvte:global:Category_Code;" />
				<input type="hidden" name="Offset" value="{ '&mvte:global:' $ Generate_Template_Prefix( l.item, l.settings ) $ 'Offset;' }" />
				<input type="hidden" name="AllOffset" value="&mvte:global:AllOffset;" />
				<input type="hidden" name="CatListingOffset" value="&mvte:global:CatListingOffset;" />
				<input type="hidden" name="RelatedOffset" value="&mvte:global:RelatedOffset;" />
				<input type="hidden" name="SearchOffset" value="&mvte:global:SearchOffset;" />
				<mvt:foreach iterator="facet" array="facets:applied_facets">
					<mvt:foreach iterator="facet_value" array="facet:input_values">
						<input type="hidden" name="&mvte:facet:code;" value="&mvte:facet_value;" />
					</mvt:foreach>
				</mvt:foreach>
				<mvt:item name="buttons" param="AddToBasketE" />
				</form><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MvIF EXPR = "{ l.settings:button_buy }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				<form method="post" action="&mvte:urls:OINF:secure;">
				<input type="hidden" name="Old_Screen" value="&mvte:global:Screen;" />
				<input type="hidden" name="Old_Search" value="&mvte:global:Search;" />
				<input type="hidden" name="Action" value="ADPR" />
				<input type="hidden" name="Quantity" value="1" />
				<input type="hidden" name="Attributes" value="Yes" />
				<input type="hidden" name="Product_Code" value="&mvte:product:code;" />
				<input type="hidden" name="Current_Product_Code" value="&mvte:global:Product_Code;" />
				<input type="hidden" name="Category_Code" value="&mvte:global:Category_Code;" />
				<input type="hidden" name="Offset" value="{ '&mvte:global:' $ Generate_Template_Prefix( l.item, l.settings ) $ 'Offset;' }" />
				<input type="hidden" name="AllOffset" value="&mvte:global:AllOffset;" />
				<input type="hidden" name="CatListingOffset" value="&mvte:global:CatListingOffset;" />
				<input type="hidden" name="RelatedOffset" value="&mvte:global:RelatedOffset;" />
				<input type="hidden" name="SearchOffset" value="&mvte:global:SearchOffset;" />
				<mvt:foreach iterator="facet" array="facets:applied_facets">
					<mvt:foreach iterator="facet_value" array="facet:input_values">
						<input type="hidden" name="&mvte:facet:code;" value="&mvte:facet_value;" />
					</mvt:foreach>
				</mvt:foreach>
				<mvt:item name="buttons" param="BuyNowE" />
				</form><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MvIF EXPR = "{ l.settings:button_wish }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				<form method="post" action="&mvte:urls:WISH:secure;">
				<input type="hidden" name="Old_Screen" value="&mvte:global:Screen;" />
				<input type="hidden" name="Old_Search" value="&mvte:global:Search;" />
				<input type="hidden" name="Action" value="ATWL" />
				<input type="hidden" name="Quantity" value="1" />
				<input type="hidden" name="Attributes" value="Yes" />
				<input type="hidden" name="Product_Code" value="&mvte:product:code;" />
				<input type="hidden" name="Current_Product_Code" value="&mvte:global:Product_Code;" />
				<input type="hidden" name="Category_Code" value="&mvte:global:Category_Code;" />
				<input type="hidden" name="Offset" value="{ '&mvte:global:' $ Generate_Template_Prefix( l.item, l.settings ) $ 'Offset;' }" />
				<input type="hidden" name="AllOffset" value="&mvte:global:AllOffset;" />
				<input type="hidden" name="CatListingOffset" value="&mvte:global:CatListingOffset;" />
				<input type="hidden" name="RelatedOffset" value="&mvte:global:RelatedOffset;" />
				<input type="hidden" name="SearchOffset" value="&mvte:global:SearchOffset;" />
				<mvt:foreach iterator="facet" array="facets:applied_facets">
					<mvt:foreach iterator="facet_value" array="facet:input_values">
						<input type="hidden" name="&mvte:facet:code;" value="&mvte:facet_value;" />
					</mvt:foreach>
				</mvt:foreach>
				<mvt:item name="buttons" param="AddToWishListE" />
				</form><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			</div>
			</mvt:if>
		</div>
	</div>
	</mvt:foreach>
	<div class="clear"></div><MIVA STANDARDOUTPUTLEVEL = "">
	
	</MvCAPTURE>
	
	<MvFUNCTIONRETURN VALUE = "{ l.expanded_code }">
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Code_LineItem" PARAMETERS = "settings var, item" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:price EQ 'retail' }">
		<MvASSIGN NAME = "l.price_variable"	VALUE = "l.settings:product:retail">
		<MvASSIGN NAME = "l.price_token"	VALUE = "&mvt:product:formatted_retail;">
	<MvELSEIF EXPR = "{ l.settings:price EQ 'sale' }">
		<MvASSIGN NAME = "l.price_variable"	VALUE = "l.settings:product:price">
		<MvASSIGN NAME = "l.price_token"	VALUE = "&mvt:product:formatted_price;">
	<MvELSE>
		<MvASSIGN NAME = "l.price_variable"	VALUE = "l.settings:product:base_price">
		<MvASSIGN NAME = "l.price_token"	VALUE = "&mvt:product:formatted_base_price;">
	</MvIF>

	<MvCAPTURE VARIABLE = "l.lineitem_code">
		<MIVA STANDARDOUTPUTLEVEL = "text,html"><table>
		<tr class="header-row"><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ l.settings:field_code }">
			<MIVA STANDARDOUTPUTLEVEL = "text,html">
			<td class="item-code">
				Code
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
		<MvIF EXPR = "{ l.settings:field_sku }">
			<MIVA STANDARDOUTPUTLEVEL = "text,html">
			<td class="item-code">
				SKU
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
		<MvIF EXPR = "{ l.settings:field_name }">
			<MIVA STANDARDOUTPUTLEVEL = "text,html">
			<td class="item-name">
				Name
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
		<MvIF EXPR = "{ l.settings:imagemachine_enabled OR ( l.settings:image NE 'N' ) }">
			<MIVA STANDARDOUTPUTLEVEL = "text,html">
			<td class="item-image">
				Image
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
		<MvIF EXPR = "{ l.settings:field_weight }">
			<MIVA STANDARDOUTPUTLEVEL = "text,html">
			<td class="item-weight">
				Weight
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
		<MvIF EXPR = "{ l.settings:field_price }">
			<MIVA STANDARDOUTPUTLEVEL = "text,html">
			<td class="item-price">
				Price
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF><MIVA STANDARDOUTPUTLEVEL = "">
	
		<MvASSIGN NAME = "l.customfield_pos"		VALUE = 1>
		<MvASSIGN NAME = "l.customfield_count"		VALUE = "{ miva_array_elements( l.settings:fields_custom ) }">
		<MvWHILE EXPR = "{ l.customfield_pos LE l.customfield_count }">
			<MvASSIGN NAME = "l.safe_module_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.settings:fields_custom[ l.customfield_pos ]:module ) }">
			<MvASSIGN NAME = "l.safe_field_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.settings:fields_custom[ l.customfield_pos ]:code ) }">

				<MIVA STANDARDOUTPUTLEVEL = "text,html">
			<td class="item-custom-field" id="{ l.safe_module_code $ '-' $ l.safe_field_code }">
				&mvt:customfield_names:<MvEVAL EXPR = "{ l.safe_module_code }">:<MvEVAL EXPR = "{ l.safe_field_code }">;
			</td><MIVA STANDARDOUTPUTLEVEL = "">

			<MvASSIGN NAME = "l.customfield_pos"	VALUE = "{ l.customfield_pos + 1 }">
		</MvWHILE>

		<MvIF EXPR = "{ ( l.settings:invmsg EQ 'S' ) OR ( l.settings:invmsg EQ 'L' ) }">
			<MIVA STANDARDOUTPUTLEVEL = "text,html">
			<mvt:if expr="l.settings:inventory:active">
			<td class="item-availability">
				Availability
			</td>
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:field_desc }">
			<MIVA STANDARDOUTPUTLEVEL = "text,html">
			<td class="item-description">
				Description
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.product:inv_level NE 'out' }">
			<MvIF EXPR = "{ l.settings:button_add OR l.settings:button_buy OR l.settings:button_wish }">
			<MIVA STANDARDOUTPUTLEVEL = "text,html">
			<td class="item-buttons">
				&nbsp;
			</td><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>
		</MvIF>
		<MIVA STANDARDOUTPUTLEVEL = "text,html">
		</tr>
		<mvt:foreach iterator="product" array="{ l.item $ ':products' }">
		<mvt:if expr = "l.pos1 MOD 2">
		<tr class="alt_row">
		<mvt:else>
		<tr>
		</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">

		<MvIF EXPR = "{ l.settings:field_code }">
			<MIVA STANDARDOUTPUTLEVEL = "text,html">
			<td class="item-code">
				<a data-mm-linktype="product-list-link" href="&mvte:product:link;">&mvt:product:code;</a>
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:field_sku }">
			<MIVA STANDARDOUTPUTLEVEL = "text,html">
			<td class="item-code">
				<mvt:if expr="ISNULL l.settings:product:sku">
					&nbsp;
				<mvt:else>
					<a data-mm-linktype="product-list-link" href="&mvte:product:link;">&mvt:product:sku;</a>
				</mvt:if>
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:field_name }">
			<MIVA STANDARDOUTPUTLEVEL = "text,html">
			<td class="item-name">
				&mvt:product:name;
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:imagemachine_enabled OR ( l.settings:image NE 'N' ) }">
			<MIVA STANDARDOUTPUTLEVEL = "text,html">
			<td class="item-image"><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ l.settings:imagemachine_enabled }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				<img id="main_image_&mvte:product:id;" src="graphics/en-US/cssui/blank.gif" alt="&mvte:product:name;" />
				<div id="thumbnails_&mvte:product:id;" class="thumbnails"></div>
				<div id="closeup_div_&mvte:product:id;" class="closeup"><img id="closeup_image_&mvte:product:id;" src="graphics/en-US/cssui/blank.gif" alt="&mvte:product:name;"><div><a id="closeup_close_&mvte:product:id;">close</a></div></div>
				<mvt:item name="{ l.item $ '_imagemachine' }" param="body:product:id" /><MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSEIF EXPR = "{ l.settings:image EQ 'T' }">
				<MIVA STANDARDOUTPUTLEVEL = "text,html">
				<mvt:if expr="NOT ISNULL l.settings:product:thumbnail">
				<a data-mm-linktype="product-list-link" href="&mvte:product:link;"><img src="&mvte:product:thumbnail;" alt="&mvte:product:name;" /></a>
				<mvt:else>
				&nbsp;
				</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSEIF EXPR = "{ l.settings:image EQ 'F' }">
				<MIVA STANDARDOUTPUTLEVEL = "text,html">
				<mvt:if expr="NOT ISNULL l.settings:product:image">
				<a data-mm-linktype="product-list-link" href="&mvte:product:link;"><img src="&mvte:product:image;" alt="&mvte:product:name;" /></a>
				<mvt:else>
				&nbsp;
				</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSEIF EXPR = "{ ( 'type:' IN l.settings:image ) EQ 1 }">
				<MvASSIGN NAME = "l.imagetype"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( substring( l.settings:image, 6, len( l.settings:image ) - 5 ) ) }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				<mvt:if expr="{ 'NOT ISNULL l.settings:product:imagetypes:' $ l.imagetype }">
				<a data-mm-linktype="product-list-link" href="&mvte:product:link;"><img src="{ '&mvte:product:imagetypes:' $ l.imagetype $ ';' }" alt="&mvte:product:name;" /></a>
				<mvt:else>
				&nbsp;
				</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>
			<MIVA STANDARDOUTPUTLEVEL = "text,html">
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:field_weight }">
			<MIVA STANDARDOUTPUTLEVEL = "text,html">
			<td class="item-weight">
				&mvt:product:weight;
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:field_price }">
			<MIVA STANDARDOUTPUTLEVEL = "text,html">
			<td class="item-price">
					<MvIF EXPR = "{ ( l.settings:additionalprice EQ 'retail' ) AND ( l.settings:price NE 'retail' ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="{ 'l.settings:product:retail GT ' $ l.price_variable }">
						<span style="text-decoration: line-through">&mvt:product:formatted_retail;</span>
					</mvt:if>

					<MIVA STANDARDOUTPUTLEVEL = "">
					<MvELSEIF EXPR = "{ ( l.settings:additionalprice EQ 'base' ) AND ( l.settings:price NE 'base' ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="{ 'l.settings:product:base_price GT ' $ l.price_variable }">
						<span style="text-decoration: line-through">&mvt:product:formatted_base_price;</span>
					</mvt:if>

					<MIVA STANDARDOUTPUTLEVEL = "">
					</MvIF>

					<MIVA STANDARDOUTPUTLEVEL = "text, html"><MvEVAL EXPR = "{ l.price_token }"><MIVA STANDARDOUTPUTLEVEL = "">

					<MvIF EXPR = "{ l.settings:displaydiscounts }"><MIVA STANDARDOUTPUTLEVEL = "text, html">

					<mvt:foreach iterator="discount" array="product:discounts">
						<div class="product-discount">&mvt:discount:descrip;: &mvt:discount:formatted_discount;</div>
					</mvt:foreach><MIVA STANDARDOUTPUTLEVEL = "">
					</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF><MIVA STANDARDOUTPUTLEVEL = "">

		<MvASSIGN NAME = "l.customfield_pos"		VALUE = 1>
		<MvASSIGN NAME = "l.customfield_count"		VALUE = "{ miva_array_elements( l.settings:fields_custom ) }">
		<MvWHILE EXPR = "{ l.customfield_pos LE l.customfield_count }">
			<MvASSIGN NAME = "l.safe_module_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.settings:fields_custom[ l.customfield_pos ]:module ) }">
			<MvASSIGN NAME = "l.safe_field_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.settings:fields_custom[ l.customfield_pos ]:code ) }">
			
			<MvASSIGN NAME = "l.customfield_values" VALUE = "{ l.customfield_values $ padl( '<mvt:if expr=\"NOT ISNULL l.settings:product:customfield_values:' $ l.safe_module_code $ ':' $ l.safe_field_code $ '\">' $ asciichar( 10 ), len( '<mvt:if expr=\"NOT ISNULL l.settings:product:customfield_values:' $ l.safe_module_code $ ':' $ l.safe_field_code $ '\">' $ asciichar( 10 ) ) + 4, asciichar( 9 ) ) }">
			<MvASSIGN NAME = "l.customfield_values" VALUE = "{ l.customfield_values $ padl( '<div class=\"custom-field\" id=\"' $ l.safe_module_code $ '-' $ l.safe_field_code $ '\">&mvte:customfield_names:' $ l.safe_module_code $ ':' $ l.safe_field_code $ ';: <span class=\"bold\">&mvt:product:customfield_values:' $ l.safe_module_code $ ':' $ l.safe_field_code $ ';</span></div>' $ asciichar( 10 ), len( '<div class=\"custom-field\" id=\"' $ l.safe_module_code $ '-' $ l.safe_field_code $ '\">&mvte:customfield_names:' $ l.safe_module_code $ ':' $ l.safe_field_code $ ';: <span class=\"bold\">&mvt:product:customfield_values:' $ l.safe_module_code $ ':' $ l.safe_field_code $ ';</span></div>' $ asciichar( 10 ) ) + 4, asciichar( 9 ) ) }">
			<MvASSIGN NAME = "l.customfield_values" VALUE = "{ l.customfield_values $ padl( '</mvt:if>' $ asciichar( 10 ), len( '</mvt:if>' $ asciichar( 10 ) ) + 4, asciichar( 9 ) ) }">
			
			<MIVA STANDARDOUTPUTLEVEL = "text,html">
			<td class="item-custom-field">
				<mvt:if expr="{ 'ISNULL l.settings:product:customfield_values:' $ l.safe_module_code $ ':' $ l.safe_field_code }">
				&nbsp;
				<mvt:else>
				&mvt:product:customfield_values:<MvEVAL EXPR = "{ l.safe_module_code }">:<MvEVAL EXPR = "{ l.safe_field_code }">;
				</mvt:if>
			</td><MIVA STANDARDOUTPUTLEVEL = "">
			
			<MvASSIGN NAME = "l.customfield_pos"	VALUE = "{ l.customfield_pos + 1 }">
		</MvWHILE>

		<MvIF EXPR = "{ l.settings:invmsg NE 'N' }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:inventory:active">
			<td class="item-availability">
				<mvt:if expr="l.settings:product:inv_active"><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ l.settings:invmsg EQ 'S' }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				&mvt:product:inv_short;<MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSEIF EXPR = "{ l.settings:invmsg EQ 'L' }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				&mvt:product:inv_long;<MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				<mvt:else>
				&nbsp;
				</mvt:if>
			</td>
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>

			<MvIF EXPR = "{ l.settings:field_desc }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="item-description">
				&mvt:product:descrip;
			</td><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>

			<MvIF EXPR = "{ l.settings:button_wish }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:product:inv_level EQ 'out'">
			<td class="item-buttons" style="text-align: right;">
				<form method="post" action="&mvte:urls:WISH:secure;">
				<input type="hidden" name="Old_Screen" value="&mvte:global:Screen;" />
				<input type="hidden" name="Old_Search" value="&mvte:global:Search;" />
				<input type="hidden" name="Action" value="ATWL" />
				<input type="hidden" name="Quantity" value="1" />
				<input type="hidden" name="Attributes" value="Yes" />
				<input type="hidden" name="Product_Code" value="&mvte:product:code;" />
				<input type="hidden" name="Current_Product_Code" value="&mvte:global:Product_Code;" />
				<input type="hidden" name="Category_Code" value="&mvte:global:Category_Code;" />
				<input type="hidden" name="Offset" value="{ '&mvte:global:' $ Generate_Template_Prefix( l.item, l.settings ) $ 'Offset;' }" />
				<input type="hidden" name="AllOffset" value="&mvte:global:AllOffset;" />
				<input type="hidden" name="CatListingOffset" value="&mvte:global:CatListingOffset;" />
				<input type="hidden" name="RelatedOffset" value="&mvte:global:RelatedOffset;" />
				<input type="hidden" name="SearchOffset" value="&mvte:global:SearchOffset;" />
				<mvt:foreach iterator="facet" array="facets:applied_facets">
					<mvt:foreach iterator="facet_value" array="facet:input_values">
						<input type="hidden" name="&mvte:facet:code;" value="&mvte:facet_value;" />
					</mvt:foreach>
				</mvt:foreach>
				<mvt:item name="buttons" param="AddToWishListE" />
				</form>
			</td>
			<mvt:else><MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:product:inv_level NE 'out'"><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="item-buttons"><MIVA STANDARDOUTPUTLEVEL = "">
				<MvIF EXPR = "{ l.settings:button_add }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<form method="post" action="&mvte:urls:BASK:rr;">
				<input type="hidden" name="Old_Screen" value="&mvte:global:Screen;" />
				<input type="hidden" name="Old_Search" value="&mvte:global:Search;" />
				<input type="hidden" name="Action" value="ADPR" />
				<input type="hidden" name="Quantity" value="1" />
				<input type="hidden" name="Attributes" value="Yes" />
				<input type="hidden" name="Product_Code" value="&mvte:product:code;" />
				<input type="hidden" name="Current_Product_Code" value="&mvte:global:Product_Code;" />
				<input type="hidden" name="Category_Code" value="&mvte:global:Category_Code;" />
				<input type="hidden" name="Offset" value="{ '&mvte:global:' $ Generate_Template_Prefix( l.item, l.settings ) $ 'Offset;' }" />
				<input type="hidden" name="AllOffset" value="&mvte:global:AllOffset;" />
				<input type="hidden" name="CatListingOffset" value="&mvte:global:CatListingOffset;" />
				<input type="hidden" name="RelatedOffset" value="&mvte:global:RelatedOffset;" />
				<input type="hidden" name="SearchOffset" value="&mvte:global:SearchOffset;" />
				<mvt:foreach iterator="facet" array="facets:applied_facets">
					<mvt:foreach iterator="facet_value" array="facet:input_values">
						<input type="hidden" name="&mvte:facet:code;" value="&mvte:facet_value;" />
					</mvt:foreach>
				</mvt:foreach>
				<mvt:item name="buttons" param="AddToBasketL" />
				</form><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF>
				<MvIF EXPR = "{ l.settings:button_buy }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<form method="post" action="&mvte:urls:OINF:secure;" />
				<input type="hidden" name="Old_Screen" value="&mvte:global:Screen;" />
				<input type="hidden" name="Old_Search" value="&mvte:global:Search;" />
				<input type="hidden" name="Action" value="ADPR" />
				<input type="hidden" name="Quantity" value="1" />
				<input type="hidden" name="Order" value="1" />
				<input type="hidden" name="Attributes" value="Yes" />
				<input type="hidden" name="Product_Code" value="&mvte:product:code;" />
				<input type="hidden" name="Current_Product_Code" value="&mvte:global:Product_Code;" />
				<input type="hidden" name="Category_Code" value="&mvte:global:Category_Code;" />
				<input type="hidden" name="Offset" value="{ '&mvte:global:' $ Generate_Template_Prefix( l.item, l.settings ) $ 'Offset;' }" />
				<input type="hidden" name="AllOffset" value="&mvte:global:AllOffset;" />
				<input type="hidden" name="CatListingOffset" value="&mvte:global:CatListingOffset;" />
				<input type="hidden" name="RelatedOffset" value="&mvte:global:RelatedOffset;" />
				<input type="hidden" name="SearchOffset" value="&mvte:global:SearchOffset;" />
				<mvt:foreach iterator="facet" array="facets:applied_facets">
					<mvt:foreach iterator="facet_value" array="facet:input_values">
						<input type="hidden" name="&mvte:facet:code;" value="&mvte:facet_value;" />
					</mvt:foreach>
				</mvt:foreach>
				<mvt:item name="buttons" param="BuyNowL" />
				</form><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF>
				<MvIF EXPR = "{ l.settings:button_wish }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<form method="post" action="&mvte:urls:WISH:secure;" />
				<input type="hidden" name="Old_Screen" value="&mvte:global:Screen;" />
				<input type="hidden" name="Old_Search" value="&mvte:global:Search;" />
				<input type="hidden" name="Action" value="ATWL" />
				<input type="hidden" name="Quantity" value="1" />
				<input type="hidden" name="Order" value="1" />
				<input type="hidden" name="Attributes" value="Yes" />
				<input type="hidden" name="Product_Code" value="&mvte:product:code;" />
				<input type="hidden" name="Current_Product_Code" value="&mvte:global:Product_Code;" />
				<input type="hidden" name="Category_Code" value="&mvte:global:Category_Code;" />
				<input type="hidden" name="Offset" value="{ '&mvte:global:' $ Generate_Template_Prefix( l.item, l.settings ) $ 'Offset;' }" />
				<input type="hidden" name="AllOffset" value="&mvte:global:AllOffset;" />
				<input type="hidden" name="CatListingOffset" value="&mvte:global:CatListingOffset;" />
				<input type="hidden" name="RelatedOffset" value="&mvte:global:RelatedOffset;" />
				<input type="hidden" name="SearchOffset" value="&mvte:global:SearchOffset;" />
				<mvt:foreach iterator="facet" array="facets:applied_facets">
					<mvt:foreach iterator="facet_value" array="facet:input_values">
						<input type="hidden" name="&mvte:facet:code;" value="&mvte:facet_value;" />
					</mvt:foreach>
				</mvt:foreach>
				<mvt:item name="buttons" param="AddToWishListL" />
				</form><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
			</td>
			</mvt:if>
		</tr>
		</mvt:foreach>
	</table><MIVA STANDARDOUTPUTLEVEL = "">
		
	</MvCAPTURE>

	<MvFUNCTIONRETURN VALUE = "{ l.lineitem_code }">
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Facets_Code" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code">
	<MIVA STANDARDOUTPUTLEVEL = "text, html"><div class="facets-tree">
	<form method="get" action="&mvte:urls:_self:auto_noparams;">
		<mvt:if expr="NOT ISNULL g.Category_Code AND toupper( g.Screen ) NE 'CTGY'">
			<input type="hidden" name="Category_Code" value="&mvte:global:Category_Code;" />
		<mvt:elseif expr="NOT ISNULL g.Product_Code AND toupper( g.Screen ) NE 'PROD'">
			<input type="hidden" name="Product_Code" value="&mvte:global:Product_Code;" />
		</mvt:if>

		<mvt:item name="urls" param="hidden_params:_self:auto" />
		<input type="hidden" name="Search" value="&mvte:global:Search;" />
		<input type="hidden" name="Sort_By" value="&mvte:global:Sort_By;" />
		<input type="hidden" name="Per_Page" value="&mvte:global:Per_Page;" />
		<mvt:foreach iterator="facet" array="facets:facets">
			<span class="facets-name"><b>&mvt:facet:name;</b></span><br />
			<mvt:if expr="l.settings:facet:type EQ 'radio'">
				<mvt:foreach iterator="facet_value" array="facet:values">
					<mvt:if expr="l.settings:facet_value:selected">
						<label><input type="radio" name="&mvte:facet:code;" class="mm_facet_radio" onclick="MMProdList_UpdateQuery( this ); return true;" checked value="&mvte:facet_value:value;" />&mvt:facet_value:prompt; (&mvte:facet_value:count;)</label><br />
					<mvt:else>
						<label><input type="radio" name="&mvte:facet:code;" class="mm_facet_radio" onclick="MMProdList_UpdateQuery( this ); return true;" value="&mvte:facet_value:value;" />&mvt:facet_value:prompt; (&mvte:facet_value:count;)</label><br />
					</mvt:if>
				</mvt:foreach>
			<mvt:elseif expr="l.settings:facet:type EQ 'checkbox'">
				<mvt:foreach iterator="facet_value" array="facet:values">
					<mvt:if expr="l.settings:facet_value:selected">
						<label><input type="checkbox" name="&mvte:facet:code;" class="mm_facet_checkbox" onclick="MMProdList_UpdateQuery( this ); return true;" checked value="&mvte:facet_value:value;" />&mvt:facet_value:prompt; (&mvte:facet_value:count;)</label><br />
					<mvt:else>
						<label><input type="checkbox" name="&mvte:facet:code;" class="mm_facet_checkbox" onclick="MMProdList_UpdateQuery( this ); return true;" value="&mvte:facet_value:value;" />&mvt:facet_value:prompt; (&mvte:facet_value:count;)</label><br />
					</mvt:if>
				</mvt:foreach>
			<mvt:elseif expr="l.settings:facet:type EQ 'select'">
				<select name="&mvte:facet:code;" class="mm_facet_select" onchange="MMProdList_UpdateQuery( this ); return true;">
					<option value="">&lt;Select One&gt;</option>
					<mvt:foreach iterator="facet_value" array="facet:values">
						<mvt:if expr="l.settings:facet_value:selected">
							<option value="&mvte:facet_value:value;" selected>&mvt:facet_value:prompt; (&mvte:facet_value:count;)</option>
						<mvt:else>
							<option value="&mvte:facet_value:value;">&mvt:facet_value:prompt; (&mvte:facet_value:count;)</option>
						</mvt:if>
					</mvt:foreach>
				</select>
			<mvt:elseif expr="l.settings:facet:type EQ 'rangeslider'">
				<mvt:assign name="l.settings:mm_facet_rangeslider_present" value="1" />
				<span class="mm_facet_rangeslider mm_facet_rangeslider_mini"
					  data-mm-facet-code="&mvte:facet:code;"
					  data-mm-facet-module-code="&mvte:facet:module:code;"
					  data-mm-facet-rangeslider-name="&mvte:facet:code;"
					  data-mm-facet-rangeslider-range-low="&mvte:facet:value_low;"
					  data-mm-facet-rangeslider-range-high="&mvte:facet:value_high;"
					  data-mm-facet-rangeslider-selected-range-low="&mvte:facet:selected_value_low;"
					  data-mm-facet-rangeslider-selected-range-high="&mvte:facet:selected_value_high;"></span>
            <mvt:elseif expr="l.settings:facet:type EQ 'nested'">
				<mvt:assign name="l.settings:nested_facet_margin" value="0" />
				<mvt:foreach iterator="applied_value" array="facet:applied_values">
					<input type="hidden" name="&mvte:facet:code;" value="&mvte:applied_value:value;">
					<div style="margin-left: &mvte:nested_facet_margin;px"><a href="#" onclick="MMProdList_RemoveNestedFacetValue( this, '&mvtj:facet:code;', '&mvtj:applied_value:value;', 0 ); return false;">&mvt:applied_value:prompt</a></div>
					<mvt:assign name="l.settings:nested_facet_margin" value="l.settings:nested_facet_margin + 10" />
				</mvt:foreach>

				<mvt:foreach iterator="facet_value" array="facet:values">
					<div style="margin-left: &mvte:nested_facet_margin;px"><a href="#" onclick="MMProdList_AddNestedFacetValue( this, '&mvtj:facet:code;', '&mvtj:facet_value:value;', 0 ); return false;">&mvt:facet_value:prompt; (&mvte:facet_value:count;)</a></div>
				</mvt:foreach>
			</mvt:if>
			<br />
		</mvt:foreach>
	</form>
</div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.working_settings"	VALUE = "{ l.settings }">
	
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml, 'code', l.item ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml,	'Mode',							l.working_settings:mode,			'Category,ProductList,SearchResults,RelatedProducts',	'ctgy,plst,srch,relp' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml,	'Format',						l.working_settings:format,			'Expanded,Line Item',									'X,L' )					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml,	'Image',						l.working_settings:image,			'Thumbnail,None,Full-Sized,Type',						'T,N,F,TYPE' )			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml,	'InventoryMessage',				l.working_settings:invmsg,			'Long,Short,None',										'L,S,N' )				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O', l.provide_xml,	'ProductsPerPage',				l.working_settings:per_page )																						OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O', l.provide_xml,	'PageCountNumber',				l.working_settings:page_disp_count )																				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'ItemsPerPage',					l.working_settings:items_per_page_filter )																			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'SortBySwitch',					l.working_settings:sort_by:switch )																					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByName',					l.working_settings:sort_by:name )																					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByCode',					l.working_settings:sort_by:code )																					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByBest',					l.working_settings:sort_by:best )																					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByPrice',					l.working_settings:sort_by:price )																					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByNew',					l.working_settings:sort_by:new )																					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByOld',					l.working_settings:sort_by:old )																					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByUpdated',				l.working_settings:sort_by:updated )																				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByRelevance',				l.working_settings:sort_by:relevance )																				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'o', l.provide_xml,	'ProductColumns',				l.working_settings:col_num )																						OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'NoCategoryCodeInLinks',		l.working_settings:linkexcludecat )																					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml, 'PriceDisplay',					l.working_settings:price,			'Retail,Base,Sale',										'retail,base,sale' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml, 'AdditionalPriceDisplay',		l.working_settings:additionalprice,	'None,Retail,Base',										',retail,base' )		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml, 'DisplayDiscounts',				l.working_settings:displaydiscounts )																				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml, 'PredictDiscounts',				l.working_settings:predictdiscounts )																				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml, 'ReverseDefaultDisplayOrder',	l.working_settings:reverse_default_disp_order )																		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'O', l.provide_xml,	'Notes',						l.notes ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'SortableCustomFields' ) }">
		<MvIF EXPR = "{ NOT ComponentModule_Provision_SortableCustomFields( l.module, l.provide_xml:tags:SortableCustomFields[ 1 ], l.working_settings ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'DefaultSort' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(	'O', l.provide_xml,	'DefaultSort',					l.default_sort,
																			 'disp_order,name_asc,name_desc,code_asc,code_desc,bestsellers,price_asc,price_desc,newest,oldest,updated,relevance,customfield,customfield_desc',
																			 'disp_order,name_asc,name_desc,code_asc,code_desc,bestsellers,price_asc,price_desc,newest,oldest,updated,relevance,customfield,customfield_desc' ) }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvIF EXPR = "{ ( l.default_sort NE 'customfield' ) AND ( l.default_sort NE 'customfield_desc' ) }">
			<MvASSIGN NAME = "l.working_settings:default_sort"	VALUE = "{ l.default_sort }">
		<MvELSE>
			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml:tags:DefaultSort[ 1 ], 'module',	l.module_code ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml:tags:DefaultSort[ 1 ], 'code',	l.field_code ) }">
				<MvFUNCTIONRETURN>
			</MvIF>

			<MvIF EXPR = "{ NOT miva_array_search( l.working_settings:sort_custom, 1, l.custom_field, '( l.custom_field:module EQ l.module_code ) AND ( l.custom_field:code EQ l.field_code )' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Cannot set default sort to \'' $ l.default_sort $ ':' $ l.module_code $ ':' $ l.field_code $ '\' without enabling the column to be sortable' ) }">
			<MvELSE>
				<MvASSIGN NAME = "l.working_settings:default_sort"	VALUE = "{ l.default_sort $ ':' $ l.module_code $ ':' $ l.field_code }">
			</MvIF>
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.working_settings:mode"	VALUE = "{ CSSUIProductList_Mode( l.item, l.working_settings:mode ) }">
	<MvASSIGN NAME = "l.null"					VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_List( 'O', l.provide_xml, 'ImageMachineEnabled', l.working_settings:imagemachine_enabled,	'Yes,No',	'1,0' ) }">

	<MvIF EXPR = "{ l.working_settings:image EQ 'TYPE' }">
		<MvASSIGN NAME = "l.working_settings:imagetypes"			VALUE = "">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Code( 'R', l.provide_xml, 'TypeCode', l.working_settings:image ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Must provide image type code' ) }">
		</MvIF>

		<MvIF EXPR = "{ [ g.Module_Library_DB ].ImageType_Load_Code( l.working_settings:image, l.imagetype ) }">
			<MvASSIGN NAME = "l.working_settings:image"				VALUE = "{ 'type:' $ l.imagetype:code }">
			<MvASSIGN NAME = "l.working_settings:imagetypes[ 1 ]"	VALUE = "{ l.imagetype:code }">
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Image type does not exist' ) }">
			<MvASSIGN NAME = "l.working_settings:image"				VALUE = "N">
		</MvIF>
	<MvELSEIF EXPR = "{ l.working_settings:image EQ 'N' }">
		<MvASSIGN NAME = "l.working_settings:imagetypes" VALUE = "">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'ImageTypes' ) }">
		<MvIF EXPR = "{ NOT ComponentModule_Provision_ImageTypes( l.module, l.provide_xml:tags:ImageTypes[ 1 ], l.working_settings ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to provision multiple image types' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'ImageDimensions' ) }">
		<MvASSIGN NAME = "l.dimensions" VALUE = "{ l.provide_xml:tags:ImageDimensions[ 1 ] }">
		
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Boolean( l.dimensions, 'constrain', l.constrain ) }">	
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvIF EXPR = "{ NOT l.constrain }">
			<MvASSIGN NAME = "l.working_settings:constrain" VALUE = "N">
		<MvELSE>
			<MvASSIGN NAME = "l.working_settings:constrain" VALUE = "B">

			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer( 'R', l.dimensions, 'Width',	l.working_settings:b_width ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer( 'R', l.dimensions, 'Height',	l.working_settings:b_height ) }">
				<MvFUNCTIONRETURN>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Fields' ) }">
		<MvIF EXPR = "{ NOT ComponentModule_Provision_Fields( l.module, l.provide_xml:tags:Fields[ 1 ], l.working_settings ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Buttons' ) }">
		<MvEVAL EXPR = "{ ComponentModule_Provision_Buttons( l.module, l.provide_xml:tags:Buttons[ 1 ], l.working_settings ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL  l.working_settings:col_num }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.working_settings:col_num ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Invalid value entered for Product Columns: ' $ g.Validation_Message ) }">
		</MvIF>
	<MvELSE>
		<MvASSIGN NAME = "l.working_settings:col_num" VALUE = 2>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
		<MvASSIGN NAME = "l.source"						VALUE = "{ trim( l.provide_xml:tags:Template[ 1 ]:value ) }">
		<MvASSIGN NAME = "l.working_settings:advanced"	VALUE = 1>
	<MvELSE>
		<MvASSIGN NAME = "l.working_settings:advanced"	VALUE = 0>

		<MvEVAL EXPR = "{ Generate_Code( l.module, l.working_settings, l.source, l.item ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.source, l.working_settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings" VALUE = "{ l.working_settings }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision_SortableCustomFields" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:sort_custom"		VALUE = "">

	<MvFOREACH ITERATOR = "l.child_xml" ARRAY = "l.provide_xml:children">
		<MvASSIGN NAME = "l.name"					VALUE = "{ tolower( l.child_xml:name ) }">

		<MvIF EXPR = "{ l.name EQ 'custom' }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.child_xml, 'module',	l.field_module ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.child_xml, 'code',	l.field_code ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ModuleAndFeatures_Load_Code_Cached( l.field_module, l.customfield_module ) }">
				<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
					<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
				</MvIF>

				<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Module not found' ) }">
			<MvELSEIF EXPR = "{ NOT l.customfield_module:active }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Module \'' $ l.customfield_module:code $ '\' has been deactivated' ) }">
			<MvELSEIF EXPR = "{ NOT l.customfield_module:feature_hash:fields_prod }">
					<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Module \'' $ l.customfield_module:code $ '\' does not provide the \'fields_prod\' feature' ) }">
			<MvELSEIF EXPR = "{ l.customfield_module:api_ver LT 9.07 }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Field \'' $ l.field_code $ '\' does not support sorting' ) }">
			<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreModule_Count_Module_Cached( l.customfield_module:id ) }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Module \'' $ l.customfield_module:code $ '\' is not installed in store \'' $ g.Store:code $ '\'' ) }">
			<MvELSE>
				<MvEVAL EXPR = "{ [ g.Module_Root $ l.customfield_module:module ].Module_Product_Field_Capabilities( l.customfield_module, l.field_code, l.capabilities ) }">

				<MvIF EXPR = "{ NOT l.capabilities:orderby }">
					<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage(  l.provide_xml, 'Field \'' $ l.field_code $ '\' does not support sorting' ) }">
				<MvELSE>
					<MvASSIGN NAME = "l.customfield"		VALUE = "">
					<MvASSIGN NAME = "l.customfield:module"	VALUE = "{ l.field_module }">
					<MvASSIGN NAME = "l.customfield:code"	VALUE = "{ l.field_code }">

					<MvASSIGN NAME = "l.null"				VALUE = "{ miva_array_insert_var( l.settings:sort_custom, l.customfield, -1 ) }">
				</MvIF>
			</MvIF>
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.child_xml, 'Unknown tag' ) }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision_Fields" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:field_name"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_code"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_sku"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_price"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_weight"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_desc"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:fields_custom"	VALUE = "">

	<MvASSIGN NAME = "l.child_pos"		VALUE = 1>
	<MvASSIGN NAME = "l.child_count"	VALUE = "{ miva_array_elements( l.provide_xml:children ) }">
	<MvWHILE EXPR = "{ l.child_pos LE l.child_count }">
		<MvASSIGN NAME = "l.name"		VALUE = "{ tolower( l.provide_xml:children[ l.child_pos ]:name ) }">

		<MvIF EXPR = "{ l.name EQ 'name' }">			<MvASSIGN NAME = "l.settings:field_name"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'code' }">		<MvASSIGN NAME = "l.settings:field_code"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'sku' }">			<MvASSIGN NAME = "l.settings:field_sku"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'price' }">		<MvASSIGN NAME = "l.settings:field_price"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'weight' }">		<MvASSIGN NAME = "l.settings:field_weight"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'description' }">	<MvASSIGN NAME = "l.settings:field_desc"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'custom' }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml:children[ l.child_pos ], 'module',	l.field_module ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml:children[ l.child_pos ], 'code',		l.field_code ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ModuleAndFeatures_Load_Code_Cached( l.field_module, l.customfield_module ) }">
				<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
					<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml:children[ l.child_pos ] ) }">
				</MvIF>

				<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml:children[ l.child_pos ], 'Module not found' ) }">
			<MvELSEIF EXPR = "{ NOT l.customfield_module:active }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Module \'' $ l.customfield_module:code $ '\' has been deactivated' ) }">
			<MvELSEIF EXPR = "{ NOT l.customfield_module:feature_hash:fields_prod }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml:children[ l.child_pos ], 'Module \'' $ l.customfield_module:code $ '\' does not provide the \'fields_prod\' feature' ) }">
			<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreModule_Count_Module_Cached( l.customfield_module:id ) }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml:children[ l.child_pos ], 'Module \'' $ l.customfield_module:code $ '\' is not installed in store \'' $ g.Store:name $ '\'' ) }">
			<MvELSE>
				<MvASSIGN NAME = "l.customfield"		VALUE = "">
				<MvASSIGN NAME = "l.customfield:module"	VALUE = "{ l.field_module }">
				<MvASSIGN NAME = "l.customfield:code"	VALUE = "{ l.field_code }">

				<MvASSIGN NAME = "l.null"				VALUE = "{ miva_array_insert_var( l.settings:fields_custom, l.customfield, -1 ) }">
			</MvIF>
		</MvIF>

		<MvASSIGN NAME = "l.child_pos"	VALUE = "{ l.child_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision_ImageTypes" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:imagetypes" VALUE = "">

	<MvASSIGN NAME = "l.type_pos"		VALUE = 1>
	<MvASSIGN NAME = "l.type_found"		VALUE = 1>
	<MvASSIGN NAME = "l.type_count"		VALUE = "{ miva_array_elements( l.provide_xml:children ) }">

	<MvWHILE EXPR = "{ l.type_pos LE l.type_count }">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].ImageType_Load_Code( l.provide_xml:children[ l.type_pos ]:value, l.imagetype ) }">
			<MvASSIGN NAME = "l.settings:imagetypes" INDEX = "{ l.type_found }" VALUE = "{ l.provide_xml:children[ l.type_pos ]:value }">
			<MvASSIGN NAME = "l.type_found"										VALUE = "{ l.type_found + 1 }">
		</MvIF>

		<MvASSIGN NAME = "l.type_pos" VALUE = "{ l.type_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision_Buttons" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:button_add"	VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Add' ) }">
	<MvASSIGN NAME = "l.settings:button_buy"	VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Buy' ) }">
	<MvASSIGN NAME = "l.settings:button_wish"	VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'AddToWishList' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pages" VALUE = "{ [ g.Module_Feature_TUI_DB ].Item_Load_PageList( l.item ) }">
	
	<MvFUNCTIONRETURN VALUE = "{ 'Exports Global Facets Layout & Product List design/template from pages: ' $ l.pages }">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item:code EQ 'facets' }">	<MvFUNCTIONRETURN VALUE = "{ SkinsComponentModule_Export_Item_Facets( l.module, l.item, l.output ) }">
	<MvELSE>									<MvFUNCTIONRETURN VALUE = "{ SkinsComponentModule_Export_Item_ProdList( l.module, l.item, l.output ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item_Facets" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( 'cssui-prodlist-facets.mvc', l.managedtemplateversion ) }">
		<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
			<Module code="{ encodeentities( l.module:code ) }" feature="component">
				<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.managedtemplateversion:source ) }"></Template>
				<Notes>#Set_Current_Time#</Notes>
			</Module>
		</MvCAPTURE>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item_ProdList" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item_Runtime( l.item:id, l.pages ) }">
			<Page_Update code="{ encodeentities( l.page:code ) }">
				<Item code="{ encodeentities( l.item:code ) }">
					<MvASSIGN NAME = "l.product_list"	VALUE = "{ miva_variable_value( 'l.page:settings:' $ l.item:code ) }">
					<MvASSIGN NAME = "l.mode"			VALUE = "{ CSSUIProductList_Mode( l.item:code, l.product_list:mode ) }">

					<MvIF EXPR = "{ l.mode EQ 'ctgy' }">		<Mode>Category</Mode>
					<MvELSEIF EXPR = "{ l.mode EQ 'plst' }">	<Mode>ProductList</Mode>
					<MvELSEIF EXPR = "{ l.mode EQ 'srch' }">	<Mode>SearchResults</Mode>
					<MvELSEIF EXPR = "{ l.mode EQ 'relp' }">	<Mode>RelatedProducts</Mode>
					</MvIF>
		
					<NoCategoryCodeInLinks><MvIF EXPR = "{ l.product_list:linkexcludecat }">Yes<MvELSE>No</MvIF></NoCategoryCodeInLinks>
					<ImageMachineEnabled><MvIF EXPR = "{ CSSUIProductList_AssociatedImageMachine_IsCurrentlyEnabled( l.page:code, l.item:code ) }">Yes<MvELSE>No</MvIF></ImageMachineEnabled>
					<ProductsPerPage><MvEVAL EXPR = "{ int( l.product_list:per_page ) }"></ProductsPerPage>
					<PageCountNumber><MvEVAL EXPR = "{ int( l.product_list:page_disp_count ) }"></PageCountNumber>
					<ItemsPerPage><MvIF EXPR = "{ l.product_list:items_per_page_filter }">Yes<MvELSE>No</MvIF></ItemsPerPage>
					<SortBySwitch><MvIF EXPR = "{ l.product_list:sort_by:switch }">Yes<MvELSE>No</MvIF></SortBySwitch>
					<SortByName><MvIF EXPR = "{ l.product_list:sort_by:name }">Yes<MvELSE>No</MvIF></SortByName>
					<SortByCode><MvIF EXPR = "{ l.product_list:sort_by:code }">Yes<MvELSE>No</MvIF></SortByCode>
					<SortByBest><MvIF EXPR = "{ l.product_list:sort_by:best }">Yes<MvELSE>No</MvIF></SortByBest>
					<SortByPrice><MvIF EXPR = "{ l.product_list:sort_by:price }">Yes<MvELSE>No</MvIF></SortByPrice>
					<SortByNew><MvIF EXPR = "{ l.product_list:sort_by:new }">Yes<MvELSE>No</MvIF></SortByNew>
					<SortByOld><MvIF EXPR = "{ l.product_list:sort_by:old }">Yes<MvELSE>No</MvIF></SortByOld>
					<SortByUpdated><MvIF EXPR = "{ l.product_list:sort_by:updated }">Yes<MvELSE>No</MvIF></SortByUpdated>
					<SortByRelevance><MvIF EXPR = "{ l.product_list:sort_by:relevance }">Yes<MvELSE>No</MvIF></SortByRelevance>

					<SortableCustomFields>
						<MvFOREACH ITERATOR = "l.custom_field" ARRAY = "l.product_list:sort_custom">
							<Custom module="{ encodeentities( l.custom_field:module ) }" code="{ encodeentities( l.custom_field:code ) }" />
						</MvFOREACH>
					</SortableCustomFields>
					
					<MvIF EXPR = "{ ISNULL l.product_list:default_sort }">
						<DefaultSort>disp_order</DefaultSort>
					<MvELSEIF EXPR = "{ ( ( 'customfield:' IN l.product_list:default_sort ) EQ 1 ) OR
										( ( 'customfield_desc:' IN l.product_list:default_sort ) EQ 1 ) }">
						<MvASSIGN NAME = "l.sort_type"		VALUE = "{ gettoken( l.product_list:default_sort, ':', 1 ) }">
						<MvASSIGN NAME = "l.module_code"	VALUE = "{ gettoken( l.product_list:default_sort, ':', 2 ) }">
						<MvASSIGN NAME = "l.field_code"		VALUE = "{ gettoken( l.product_list:default_sort, ':', 3 ) }">

						<DefaultSort module="{ encodeentities( l.module_code ) }" code="{ encodeentities( l.field_code ) }"><MvEVAL EXPR = "{ miva_cdata_encode( l.sort_type ) }"></DefaultSort>
					<MvELSE>
						<DefaultSort><MvEVAL EXPR = "{ miva_cdata_encode( l.product_list:default_sort ) }"></DefaultSort>
					</MvIF>

					<ReverseDefaultDisplayOrder><MvIF EXPR = "{ l.product_list:reverse_default_disp_order }">Yes<MvELSE>No</MvIF></ReverseDefaultDisplayOrder>

					<Fields>
						<MvIF EXPR = "{ l.product_list:field_name }">	<Name />		</MvIF>
						<MvIF EXPR = "{ l.product_list:field_code }">	<Code />		</MvIF>
						<MvIF EXPR = "{ l.product_list:field_sku }">	<SKU />			</MvIF>
						<MvIF EXPR = "{ l.product_list:field_price }">	<Price />		</MvIF>
						<MvIF EXPR = "{ l.product_list:field_desc }">	<Description />	</MvIF>
						<MvIF EXPR = "{ l.product_list:field_weight }">	<Weight />		</MvIF>

						<MvFOREACH ITERATOR = "l.custom_field" ARRAY = "l.product_list:fields_custom">
							<Custom module="{ encodeentities( l.custom_field:module ) }" code="{ encodeentities( l.custom_field:code ) }" />
						</MvFOREACH>
					</Fields>

					<MvIF EXPR = "{ l.product_list:price EQ 'retail' }">	<PriceDisplay>Retail</PriceDisplay>
					<MvELSEIF EXPR = "{ l.product_list:price EQ 'sale' }">	<PriceDisplay>Sale</PriceDisplay>
					<MvELSE>												<PriceDisplay>Base</PriceDisplay>
					</MvIF>

					<MvIF EXPR = "{ l.product_list:additionalprice EQ 'retail' }">		<AdditionalPriceDisplay>Retail</AdditionalPriceDisplay>
					<MvELSEIF EXPR = "{ l.product_list:additionalprice EQ 'base' }">	<AdditionalPriceDisplay>Base</AdditionalPriceDisplay>
					<MvELSE>															<AdditionalPriceDisplay>None</AdditionalPriceDisplay>
					</MvIF>

					<DisplayDiscounts><MvIF EXPR = "{ l.product_list:displaydiscounts }">Yes<MvELSE>No</MvIF></DisplayDiscounts>
					<PredictDiscounts><MvIF EXPR = "{ l.product_list:predictdiscounts }">Yes<MvELSE>No</MvIF></PredictDiscounts>

					<MvIF EXPR = "{ l.product_list:advanced }">
						<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.product_list:template_filename, l.templateversion ) }">
							<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.templateversion:source ) }"></Template>
						</MvIF>

						<ImageTypes>
							<MvFOREACH ITERATOR = "l.imagetype" ARRAY = "l.product_list:imagetypes">
								<ImageType><MvEVAL EXPR = "{ miva_cdata_encode( l.imagetype ) }"></ImageType>
							</MvFOREACH>
						</ImageTypes>
					<MvELSE>
						<MvIF EXPR = "{ l.product_list:format EQ 'X' }">	<Format>Expanded</Format>
						<MvELSE>											<Format>Line Item</Format>
						</MvIF>

						<MvIF EXPR = "{ l.product_list:invmsg EQ 'L' }">		<InventoryMessage>Long</InventoryMessage>
						<MvELSEIF EXPR = "{ l.product_list:invmsg EQ 'S' }">	<InventoryMessage>Short</InventoryMessage>
						<MvELSE>												<InventoryMessage>None</InventoryMessage>
						</MvIF>

						<ProductColumns><MvEVAL EXPR = "{ int( l.product_list:col_num ) }"></ProductColumns>

						<Buttons>
							<MvIF EXPR = "{ l.product_list:button_add }">	<Add />				</MvIF>
							<MvIF EXPR = "{ l.product_list:button_buy }">	<Buy />				</MvIF>
							<MvIF EXPR = "{ l.product_list:button_wish }">	<AddToWishList />	</MvIF>
						</Buttons>

						<MvIF EXPR = "{ l.product_list:image EQ 'T' }">			<Image>Thumbnail</Image>
						<MvELSEIF EXPR = "{ l.product_list:image EQ 'F' }">		<Image>Full-Sized</Image>
						<MvELSEIF EXPR = "{ l.product_list:image NE 'N' }">
							<Image>TYPE</Image>
							<TypeCode><MvEVAL EXPR = "{ miva_cdata_encode( substring( l.product_list:image, 6, len( l.product_list:image ) ) ) }"></TypeCode>
						<MvELSE>
							<Image>None</Image>
						</MvIF>
					</MvIF>

					<Notes>#Set_Current_Time#</Notes>

					<MvIF EXPR = "{ l.product_list:constrain NE 'B' }">
						<ImageDimensions constrain="No" />
					<MvELSE>
						<ImageDimensions constrain="Yes">
							<Width><MvEVAL EXPR = "{ int( l.product_list:b_width ) }"></Width>
							<Height><MvEVAL EXPR = "{ int( l.product_list:b_height ) }"></Height>
						</ImageDimensions>
					</MvIF>
				</Item>
			</Page_Update>
		</MvFOREACH>
	</MvCAPTURE>
</MvFUNCTION>

<MvCOMMENT>
|
|	Feature provision_store
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Provision_Store" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml, 'Template',	l.template ) OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml, 'Notes',		l.notes ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'cssui-prodlist-facets.mvc', l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.template, l.null_settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvINCLUDE FILE = "modules/component/seo_include.mv">

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-SF-
| Next Error Code: 10
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-searchfield">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI Search Field">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0900">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "9.07">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, json, clientside, clientside_sri, vis_store, data_store, provision_store, skins">
</MvFUNCTION>

<MvCOMMENT>
|
| Store-level Module Data Support Feature (data_store)
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'cssui-menuitem-searchfield.mvc', l.null_managedtemplate ) }">
		<MvASSIGN NAME = "l.menuitem_settings:advanced"						VALUE = 0>
		<MvASSIGN NAME = "l.menuitem_settings:prod_code"					VALUE = 1>
		<MvASSIGN NAME = "l.menuitem_settings:prod_name"					VALUE = 1>
		<MvASSIGN NAME = "l.menuitem_settings:prod_sku"						VALUE = 0>
		<MvASSIGN NAME = "l.menuitem_settings:prod_price"					VALUE = 1>
		<MvASSIGN NAME = "l.menuitem_settings:prod_wt"						VALUE = 0>
		<MvASSIGN NAME = "l.menuitem_settings:prod_desc"					VALUE = 0>
		<MvASSIGN NAME = "l.menuitem_settings:price"						VALUE = "base">
		<MvASSIGN NAME = "l.menuitem_settings:additionalprice"				VALUE = "">
		<MvASSIGN NAME = "l.menuitem_settings:displaydiscounts"				VALUE = 0>
		<MvASSIGN NAME = "l.menuitem_settings:predictdiscounts"				VALUE = 0>
		<MvASSIGN NAME = "l.menuitem_settings:image"						VALUE = "type:main">
		<MvASSIGN NAME = "l.menuitem_settings:imagetypes" INDEX = 1			VALUE = "main">
		<MvASSIGN NAME = "l.menuitem_settings:constrain"					VALUE = "B">
		<MvASSIGN NAME = "l.menuitem_settings:b_width"						VALUE = 50>
		<MvASSIGN NAME = "l.menuitem_settings:b_height"						VALUE = 50>
		<MvASSIGN NAME = "l.menuitem_settings:sort_by"						VALUE = "code_asc">
		<MvASSIGN NAME = "l.menuitem_settings:customflds"					VALUE = "">

		<MvEVAL EXPR = "{ Generate_MenuItem_Template_Code( l.menuitem_settings, l.menuitem_template_code ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.menuitem_template_code, l.menuitem_settings, 'cssui-menuitem-searchfield.mvc' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'cssui-javascript-searchfield.mvc', l.null_managedtemplate ) }">
		<MvASSIGN NAME = "l.javascript_settings:srch_page"					VALUE = "SRCH">
		<MvASSIGN NAME = "l.javascript_settings:defer"						VALUE = 1>

		<MvEVAL EXPR = "{ Generate_JavaScript_Template_Code( l.javascript_template_code ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.javascript_template_code, l.javascript_settings, 'cssui-javascript-searchfield.mvc' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.module:version EQ l.version }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CSSUI-CMP-SF-00009', 'Module \'' $ l.module:name $ '\' does not support manual upgrade.  New versions may only be obtained through the streaming update system.' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_GlobalDelete_ManagedTemplate_Filename( 'cssui-menuitem-searchfield.mvc' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_GlobalDelete_ManagedTemplate_Filename( 'cssui-javascript-searchfield.mvc' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Edit Store Screen Feature (vis_store)
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Store_Tabs" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current_Cached( 'cssui-menuitem-searchfield.mvc', l.managedtemplateversion ) }">
		<MvFUNCTIONRETURN VALUE = "">
	</MvIF>

	<MvASSIGN NAME = "l.tabs"		VALUE = "GT_SRCHSETT/CMP-CSSUI-SEARCHFIELD:Search Preview Settings">

	<MvIF EXPR = "{ miva_array_elements( l.managedtemplateversion:settings:imagetypes ) GT 0 }">
		<MvASSIGN NAME = "l.tabs"	VALUE = "{ l.tabs $ ',GT_SRCHSETT/CMP-CSSUI-SEARCHFIELD_dimensions:Search Preview Settings Image Dimensions' }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.tabs }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.CSSUI_SearchField:javascript_settings:srch_page"		VALUE = "{ trim( g.CSSUI_SearchField:javascript_settings:srch_page ) }">
	<MvASSIGN NAME = "g.CSSUI_SearchField:javascript_settings:defer"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.CSSUI_SearchField:javascript_settings:defer ) }">
	<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:advanced"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.CSSUI_SearchField:menuitem_settings:advanced ) }">
	<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:prod_code"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.CSSUI_SearchField:menuitem_settings:prod_code ) }">
	<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:prod_name"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.CSSUI_SearchField:menuitem_settings:prod_name ) }">
	<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:prod_sku"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.CSSUI_SearchField:menuitem_settings:prod_sku ) }">
	<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:prod_price"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.CSSUI_SearchField:menuitem_settings:prod_price ) }">
	<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:prod_wt"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.CSSUI_SearchField:menuitem_settings:prod_wt ) }">
	<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:prod_desc"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.CSSUI_SearchField:menuitem_settings:prod_desc ) }">
	<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:sort_by"			VALUE = "{ trim( g.CSSUI_SearchField:menuitem_settings:sort_by ) }">
	<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:price"				VALUE = "{ trim( g.CSSUI_SearchField:menuitem_settings:price ) }">
	<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:additionalprice"	VALUE = "{ trim( g.CSSUI_SearchField:menuitem_settings:additionalprice ) }">
	<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:displaydiscounts"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.CSSUI_SearchField:menuitem_settings:displaydiscounts ) }">
	<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:predictdiscounts"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.CSSUI_SearchField:menuitem_settings:predictdiscounts ) }">
	<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:image"				VALUE = "{ trim( g.CSSUI_SearchField:menuitem_settings:image ) }">
	<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:constrain"			VALUE = "{ trim( g.CSSUI_SearchField:menuitem_settings:constrain ) }">
	<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:b_width"			VALUE = "{ int( g.CSSUI_SearchField:menuitem_settings:b_width ) }">
	<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:b_height"			VALUE = "{ int( g.CSSUI_SearchField:menuitem_settings:b_height ) }">

	<MvIF EXPR = "{ g.CSSUI_SearchField:menuitem_settings:sort_by NE 'disp_order'	AND
					g.CSSUI_SearchField:menuitem_settings:sort_by NE 'name_asc'		AND
					g.CSSUI_SearchField:menuitem_settings:sort_by NE 'name_desc'	AND
					g.CSSUI_SearchField:menuitem_settings:sort_by NE 'code_asc'		AND
					g.CSSUI_SearchField:menuitem_settings:sort_by NE 'code_desc'	AND
					g.CSSUI_SearchField:menuitem_settings:sort_by NE 'bestsellers'	AND
					g.CSSUI_SearchField:menuitem_settings:sort_by NE 'price_asc'	AND
					g.CSSUI_SearchField:menuitem_settings:sort_by NE 'price_desc'	AND
					g.CSSUI_SearchField:menuitem_settings:sort_by NE 'newest'		AND
					g.CSSUI_SearchField:menuitem_settings:sort_by NE 'relevance' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-CSSUI-SEARCHFIELD', '', 'Please select a valid sort-by field' ) }">
	</MvIF>

	<MvFOREACH ITERATOR = "l.imagetype" ARRAY = "g.CSSUI_SearchField:menuitem_settings:imagetypes">
		<MvASSIGN NAME = "l.imagetype"		VALUE = "{ trim( l.imagetype ) }">
	</MvFOREACH>

	<MvFOREACH ITERATOR = "l.customfield" ARRAY = "g.CSSUI_SearchField:menuitem_settings:customfields">
		<MvASSIGN NAME = "l.customfield"	VALUE = "{ trim( l.customfield ) }">
	</MvFOREACH>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( g.CSSUI_SearchField:menuitem_settings:b_width ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-CSSUI-SEARCHFIELD_dimensions', 'CSSUI_SearchField:menuitem_settings:b_width', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( g.CSSUI_SearchField:menuitem_settings:b_height ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-CSSUI-SEARCHFIELD_dimensions', 'CSSUI_SearchField:menuitem_settings:b_height', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ g.CSSUI_SearchField:menuitem_settings:advanced }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( g.CSSUI_SearchField:menuitem_settings:template_code ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-CSSUI-SEARCHFIELD', 'CSSUI_SearchField:menuitem_settings:template_code', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( g.CSSUI_SearchField:javascript_settings:template_code ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-CSSUI-SEARCHFIELD', 'CSSUI_SearchField:javascript_settings:template_code', g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Filename( 'cssui-javascript-searchfield.mvc', l.javascript_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Filename( 'cssui-menuitem-searchfield.mvc', l.menuitem_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.menuitem_settings"										VALUE = "{ l.menuitem_managedtemplate:version:settings }">
	<MvASSIGN NAME = "l.javascript_settings"									VALUE = "{ l.javascript_managedtemplate:version:settings }">
	<MvASSIGN NAME = "l.settings_updated"										VALUE = 0>

	<MvIF EXPR = "{ g.CSSUI_SearchField:menuitem_settings:advanced }">
		<MvASSIGN NAME = "l.predictdiscounts"									VALUE = "{ g.CSSUI_SearchField:menuitem_settings:predictdiscounts }">
	<MvELSE>
		<MvASSIGN NAME = "l.predictdiscounts"									VALUE = "{ l.menuitem_settings:displaydiscounts OR ( l.menuitem_settings:price EQ 'sale' ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| If Point + Click, add the single selected image type (if any) to the list
	| of image types to be initialized.  Otherwise, add all selected image types.
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.null"													VALUE = "{ [ g.Module_Library_Utilities ].CustomFieldSelect_Selected( g.CSSUI_SearchField:menuitem_settings:customfields, l.customfields ) }">

	<MvIF EXPR = "{ g.CSSUI_SearchField:menuitem_settings:advanced }">
		<MvASSIGN NAME = "l.imagetype_count"									VALUE = "{ [ g.Module_Library_Utilities ].ImageTypeSelect_Selected( g.CSSUI_SearchField:menuitem_settings:imagetypes, l.imagetypes ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.imagetype_count"									VALUE = 0>
		<MvASSIGN NAME = "l.imagetypes"											VALUE = "">

		<MvIF EXPR = "{ ( 'type:' IN g.CSSUI_SearchField:menuitem_settings:image ) EQ 1 }">
			<MvASSIGN NAME = "l.imagetypes" INDEX = "{ ++l.imagetype_count }"	VALUE = "{ substring_var( g.CSSUI_SearchField:menuitem_settings:image, 6, len_var( g.CSSUI_SearchField:menuitem_settings:image ) - 5 ) }">
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.menuitem_settings:advanced"								VALUE = "{ g.CSSUI_SearchField:menuitem_settings:advanced }">
	<MvASSIGN NAME = "l.menuitem_settings:predictdiscounts"						VALUE = "{ l.predictdiscounts }">
	<MvASSIGN NAME = "l.menuitem_settings:customfields"							VALUE = "{ l.customfields }">
	<MvASSIGN NAME = "l.menuitem_settings:imagetypes"							VALUE = "{ l.imagetypes }">

	<MvIF EXPR = "{ l.imagetype_count }">
		<MvASSIGN NAME = "l.menuitem_settings:constrain"						VALUE = "{ g.CSSUI_SearchField:menuitem_settings:constrain }">
		<MvASSIGN NAME = "l.menuitem_settings:b_width"							VALUE = "{ g.CSSUI_SearchField:menuitem_settings:b_width }">
		<MvASSIGN NAME = "l.menuitem_settings:b_height"							VALUE = "{ g.CSSUI_SearchField:menuitem_settings:b_height }">
	</MvIF>

	<MvIF EXPR = "{ NOT g.CSSUI_SearchField:menuitem_settings:advanced }">
		<MvASSIGN NAME = "l.menuitem_settings:prod_code"						VALUE = "{ g.CSSUI_SearchField:menuitem_settings:prod_code }">
		<MvASSIGN NAME = "l.menuitem_settings:prod_name"						VALUE = "{ g.CSSUI_SearchField:menuitem_settings:prod_name }">
		<MvASSIGN NAME = "l.menuitem_settings:prod_sku"							VALUE = "{ g.CSSUI_SearchField:menuitem_settings:prod_sku }">
		<MvASSIGN NAME = "l.menuitem_settings:prod_price"						VALUE = "{ g.CSSUI_SearchField:menuitem_settings:prod_price }">
		<MvASSIGN NAME = "l.menuitem_settings:prod_wt"							VALUE = "{ g.CSSUI_SearchField:menuitem_settings:prod_wt }">
		<MvASSIGN NAME = "l.menuitem_settings:prod_desc"						VALUE = "{ g.CSSUI_SearchField:menuitem_settings:prod_desc }">
		<MvASSIGN NAME = "l.menuitem_settings:price"							VALUE = "{ g.CSSUI_SearchField:menuitem_settings:price }">
		<MvASSIGN NAME = "l.menuitem_settings:additionalprice"					VALUE = "{ g.CSSUI_SearchField:menuitem_settings:additionalprice }">
		<MvASSIGN NAME = "l.menuitem_settings:displaydiscounts"					VALUE = "{ g.CSSUI_SearchField:menuitem_settings:displaydiscounts }">
		<MvASSIGN NAME = "l.menuitem_settings:image"							VALUE = "{ g.CSSUI_SearchField:menuitem_settings:image }">
	</MvIF>

	<MvIF EXPR = "{ g.CSSUI_SearchField:menuitem_settings:sort_by EQ '' }">
		<MvASSIGN NAME = "l.menuitem_settings:sort_by"							VALUE = "code_asc">
	<MvELSE>
		<MvASSIGN NAME = "l.menuitem_settings:sort_by"							VALUE = "{ g.CSSUI_SearchField:menuitem_settings:sort_by }">
	</MvIF>

	<MvIF EXPR = "{ NOT g.CSSUI_SearchField:menuitem_settings:advanced }">
		<MvEVAL EXPR = "{ Generate_MenuItem_Template_Code( l.menuitem_settings, g.CSSUI_SearchField:menuitem_settings:template_code ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.menuitem_managedtemplate, '', g.CSSUI_SearchField:menuitem_settings:template_code, l.menuitem_settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-CSSUI-SEARCHFIELD', 'CSSUI_SearchField:menuitem_settings:template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.javascript_settings:srch_page"	VALUE = "{ g.CSSUI_SearchField:javascript_settings:srch_page }">
	<MvASSIGN NAME = "l.javascript_settings:defer"		VALUE = "{ g.CSSUI_SearchField:javascript_settings:defer }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.javascript_managedtemplate, '', g.CSSUI_SearchField:javascript_settings:template_code, l.javascript_settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-CSSUI-SEARCHFIELD', 'CSSUI_SearchField:javascript_settings:template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Head" PARAMETERS = "module var, tab" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.tab NE 'GT_SRCHSETT' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].Element_PageLookup_Dialog_CSS() }">

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_AD ].Element_Feature_TUI_JavaScript_Combined_Begin() }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].Element_PageLookup_Dialog_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].Element_Feature_TUI_JavaScript_Combined_End() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvASSIGN NAME = "l.customfield_count"	VALUE = "{ [ g.Module_Library_Utilities ].ProductCustomFieldList_Load( l.customfields ) }">
	<MvASSIGN NAME = "l.imagetype_count"	VALUE = "{ [ g.Module_Library_DB ].ImageTypeList_Load_All( l.imagetypes ) }">

	<MvIF EXPR = "{ l.load_fields }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current_Cached( 'cssui-javascript-searchfield.mvc', l.javascript_template ) OR
						NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current_Cached( 'cssui-menuitem-searchfield.mvc', l.menuitem_template ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.javascript_settings"												VALUE = "{ l.javascript_template:settings }">
		<MvASSIGN NAME = "l.menuitem_settings"													VALUE = "{ l.menuitem_template:settings }">

		<MvASSIGN NAME = "g.CSSUI_SearchField"													VALUE = "">
		<MvASSIGN NAME = "g.CSSUI_SearchField:javascript_settings:srch_page"					VALUE = "{ l.javascript_settings:srch_page }">
		<MvASSIGN NAME = "g.CSSUI_SearchField:javascript_settings:defer"						VALUE = "{ l.javascript_settings:defer }">

		<MvASSIGN NAME = "g.CSSUI_SearchField:javascript_settings:template_id" 					VALUE = "{ l.javascript_template:templ_id }">
		<MvASSIGN NAME = "g.CSSUI_SearchField:javascript_settings:template_code" 				VALUE = "{ l.javascript_template:source }">

		<MvIF EXPR = "{ ISNULL l.menuitem_settings:price }">
			<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:price"						VALUE = "base">
		<MvELSE>
			<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:price"						VALUE = "{ l.menuitem_settings:price }">
		</MvIF>

		<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:additionalprice"				VALUE = "{ l.menuitem_settings:additionalprice }">
		<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:displaydiscounts"				VALUE = "{ l.menuitem_settings:displaydiscounts }">
		<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:predictdiscounts"				VALUE = "{ l.menuitem_settings:predictdiscounts }">

		<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:prod_code"						VALUE = "{ l.menuitem_settings:prod_code }">
		<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:prod_name"						VALUE = "{ l.menuitem_settings:prod_name }">
		<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:prod_sku"						VALUE = "{ l.menuitem_settings:prod_sku }">
		<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:prod_price"						VALUE = "{ l.menuitem_settings:prod_price }">
		<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:prod_wt"						VALUE = "{ l.menuitem_settings:prod_wt }">
		<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:prod_desc"						VALUE = "{ l.menuitem_settings:prod_desc }">

		<MvIF EXPR = "{ l.menuitem_settings:sort_by EQ '' }">
			<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:sort_by"					VALUE = "code_asc">
		<MvELSE>
			<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:sort_by"					VALUE = "{ l.menuitem_settings:sort_by }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.menuitem_settings:constrain }">
			<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:constrain"					VALUE = "B">
		<MvELSE>
			<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:constrain"					VALUE = "{ l.menuitem_settings:constrain }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.menuitem_settings:b_width }">
			<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:b_width"					VALUE = 50>
		<MvELSE>
			<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:b_width"					VALUE = "{ l.menuitem_settings:b_width }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.menuitem_settings:b_height }">
			<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:b_height"					VALUE = 50>
		<MvELSE>
			<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:b_height"					VALUE = "{ l.menuitem_settings:b_height }">
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].InitializeCustomFieldSelect( l.menuitem_settings:customfields, g.CSSUI_SearchField:menuitem_settings:customfields ) }">

		<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:image"							VALUE = "{ l.menuitem_settings:image }">
		<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:imagetypes"						VALUE = "{ l.menuitem_settings:imagetypes }">

		<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:advanced"						VALUE = "{ l.menuitem_settings:advanced }">
		<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:template_id" 					VALUE = "{ l.menuitem_template:templ_id }">
		<MvASSIGN NAME = "g.CSSUI_SearchField:menuitem_settings:template_code" 					VALUE = "{ l.menuitem_template:source }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( g.Tab, 'CMP-CSSUI-SEARCHFIELD' ) }">
		<MvHIDE FIELDS = "g.CSSUI_SearchField:javascript_settings:srch_page, g.CSSUI_SearchField:javascript_settings:defer, g.CSSUI_SearchField:javascript_settings:template_id,
						  g.CSSUI_SearchField:javascript_settings:template_code, g.CSSUI_SearchField:menuitem_settings:advanced, g.CSSUI_SearchField:menuitem_settings:template_id,
						  g.CSSUI_SearchField:menuitem_settings:template_code, g.CSSUI_SearchField:menuitem_settings:sort_by, g.CSSUI_SearchField:menuitem_settings:prod_code,
						  g.CSSUI_SearchField:menuitem_settings:prod_name, g.CSSUI_SearchField:menuitem_settings:prod_sku, g.CSSUI_SearchField:menuitem_settings:prod_price,
						  g.CSSUI_SearchField:menuitem_settings:prod_wt, g.CSSUI_SearchField:menuitem_settings:prod_desc, g.CSSUI_SearchField:menuitem_settings:customfields,
						  g.CSSUI_SearchField:menuitem_settings:price, g.CSSUI_SearchField:menuitem_settings:additionalprice, g.CSSUI_SearchField:menuitem_settings:predictdiscounts,
						  g.CSSUI_SearchField:menuitem_settings:displaydiscounts, g.CSSUI_SearchField:menuitem_settings:imagetypes, g.CSSUI_SearchField:menuitem_settings:image">
	<MvELSE>
		<MvHIDE FIELDS = "g.CSSUI_SearchField:javascript_settings:template_id, g.CSSUI_SearchField:menuitem_settings:template_id">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_SRCHSETT', 'CMP-CSSUI-SEARCHFIELD' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<tr><td nowrap>
			Search Page:
		</td><td width="100%">
			<input type="text" id="CSSUI_SearchField_Search_Page" name="CSSUI_SearchField:javascript_settings:srch_page" value="{ encodeentities( g.CSSUI_SearchField:javascript_settings:srch_page ) }" style="width: 75px;" />
			<a href="JavaScript:PageLookupDialog( 'CSSUI_SearchField_Search_Page' );" style="position: relative; top: 4px;"><img alt="Lookup" border="0" src="{ g.AdminGraphics_Path $ 'lookup.gif' }"></a>
		</td></tr>

		<tr><td nowrap>
			&nbsp;
		</td><td width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox_With_Label( g.CSSUI_SearchField:javascript_settings:defer, 'CSSUI_SearchField:javascript_settings:defer', 'Yes', 'Defer Loading of JavaScript' ) }">
		</td></tr>

		<tr><td colspan="2">
			&nbsp;
		</td></tr>

		<tr><td valign="top" nowrap>
			JavaScript Template:
		</td><td width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( 'CSSUI_SearchField:javascript_settings:template_code', g.CSSUI_SearchField:javascript_settings:template_code, 10, 58, g.CSSUI_SearchField:javascript_settings:template_id ) }">
		</td></tr>

		<MvIF EXPR = "{ NOT g.CSSUI_SearchField:menuitem_settings:advanced }">
			<MvHIDE FIELDS = "g.CSSUI_SearchField:menuitem_settings:template_code">
		<MvELSE>
			<tr><td valign="top" nowrap>
				Menu Item Template:
			</td><td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( 'CSSUI_SearchField:menuitem_settings:template_code', g.CSSUI_SearchField:menuitem_settings:template_code, 10, 58, g.CSSUI_SearchField:menuitem_settings:template_id ) }">
			</td></tr>
		</MvIF>

		<tr><td valign="top" nowrap>
			<b>Sort By</b>:
		</td><td width="100%">
			<select name="CSSUI_SearchField:menuitem_settings:sort_by">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'disp_order', 	g.CSSUI_SearchField:menuitem_settings:sort_by, 'Display Order' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'name_asc', 	g.CSSUI_SearchField:menuitem_settings:sort_by, 'Name Ascending' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'name_desc', 	g.CSSUI_SearchField:menuitem_settings:sort_by, 'Name Descending' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'code_asc', 	g.CSSUI_SearchField:menuitem_settings:sort_by, 'Code Ascending' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'code_desc', 	g.CSSUI_SearchField:menuitem_settings:sort_by, 'Code Descending' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'bestsellers', g.CSSUI_SearchField:menuitem_settings:sort_by, 'Best Selling' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'price_asc', 	g.CSSUI_SearchField:menuitem_settings:sort_by, 'Lowest Price' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'price_desc', 	g.CSSUI_SearchField:menuitem_settings:sort_by, 'Highest Price' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'newest', 		g.CSSUI_SearchField:menuitem_settings:sort_by, 'Newest' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'relevance', 	g.CSSUI_SearchField:menuitem_settings:sort_by, 'Relevance' ) }">
			</select>
		</td></tr>

		<MvIF EXPR = "{ g.CSSUI_SearchField:menuitem_settings:advanced }">
			<MvHIDE FIELDS = "g.CSSUI_SearchField:menuitem_settings:prod_code, g.CSSUI_SearchField:menuitem_settings:prod_name, g.CSSUI_SearchField:menuitem_settings:prod_sku,
							  g.CSSUI_SearchField:menuitem_settings:prod_price, g.CSSUI_SearchField:menuitem_settings:prod_wt, g.CSSUI_SearchField:menuitem_settings:prod_desc">
		<MvELSE>
			<tr><td valign="top" nowrap>
				<b>Product Fields</b>:
			</td><td width="100%">
				<table border="0" cellpadding="0" cellspacing="0">
				<tr><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox_With_Label( g.CSSUI_SearchField:menuitem_settings:prod_code,	'CSSUI_SearchField:menuitem_settings:prod_code',	'Yes', 'Product Code' ) }">
				</td><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox_With_Label( g.CSSUI_SearchField:menuitem_settings:prod_name,	'CSSUI_SearchField:menuitem_settings:prod_name',	'Yes', 'Product Name' ) }">
				</td></tr>
				<tr><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox_With_Label( g.CSSUI_SearchField:menuitem_settings:prod_sku,	'CSSUI_SearchField:menuitem_settings:prod_sku',		'Yes', 'Product SKU' ) }">
				</td><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox_With_Label( g.CSSUI_SearchField:menuitem_settings:prod_price,	'CSSUI_SearchField:menuitem_settings:prod_price',	'Yes', 'Product Price' ) }">
				</td></tr>
				<tr><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox_With_Label( g.CSSUI_SearchField:menuitem_settings:prod_wt,	'CSSUI_SearchField:menuitem_settings:prod_wt',		'Yes', 'Product Weight' ) }">
				</td><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox_With_Label( g.CSSUI_SearchField:menuitem_settings:prod_desc,	'CSSUI_SearchField:menuitem_settings:prod_desc',	'Yes', 'Product Description' ) }">
				</td></tr>
				</table>
			</td></tr>
		</MvIF>

		<MvIF EXPR = "{ l.customfield_count }">
			<tr><td align="left" valign="top" nowrap>
				<b>Custom Fields:</b>
			</td><td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCustomFieldSelect( 'CSSUI_SearchField:menuitem_settings:customfields', g.CSSUI_SearchField:menuitem_settings:customfields, l.customfields, l.customfield_count ) }">
			</td></tr>
		</MvIF>

		<MvIF EXPR = "{ g.CSSUI_SearchField:menuitem_settings:advanced }">
			<MvHIDE FIELDS = "g.CSSUI_SearchField:menuitem_settings:price, g.CSSUI_SearchField:menuitem_settings:additionalprice, g.CSSUI_SearchField:menuitem_settings:displaydiscounts">

			<tr><td>
				&nbsp;
			</td><td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox_With_Label( g.CSSUI_SearchField:menuitem_settings:predictdiscounts, 'CSSUI_SearchField:menuitem_settings:predictdiscounts', 1, 'Calculate Predicted Discounts and Sale Price' ) }">
			</td></tr>
		<MvELSE>
			<MvHIDE FIELDS = "g.CSSUI_SearchField:menuitem_settings:predictdiscounts">

			<tr><td colspan="2">&nbsp;</td></tr>

			<tr><td align="left" nowrap valign="top">
				<b>Displayed Price:</b>
			</td><td align="left" nowrap width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio_With_Label( 'CSSUI_SearchField:menuitem_settings:price', 'retail',	g.CSSUI_SearchField:menuitem_settings:price, 'Retail Price' ) }"><br />
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio_With_Label( 'CSSUI_SearchField:menuitem_settings:price', 'base',		g.CSSUI_SearchField:menuitem_settings:price, 'Base Price (includes any legacy price group discounts but does not reflect sales, coupons or discounts applied in the basket)' ) }"><br />
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio_With_Label( 'CSSUI_SearchField:menuitem_settings:price', 'sale',		g.CSSUI_SearchField:menuitem_settings:price, 'Sale Price (includes legacy price groups and predicted discounts)' ) }"><br />
			</td></tr>

			<tr><td align="left" nowrap valign="top">
				<b>Additional Price Display:</b>
			</td><td align="left" nowrap width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio_With_Label( 'CSSUI_SearchField:menuitem_settings:additionalprice', '',		g.CSSUI_SearchField:menuitem_settings:additionalprice, 'None' ) }"><br />
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio_With_Label( 'CSSUI_SearchField:menuitem_settings:additionalprice', 'retail',	g.CSSUI_SearchField:menuitem_settings:additionalprice, 'Retail Price' ) }"><br />
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio_With_Label( 'CSSUI_SearchField:menuitem_settings:additionalprice', 'base',	g.CSSUI_SearchField:menuitem_settings:additionalprice, 'Base Price (includes any legacy price group discounts but does not reflect sales, coupons or discounts applied in the basket)' ) }"><br />
			</td></tr>

			<tr><td>
				&nbsp;
			</td><td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( g.CSSUI_SearchField:menuitem_settings:displaydiscounts, 'CSSUI_SearchField:menuitem_settings:displaydiscounts', 1, 'Display Predicted Discounts' ) }">
			</td></tr>

			<tr><td colspan="2">&nbsp;</td></tr>
		</MvIF>

		<MvIF EXPR = "{ NOT g.CSSUI_SearchField:menuitem_settings:advanced }">
			<MvHIDE FIELDS = "g.CSSUI_SearchField:menuitem_settings:imagetypes">

			<tr><td align="left" nowrap>
				<b>Image:</b>
			</td><td align="left" width="100%">
				<select name="CSSUI_SearchField:menuitem_settings:image">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'N', g.CSSUI_SearchField:menuitem_settings:image, 'None' ) }">

					<MvFOREACH ITERATOR = "l.imagetype" ARRAY = "l.imagetypes" COUNT = "{ l.imagetype_count }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'type:' $ l.imagetype:code, g.CSSUI_SearchField:menuitem_settings:image, encodeentities( l.imagetype:descrip ) ) }">
					</MvFOREACH>

					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'T', g.CSSUI_SearchField:menuitem_settings:image, 'Legacy Thumbnail' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'F', g.CSSUI_SearchField:menuitem_settings:image, 'Legacy Full-Sized' ) }">
				</select>
			</td></tr>
		<MvELSE>
			<MvHIDE FIELDS = "g.CSSUI_SearchField:menuitem_settings:image">

			<MvIF EXPR = "{ l.imagetype_count GT 0 }">
				<tr><td align="left" valign="top" nowrap>
					<b>Product Images:</b>
				</td><td align="left" width="100%">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawImageTypeSelect( 'CSSUI_SearchField:menuitem_settings:imagetypes', g.CSSUI_SearchField:menuitem_settings:imagetypes, l.imagetypes, l.imagetype_count ) }">
				</td></tr>
			</MvIF>
		</MvIF>

		<tr><td>
			&nbsp;
		</td><td width="100%">
			<input type="hidden" name="CSSUI_SearchField:menuitem_settings:advanced" value="{ encodeentities( g.CSSUI_SearchField:menuitem_settings:advanced ) }">
			<MvIF EXPR = "{ g.CSSUI_SearchField:menuitem_settings:advanced }">
				<a href="#" onclick="Toggle( 'CSSUI_SearchField:menuitem_settings:advanced', 0 ); return false;">Point + Click Mode</a>
			<MvELSE>
				<a href="#" onclick="Toggle( 'CSSUI_SearchField:menuitem_settings:advanced', 1 ); return false;">Advanced Mode</a>
			</MvIF>
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].Element_PageLookup_Dialog_HTML() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, 'CMP-CSSUI-SEARCHFIELD_dimensions' ) }">
		<MvHIDE FIELDS = "g.CSSUI_SearchField:menuitem_settings:constrain, g.CSSUI_SearchField:menuitem_settings:b_width, g.CSSUI_SearchField:menuitem_settings:b_height">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_SRCHSETT', 'CMP-CSSUI-SEARCHFIELD_dimensions' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
			<tr>
				<td align="left" valign="top" nowrap><b>Product Image:</b></td>
				<td align="left" width="100%">
					<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'CSSUI_SearchField:menuitem_settings:constrain', 'N', g.CSSUI_SearchField:menuitem_settings:constrain, 'No constraints' ) }"></label><br />
					<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'CSSUI_SearchField:menuitem_settings:constrain', 'B', g.CSSUI_SearchField:menuitem_settings:constrain, 'Resize to fit within bounding box:' ) }"></label>
					<input type="text" name="CSSUI_SearchField:menuitem_settings:b_width" value="{ encodeentities( g.CSSUI_SearchField:menuitem_settings:b_width ) }" style="text-align:right;" size="5" /> x
					<input type="text" name="CSSUI_SearchField:menuitem_settings:b_height" value="{ encodeentities( g.CSSUI_SearchField:menuitem_settings:b_height ) }" style="text-align:right;" size="5" /> pixels
				</td>
			</tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Component Feature (component)
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current_Cached( 'cssui-javascript-searchfield.mvc', l.managedtemplateversion ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_JSON_Variables( l.all_settings ) }">

	<MvIF EXPR = "{ l.managedtemplateversion:settings:defer }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside_Deferred( l.all_settings, 'ajax.js' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside_Deferred( l.all_settings, 'runtime_ui.js' ) }">

		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Output_Module_Link_JavaScript_Deferred( l.module, 'runtime.js' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside( l.all_settings, 'ajax.js' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside( l.all_settings, 'runtime_ui.js' ) }">

		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Output_Module_Link_JavaScript( l.module, 'runtime.js' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.flags:sep"				VALUE = 1>
	<MvASSIGN NAME = "l.flags:force_secure"		VALUE = "{ g.Secure }">
	<MvASSIGN NAME = "l.search_url"				VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Page_Code_URL( l.managedtemplateversion:settings:srch_page, l.flags ) }">

	<script type="text/javascript">
		var MMSearchField_Search_URL_sep = "<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScriptEncode( l.search_url ) }">Search=";

		(function( obj, eventType, fn )
		{
			if ( obj.addEventListener )
			{
				obj.addEventListener( eventType, fn, false );
			}
			else if ( obj.attachEvent )
			{
				obj.attachEvent( 'on' + eventType, fn );
			}
		})( window, 'mmsearchfield_override', function()
		{
			<MvDO FILE = "{ g.Store_Template_Path $ 'cssui-javascript-searchfield.mvc' }" NAME = "l.null" VALUE = "{ Template_Render( l.null, l.all_settings ) }">
		} );
	</script>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvASSIGN NAME = "l.mm_searchfield_id" VALUE = "{ MakeSessionID() }">

	<span class="mm_searchfield_container">
		<input type="text" name="Search" class="mm_searchfield textfield" data-mm_searchfield="Yes" data-mm_searchfield_id="{ encodeentities( l.mm_searchfield_id ) }" value="{ encodeentities( g.Search ) }" autocomplete="off" />
		<span class="mm_searchfield_menu" data-mm_searchfield_menu="Yes" data-mm_searchfield_id="{ encodeentities( l.mm_searchfield_id ) }"></span>
	</span>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvCOMMENT>
|
| Module JSON Feature (json)
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_JSON" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Module_Function EQ 'Search' }">	<MvFUNCTIONRETURN VALUE = "{ Module_JSON_Search( l.module ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_JSON_Search" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.Session_Type NE 'runtime' }">				<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">	<MvFUNCTIONRETURN> </MvIF>

	<MvASSIGN NAME = "g.Search"	VALUE = "{ [ g.Module_JSON ].JSON_Decode( g.Search ) }">
	<MvASSIGN NAME = "g.Fields" VALUE = "{ [ g.Module_JSON ].JSON_Decode( g.Fields ) }">
	<MvASSIGN NAME = "g.Count"	VALUE = "{ int( g.Count ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current_Cached( 'cssui-menuitem-searchfield.mvc',		l.menuitem_template ) OR
				    NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current_Cached( 'cssui-javascript-searchfield.mvc',	l.javascript_template ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.menuitem_settings"					VALUE = "{ l.menuitem_template:settings }">
	<MvASSIGN NAME = "l.javascript_settings"				VALUE = "{ l.javascript_template:settings }">

	<MvASSIGN NAME = "l.all_settings:searchfield"			VALUE = "{ l.menuitem_settings }">
	<MvASSIGN NAME = "l.all_settings:searchfield:srch_page"	VALUE = "{ l.javascript_settings:srch_page }">
	<MvASSIGN NAME = "l.all_settings:searchfield:templ_id"	VALUE = "{ l.menuitem_template:templ_id }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query( l.query ) }">

	<MvIF EXPR = "{ l.menuitem_settings:price EQ 'sale' }">
		<MvIF EXPR = "{ [ g.Module_Feature_PGR_UT ].DiscountSignature_Calculate_Cached( g.Basket:cust_id, l.discount_signature ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query_Set_DiscountSignature( l.query, l.discount_signature ) }">
		</MvIF>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query_OutputCustomFields( l.query, l.menuitem_settings:customfields, miva_array_elements( l.menuitem_settings:customfields ), l.all_settings:customfield_names ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query_Search( l.query, g.Search ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query_Search_Origin( l.query, 'Search Preview' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query_ORDER_BY( l.query, l.menuitem_settings:sort_by ) }">

	<MvASSIGN NAME = "l.product_count"					VALUE = "{ [ g.Module_Library_DB ].Runtime_ProductList_Load( l.query, 0, g.Count, l.null, l.null, l.products ) }">
	<MvASSIGN NAME = "l.have_unprecalculated_products"	VALUE = 0>

	<MvASSIGN NAME = "l.url_flags"						VALUE = "">
	<MvASSIGN NAME = "l.url_flags:sep"					VALUE = 1>

	<MvFOREACH ITERATOR = "l.product" ARRAY = "l.products" COUNT = "{ l.product_count }">
		<MvIF EXPR = "{ ISNULL l.product:sale_price }">
			<MvASSIGN NAME = "l.have_unprecalculated_products" VALUE = 1>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_Runtime( l.product ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.null"			VALUE = "{ [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_ImageTypes( l.product:id, 0, l.product, l.null_all_settings, l.menuitem_settings:imagetypes, l.menuitem_settings:constrain, l.menuitem_settings:b_width, l.menuitem_settings:b_height ) }">
		<MvASSIGN NAME = "l.product:link"	VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Product_URL( l.product, l.url_flags ) $ 'Category_Code=' $ encodeentities( l.product:canonical_category:code ) }">
	</MvFOREACH>

	<MvCOMMENT>
	|
	| We have to create a discount state if we are displaying specific discount totals (predictdiscounts) or if we
	| are displaying sale price and at least one of our products did not have a sale price already calculated.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.menuitem_settings:predictdiscounts OR ( l.menuitem_settings:price EQ 'sale' AND l.have_unprecalculated_products ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_CreateEmpty( l.discount_state ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.pricegroup_count" VALUE = "{ [ g.Module_Feature_PGR_UT ].ResolvedPriceGroupAndModuleList_Load_Customer_Cached( g.Basket:cust_id, l.pricegroups ) }">
		
		<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Set_PriceGroups( l.discount_state, l.pricegroups, l.pricegroup_count ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_Disable_Unused_Item_PriceGroups( l.discount_state ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_Predict_Baseline_Discounts( l.discount_state ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFOREACH ITERATOR = "l.product" ARRAY = "l.products" COUNT = "{ l.product_count }">
			<MvIF EXPR = "{ l.product:id LE 0 }">																			<MvFOREACHCONTINUE>
			<MvELSEIF EXPR = "{ ( NOT l.menuitem_settings:predictdiscounts ) AND ( NOT ISNULL l.product:sale_price ) }">	<MvFOREACHCONTINUE>
			</MvIF>

			<MvIF EXPR = "{ NOT ISNULL l.product:base_price }">
				<MvASSIGN NAME = "l.product:price" VALUE = "{ l.product:base_price }">
			</MvIF>

			<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_Predict_Product_Discounts_WithProductSubscriptionTerm( l.discount_state, l.product, 0, 0, 1, l.product:price, l.product:discounts, l.product:discount_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.product:formatted_price" VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.product:price ) }">

			<MvFOREACH ITERATOR = "l.discount" ARRAY = "l.product:discounts" COUNT = "{ l.product:discount_count }">
				<MvASSIGN NAME = "l.discount:formatted_discount" VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.discount:discount ) }">
			</MvFOREACH>
		</MvFOREACH>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	[
		<MvASSIGN NAME = "l.arrayelement_count"					VALUE = 0>

		<MvFOREACH ITERATOR = "l.product" ARRAY = "l.products" COUNT = "{ l.product_count }">
			<MvASSIGN NAME = "l.all_settings:product"			VALUE = "{ l.product }">

			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.arrayelement_count ) }">
				<MvCAPTURE VARIABLE = "l.rendered_menuitem"><MvEVAL EXPR = "{ [ g.Store_Template_Path $ 'cssui-menuitem-searchfield.mvc' ].Template_Render( l.null, l.all_settings ) }"></MvCAPTURE>

				"product_link":	"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.product:link ) }">",
				"menuitem":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.rendered_menuitem ) }">"
			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
		</MvFOREACH>
	]
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvCOMMENT>
|
| Module Client Side Feature (clientside)
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Clientside" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.len"	VALUE = "{ len_var( g.Filename ) }">

	<MvIF EXPR = "{ ( '.js' EIN g.Filename ) EQ l.len }">		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
	</MvIF>

	<MvIF EXPR = "{ Module_Clientside_Output_File( l.module, g.Filename ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.null"	VALUE = "{ miva_output_header( 'Status', '404 Not Found' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Clientside_Output_File" PARAMETERS = "module var, filename" STANDARDOUTPUTLEVEL = "">
	<MvINCLUDE FILE = "modules/component/cmp-cssui-searchfield/output.mv">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Clientside_File_Integrity" PARAMETERS = "module var, filename, minified" STANDARDOUTPUTLEVEL = "">
	<MvINCLUDE FILE = "modules/component/cmp-cssui-searchfield/integrity.mv">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Clientside_Combined_Integrity" PARAMETERS = "module var, filename, minified" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature provision_store
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Provision_Store" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Exists( l.provide_xml, 'version' ) }">				<MvASSIGN NAME = "l.version" VALUE = "9.1200">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml, 'version', l.version ) }">	<MvFUNCTIONRETURN>
	<MvELSEIF EXPR = "{ l.version EQ 'latest' }">																		<MvASSIGN NAME = "l.version" VALUE = "{ l.module:version }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Search_Page' )		OR
					[ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'DeferJavaScript' )	OR
					[ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'JavaScript' ) }">
		<MvEVAL EXPR = "{ Module_Provision_Store_JavaScript( l.module, l.provide_xml, l.version ) }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'MenuItem' ) }">
		<MvEVAL EXPR = "{ Module_Provision_Store_MenuItem( l.module, l.provide_xml:tags:MenuItem[ 1 ] ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Store_JavaScript" PARAMETERS = "module var, provide_xml var, version" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Filename( 'cssui-javascript-searchfield.mvc', l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings"	VALUE = "{ l.managedtemplate:version:settings }">
	<MvASSIGN NAME = "l.source"		VALUE = "{ l.managedtemplate:version:source }">

	<MvIF EXPR = "{ l.version LT 9.1300 }">
		<MvASSIGN NAME = "l.settings:defer"	VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.version LT 10.0300 }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Code(		'O', l.provide_xml, 'Search_Page',		l.settings:srch_page ) OR
						NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml, 'DeferJavaScript',	l.settings:defer ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'JavaScript' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Code(		'O', l.provide_xml:tags:JavaScript[ 1 ], 'Search_Page',		l.settings:srch_page )	OR
						NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml:tags:JavaScript[ 1 ], 'DeferJavaScript',	l.settings:defer )		OR
						NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 		'o', l.provide_xml:tags:JavaScript[ 1 ], 'Template',		l.source )				OR
						NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'O', l.provide_xml:tags:JavaScript[ 1 ], 'Notes',			l.notes ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.source, l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Store_MenuItem" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Filename( 'cssui-menuitem-searchfield.mvc', l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings" VALUE = "{ l.managedtemplate:version:settings }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List( 		'O', l.provide_xml, 'SortBy', 					l.settings:sort_by,
																														'disp_order,name_asc,name_desc,code_asc,code_desc,bestsellers,price_asc,price_desc,newest,relevance',
																														'disp_order,name_asc,name_desc,code_asc,code_desc,bestsellers,price_asc,price_desc,newest,relevance' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml, 'PriceDisplay',				l.settings:price,			'Retail,Base,Sale',					'retail,base,sale' )					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml, 'AdditionalPriceDisplay',	l.settings:additionalprice,	'None,Retail,Base',					',retail,base' )						OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml,	'Image',					l.settings:image,			'Thumbnail,None,Full-Sized,Type',	'T,N,F,TYPE' )							OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml, 'DisplayDiscounts',			l.settings:displaydiscounts )																			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml, 'PredictDiscounts',			l.settings:predictdiscounts )																			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'O', l.provide_xml,	'Notes',					l.notes ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Fields' ) }">
		<MvIF EXPR = "{ NOT Module_Provision_Store_Fields( l.module, l.provide_xml:tags:Fields[ 1 ], l.settings ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.settings:image EQ 'N' }">
		<MvASSIGN NAME = "l.settings:imagetypes"	VALUE = "">
	<MvELSEIF EXPR = "{ l.settings:image EQ 'TYPE' }">
		<MvASSIGN NAME = "l.settings:imagetypes"	VALUE = "">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Code( 'R', l.provide_xml, 'TypeCode', l.imagetype_code ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Must provide image type code' ) }">
		</MvIF>

		<MvIF EXPR = "{ [ g.Module_Library_DB ].ImageType_Load_Code( l.imagetype_code, l.imagetype ) }">
			<MvASSIGN NAME = "l.settings:image"					VALUE = "{ 'type:' $ l.imagetype:code }">
			<MvASSIGN NAME = "l.settings:imagetypes" INDEX = 1	VALUE = "{ l.imagetype:code }">
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Image type does not exist' ) }">
			<MvASSIGN NAME = "l.settings:image"		VALUE = "N">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'ImageTypes' ) }">
		<MvASSIGN NAME = "l.imagetypes"				VALUE = "{ l.provide_xml:tags:imagetypes[ 1 ] }">
		<MvASSIGN NAME = "l.imagetype_count"		VALUE = 0>
		<MvASSIGN NAME = "l.settings:imagetypes"	VALUE = "">

		<MvFOREACH ITERATOR = "l.imagetype_tag" ARRAY = "l.imagetypes:tags:imagetype">
			<MvASSIGN NAME = "l.imagetype"			VALUE = "{ trim( l.imagetype_tag:value ) }">

			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ImageType_Load_Code( l.imagetype, l.null ) }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Image type \'' $ l.imagetype $ '\' does not exist.' ) }">
				<MvFOREACHCONTINUE>
			</MvIF>

			<MvASSIGN NAME = "l.settings:imagetypes" INDEX = "{ ++l.imagetype_count }" VALUE = "{ l.imagetype }">
		</MvFOREACH>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'ImageDimensions' ) }">
		<MvASSIGN NAME = "l.dimensions" VALUE = "{ l.provide_xml:tags:ImageDimensions[ 1 ] }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Boolean( l.dimensions, 'constrain', l.constrain ) }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvIF EXPR = "{ NOT l.constrain }">
			<MvASSIGN NAME = "l.settings:constrain" VALUE = "N">
		<MvELSE>
			<MvASSIGN NAME = "l.settings:constrain" VALUE = "B">

			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer( 'R', l.dimensions, 'Width',	l.settings:b_width ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer( 'R', l.dimensions, 'Height',	l.settings:b_height ) }">
				<MvFUNCTIONRETURN>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
		<MvASSIGN NAME = "l.source"					VALUE = "{ trim( l.provide_xml:tags:Template[ 1 ]:value ) }">
		<MvASSIGN NAME = "l.settings:advanced"		VALUE = 1>
	<MvELSE>
		<MvASSIGN NAME = "l.settings:advanced"		VALUE = 0>

		<MvEVAL EXPR = "{ Generate_MenuItem_Template_Code( l.settings, l.source ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.source, l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Store_Fields" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:prod_code"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:prod_name"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:prod_sku"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:prod_price"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:prod_wt"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:prod_desc"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:customfields"	VALUE = "">
	<MvASSIGN NAME = "l.customfield_count"		VALUE = 0>

	<MvFOREACH ITERATOR = "l.child_xml" ARRAY = "l.provide_xml:children">
		<MvASSIGN NAME = "l.name" VALUE = "{ tolower( l.child_xml:name ) }">

		<MvIF EXPR = "{ l.name EQ 'code' }">				<MvASSIGN NAME = "l.settings:prod_code"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'name' }">			<MvASSIGN NAME = "l.settings:prod_name"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'sku' }">				<MvASSIGN NAME = "l.settings:prod_sku"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'price' }">			<MvASSIGN NAME = "l.settings:prod_price"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'weight' }">			<MvASSIGN NAME = "l.settings:prod_wt"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'description' }">		<MvASSIGN NAME = "l.settings:prod_desc"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'custom' }">
			<MvASSIGN NAME = "l.customfield_count" VALUE = "{ l.customfield_count + 1 }">

			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.child_xml, 'module',	l.settings:customfields[ l.customfield_count ]:module ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.child_xml, 'code',	l.settings:customfields[ l.customfield_count ]:code ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature skins
|
</MvCOMMENT>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "Exports Global Search Preview">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( 'cssui-javascript-searchfield.mvc',	l.javascript_managedtemplateversion )	OR 
					NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( 'cssui-menuitem-searchfield.mvc',		l.menuitem_managedtemplateversion ) }">
		<MvFUNCTIONRETURN>
	</MvIF>
	
	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<Module code="{ encodeentities( l.module:code ) }" feature="component" version="{ encodeentities( l.module:version ) }">
			<JavaScript>
				<Search_Page><MvEVAL EXPR = "{ miva_cdata_encode( l.javascript_managedtemplateversion:settings:srch_page ) }"></Search_Page>
				<DeferJavaScript><MvIF EXPR = "{ l.javascript_managedtemplateversion:settings:defer }">Yes<MvELSE>No</MvIF></DeferJavaScript>
				<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.javascript_managedtemplateversion:source ) }"></Template>
				<Notes>#Set_Current_Time#</Notes>
			</JavaScript>
			<MenuItem>
				<MvIF EXPR = "{ l.menuitem_managedtemplateversion:settings:price EQ 'retail' }">	<PriceDisplay>Retail</PriceDisplay>
				<MvELSEIF EXPR = "{ l.menuitem_managedtemplateversion:settings:price EQ 'sale' }">	<PriceDisplay>Sale</PriceDisplay>
				<MvELSE>																			<PriceDisplay>Base</PriceDisplay>
				</MvIF>

				<MvIF EXPR = "{ l.menuitem_managedtemplateversion:settings:additionalprice EQ 'retail' }">		<AdditionalPriceDisplay>Retail</AdditionalPriceDisplay>
				<MvELSEIF EXPR = "{ l.menuitem_managedtemplateversion:settings:additionalprice EQ 'base' }">	<AdditionalPriceDisplay>Base</AdditionalPriceDisplay>
				<MvELSE>																						<AdditionalPriceDisplay>None</AdditionalPriceDisplay>
				</MvIF>

				<DisplayDiscounts><MvIF EXPR = "{ l.menuitem_managedtemplateversion:settings:displaydiscounts }">Yes<MvELSE>No</MvIF></DisplayDiscounts>
				<PredictDiscounts><MvIF EXPR = "{ l.menuitem_managedtemplateversion:settings:predictdiscounts }">Yes<MvELSE>No</MvIF></PredictDiscounts>
				<Fields>
					<MvIF EXPR = "{ l.menuitem_managedtemplateversion:settings:prod_code }">	<Code />		</MvIF>
					<MvIF EXPR = "{ l.menuitem_managedtemplateversion:settings:prod_name }">	<Name />		</MvIF>
					<MvIF EXPR = "{ l.menuitem_managedtemplateversion:settings:prod_sku }">		<SKU />			</MvIF>
					<MvIF EXPR = "{ l.menuitem_managedtemplateversion:settings:prod_price }">	<Price />		</MvIF>
					<MvIF EXPR = "{ l.menuitem_managedtemplateversion:settings:prod_wt }">		<Weight />		</MvIF>
					<MvIF EXPR = "{ l.menuitem_managedtemplateversion:settings:prod_desc }">	<Description />	</MvIF>

					<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.menuitem_managedtemplateversion:settings:customfields">
						<Custom module="{ encodeentities( l.customfield:module ) }" code="{ encodeentities( l.customfield:code ) }" />
					</MvFOREACH>
				</Fields>

				<MvIF EXPR = "{ l.menuitem_managedtemplateversion:settings:advanced }">
					<ImageTypes>
						<MvFOREACH ITERATOR = "l.imagetype" ARRAY = "l.menuitem_managedtemplateversion:settings:imagetypes">
							<ImageType><MvEVAL EXPR = "{ miva_cdata_encode( l.imagetype ) }"></ImageType>
						</MvFOREACH>
					</ImageTypes>
				<MvELSE>
					<MvIF EXPR = "{ l.menuitem_managedtemplateversion:settings:image EQ 'T' }">		<Image>Thumbnail</Image>
					<MvELSEIF EXPR = "{ l.menuitem_managedtemplateversion:settings:image EQ 'F' }">	<Image>Full-Sized</Image>
					<MvELSEIF EXPR = "{ l.menuitem_managedtemplateversion:settings:image NE 'N' }">
						<Image>TYPE</Image>
						<TypeCode><MvEVAL EXPR = "{ miva_cdata_encode( substring( l.menuitem_managedtemplateversion:settings:image, 6, len( l.menuitem_managedtemplateversion:settings:image ) ) ) }"></TypeCode>
					<MvELSE>
						<Image>None</Image>
					</MvIF>
				</MvIF>

				<MvIF EXPR = "{ l.menuitem_managedtemplateversion:settings:constrain NE 'B' }">
					<ImageDimensions constrain="No" />
				<MvELSE>
					<ImageDimensions constrain="Yes">
						<Width><MvEVAL EXPR = "{ int( l.menuitem_managedtemplateversion:settings:b_width ) }"></Width>
						<Height><MvEVAL EXPR = "{ int( l.menuitem_managedtemplateversion:settings:b_height ) }"></Height>
					</ImageDimensions>
				</MvIF>

				<SortBy><MvEVAL EXPR = "{ miva_cdata_encode( l.menuitem_managedtemplateversion:settings:sort_by ) }"></SortBy>

				<MvIF EXPR = "{ l.menuitem_managedtemplateversion:settings:advanced }">
					<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.menuitem_managedtemplateversion:source ) }"></Template>
				</MvIF>

				<Notes>#Set_Current_Time#</Notes>
			</MenuItem>
		</Module>
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_JavaScript_Template_Code" PARAMETERS = "code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code"><MIVA STANDARDOUTPUTLEVEL = "text, html">MMSearchField.prototype.onMenuAppendHeader = function()
{
	return null;
}

MMSearchField.prototype.onMenuAppendItem = function( data )
{
	var span;

	span            = newElement( 'span', null, null, null );
	span.innerHTML  = data;

	return span;
}

MMSearchField.prototype.onMenuAppendStoreSearch = function( search_value )
{
	var item;

	item                = newElement( 'div', { 'class': 'mm_searchfield_menuitem mm_searchfield_menuitem_storesearch' }, null, null );
	item.element_text   = newTextNode( 'Search store for product "' + search_value + '"', item );

	return item;
}

MMSearchField.prototype.onFocus = function( e ) { ; };
MMSearchField.prototype.onBlur  = function( e ) { ; };<MIVA STANDARDOUTPUTLEVEL = ""></MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_MenuItem_Template_Code" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:price EQ 'retail' }">
		<MvASSIGN NAME = "l.price_variable"	VALUE = "l.settings:product:retail">
		<MvASSIGN NAME = "l.price_token"	VALUE = "&mvt:product:formatted_retail;">
	<MvELSEIF EXPR = "{ l.settings:price EQ 'sale' }">
		<MvASSIGN NAME = "l.price_variable"	VALUE = "l.settings:product:price">
		<MvASSIGN NAME = "l.price_token"	VALUE = "&mvt:product:formatted_price;">
	<MvELSE>
		<MvASSIGN NAME = "l.price_variable"	VALUE = "l.settings:product:base_price">
		<MvASSIGN NAME = "l.price_token"	VALUE = "&mvt:product:formatted_base_price;">
	</MvIF>

	<MvCAPTURE VARIABLE = "l.code"><MIVA STANDARDOUTPUTLEVEL = "text, html"><div class="mm_searchfield_menuitem"><MIVA STANDARDOUTPUTLEVEL = "">

	<MvIF EXPR = "{ l.settings:image EQ 'T' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<span class="mm_searchfield_menuitem_image_container">
		<mvt:if expr="NOT ISNULL l.settings:product:thumbnail">
			<img class="mm_searchfield_menuitem_image" src="&mvte:product:thumbnail;" alt="&mvte:product:name;" />
		</mvt:if>
	</span><MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSEIF EXPR = "{ l.settings:image EQ 'F' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<span class="mm_searchfield_menuitem_image_container">
		<mvt:if expr="NOT ISNULL l.settings:product:image">
			<img class="mm_searchfield_menuitem_image" src="&mvte:product:image;" alt="&mvte:product:name;" />
		</mvt:if>
	</span><MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSEIF EXPR = "{ ( 'type:' IN l.settings:image ) EQ 1 }">
		<MvASSIGN NAME = "l.imagetype"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( substring_var( l.settings:image, 6, len_var( l.settings:image ) - 5 ) ) }">

	<MIVA STANDARDOUTPUTLEVEL = "text, html">
	<span class="mm_searchfield_menuitem_image_container">
		<mvt:if expr="{ 'NOT ISNULL l.settings:product:imagetypes:' $ l.imagetype }">
			<img class="mm_searchfield_menuitem_image" src="{ '&mvte:product:imagetypes:' $ l.imagetype $ ';' }" alt="&mvte:product:name;" />
		</mvt:if>
	</span><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>

	<MvASSIGN NAME = "l.customfield_count" VALUE = "{ miva_array_elements( l.settings:customfields ) }">

	<MvIF EXPR = "{ l.settings:prod_code OR l.settings:prod_name OR l.settings:prod_sku OR l.settings:prod_price OR l.settings:prod_wt OR l.settings:prod_desc OR l.customfield_count }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<span class="mm_searchfield_menuitem_title_container"><MIVA STANDARDOUTPUTLEVEL = "">

		<MvIF EXPR = "{ l.settings:prod_code OR l.settings:prod_name }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<span class="mm_searchfield_menuitem_field mm_searchfield_menuitem_name"><MvIF EXPR = "{ l.settings:prod_code }">&mvte:product:code;</MvIF><MvIF EXPR = "{ l.settings:prod_code AND l.settings:prod_name }"> - </MvIF><MvIF EXPR = "{ l.settings:prod_name }">&mvte:product:name;</MvIF></span><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:prod_sku }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<span class="mm_searchfield_menuitem_field">&mvte:product:sku;</span><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:prod_price }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<span class="mm_searchfield_menuitem_field mm_searchfield_menuitem_price">Price: <MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ ( l.settings:additionalprice EQ 'retail' ) AND ( l.settings:price NE 'retail' ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="{ 'l.settings:product:retail GT ' $ l.price_variable }">
				<span style="text-decoration: line-through">&mvt:product:formatted_retail;</span>
			</mvt:if>
			<MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSEIF EXPR = "{ ( l.settings:additionalprice EQ 'base' ) AND ( l.settings:price NE 'base' ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="{ 'l.settings:product:base_price GT ' $ l.price_variable }">
				<span style="text-decoration: line-through">&mvt:product:formatted_base_price;</span>
			</mvt:if>

			<MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>

			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<span class="bold"><MvEVAL EXPR = "{ l.price_token }"></span>
		</span><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:displaydiscounts }"><MIVA STANDARDOUTPUTLEVEL = "text, html">

		<mvt:foreach iterator="discount" array="product:discounts">
			<span class="mm_searchfield_menuitem_field mm_searchfield_menuitem_discount">&mvt:discount:descrip;: &mvt:discount:formatted_discount;</span>
		</mvt:foreach>
		<MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:prod_wt }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<span class="mm_searchfield_menuitem_field mm_searchfield_menuitem_weight">Weight: &mvte:product:weight;</span><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.customfield_count }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:customfields" COUNT = "{ l.customfield_count }">
			<MvASSIGN NAME = "l.safe_module_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.customfield:module ) }">
			<MvASSIGN NAME = "l.safe_field_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.customfield:code ) }">

			<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="{ 'NOT ISNULL l.settings:product:customfield_values:' $ l.safe_module_code $ ':' $ l.safe_field_code }">
			<span class="mm_searchfield_menuitem_field mm_searchfield_menuitem_customfield">&mvte:customfield_names:<MvEVAL EXPR = "{ l.safe_module_code $ ':' $ l.safe_field_code }">;: &mvt:product:customfield_values:<MvEVAL EXPR = "{ l.safe_module_code $ ':' $ l.safe_field_code }">;</span>
		</mvt:if>
		<MIVA STANDARDOUTPUTLEVEL = "">
		</MvFOREACH>

		<MvIF EXPR = "{ l.settings:prod_desc }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<span class="mm_searchfield_menuitem_field mm_searchfield_menuitem_descrip">&mvt:product:descrip;</span><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
	</span><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
</div><MIVA STANDARDOUTPUTLEVEL = ""></MvCAPTURE>
</MvFUNCTION>

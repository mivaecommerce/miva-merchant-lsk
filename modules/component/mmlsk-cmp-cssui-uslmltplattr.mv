<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2020 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-UPSM-
| Next Error Code: 1    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-uslmltplattr">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI Upsell Multiple Product Attributes">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.00">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "text,html,compresswhitespace">
	<MvASSIGN NAME = "l.pos"		VALUE = "{ [ g.Module_Library_Utilities ].FirstSparseArrayElement( g.Upsell_Selected ) }">
	<MvASSIGN NAME = "l.count"		VALUE = "{ miva_array_max( g.Upsell_Selected ) }">

	<MvWHILE EXPR = "{ l.pos LE l.count }">
		<MvIF EXPR = "{ g.Upsell_Selected[ l.pos ] }">
			<input type="hidden" name="{ 'Upsell_Selected[' $ l.pos $ ']' }" value="Yes" />
			<input type="hidden" name="{ 'Product[' $ l.pos $ ']:code' }" value="{ encodeentities( g.Product[ l.pos ]:code ) }" />

			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_Product_Load_Code( g.Product[ l.pos ]:code, l.product ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.attribute_pos"				VALUE = 1>
			<MvASSIGN NAME = "l.product_attribute_count"	VALUE = 0>
			<MvASSIGN NAME = "l.attribute_count"			VALUE = "{ [ g.Module_Library_DB ].AttributeList_Load_Product( l.product:id, l.attributes ) }">
			<MvIF EXPR = "{ l.attribute_count }">
				<div class="required"><MvEVAL EXPR = "{ l.product:name }"></div>
				<div class="product-attributes">
					<table>
					<MvWHILE EXPR = "{ l.attribute_pos LE l.attribute_count }">
						<MvIF EXPR = "{ l.attributes[ l.attribute_pos ]:type EQ 'template' }">
							<MvIF EXPR = "{ [ g.Module_Feature_ATT_DB ].AttributeTemplate_Load_ID( l.attributes[ l.attribute_pos ]:attemp_id, l.template ) }">
								<MvASSIGN NAME = "l.template_attribute_pos"		VALUE = 1>
								<MvASSIGN NAME = "l.template_attribute_count"	VALUE = "{ [ g.Module_Feature_ATT_DB ].AttributeTemplateAttrList_Load_Template( l.template:id, l.template_attributes ) }">

								<MvWHILE EXPR = "{ l.template_attribute_pos LE l.template_attribute_count }">
									<MvEVAL EXPR = "{ CSSUI_Draw_Attribute( l.all_settings, l.attributes[ l.attribute_pos ]:code, l.product_attribute_count, l.template_attributes[ l.template_attribute_pos ], l.pos ) }">
									<MvASSIGN NAME = "l.template_attribute_pos"	VALUE = "{ l.template_attribute_pos + 1 }">
								</MvWHILE>
							</MvIF>
						<MvELSE>
							<MvEVAL EXPR = "{ CSSUI_Draw_Attribute( l.all_settings, '', l.product_attribute_count, l.attributes[ l.attribute_pos ], l.pos ) }">
						</MvIF>

						<MvASSIGN NAME = "l.attribute_pos"	VALUE = "{ l.attribute_pos + 1 }">
					</MvWHILE>
					</table>
				</div>
			</MvIF>
		</MvIF>

		<MvASSIGN NAME = "l.pos"	VALUE = "{ [ g.Module_Library_Utilities ].NextSparseArrayElement( g.Upsell_Selected, l.pos ) }">
	</MvWHILE>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUI_Draw_Attribute" PARAMETERS = "all_settings var, template_code, product_attribute_count var, attribute var, product_pos" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvASSIGN NAME = "l.product_attribute_count" VALUE = "{ l.product_attribute_count + 1 }">
	<tr>
		<td class="prompt">
			<MvIF EXPR = "{ len( l.template_code ) }">
				<input type="hidden" name="{ 'Product[' $ l.product_pos $ ']:Attributes[' $ l.product_attribute_count $ ']:code' }" value="{ encodeentities( l.template_code ) }" />
				<input type="hidden" name="{ 'Product[' $ l.product_pos $ ']:Attributes[' $ l.product_attribute_count $ ']:template_code' }" value="{ encodeentities( l.attribute:code ) }" />
			<MvELSE>
				<input type="hidden" name="{ 'Product[' $ l.product_pos $ ']:Attributes[' $ l.product_attribute_count $ ']:code' }" value="{ encodeentities( l.attribute:code ) }" />
			</MvIF>

			<MvIF EXPR = "{ l.attribute:type NE 'checkbox' }">
				<MvIF EXPR = "{ l.attribute:image }">
					<img src="{ l.attribute:image }" alt="{ l.attribute:prompt }" />
				<MvELSE>		
					<MvIF EXPR = "{ l.attribute:required }">
						<span class="required"><MvEVAL EXPR = "{ l.attribute:prompt }"></span>:
					<MvELSE>
						<MvEVAL EXPR = "{ l.attribute:prompt }">:
					</MvIF>
				</MvIF>
			<MvELSE>
				&nbsp;
			</MvIF>
		</td>
		<td class="field">
			<MvIF EXPR = "{ l.attribute:type EQ 'text' }">
				<input type="text" name="{ 'Product[' $ l.product_pos $ ']:Attributes[' $ l.product_attribute_count $ ']:value' }" value="{ encodeentities( g.Product[ l.product_pos ]:Attributes[ l.product_attribute_count ]:value ) }" size="40" class="textfield" />
			<MvELSEIF EXPR = "{ l.attribute:type EQ 'memo' }">
				<textarea name="{ 'Product[' $ l.product_pos $ ']:Attributes[' $ l.product_attribute_count $ ']:value' }" wrap="on"><MvEVAL EXPR = "{ encodeentities( g.Product[ l.product_pos ]:Attributes[ l.product_attribute_count ]:value ) }"></textarea>
			<MvELSEIF EXPR = "{ l.attribute:type EQ 'radio' }">
				<MvIF EXPR = "{ len( l.template_code ) }">
					<MvASSIGN NAME = "l.option_count" VALUE = "{ [ g.Module_Feature_ATT_DB ].AttributeTemplateOptionList_Load_Attribute( l.attribute:id, l.options ) }">
				<MvELSE>
					<MvASSIGN NAME = "l.option_count" VALUE = "{ [ g.Module_Library_DB ].OptionList_Load_Attribute( l.attribute:id, l.options ) }">
				</MvIF>
				
				<MvASSIGN NAME = "l.option_pos"	VALUE = 1>
				<MvWHILE EXPR = "{ l.option_pos LE l.option_count }">
					<div>
						<MvIF EXPR = "{ ( ( len( g.Product[ l.product_pos ]:Attributes[ l.product_attribute_count ]:value ) EQ 0 ) AND ( l.options[ l.option_pos ]:id EQ l.attribute:default_id ) ) OR
										( g.Product[ l.product_pos ]:Attributes[ l.product_attribute_count ]:value EQ l.options[ l.option_pos ]:code ) }">
							<input type="radio" name="{ 'Product[' $ l.product_pos $ ']:Attributes[' $ l.product_attribute_count $ ']:value' }" value="{ encodeentities( l.options[ l.option_pos ]:code ) }" checked />
						<MvELSE>
							<input type="radio" name="{ 'Product[' $ l.product_pos $ ']:Attributes[' $ l.product_attribute_count $ ']:value' }" value="{ encodeentities( l.options[ l.option_pos ]:code ) }" />
						</MvIF>
					
						<MvIF EXPR = "{ len( l.options[ l.option_pos ]:image ) }">
							<img src="{ encodeentities( l.options[ l.option_pos ]:image ) }" alt="{ encodeentities( l.options[ l.option_pos ]:prompt ) }" />
						<MvELSE>
							<MvEVAL EXPR = "{ l.options[ l.option_pos ]:prompt }">
						</MvIF>
						
						<MvASSIGN NAME = "l.option_pos"	VALUE = "{ l.option_pos + 1 }">
					</div>
				</MvWHILE>			
			<MvELSEIF EXPR = "{ l.attribute:type EQ 'select' OR l.attribute:type EQ 'swatch-select' }">
				<select name="{ 'Product[' $ l.product_pos $ ']:Attributes[' $ l.product_attribute_count $ ']:value' }">
					<MvIF EXPR = "{ len( l.template_code ) }">
						<MvASSIGN NAME = "l.option_count"	VALUE = "{ [ g.Module_Feature_ATT_DB ].AttributeTemplateOptionList_Load_Attribute( l.attribute:id, l.options ) }">
					<MvELSE>
						<MvASSIGN NAME = "l.option_count"	VALUE = "{ [ g.Module_Library_DB ].OptionList_Load_Attribute( l.attribute:id, l.options ) }">
					</MvIF>

					<MvASSIGN NAME = "l.option_pos"		VALUE = 1>
					<MvWHILE EXPR = "{ l.option_pos LE l.option_count }">
						<MvIF EXPR = "{ ( ( len( g.Product[ l.product_pos ]:Attributes[ l.product_attribute_count ]:value ) EQ 0 ) AND ( l.options[ l.option_pos ]:id EQ l.attribute:default_id ) ) OR
										( g.Product[ l.product_pos ]:Attributes[ l.product_attribute_count ]:value EQ l.options[ l.option_pos ]:code ) }">
							<option value="{ encodeentities( l.options[ l.option_pos ]:code ) }" selected><MvEVAL EXPR = "{ l.options[ l.option_pos ]:prompt }"></option>
						<MvELSE>
							<option value="{ encodeentities( l.options[ l.option_pos ]:code ) }"><MvEVAL EXPR = "{ l.options[ l.option_pos ]:prompt }"></option>
						</MvIF>

						<MvASSIGN NAME = "l.option_pos"	VALUE = "{ l.option_pos + 1 }">
					</MvWHILE>
				</select>
			<MvELSE>
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Product[ l.product_pos ]:Attributes[ l.product_attribute_count ]:value, 'Product[' $ l.product_pos $ ']:Attributes[' $ l.product_attribute_count $ ']:value', 'Yes', '' ) }"> 
				<MvIF EXPR = "{ l.attribute:required }">
					<span class="required">
				<MvELSE>
					<span>
				</MvIF>
						<MvEVAL EXPR = "{ l.attribute:prompt }">	
					</span>
			</MvIF>
		</td>
	</tr>
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUI_Draw_Attribute_Prompt" PARAMETERS = "attribute var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ len( l.attribute:image ) }">
		<label><img src="{ encodeentities( l.attribute:image ) }" alt="{ encodeentities( l.attribute:prompt ) }" /></label>
	<MvELSE>
		<MvIF EXPR = "{ ( l.attribute:type NE 'checkbox' ) AND
						( substring( l.attribute:prompt, len( l.attribute:prompt ), 1 ) NE ':' ) }">
			<MvASSIGN NAME = "l.colon"	VALUE = ":">
		</MvIF>

		<MvIF EXPR = "{ l.attribute:required }">
			<label class="required"><MvEVAL EXPR = "{ l.attribute:prompt $ l.colon }"></label>
		<MvELSE>
			<label><MvEVAL EXPR = "{ l.attribute:prompt $ l.colon }"></label>
		</MvIF>
	</MvIF>
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CMP-MV-PAYF-
| Next Error Code: 1    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-mv-paymentfields">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Payment Module Fields">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0800">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.00">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Initialize_Item( l.all_settings, 'paymentmethod' ) }">
		<MvIF EXPR = "{ g.Basket:cust_id AND g.Customer_Session_Verified }">	<MvASSIGN NAME = "l.customer_id" VALUE = "{ g.Basket:cust_id }">
		<MvELSE>																<MvASSIGN NAME = "l.customer_id" VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PAY_UT ].PaymentMethod_Parse_Runtime_WithModule( g.PaymentMethod, l.customer_id, l.settings:module, l.settings:module_data, l.settings:paymentcard, l.settings:paymentcardtype ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	<MvELSE>
		<MvREFERENCE NAME = "l.settings:module_data"			VARIABLE = "l.all_settings:paymentmethod:module_data">
		<MvREFERENCE NAME = "l.settings:module"					VARIABLE = "l.all_settings:paymentmethod:module">
		<MvREFERENCE NAME = "l.settings:paymentcard"			VARIABLE = "l.all_settings:paymentmethod:paymentcard">
		<MvREFERENCE NAME = "l.settings:paymentcardtype"		VARIABLE = "l.all_settings:paymentmethod:paymentcardtype">

		<MvIF EXPR = "{ NOT l.all_settings:paymentmethod:valid }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.settings:valid"				VALUE = 1>

	<MvIF EXPR = "{ l.settings:paymentcardtype:id EQ -1 }">
		<MvASSIGN NAME = "l.settings:capabilities"	VALUE = "">
		<MvASSIGN NAME = "l.settings:url"			VALUE = "{ g.secure_sessionurl }">

		<MvIF EXPR = "{ [ g.Module_Feature_PAY_DB ].MivaPaySettings_Load_Cached( l.mivapay_settings ) }">
			<MvASSIGN NAME = "l.settings:desc"		VALUE = "{ l.mivapay_settings:pct_desc }">
		</MvIF>
	<MvELSE>
		<MvREFERENCE NAME = "l.settings:capabilities"			VARIABLE = "l.settings:module:capabilities">

		<MvASSIGN NAME = "l.settings:message"		VALUE = "{ [ g.Module_Root $ l.settings:module:module ].PaymentModule_Payment_Message( l.settings:module, l.settings:module_data ) }">

		<MvIF EXPR = "{ l.settings:module:api_ver LT 10.00 }">
			<MvASSIGN NAME = "l.settings:url"		VALUE = "{ [ g.Module_Root $ l.settings:module:module ].PaymentModule_Payment_URL( l.settings:module ) }">
		<MvELSE>
			<MvASSIGN NAME = "l.settings:url"		VALUE = "{ [ g.Module_Root $ l.settings:module:module ].PaymentModule_Payment_URL( l.settings:module, l.settings:module_data ) }">
		</MvIF>

		<MvASSIGN NAME = "l.settings:payment_url"	VALUE = "{ l.settings:url }">

		<MvIF EXPR = "{ l.settings:paymentcard:id }">
			<MvASSIGN NAME = "l.pmr_module_code"	VALUE = "">
			<MvASSIGN NAME = "l.pmr_method_code"	VALUE = "">
			<MvASSIGN NAME = "l.pmr_pctype_id"		VALUE = "{ l.settings:paymentcard:type_id }">
		<MvELSEIF EXPR = "{ l.settings:paymentcardtype:id }">
			<MvASSIGN NAME = "l.pmr_module_code"	VALUE = "">
			<MvASSIGN NAME = "l.pmr_method_code"	VALUE = "">
			<MvASSIGN NAME = "l.pmr_pctype_id"		VALUE = "{ l.settings:paymentcardtype:id }">
		<MvELSE>
			<MvASSIGN NAME = "l.pmr_module_code"	VALUE = "{ l.settings:module:code }">
			<MvASSIGN NAME = "l.pmr_method_code"	VALUE = "{ l.settings:module_data }">
			<MvASSIGN NAME = "l.pmr_pctype_id"		VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PAY_DB ].PaymentMethodRules_Load_MethodOrDefault_Cached( l.pmr_module_code, l.pmr_method_code, l.pmr_pctype_id, l.settings:paymentmethodrules ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ l.settings:paymentcardtype:id }">
			<MvASSIGN NAME = "l.settings:desc"		VALUE = "{ l.settings:paymentcardtype:type }">
		<MvELSEIF EXPR = "{ NOT ISNULL l.settings:paymentmethodrules:descrip }">
			<MvASSIGN NAME = "l.settings:desc" 		VALUE = "{ l.settings:paymentmethodrules:descrip }">
		<MvELSE>
			<MvASSIGN NAME = "l.settings:desc"		VALUE = "{ [ g.Module_Root $ l.settings:module:module ].PaymentModule_Payment_Description( l.settings:module, l.settings:module_data ) }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:url }">
			<MvASSIGN NAME = "l.settings:url"		VALUE = "{ g.secure_sessionurl }">
		</MvIF>

		<MvASSIGN NAME = "l.fields"					VALUE = "{ [ g.Module_Root $ l.settings:module:module ].PaymentModule_Payment_Fields( l.settings:module, l.settings:module_data ) }">

		<MvASSIGN NAME = "l.field_pos"				VALUE = 1>
		<MvASSIGN NAME = "l.field"					VALUE = "{ trim( gettoken( l.fields, ',', l.field_pos ) ) }">

		<MvWHILE EXPR = "{ NOT ISNULL l.field }">
			<MvASSIGN NAME = "l.settings:fields" INDEX = "{ l.field_pos }" MEMBER = "code"		VALUE = "{ l.field }">
			<MvASSIGN NAME = "l.settings:fields" INDEX = "{ l.field_pos }" MEMBER = "prompt"	VALUE = "{ [ g.Module_Root $ l.settings:module:module ].PaymentModule_Payment_Prompt( l.settings:module, l.settings:module_data, l.field ) }">
			<MvASSIGN NAME = "l.settings:fields" INDEX = "{ l.field_pos }" MEMBER = "invalid"	VALUE = "{ [ g.Module_Root $ l.settings:module:module ].PaymentModule_Payment_Invalid( l.settings:module, l.settings:module_data, l.field ) }">

			<MvASSIGN NAME = "l.field_pos"			VALUE = "{ l.field_pos + 1 }">
			<MvASSIGN NAME = "l.field"				VALUE = "{ trim( gettoken( l.fields, ',', l.field_pos ) ) }">
		</MvWHILE>
	</MvIF>

	<MvIF EXPR = "{ l.settings:capabilities:balance }">
		<MvASSIGN NAME = "l.settings:balance"		VALUE = "{ [ g.Module_Root $ l.settings:module:module ].PaymentModule_Balance( l.settings:module, l.settings:module_data ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:balance"		VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.settings:formatted_balance"	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.settings:balance ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT l.settings:valid }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ len( l.param ) }">
		<MvIF EXPR = "{ NOT ISNULL l.settings:module }">
			<MvDO FILE = "{ g.Module_Root $ l.settings:module:module }" NAME = "l.null" VALUE = "{ PaymentModule_Payment_Field( l.settings:module, l.settings:module_data, miva_variable_value( 'l.all_settings:' $ l.param ) ) }">
		</MvIF>
	<MvELSE>
		<MvIF EXPR = "{ ( NOT ISNULL l.settings:module ) AND ( l.settings:paymentcard:id EQ 0 ) AND ( l.settings:paymentcardtype:id EQ 0 ) }">
			<MvDO FILE = "{ g.Module_Root $ l.settings:module:module }" NAME = "l.null" VALUE = "{ PaymentModule_Payment_Hide_Additional_Fields( l.settings:module, l.settings:module_data ) }">
		</MvIF>

		<MvCOMMENT>
		|
		| Do NOT load cached version of PaymentRules, as Payment_reCAPTCHA_Is_Active
		| needs correct, up-to-date values in order to determine whether to show
		| the reCAPTCHA fields.
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ [ g.Module_Feature_PAY_DB ].PaymentRules_Load( l.paymentrules ) }">
			<MvIF EXPR = "{ l.paymentrules:use_token }">
				<input type="hidden" name="PaymentAuthorizationToken" value="{ encodeentities( g.Basket:auth_token ) }">
			</MvIF>

			<MvIF EXPR = "{ l.settings:paymentcard:id }">			<MvASSIGN NAME = "l.paymentcardtype_id" VALUE = "{ l.settings:paymentcard:type_id }">
			<MvELSEIF EXPR = "{ l.settings:paymentcardtype:id }">	<MvASSIGN NAME = "l.paymentcardtype_id" VALUE = "{ l.settings:paymentcardtype:id }">
			<MvELSE>												<MvASSIGN NAME = "l.paymentcardtype_id" VALUE = 0>
			</MvIF>

			<MvIF EXPR = "{ [ g.Module_Feature_PAY_UT ].Payment_reCAPTCHA_Is_Active( l.paymentrules, l.settings:module, l.settings:module_data, l.paymentcardtype_id ) }">
				<script src="{ 'https://www.google.com/recaptcha/api.js?hl=' $ encodeattribute( l.paymentrules:rc_lang ) }" async defer></script>
				<div class="g-recaptcha" data-sitekey="{ encodeentities( l.paymentrules:rc_pub_key ) }" data-theme="{ encodeentities( l.paymentrules:rc_theme ) }" data-type="{ encodeentities( l.paymentrules:rc_type ) }" data-size="{ encodeentities( l.paymentrules:rc_size ) }"></div>			
			</MvIF>
		</MvIF>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

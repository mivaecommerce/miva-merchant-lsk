<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-BTNS-
| Next Error Code: 16
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-buttons">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI Buttons">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0800">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.60">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, data_store, vis_store, provision_store, skins">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: data_store
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'AddToBasketE',		'Add To Basket (Expanded)',			'Add To Basket',			'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'AddToBasketL',		'Add To Basket (Line Item)',		'Add To Basket',			'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'AddToOrder',			'Add To Order',						'Add To Order',				'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'AddToBasket',		'Add To Basket',					'Add To Basket',			'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'BuyNowE',			'Buy Now (Expanded)',				'Buy Now',					'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'BuyNowL',			'Buy Now (Line Item)',				'Buy Now',					'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'Continue',			'Continue',							'Continue',					'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'DoNotAddToOrder',	'Do Not Add To Order',				'Do Not Add To Order',		'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'Login',				'Login',							'Login',					'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'Next',				'Next',								'Next >',					'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'Previous',			'Previous',							'< Previous',				'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'Remove',				'Remove',							'Remove',					'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'Save',				'Save',								'Save',						'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'Back',				'Back',								'Back',						'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'Search',				'Search',							'Search',					'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'Update',				'Update',							'Update',					'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'ApplyCoupon',		'Apply Coupon',						'Redeem',					'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'ApplyGiftCert',		'Apply Gift Certificate',			'Redeem',					'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'AddAllToBasket',		'Add All To Basket',				'Add All To Basket',		'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'AddToWishListE',		'Add To Wish List (Expanded)',		'Add To Wish List',			'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'AddToWishListL',		'Add To Wish List (Line Item)',		'Add To Wish List',			'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'AddToWishList',		'Add To Wish List',					'Add To Wish List',			'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'MoveToWishList',		'Move To Wish List',				'Move To Wish List',		'' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ CSSUI_Button_Create( 'MoveAllToWishList',	'Move All To Wish List',			'Move All To Wish List',	'' ) }">
	
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: vis_store
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Store_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH INDEX = "l.button_pos" ITERATOR = "l.button" ARRAY = "g.CSSUI_Buttons">
		<MvASSIGN NAME = "l.button:text"	VALUE = "{ trim( l.button:text ) }">

		<MvIF EXPR = "{ NOT l.button:advanced }">
			<MvIF EXPR = "{ ISNULL l.button:text }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-CSSUI-BUTTONS', 'CSSUI_Buttons[' $ l.button_pos $ ']:text', 'You must specify button text' ) }">
			</MvIF>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.button" ARRAY = "g.CSSUI_Buttons">
		<MvIF EXPR = "{ NOT l.button:advanced }">
			<MvEVAL EXPR = "{ CSSUI_Button_Generate_Code( l.button, l.button:template_code ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT CSSUI_Button_Update( l.button ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Tabs" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "GT_UISETTINGS/CMP-CSSUI-BUTTONS:Buttons">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.load_fields }">
		<MvIF EXPR = "{ NOT CSSUI_ButtonList_Load( g.CSSUI_Buttons ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( g.Tab, 'CMP-CSSUI-BUTTONS' ) }">
		<MvHIDE FIELDS = "g.CSSUI_Buttons">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_UISETTINGS', 'CMP-CSSUI-BUTTONS' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<MvFOREACH INDEX = "l.button_pos" ITERATOR = "l.button" ARRAY = "g.CSSUI_Buttons">
			<tr><td nowrap valign="top">
				<input type="hidden" class="button" name="{ 'CSSUI_Buttons[' $ l.button_pos $ ']:code' }" 		value="{ encodeentities( l.button:code ) }">
				<input type="hidden" class="button" name="{ 'CSSUI_Buttons[' $ l.button_pos $ ']:prompt' }" 	value="{ encodeentities( l.button:prompt ) }">
				<input type="hidden" class="button" name="{ 'CSSUI_Buttons[' $ l.button_pos $ ']:advanced' }" 	value="{ encodeentities( l.button:advanced ) }">
				
				<b><MvEVAL EXPR = "{ encodeentities( l.button:prompt ) }">:</b>
			</td><td width="100%" nowrap valign="top">
				<MvIF EXPR = "{ l.button:advanced }">
					<input type="hidden" name="{ 'CSSUI_Buttons[' $ l.button_pos $ ']:text' }" value="{ encodeentities( l.button:text ) }">

					<textarea name="{ 'CSSUI_Buttons[' $ l.button_pos $ ']:template_code' }" rows="5" cols="40" wrap="off"><MvEVAL EXPR = "{ encodeentities( l.button:template_code ) }"></textarea><br>
					<a href="#" onclick="{ 'Toggle( \'CSSUI_Buttons[' $ l.button_pos $ ']:advanced' $ '\', 0 ); return false;' }">Hide Code</a>
				<MvELSE>
					<input type="hidden" name="{ 'CSSUI_Buttons[' $ l.button_pos $ ']:template_code' }" value="{ encodeentities( l.button:template_code ) }">

					<table border="0" cellpadding="2" cellspacing="0">
						<tr>
							<td nowrap>
								<b>Text:</b>
							</td><td>
								<input type="text" size="40" name="{ 'CSSUI_Buttons[' $ l.button_pos $ ']:text' }" value="{ encodeentities( l.button:text ) }">
							</td><td nowrap>
								<a href="#" onclick="{ 'Toggle( \'CSSUI_Buttons[' $ l.button_pos $ ']:advanced' $ '\', 1 ); return false;' }">Show Code</a>
							</td>
						</tr>
					</table>
				</MvIF>
			</td></tr>
		</MvFOREACH>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: component
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Runtime_ManagedProperty_Load_Code( 'cssui_button', l.param, l.managedproperty ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Store_Template_Path $ l.managedproperty:template_filename ].Template_Render( l.null, l.all_settings ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: provision_store
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Provision_Store" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.child_xml" ARRAY = "l.provide_xml:children">
		<MvASSIGN NAME = "l.name"		VALUE = "{ tolower( l.child_xml:name ) }">

		<MvIF EXPR = "{ l.name EQ 'button_update' }">	<MvEVAL EXPR = "{ Module_Provision_Store_Button_Update( l.module, l.child_xml ) }">
		</MvIF>
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Store_Button_Update" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml, 'code', l.code ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT CSSUI_Button_Load_Code( l.code, l.button ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load button \'' $ l.code $ '\'' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml, 'Text',		l.button:text ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml, 'Template',	l.button:template_code ) }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvASSIGN NAME = "l.button:advanced"	VALUE = 1>
	<MvELSEIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Text' ) }">
		<MvEVAL EXPR = "{ CSSUI_Button_Generate_Code( l.button, l.button:template_code ) }">

		<MvASSIGN NAME = "l.button:advanced"	VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT CSSUI_Button_Update( l.button ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: skins
|
</MvCOMMENT>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "Exports Shopping Buttons design/template (Global Code)">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<Module code="{ encodeentities( l.module:code ) }" feature="component">
			<MvFOREACH ITERATOR = "l.button" ARRAY = "l.buttons" COUNT = "{ CSSUI_ButtonList_Load( l.buttons ) }">
				<Button_Update code="{ encodeentities( l.button:code ) }">
					<MvIF EXPR = "{ NOT ISNULL l.button:text }">
						<Text><MvEVAL EXPR = "{ miva_cdata_encode( l.button:text ) }"></Text>
					</MvIF>

					<MvIF EXPR = "{ l.button:advanced }">
						<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.button:template_code ) }"></Template>
					</MvIF>
				</Button_Update>
			</MvFOREACH>
		</Module>
	</MvCAPTURE>
</MvFUNCTION>

<MvCOMMENT>
|
| Helper Functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "CSSUI_Button_Generate_Code" PARAMETERS = "button var, code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code" STANDARDOUTPUTLEVEL = "text, html"><input type="submit" value="{ encodeentities( l.button:text ) }" class="button" /></MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUI_Button_Create" PARAMETERS = "code, prompt, text, image" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedProperty_Load_Code( 'cssui_button', l.code, l.managedproperty ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.settings:prompt"	VALUE = "{ l.prompt }">
	<MvASSIGN NAME = "l.settings:advanced"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:text"		VALUE = "{ l.text }">

	<MvEVAL EXPR = "{ CSSUI_Button_Generate_Code( l.settings, l.source ) }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedProperty( 'cssui_button', l.code, '', l.source, l.settings, l.managedproperty ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUI_ButtonList_Load" PARAMETERS = "buttons var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.button_count"				VALUE = 0>
	<MvASSIGN NAME = "l.buttons"					VALUE = "">

	<MvFOREACH ITERATOR = "l.managedproperty" ARRAY = "l.managedproperties" COUNT = "{ [ g.Module_Feature_TUI_DB ].ManagedPropertyList_Load_Type( 'cssui_button', l.managedproperties ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedPropertyVersion_Load_ID( l.managedproperty:current_id, l.managedpropertyversion ) OR
						NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current( l.managedproperty:id, l.managedtemplateversion ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.button:code"			VALUE = "{ l.managedproperty:code }">
		<MvASSIGN NAME = "l.button:prompt"			VALUE = "{ l.managedpropertyversion:settings:prompt }">
		<MvASSIGN NAME = "l.button:advanced"		VALUE = "{ l.managedpropertyversion:settings:advanced }">
		<MvASSIGN NAME = "l.button:text"			VALUE = "{ l.managedpropertyversion:settings:text }">
		<MvASSIGN NAME = "l.button:template_code"	VALUE = "{ l.managedtemplateversion:source }">

		<MvASSIGN NAME = "l.button_count"			VALUE = "{ miva_array_insert_var( l.buttons, l.button, -1 ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-CSSUI-CMP-BTNS-00013', l.button_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUI_Button_Load_Code" PARAMETERS = "code, button var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedProperty_Load_Code( 'cssui_button', l.code, l.managedproperty ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedPropertyVersion_Load_ID( l.managedproperty:current_id, l.managedpropertyversion ) OR
					NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current( l.managedproperty:id, l.managedtemplateversion ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.button:code"			VALUE = "{ l.managedproperty:code }">
	<MvASSIGN NAME = "l.button:prompt"			VALUE = "{ l.managedpropertyversion:settings:prompt }">
	<MvASSIGN NAME = "l.button:advanced"		VALUE = "{ l.managedpropertyversion:settings:advanced }">
	<MvASSIGN NAME = "l.button:text"			VALUE = "{ l.managedpropertyversion:settings:text }">
	<MvASSIGN NAME = "l.button:template_code"	VALUE = "{ l.managedtemplateversion:source }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUI_Button_Update" PARAMETERS = "button var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedProperty_Load_Code( 'cssui_button', l.button:code, l.managedproperty ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CSSUI-CMP-BTNS-00014', 'CSSUI Button not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedPropertyVersion_Load_ID( l.managedproperty:current_id, l.managedpropertyversion ) OR
					NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current( l.managedproperty:id, l.managedtemplateversion ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.modified"			VALUE = 0>

	<MvASSIGN NAME = "l.settings:prompt"	VALUE = "{ l.button:prompt }">
	<MvASSIGN NAME = "l.settings:advanced"	VALUE = "{ l.button:advanced }">
	<MvASSIGN NAME = "l.settings:text"		VALUE = "{ l.button:text }">

	<MvIF EXPR = "{ ( l.settings:prompt NE l.managedpropertyversion:settings:prompt )		OR
					( l.settings:advanced NE l.managedpropertyversion:settings:advanced )	OR
					( l.settings:text NE l.managedpropertyversion:settings:text ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedPropertyVersion( l.managedproperty, l.settings ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.modified"		VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ crypto_md5( l.button:template_code ) NE crypto_md5( l.managedtemplateversion:source ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Property_ManagedTemplateVersion( l.managedproperty, l.button:template_code ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.modified"		VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.modified }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-BTNS-00015', 'CSSUI button \'' $ l.button:code $ '\' updated' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

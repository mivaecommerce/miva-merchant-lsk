<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CMP-INC-IMTY-
| Next Error Code: 4
|
</MvCOMMENT>

<MvCOMMENT>
|
| Feature: component
|
</MvCOMMENT>

<MvIFNDEF NAME = "IMAGETYPES_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ ImageTypesComponentModule_Tabs( l.module, l.item, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "ImageTypesComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ 'GT_PAGE/' $ l.item $ ':' $ l.module:name }">
</MvFUNCTION>

<MvIFNDEF NAME = "IMAGETYPES_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ ImageTypesComponentModule_Validate( l.module, l.item, l.field_prefix, l.fields, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "ImageTypesComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.fields:b_width"						VALUE = "{ trim( l.fields:b_width ) }">
	<MvASSIGN NAME = "l.fields:b_height"					VALUE = "{ trim( l.fields:b_height ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:b_width ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'b_width', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:b_height ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'b_height', g.Validation_Message ) }">
	</MvIF>

    <MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "IMAGETYPES_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ ImageTypesComponentModule_Update( l.module, l.item, l.field_prefix, l.fields, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvIFNDEF NAME = "IMAGETYPES_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Update_Requires_Version" PARAMETERS = "module var, page var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>
</MvIFNDEF>

<MvIFNDEF NAME = "IMAGETYPES_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_PostUpdate" PARAMETERS = "module var, page var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "ImageTypesComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.imagetype_count"		VALUE = "{ [ g.Module_Library_Utilities ].ImageTypeSelect_Selected( l.fields:imagetypes, l.settings:imagetypes ) }">
	<MvASSIGN NAME = "l.settings:constrain"		VALUE = "{ l.fields:constrain }">
	<MvASSIGN NAME = "l.settings:b_width"		VALUE = "{ l.fields:b_width }">
	<MvASSIGN NAME = "l.settings:b_height"		VALUE = "{ l.fields:b_height }">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CMP-INC-IMTY-00001', l.module:name $ ' \'' $ l.item $ '\' image type settings updated' ) }">

    <MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "IMAGETYPES_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ ImageTypesComponentModule_Content( l.module, l.item, l.tab, l.load_fields, l.field_prefix, l.fields, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "ImageTypesComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvASSIGN NAME = "l.imagetype_count"						VALUE = "{ [ g.Module_Library_DB ].ImageTypeList_Load_All( l.imagetypes ) }">

	<MvIF EXPR = "{ l.load_fields }">
		<MvIF EXPR = "{ ISNULL l.settings:constrain }">
			<MvASSIGN NAME = "l.fields:constrain"				VALUE = "B">
		<MvELSE>
			<MvASSIGN NAME = "l.fields:constrain"				VALUE = "{ l.settings:constrain }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:b_width }">
			<MvASSIGN NAME = "l.fields:b_width"					VALUE = 75>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:b_width"					VALUE = "{ l.settings:b_width }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:b_height }">
			<MvASSIGN NAME = "l.fields:b_height"				VALUE = 75>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:b_height"				VALUE = "{ l.settings:b_height }">
		</MvIF>

		<MvASSIGN NAME = "l.fields:imagetypes"					VALUE = "{ l.settings:imagetypes }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'imagetypes,constrain,b_width,b_height' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
			<tr>
				<td align="left" valign="top" nowrap><b>Product Images:</b></td>
				<td align="left" width="100%">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawImageTypeSelect( l.field_prefix $ 'imagetypes', l.fields:imagetypes, l.imagetypes, l.imagetype_count ) }">
				</td>
			</tr>
			<tr>
				<td align="left" valign="top" nowrap><b>Image Dimensions:</b></td>
				<td align="left" width="100%">
					<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'constrain', 'N', l.fields:constrain, 'No constraints' ) }"></label><br />
					<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'constrain', 'B', l.fields:constrain, 'Resize to fit within bounding box:' ) }"></label>
					<input type="text" name="{ encodeentities( l.field_prefix ) $ 'b_width' }" value="{ encodeentities( l.fields:b_width ) }" style="text-align:right;" size="5" /> x 
					<input type="text" name="{ encodeentities( l.field_prefix ) $ 'b_height' }" value="{ encodeentities( l.fields:b_height ) }" style="text-align:right;" size="5" /> pixels
				</td>
			</tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "IMAGETYPES_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ ImageTypesComponentModule_Defaults( l.module, l.item, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "ImageTypesComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:imagetypes"		VALUE = "">
	<MvASSIGN NAME = "l.settings:constrain"			VALUE = "B">
	<MvASSIGN NAME = "l.settings:b_width"			VALUE = "75">
	<MvASSIGN NAME = "l.settings:b_height"			VALUE = "75">
</MvFUNCTION>

<MvIFNDEF NAME = "IMAGETYPES_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ ImageTypesComponentModule_Page_Assign( l.module, l.page, l.item, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "ImageTypesComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "IMAGETYPES_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ ImageTypesComponentModule_Page_Unassign( l.module, l.page, l.item, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "ImageTypesComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "IMAGETYPES_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ ImageTypesComponentModule_Page_Copy( l.module, l.item, l.source_branch, l.source_page, l.source_settings, l.dest_branch, l.dest_page, l.dest_settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "ImageTypesComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.dest_settings" VALUE = "{ l.source_settings }">

	<MvIF EXPR = "{ l.source_branch:id EQ l.dest_branch:id }">	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CMP-INC-IMTY-00002', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' on branch \'' $ l.dest_branch:name $ '\'' ) }">
	<MvELSE>													<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CMP-INC-IMTY-00003', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' from branch \'' $ l.source_branch:name $ '\' to branch \'' $ l.dest_branch:name $ '\'' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "IMAGETYPES_COMPONENT_NO_INITIALIZE">
<MvIFNDEF NAME = "IMAGETYPES_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ ImageTypesComponentModule_Initialize( l.module, l.item, l.all_settings, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "ImageTypesComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>
</MvIFNDEF>

<MvIFNDEF NAME = "IMAGETYPES_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ ImageTypesComponentModule_Prerender( l.module, l.item, l.all_settings, l.settings, l.param ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "ImageTypesComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "IMAGETYPES_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ ImageTypesComponentModule_Render_Head( l.module, l.item, l.all_settings, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "ImageTypesComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvIFNDEF NAME = "IMAGETYPES_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ ImageTypesComponentModule_Render_Start( l.module, l.item, l.all_settings, l.settings, l.param ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "ImageTypesComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvIFNDEF NAME = "IMAGETYPES_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ ImageTypesComponentModule_Render_End( l.module, l.item, l.all_settings, l.settings, l.param ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "ImageTypesComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: component_prov
|
</MvCOMMENT>

<MvIFNDEF NAME = "IMAGETYPES_COMPONENT_PROV_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ ImageTypesComponentModule_Provision( l.module, l.provide_xml, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "ImageTypesComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'ImageTypes' ) }">
		<MvIF EXPR = "{ NOT ImageTypesComponentModule_Provision_ImageTypes( l.module, l.provide_xml:tags:ImageTypes[ 1 ], l.settings ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to provision multiple image types' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'ImageDimensions' ) }">
		<MvASSIGN NAME = "l.dimensions" VALUE = "{ l.provide_xml:tags:ImageDimensions[ 1 ] }">
		
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Boolean( l.dimensions, 'constrain', l.constrain ) }">	
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvIF EXPR = "{ NOT l.constrain }">
			<MvASSIGN NAME = "l.settings:constrain" VALUE = "N">
		<MvELSE>
			<MvASSIGN NAME = "l.settings:constrain" VALUE = "B">

			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer( 'R', l.dimensions, 'Width',	l.settings:b_width ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer( 'R', l.dimensions, 'Height',	l.settings:b_height ) }">
				<MvFUNCTIONRETURN>
			</MvIF>
		</MvIF>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ImageTypesComponentModule_Provision_ImageTypes" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:imagetypes" VALUE = "">

	<MvASSIGN NAME = "l.type_pos"		VALUE = 1>
	<MvASSIGN NAME = "l.type_found"		VALUE = 1>
	<MvASSIGN NAME = "l.type_count"		VALUE = "{ miva_array_elements( l.provide_xml:children ) }">

	<MvWHILE EXPR = "{ l.type_pos LE l.type_count }">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].ImageType_Load_Code( l.provide_xml:children[ l.type_pos ]:value, l.imagetype ) }">
			<MvASSIGN NAME = "l.settings:imagetypes" INDEX = "{ l.type_found }" VALUE = "{ l.provide_xml:children[ l.type_pos ]:value }">
			<MvASSIGN NAME = "l.type_found"										VALUE = "{ l.type_found + 1 }">
		</MvIF>

		<MvASSIGN NAME = "l.type_pos" VALUE = "{ l.type_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: skins
|
</MvCOMMENT>

<MvIFNDEF NAME = "IMAGETYPES_SKINS_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ ImageTypesSkinsComponentModule_Description( l.module, l.item ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "ImageTypesSkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pages" VALUE = "{ [ g.Module_Feature_TUI_DB ].Item_Load_PageList( l.item ) }">

	<MvFUNCTIONRETURN VALUE = "{ 'Exports ' $ l.module:name $ ' template from pages: ' $ l.pages }">
</MvFUNCTION>

<MvIFNDEF NAME = "IMAGETYPES_SKINS_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ ImageTypesSkinsComponentModule_Export_Item( l.module, l.item, l.output ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "ImageTypesSkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item_Runtime( l.item:id, l.pages ) }">
		<MvREFERENCEARRAY NAME = "l.settings" VARIABLE = "l.page:settings">
			<MvMEMBER NAME = "{ l.item:code }">
		</MvREFERENCEARRAY>

		<MvCAPTURE VARIABLE = "l.page_output"><MIVA STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
			<Page_Update code="{ l.page:code }">
				<Item code="{ l.item:code }">
					<ImageTypes>
						<MvFOREACH ITERATOR = "l.imagetype" ARRAY = "l.settings:imagetypes">
							<ImageType><MvEVAL EXPR = "{ miva_cdata_encode( l.imagetype ) }"></ImageType>
						</MvFOREACH>
					</ImageTypes>

					<MvIF EXPR = "{ l.settings:constrain NE 'B' }">
						<ImageDimensions constrain="No" />
					<MvELSE>
						<ImageDimensions constrain="Yes">
							<Width><MvEVAL EXPR = "{ int( l.settings:b_width ) }"></Width>
							<Height><MvEVAL EXPR = "{ int( l.settings:b_height ) }"></Height>
						</ImageDimensions>
					</MvIF>
				</Item>
			</Page_Update>
<MIVA STANDARDOUTPUTLEVEL = ""></MvCAPTURE>

		<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.page_output }">
	</MvFOREACH>
</MvFUNCTION>

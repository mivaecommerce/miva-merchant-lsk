<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-WSHLI-
| Next Error Code: 6    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-wishlistitems">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI Wish List Items">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.1100">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "10.08">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, component_prov, skins">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.tabs"		VALUE = "{ 'GT_PAGE/' $ l.item $ ':Wish List Items Layout' }">

	<MvIF EXPR = "{ miva_array_elements( l.settings:imagetypes ) GT 0 }">
		<MvASSIGN NAME = "l.tabs"	VALUE = "{ l.tabs $ ',GT_PAGE/' $ l.item $ '_dimensions:Wish List Items Image Dimensions' }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.tabs }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModuleValidate_SortMethod" PARAMETERS = "item, default_sort, value, sort_by, sorting_option, sorting_method" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( l.default_sort EQ l.value ) AND ( NOT l.sort_by ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, '', 'Sorting option \'' $ l.sorting_option $ '\' must be enabled to use the Default Sort Method of ' $ l.sorting_method ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.fields:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.fields:b_width"						VALUE = "{ trim( l.fields:b_width ) }">
	<MvASSIGN NAME = "l.fields:b_height"					VALUE = "{ trim( l.fields:b_height ) }">
	<MvASSIGN NAME = "l.fields:price"						VALUE = "{ trim( l.fields:price ) }">
	<MvASSIGN NAME = "l.fields:additionalprice"				VALUE = "{ trim( l.fields:additionalprice ) }">
	<MvASSIGN NAME = "l.fields:per_page"					VALUE = "{ trim( l.fields:per_page ) }">
	<MvASSIGN NAME = "l.fields:page_disp_count"				VALUE = "{ trim( l.fields:page_disp_count ) }">
	<MvASSIGN NAME = "l.fields:reverse_default_disp_order"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:reverse_default_disp_order ) }">
	<MvASSIGN NAME = "l.fields:displaydiscounts"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:displaydiscounts ) }">
	<MvASSIGN NAME = "l.fields:predictdiscounts"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:predictdiscounts ) }">
	<MvASSIGN NAME = "l.fields:field_code"					VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:field_code ) }">
	<MvASSIGN NAME = "l.fields:field_name"					VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:field_name ) }">
	<MvASSIGN NAME = "l.fields:field_options"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:field_options ) }">
	<MvASSIGN NAME = "l.fields:field_dtadded"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:field_dtadded ) }">
	<MvASSIGN NAME = "l.fields:field_notes"					VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:field_notes ) }">
	<MvASSIGN NAME = "l.fields:field_quantity"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:field_quantity ) }">
	<MvASSIGN NAME = "l.fields:field_price"					VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:field_price ) }">
	<MvASSIGN NAME = "l.fields:button_add"					VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:button_add ) }">

	<MvIF EXPR = "{ l.fields:paginate }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:per_page ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'per_page', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.fields:paginate_count }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:page_disp_count ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'page_disp_count', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:b_width ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item $ '_dimensions', l.field_prefix $ 'b_width', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:b_height ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item $ '_dimensions', l.field_prefix $ 'b_height', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT l.fields:advanced AND l.fields:sort_by:switch }">
		<MvASSIGN NAME = "l.ds" VALUE = "{ l.fields:default_sort }">
		<MvASSIGN NAME = "l.sb" VALUE = "{ l.fields:sort_by }">

		<MvIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( 	l.item, l.ds, 'name_asc', 		l.sb:name, 		'Name', 		'Name Ascending' ) }">			<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'name_desc', 		l.sb:name, 		'Name', 		'Name Descending' ) }">			<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'code_asc', 		l.sb:code, 		'Code', 		'Code Ascending' ) }">			<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'code_desc', 		l.sb:code, 		'Code', 		'Code Descending' ) }">			<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'price_asc', 		l.sb:price, 	'Price', 		'Lowest Price' ) }">			<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'price_desc', 	l.sb:price, 	'Price', 		'Highest Price' ) }">			<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'dtadded_asc', 	l.sb:dtadded, 	'Date Added', 	'Date Added Ascending' ) }">	<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'dtadded_desc', 	l.sb:dtadded, 	'Date Added', 	'Date Added Descending' ) }">	<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.fields:advanced }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.fields:template_code ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.settings:advanced"						VALUE = "{ l.fields:advanced }">
	<MvASSIGN NAME = "l.settings:reverse_default_disp_order" 	VALUE = "{ l.fields:reverse_default_disp_order }">

	<MvASSIGN NAME = "l.selectedfield_count"					VALUE = "{ [ g.Module_Library_Utilities ].CustomFieldSelect_Selected( l.fields:fields_custom, l.settings:fields_custom ) }">

	<MvIF EXPR = "{ l.fields:advanced }">
		<MvASSIGN NAME = "l.settings:predictdiscounts"	VALUE = "{ l.fields:predictdiscounts }">
		<MvASSIGN NAME = "l.imagetype_count"			VALUE = "{ [ g.Module_Library_Utilities ].ImageTypeSelect_Selected( l.fields:imagetypes, l.settings:imagetypes ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:imagetypes"		VALUE = "">
		<MvIF EXPR = "{ NOT ISNULL l.fields:image }">
			<MvIF EXPR = "{ [ g.Module_Library_DB ].ImageType_Load_Code( l.fields:image, l.imagetype ) }">
				<MvASSIGNARRAY NAME = "l.settings"		VALUE = "{ l.imagetype:code }">
					<MvMEMBER NAME = "imagetypes">
					<MvDIMENSION INDEX = 1>
				</MvASSIGNARRAY>
			</MvIF>
		</MvIF>

		<MvASSIGN NAME = "l.settings:price"				VALUE = "{ l.fields:price }">
		<MvASSIGN NAME = "l.settings:additionalprice"	VALUE = "{ l.fields:additionalprice }">
		<MvASSIGN NAME = "l.settings:displaydiscounts"	VALUE = "{ l.fields:displaydiscounts }">
		<MvASSIGN NAME = "l.settings:predictdiscounts"	VALUE = "{ l.settings:displaydiscounts OR ( l.settings:price EQ 'sale' ) }">
		<MvASSIGN NAME = "l.settings:invmsg"			VALUE = "{ l.fields:invmsg }">
		<MvASSIGN NAME = "l.settings:field_code" 		VALUE = "{ l.fields:field_code }">
		<MvASSIGN NAME = "l.settings:field_name" 		VALUE = "{ l.fields:field_name }">
		<MvASSIGN NAME = "l.settings:field_options" 	VALUE = "{ l.fields:field_options }">
		<MvASSIGN NAME = "l.settings:field_dtadded" 	VALUE = "{ l.fields:field_dtadded }">
		<MvASSIGN NAME = "l.settings:field_notes" 		VALUE = "{ l.fields:field_notes }">
		<MvASSIGN NAME = "l.settings:field_quantity"	VALUE = "{ l.fields:field_quantity }">
		<MvASSIGN NAME = "l.settings:field_price" 		VALUE = "{ l.fields:field_price }">
		<MvASSIGN NAME = "l.settings:button_add" 		VALUE = "{ l.fields:button_add }">
	</MvIF>

	<MvCOMMENT>
	|
	| These settings are common between advanced and point + click mode
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.fields:paginate }">
		<MvASSIGN NAME = "l.settings:per_page"			VALUE = "{ l.fields:per_page }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:per_page"			VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.fields:paginate_count }">
		<MvASSIGN NAME = "l.settings:page_disp_count"	VALUE = "{ l.fields:page_disp_count }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:page_disp_count"	VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.settings:items_per_page_filter"	VALUE = "{ l.fields:items_per_page_filter }">

	<MvASSIGN NAME = "l.settings:sort_by:switch"		VALUE = "{ l.fields:sort_by:switch }">
	<MvASSIGN NAME = "l.settings:sort_by:name"			VALUE = "{ l.fields:sort_by:name }">
	<MvASSIGN NAME = "l.settings:sort_by:code"			VALUE = "{ l.fields:sort_by:code }">
	<MvASSIGN NAME = "l.settings:sort_by:price"			VALUE = "{ l.fields:sort_by:price }">
	<MvASSIGN NAME = "l.settings:sort_by:dtadded"		VALUE = "{ l.fields:sort_by:dtadded }">

	<MvASSIGN NAME = "l.settings:constrain"				VALUE = "{ l.fields:constrain }">
	<MvASSIGN NAME = "l.settings:b_width"				VALUE = "{ l.fields:b_width }">
	<MvASSIGN NAME = "l.settings:b_height"				VALUE = "{ l.fields:b_height }">

	<MvIF EXPR = "{ l.fields:default_sort EQ '' }">
		<MvASSIGN NAME = "l.settings:default_sort"		VALUE = "id_desc">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:default_sort"		VALUE = "{ l.fields:default_sort }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-WSHLI-00001', l.module:name $ ' \'' $ l.item $ '\' settings updated' ) }">

	<MvIF EXPR = "{ NOT l.fields:advanced }">
		<MvEVAL EXPR = "{ ComponentModule_Generate_Code( l.settings, l.fields:template_code ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, '', l.fields:template_code, l.settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update_Requires_Version" PARAMETERS = "module var, page var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_PostUpdate" PARAMETERS = "module var, page var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvCOMMENT>
	|
	| Load all image types
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.imagetype_count"						VALUE = "{ [ g.Module_Library_DB ].ImageTypeList_Load_All( l.imagetypes ) }">

	<MvIF EXPR = "{ l.load_fields }">
		<MvIF EXPR = "{ ISNULL l.settings:constrain }">
			<MvASSIGN NAME = "l.fields:constrain"				VALUE = "B">
		<MvELSE>
			<MvASSIGN NAME = "l.fields:constrain"				VALUE = "{ l.settings:constrain }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:b_width }">
			<MvASSIGN NAME = "l.fields:b_width"					VALUE = 75>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:b_width"					VALUE = "{ l.settings:b_width }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:b_height }">
			<MvASSIGN NAME = "l.fields:b_height"				VALUE = 75>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:b_height"				VALUE = "{ l.settings:b_height }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:price }">
			<MvASSIGN NAME = "l.fields:price"					VALUE = "base">
		<MvELSE>
			<MvASSIGN NAME = "l.fields:price"					VALUE = "{ l.settings:price }">
		</MvIF>

		<MvASSIGN NAME = "l.fields:advanced"					VALUE = "{ l.settings:advanced }">
		<MvASSIGN NAME = "l.fields:template_code"				VALUE = "{ l.templateversion:source }">
		<MvASSIGN NAME = "l.fields:template_filename"			VALUE = "{ l.settings:template_filename }">
		<MvASSIGN NAME = "l.fields:additionalprice"				VALUE = "{ l.settings:additionalprice }">
		<MvASSIGN NAME = "l.fields:displaydiscounts"			VALUE = "{ l.settings:displaydiscounts }">
		<MvASSIGN NAME = "l.fields:predictdiscounts"			VALUE = "{ l.settings:predictdiscounts }">
		<MvASSIGN NAME = "l.fields:field_code"					VALUE = "{ l.settings:field_code }">
		<MvASSIGN NAME = "l.fields:field_name"					VALUE = "{ l.settings:field_name }">
		<MvASSIGN NAME = "l.fields:field_options"				VALUE = "{ l.settings:field_options }">
		<MvASSIGN NAME = "l.fields:field_dtadded"				VALUE = "{ l.settings:field_dtadded }">
		<MvASSIGN NAME = "l.fields:field_notes"					VALUE = "{ l.settings:field_notes }">
		<MvASSIGN NAME = "l.fields:field_quantity"				VALUE = "{ l.settings:field_quantity }">
		<MvASSIGN NAME = "l.fields:field_price"					VALUE = "{ l.settings:field_price }">
		<MvASSIGN NAME = "l.fields:button_add"					VALUE = "{ l.settings:button_add }">
		<MvASSIGN NAME = "l.fields:invmsg"						VALUE = "{ l.settings:invmsg }">
		<MvASSIGN NAME = "l.fields:imagetypes"					VALUE = "{ l.settings:imagetypes }">
		<MvASSIGN NAME = "l.fields:image"						VALUE = "{ l.settings:imagetypes[ 1 ] }">

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].InitializeCustomFieldSelect( l.settings:fields_custom, l.fields:fields_custom ) }">

		<MvIF EXPR = "{ l.settings:per_page EQ 0 }">
			<MvASSIGN NAME = "l.fields:paginate"				VALUE = 0>
			<MvASSIGN NAME = "l.fields:per_page"				VALUE = 10>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:paginate"				VALUE = 1>
			<MvASSIGN NAME = "l.fields:per_page"				VALUE = "{ l.settings:per_page }">
		</MvIF>

		<MvIF EXPR = "{ l.settings:page_disp_count EQ 0 }">
			<MvASSIGN NAME = "l.fields:paginate_count"			VALUE = 0>
			<MvASSIGN NAME = "l.fields:page_disp_count"			VALUE = 3>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:paginate_count"			VALUE = 1>
			<MvASSIGN NAME = "l.fields:page_disp_count"			VALUE = "{ l.settings:page_disp_count }">
		</MvIF>

		<MvIF EXPR = "{ l.settings:items_per_page_filter EQ 0 }">
			<MvASSIGN NAME = "l.fields:items_per_page_filter"	VALUE = 0>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:items_per_page_filter"	VALUE = "{ l.settings:items_per_page_filter }">
		</MvIF>

		<MvASSIGN NAME = "l.fields:sort_by:switch"				VALUE = "{ l.settings:sort_by:switch }">
		<MvASSIGN NAME = "l.fields:sort_by:name"				VALUE = "{ l.settings:sort_by:name }">
		<MvASSIGN NAME = "l.fields:sort_by:code"				VALUE = "{ l.settings:sort_by:code }">
		<MvASSIGN NAME = "l.fields:sort_by:price"				VALUE = "{ l.settings:sort_by:price }">
		<MvASSIGN NAME = "l.fields:sort_by:dtadded"				VALUE = "{ l.settings:sort_by:dtadded }">

		<MvIF EXPR = "{ l.settings:default_sort EQ '' }">
			<MvASSIGN NAME = "l.fields:default_sort"			VALUE = "id_desc">
		<MvELSE>
			<MvASSIGN NAME = "l.fields:default_sort"			VALUE = "{ l.settings:default_sort }">
		</MvIF>

		<MvASSIGN NAME = "l.fields:reverse_default_disp_order" 	VALUE = "{ l.settings:reverse_default_disp_order }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'fields_custom,image,imagetypes,paginate,per_page,paginate_count,items_per_page_filter,page_disp_count,price,additionalprice,displaydiscounts,predictdiscounts,field_code,field_name,field_options,field_dtadded,field_notes,field_quantity,field_price,button_add,invmsg,default_sort,sort_by:switch,sort_by:name,sort_by:code,sort_by:price,sort_by:dtadded,reverse_default_disp_order,template_code,template_filename,advanced' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_filename' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<MvIF EXPR = "{ NOT l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_code' ) }">
		<MvELSE>
			<tr><td colspan="2" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( l.field_prefix $ 'template_code', l.fields:template_code, 20, 100, l.templateversion:templ_id ) }">
			</td></tr>
		</MvIF>

		<MvIF EXPR = "{ l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'field_code,field_name,field_options,field_dtadded,field_notes,field_quantity,field_price' ) }">
		<MvELSE>
			<tr><td align="left" valign="top" nowrap>
				<b>Displayed Fields:</b>
			</td><td align="left" width="100%">
				<table border="0" cellpadding="0" cellspacing="0">
				<tr><td>
					<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_code, l.field_prefix $ 'field_code', 1, 'Product Code' ) }"></label>
				</td><td>
					<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_notes, l.field_prefix $ 'field_notes', 1, 'Notes' ) }"></label>
				</td></tr>

				<tr><td>
					<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_name, l.field_prefix $ 'field_name', 1, 'Product Name' ) }"></label>
				</td><td>
					<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_quantity, l.field_prefix $ 'field_quantity', 1, 'Product Quantity' ) }"></label>
				</td></tr>

				<tr><td>
					<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_options, l.field_prefix $ 'field_options', 1, 'Product Options' ) }"></label>
				</td><td>
					<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_price, l.field_prefix $ 'field_price', 1, 'Product Price' ) }"></label>
				</td></tr>

				<tr><td>
					<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_dtadded, l.field_prefix $ 'field_dtadded', 1, 'Date Added' ) }"></label>
				</td><td>
					&nbsp;
				</td></tr>
				</table>
			</td></tr>
		</MvIF>

		<MvASSIGN NAME = "l.customfield_count"					VALUE = "{ [ g.Module_Library_Utilities ].ProductCustomFieldList_Load( l.customfields ) }">

		<MvIF EXPR = "{ l.customfield_count }">
			<tr><td align="left" valign="top" nowrap>
				<b>Custom Fields:</b>
			</td><td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCustomFieldSelect( l.field_prefix $ 'fields_custom', l.fields:fields_custom, l.customfields, l.customfield_count ) }">
			</td></tr>
		</MvIF>

		<MvIF EXPR = "{ l.imagetype_count EQ 0 }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'image,imagetypes' ) }">
		<MvELSE>
			<MvIF EXPR = "{ l.fields:advanced }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'image' ) }">

				<tr><td align="left" valign="top" nowrap>
					<b>Product Images:</b>
				</td><td align="left" width="100%">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawImageTypeSelect( l.field_prefix $ 'imagetypes', l.fields:imagetypes, l.imagetypes, l.imagetype_count ) }">
				</td></tr>
			<MvELSE>
				<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'imagetypes' ) }">

				<tr><td align="left" valign="top" nowrap>
					<b>Image:</b>
				</td><td align="left" width="100%">
					<select name="{ encodeentities( l.field_prefix ) $ 'image' }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '', l.fields:image, 'None' ) }">

						<MvFOREACH ITERATOR = "l.imagetype" ARRAY = "l.imagetypes" COUNT = "{ l.imagetype_count }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( l.imagetype:code, l.fields:image, encodeentities( l.imagetype:descrip ) ) }">
						</MvFOREACH>
					</select>
				</td></tr>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'price,additionalprice,displaydiscounts' ) }">

			<tr><td>
				&nbsp;
			</td><td width="100%">
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:predictdiscounts, l.field_prefix $ 'predictdiscounts', 1, 'Calculate Predicted Discounts and Sale Price' ) }"></label>
			</td></tr>
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'predictdiscounts' ) }">

			<tr><td colspan="2">&nbsp;</td></tr>

			<tr><td align="left" nowrap valign="top">
				<b>Displayed Price:</b>
			</td><td align="left" nowrap width="100%">
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'price', 'retail',	l.fields:price, 'Retail Price' ) }"></label><br />
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'price', 'base',	l.fields:price, 'Base Price (includes any legacy price group discounts but does not reflect sales, coupons or discounts applied in the basket)' ) }"></label><br />
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'price', 'sale',	l.fields:price, 'Sale Price (includes legacy price groups and predicted discounts)' ) }"></label>
			</td></tr>

			<tr><td align="left" nowrap valign="top">
				<b>Additional Price Display:</b>
			</td><td align="left" nowrap width="100%">
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'additionalprice', '',			l.fields:additionalprice, 'None' ) }"></label><br />
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'additionalprice', 'retail',	l.fields:additionalprice, 'Retail Price' ) }"></label><br />
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'additionalprice', 'base',		l.fields:additionalprice, 'Base Price (includes any legacy price group discounts but does not reflect sales, coupons or discounts applied in the basket)' ) }"></label>
			</td></tr>

			<tr><td>
				&nbsp;
			</td><td width="100%">
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:displaydiscounts, l.field_prefix $ 'displaydiscounts', 1, 'Display Predicted Discounts' ) }"></label>
			</td></tr>

			<tr><td colspan="2">&nbsp;</td></tr>
		</MvIF>

		<MvIF EXPR = "{ l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'button_add,invmsg' ) }">
		<MvELSE>
			<tr><td align="left" valign="top" nowrap>
				<b>Buttons:</b>
			</td><td nowrap width="100%">
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:button_add, 		l.field_prefix $ 'button_add', 		1, 'Add To Basket' ) }"></label>
			</td></tr>

			<tr><td align="left" nowrap>
				<b>Inventory Level Message:</b>
			</td><td width="100%">
				<select name="{ encodeentities( l.field_prefix ) $ 'invmsg' }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'N', l.fields:invmsg, 'None' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'S', l.fields:invmsg, 'Short' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'L', l.fields:invmsg, 'Long' ) }">
				</select>
			</td></tr>
		</MvIF>

		<tr>
			<td align="left" nowrap>
				<b>Sorting Options:</b>
			</td>
			<td width="100%">
				<table width="100%">
					<tr>
						<td align="left" style="padding-right: 15px;" nowrap>
							<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'sort_by:switch', 0, l.fields:sort_by:switch, 'Off' ) }"></label><br />
							<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'sort_by:switch', 1, l.fields:sort_by:switch, 'On' ) }"></label>
						</td>
						<td width="100%">
							<MvIF EXPR = "{ l.fields:advanced }">
								<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'sort_by:name,sort_by:code,sort_by:price,sort_by:dtadded' ) }">
							<MvELSE>
								<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:name, 	l.field_prefix $ 'sort_by:name', 	'1', 'Name' ) }"></label><br />
								<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:code,	l.field_prefix $ 'sort_by:code',	'1', 'Code' ) }"></label><br />
								<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:price, 	l.field_prefix $ 'sort_by:price', 	'1', 'Price' ) }"></label><br />
								<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:dtadded, l.field_prefix $ 'sort_by:dtadded', '1', 'Date Added' ) }"></label><br />
							</MvIF>
						</td>
					</tr>
				</table>
			</td>
		</tr>

		<tr>
			<td align="left" style="padding-right: 15px;" nowrap>
				<b>Default Sort Method:</b>
			</td>
			<td width="100%">
				<table width="100%">
					<tr>
						<td align="left" style="padding-right: 15px;" nowrap>
							<select name="{ encodeentities( l.field_prefix ) $ 'default_sort' }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'id_desc',			l.fields:default_sort, 'Default' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'name_asc', 		l.fields:default_sort, 'Name Ascending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'name_desc',	 	l.fields:default_sort, 'Name Descending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'code_asc',		l.fields:default_sort, 'Code Ascending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'code_desc',		l.fields:default_sort, 'Code Descending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'price_asc', 		l.fields:default_sort, 'Lowest Price' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'price_desc', 		l.fields:default_sort, 'Highest Price' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'dtadded_asc', 	l.fields:default_sort, 'Date Added Ascending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'dtadded_desc',	l.fields:default_sort, 'Date Added Descending' ) }">
							</select>
						</td>
						<td width="100%">
							<font color="red"><b>WARNING:</b></font>&nbsp;&nbsp;Changing the default sorting option to any value other than "default" will increase page load times and increase the load on your server. Be fully aware of the effect this will have on your store before changing this option!
						</td>
					</tr>
				</table>
			</td>
		</tr>

		<tr><td align="left" nowrap>
				&nbsp;
		</td><td width="100%">
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:reverse_default_disp_order, l.field_prefix $ 'reverse_default_disp_order', '1', 'Reverse Default Display Order' ) }"></label>
		</td></tr>

		<tr><td align="left" nowrap valign="top">
			<b>Pagination:</b>
		</td><td align="left" nowrap width="100%">
			<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate', 0, l.fields:paginate, 'Do not paginate wish list items' ) }"></label><br />
			<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate', 1, l.fields:paginate, 'Display' ) }"></label> <input type="text" name="{ encodeentities( l.field_prefix ) $ 'per_page' }" value="{ encodeentities( l.fields:per_page ) }" size="5"> wish list items per page<br />
		</td></tr>

		<tr><td align="left" nowrap valign="top">
			<b>Page Numbers:</b>
		</td><td align="left" nowrap width="100%">
			<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate_count', 0, l.fields:paginate_count, 'Do not show page count' ) }"></label><br />
			<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate_count', 1, l.fields:paginate_count, 'Display' ) }"></label> <input type="text" name="{ encodeentities( l.field_prefix ) $ 'page_disp_count' }" value="{ encodeentities( l.fields:page_disp_count ) }" size="5"> pages at a time
		</td></tr>

		<tr><td align="left" nowrap valign="top">
			<b>Items Per Page Filter:</b>
		</td><td align="left" nowrap width="100%">
			<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'items_per_page_filter', 0, l.fields:items_per_page_filter, 'Off' ) }"></label><br />
			<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'items_per_page_filter', 1, l.fields:items_per_page_filter, 'On' ) }"></label>
		</td></tr>

		<tr><td colspan="2">&nbsp;</td></tr>

		<tr><td>
			&nbsp;
		</td><td width="100%">
			<input type="hidden" name="{ encodeentities( l.field_prefix ) $ 'advanced' }" value="{ encodeentities( l.fields:advanced ) }">
			<MvIF EXPR = "{ l.fields:advanced }">
				<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 0 ); return false;' }">Point + Click Mode</a>
			<MvELSE>
				<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 1 ); return false;' }">Advanced Mode</a>
			</MvIF>
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, l.item $ '_dimensions' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'constrain,b_width,b_height' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item $ '_dimensions' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
			<tr>
				<td align="left" valign="top" nowrap><b>Product Image:</b></td>
				<td align="left" width="100%">
					<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'constrain', 'N', l.fields:constrain, 'No constraints' ) }"></label><br />
					<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'constrain', 'B', l.fields:constrain, 'Resize to fit within bounding box:' ) }"></label>
					<input type="text" name="{ encodeentities( l.field_prefix ) $ 'b_width' }" value="{ encodeentities( l.fields:b_width ) }" style="text-align:right;" size="5" /> x 
					<input type="text" name="{ encodeentities( l.field_prefix ) $ 'b_height' }" value="{ encodeentities( l.fields:b_height ) }" style="text-align:right;" size="5" /> pixels
				</td>
			</tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:fields_custom"					VALUE = "">
	<MvASSIGN NAME = "l.settings:field_code"					VALUE = 1>
	<MvASSIGN NAME = "l.settings:field_name"					VALUE = 1>
	<MvASSIGN NAME = "l.settings:field_options"					VALUE = 1>
	<MvASSIGN NAME = "l.settings:field_dtadded"					VALUE = 1>
	<MvASSIGN NAME = "l.settings:field_notes"					VALUE = 1>
	<MvASSIGN NAME = "l.settings:field_quantity"				VALUE = 1>
	<MvASSIGN NAME = "l.settings:field_price"					VALUE = 1>
	<MvASSIGN NAME = "l.settings:button_add"					VALUE = 1>
	<MvASSIGN NAME = "l.settings:price"							VALUE = "base">
	<MvASSIGN NAME = "l.settings:additionalprice"				VALUE = "">
	<MvASSIGN NAME = "l.settings:displaydiscounts"				VALUE = 0>
	<MvASSIGN NAME = "l.settings:predictdiscounts"				VALUE = 0>
	<MvASSIGN NAME = "l.settings:invmsg"						VALUE = "S">
	<MvASSIGN NAME = "l.settings:per_page"						VALUE = 10>
	<MvASSIGN NAME = "l.settings:page_disp_count"				VALUE = 3>
	<MvASSIGN NAME = "l.settings:items_per_page_filter"			VALUE = 1>
	<MvASSIGN NAME = "l.settings:imagetypes"					VALUE = "">
	<MvASSIGN NAME = "l.settings:constrain"						VALUE = "B">
	<MvASSIGN NAME = "l.settings:b_width"						VALUE = 75>
	<MvASSIGN NAME = "l.settings:b_height"						VALUE = 75>
	<MvASSIGN NAME = "l.settings:sort_by:switch"				VALUE = 0>
	<MvASSIGN NAME = "l.settings:sort_by:name"					VALUE = 0>
	<MvASSIGN NAME = "l.settings:sort_by:code"					VALUE = 0>
	<MvASSIGN NAME = "l.settings:sort_by:price"					VALUE = 0>
	<MvASSIGN NAME = "l.settings:sort_by:dtadded"				VALUE = 0>
	<MvASSIGN NAME = "l.settings:default_sort"					VALUE = "id_desc">
	<MvASSIGN NAME = "l.settings:reverse_default_disp_order" 	VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ ComponentModule_Generate_Code( l.settings, l.template_source ) }">

	<MvASSIGN NAME = "l.settings:template_filename"	VALUE = "{ tolower( l.page:code ) $ '-' $ tolower( l.item ) $ '.mvc' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.template_source, l.settings, l.settings:template_filename ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'buttons' ) OR
					NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'customerwishlists' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:template_filename ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:template_filename, l.source_templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:template_filename, l.dest_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.dest_settings"						VALUE = "{ l.source_settings }">
	<MvASSIGN NAME = "l.dest_settings:template_filename"	VALUE = "{ l.dest_managedtemplate:version:settings:template_filename }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_managedtemplate, '', l.source_templateversion:source, l.dest_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.source_branch:id EQ l.dest_branch:id }">	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-WSHLI-00004', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' on branch \'' $ l.dest_branch:name $ '\'' ) }">
	<MvELSE>													<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-WSHLI-00005', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' from branch \'' $ l.source_branch:name $ '\' to branch \'' $ l.dest_branch:name $ '\'' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Create_Pagination" PARAMETERS = "all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_WSH_DB ].Runtime_WishListProduct_Count_WishList( g.WishList_ID, l.settings:total_count ) }">
		<MvASSIGN NAME = "l.settings:page_disp_count" VALUE = 0>
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.settings:page_disp_count EQ 0 OR l.settings:per_page EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.link_additions" VALUE = "{ '&WishList_ID=' $ encodeattribute( g.WishList_ID ) }">

	<MvIF EXPR = "{ NOT ISNULL g.Per_Page }">	<MvASSIGN NAME = "l.link_additions"	VALUE = "{ l.link_additions $ '&Per_Page=' $ encodeattribute( g.Per_Page ) }">	</MvIF>
	<MvIF EXPR = "{ NOT ISNULL g.Sort_By }">	<MvASSIGN NAME = "l.link_additions"	VALUE = "{ l.link_additions $ '&Sort_By=' $ encodeattribute( g.Sort_By ) }">	</MvIF>

	<MvASSIGN NAME = "l.page_count"						VALUE = "{ ceil( l.settings:total_count / l.settings:per_page ) }">
	<MvASSIGN NAME = "l.settings:page_links:last_page"	VALUE = "{ l.page_count }">

	<MvIF EXPR = "{ g.NextOffset }">	<MvASSIGN NAME = "l.settings:page_links:current_page" VALUE = "{ ceil( g.NextOffset / l.settings:per_page ) }">
	<MvELSE>							<MvASSIGN NAME = "l.settings:page_links:current_page" VALUE = "{ ceil( l.settings:total_count / l.settings:per_page ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings:page_links:next_link_params"		VALUE = "{ 'Offset=' $ encodeattribute( ( l.settings:page_links:current_page ) * l.settings:per_page ) $ l.link_additions }">
	<MvASSIGN NAME = "l.settings:page_links:prev_link_params"		VALUE = "{ 'Offset=' $ encodeattribute( ( l.settings:page_links:current_page - 2 ) * l.settings:per_page ) $ l.link_additions }">
	<MvASSIGN NAME = "l.settings:page_links:first_link_params"		VALUE = "{ 'Offset=0' $ l.link_additions }">

	<MvIF EXPR = "{ floor( l.settings:total_count / l.settings:per_page ) * l.settings:per_page EQ l.settings:total_count }">
		<MvASSIGN NAME = "l.last_page_offset"						VALUE = "{ floor( ( l.settings:total_count - 1 ) / l.settings:per_page ) * l.settings:per_page }">
		<MvASSIGN NAME = "l.settings:page_links:last_link_params"	VALUE = "{ 'Offset=' $ encodeattribute( floor( ( l.settings:total_count - 1 ) / l.settings:per_page ) * l.settings:per_page ) $ l.link_additions }">
	<MvELSE>
		<MvASSIGN NAME = "l.last_page_offset"						VALUE = "{ floor( ( l.settings:total_count ) / l.settings:per_page ) * l.settings:per_page }">
		<MvASSIGN NAME = "l.settings:page_links:last_link_params"	VALUE = "{ 'Offset=' $ encodeattribute( floor( l.settings:total_count / l.settings:per_page ) * l.settings:per_page ) $ l.link_additions }">
	</MvIF>

	<MvIF EXPR = "{ l.page_count LT l.settings:page_disp_count }">
		<MvASSIGN NAME = "l.left"	VALUE = "{ floor( ( l.page_count - 1 ) / 2 ) }">
		<MvASSIGN NAME = "l.right"	VALUE = "{ ceil( ( l.page_count - 1 ) / 2 ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.left"	VALUE = "{ floor( ( l.settings:page_disp_count - 1 ) / 2 ) }">
		<MvASSIGN NAME = "l.right"	VALUE = "{ ceil( ( l.settings:page_disp_count - 1 ) / 2 ) }">
	</MvIF>

	<MvASSIGN NAME = "l.counter"	VALUE = 0>
	<MvASSIGN NAME = "l.left_final" VALUE = 0>

	<MvWHILE EXPR = "{ l.counter LT l.left }">
		<MvIF EXPR = "{ ( ( l.settings:page_links:current_page - l.left + 1 ) + l.counter ) GT 1 }">
			<MvASSIGN NAME = "l.left_final"	VALUE = "{ l.left_final + 1 }">
		<MvELSE>
			<MvASSIGN NAME = "l.right" VALUE = "{ l.right + 1 }">
		</MvIF>

		<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
	</MvWHILE>

	<MvIF EXPR = "{ l.left_final EQ l.left }">
		<MvASSIGN NAME = "l.counter"	 VALUE = 0>
		<MvASSIGN NAME = "l.right_final" VALUE = 0>

		<MvWHILE EXPR = "{ l.counter LT l.right }">
			<MvIF EXPR = "{ ( ( l.settings:page_links:current_page + l.right - 1 ) - l.counter ) LT l.page_count }">
				<MvASSIGN NAME = "l.right_final" VALUE = "{ l.right_final + 1 }">
			<MvELSE>
				<MvASSIGN NAME = "l.left_final" VALUE = "{ l.left_final + 1 }">
			</MvIF>

			<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
		</MvWHILE>

		<MvASSIGN NAME = "l.pages" VALUE = "{ l.left_final + l.right_final + 1 }">
	<MvELSE>
		<MvASSIGN NAME = "l.pages" VALUE = "{ l.left_final + l.right + 1 }">
	</MvIF>

	<MvASSIGN NAME = "l.counter" VALUE = 0>

	<MvWHILE EXPR = "{ l.counter LT l.pages }">
		<MvASSIGN NAME = "l.settings:page_links:pages" INDEX = "{ l.counter + 1 }" MEMBER = "page_num" 		VALUE = "{ ( l.settings:page_links:current_page - l.left_final ) + l.counter }">
		<MvASSIGN NAME = "l.settings:page_links:pages" INDEX = "{ l.counter + 1 }" MEMBER = "link_params" 	VALUE = "{ 'Offset=' $ encodeattribute( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) $ l.link_additions }">

		<MvIF EXPR = "{ ( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) EQ 0 }">					<MvASSIGN NAME = "l.settings:page_links:contains_first"	VALUE = "YES">	</MvIF>
		<MvIF EXPR = "{ ( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) EQ l.last_page_offset }">	<MvASSIGN NAME = "l.settings:page_links:contains_last"	VALUE = "YES">	</MvIF>

		<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_WSH_DB ].WishList_Load_ID_Cached( g.WishList_ID, l.settings:wishlist ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Throw_Exception( 'wishlist_invalid' ) }">
	<MvELSEIF EXPR = "{ ( NOT l.settings:wishlist:shared ) AND ( l.settings:wishlist:cust_id NE g.Basket:cust_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Throw_Exception( 'wishlist_invalid' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.settings:items_per_page_filter }">
		<MvIF EXPR = "{ ISNULL g.Per_Page OR NOT [ g.Module_Admin ].Validate_WholeNumber_Required( g.Per_Page ) }">
			<MvIF EXPR = "{ l.settings:per_page EQ 0 }">
				<MvASSIGN NAME = "g.Per_Page"			VALUE = -1>
			<MvELSE>
				<MvASSIGN NAME = "g.Per_Page"			VALUE = "{ l.settings:per_page }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ g.Per_Page GE 0 }">
				<MvASSIGN NAME = "l.settings:per_page"	VALUE = "{ g.Per_Page }">
			<MvELSE>
				<MvASSIGN NAME = "g.Per_Page"			VALUE = -1>
				<MvASSIGN NAME = "l.settings:per_page"	VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.Sort_By }">
		<MvASSIGN NAME = "l.sort_by"					VALUE = "{ g.Sort_By }">
	<MvELSE>
		<MvASSIGN NAME = "l.sort_by"					VALUE = "{ l.settings:default_sort }">
		<MvASSIGN NAME = "g.Sort_By"					VALUE = "{ l.settings:default_sort }">
	</MvIF>

	<MvIF EXPR = "{ l.sort_by EQ 'id_desc' AND
	 				l.settings:reverse_default_disp_order }">
		<MvASSIGN NAME = "l.sort_by" 					VALUE = "id">
	</MvIF>

	<MvASSIGN NAME = "l.settings:item_count"			VALUE = "{ [ g.Module_Feature_WSH_DB ].Runtime_WishListProductList_Load_Offset_WishList( g.WishList_ID, g.Offset, l.settings:per_page, l.sort_by, g.NextOffset, l.settings:items ) }">
	<MvASSIGN NAME = "l.null"							VALUE = "{ Create_Pagination( l.all_settings, l.settings ) }">

	<MvIF EXPR = "{ l.settings:item_count EQ 0 }">
		<MvASSIGN NAME = "l.settings:empty"				VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "g.PrevOffset"						VALUE = "{ g.Offset - l.settings:per_page }">

	<MvIF EXPR = "{ g.PrevOffset LT 0 }">
		<MvASSIGN NAME = "g.PrevOffset"					VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_CustomField_Lookup( l.all_settings, l.settings:fields_custom ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.settings:predictdiscounts }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_CreateFromBasket( g.Basket, l.discount_state ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_Load_PriceGroups_Basket( g.Basket, l.discount_state ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_Disable_Unused_Item_PriceGroups( l.discount_state ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_Predict_Baseline_Discounts( l.discount_state ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFOREACH INDEX = "l.item_pos" ITERATOR = "l.wishlistitem" ARRAY = "l.settings:items" COUNT = "{ l.settings:item_count }">
		<MvASSIGN NAME = "l.wishlistitem:formatted_dtadded_date"				VALUE = "{ [ g.Module_Library_Utilities ].Format_Date( l.wishlistitem:dtadded, s.miva_language ) }">
		<MvASSIGN NAME = "l.wishlistitem:formatted_dtadded_time"				VALUE = "{ [ g.Module_Library_Utilities ].Format_Time( l.wishlistitem:dtadded, s.miva_language ) }">
		<MvASSIGN NAME = "l.wishlistitem:product:quantity" 						VALUE = "{ l.wishlistitem:quantity }">
		<MvASSIGN NAME = "l.wishlistitem:product:option_count" 					VALUE = "{ [ g.Module_Feature_WSH_DB ].WishListOptionList_Load_WishListItem( l.wishlistitem:id, l.wishlistitem:product:options ) }">
		<MvASSIGN NAME = "l.attribute_count"									VALUE = "{ [ g.Module_Library_DB ].AttributeList_Load_Product( l.wishlistitem:product:id, l.attributes ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].OrderItem_DetermineVariant_LowLevel( l.wishlistitem:product, l.attributes, l.attribute_count, l.wishlistitem:product:options, l.wishlistitem:product:option_count, l.variant_id ) }">
			<MvASSIGN NAME = "l.variant_id" VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_INV_RT ].Inventory_Load_Variant( l.wishlistitem:product, l.variant_id ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_INV_RT ].Inventory_Adjust_VariantPricing( l.wishlistitem:product, l.variant_id, l.attr_price_override ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_Runtime( l.wishlistitem:product ) OR
						NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_CustomFields( l.wishlistitem:product:id, l.wishlistitem:product, l.all_settings, l.settings:fields_custom ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.null" 												VALUE = "{ [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_ImageTypes( l.wishlistitem:product:id, 0, l.wishlistitem:product, l.all_settings, l.settings:imagetypes, l.settings:constrain, l.settings:b_width, l.settings:b_height ) }">

		<MvIF EXPR = "{ l.settings:predictdiscounts }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_Predict_Product_Discounts( l.discount_state, l.wishlistitem:product, l.variant_id, l.wishlistitem:quantity, l.wishlistitem:product:price, l.wishlistitem:product:discounts, l.wishlistitem:product:discount_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.wishlistitem:product:formatted_price" 			VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.wishlistitem:product:price ) }">

			<MvFOREACH ITERATOR = "l.discount" ARRAY = "l.wishlistitem:product:discounts" COUNT = "{ l.wishlistitem:product:discount_count }">
				<MvASSIGN NAME = "l.discount:formatted_discount" 				VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.discount:discount ) }">
			</MvFOREACH>
		</MvIF>

		<MvASSIGN NAME = "l.wishlistitem:product:link" 							VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Product_URL( l.wishlistitem:product, l.null ) }">
		<MvASSIGN NAME = "l.wishlistitem:product:subtotal_retail"				VALUE = "{ l.wishlistitem:product:retail * l.wishlistitem:quantity }">
		<MvASSIGN NAME = "l.wishlistitem:product:subtotal_base_price"			VALUE = "{ l.wishlistitem:product:base_price * l.wishlistitem:quantity }">
		<MvASSIGN NAME = "l.wishlistitem:product:subtotal"						VALUE = "{ l.wishlistitem:product:price * l.wishlistitem:quantity }">
		<MvASSIGN NAME = "l.wishlistitem:product:formatted_subtotal_retail"		VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.wishlistitem:product:subtotal_retail ) }">
		<MvASSIGN NAME = "l.wishlistitem:product:formatted_subtotal_base_price"	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.wishlistitem:product:subtotal_base_price ) }">
		<MvASSIGN NAME = "l.wishlistitem:product:formatted_subtotal"			VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.wishlistitem:product:subtotal ) }">
		<MvASSIGN NAME = "l.item_total"											VALUE = "{ l.wishlistitem:product:price }">

		<MvFOREACH ITERATOR = "l.wishlistoption" ARRAY = "l.wishlistitem:product:options" COUNT = "{ l.wishlistitem:product:option_count }">
			<MvIF EXPR = "{ NOT ISNULL l.wishlistoption:data }">
				<MvASSIGN NAME = "l.wishlistoption:data"						VALUE = "{ encodeentities( l.wishlistoption:data ) }">
				<MvASSIGN NAME = "l.wishlistoption:data"						VALUE = "{ glosub( l.wishlistoption:data, asciichar( 10 ), '<br />' ) }">
			<MvELSEIF EXPR = "{ NOT ISNULL l.wishlistoption:data_long }">
				<MvASSIGN NAME = "l.wishlistoption:data_long"					VALUE = "{ encodeentities( l.wishlistoption:data_long ) }">
				<MvASSIGN NAME = "l.wishlistoption:data_long"					VALUE = "{ glosub( l.wishlistoption:data_long, asciichar( 10 ), '<br />' ) }">
			</MvIF>

			<MvIF EXPR = "{ l.attr_price_override }">
				<MvASSIGN NAME = "l.wishlistoption:retail"						VALUE = 0>
				<MvASSIGN NAME = "l.wishlistoption:base_price"					VALUE = 0>
				<MvASSIGN NAME = "l.wishlistoption:price"						VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.wishlistoption:subtotal_retail"					VALUE = "{ l.wishlistoption:retail * l.wishlistitem:quantity }">
			<MvASSIGN NAME = "l.wishlistoption:subtotal_base_price"				VALUE = "{ l.wishlistoption:base_price * l.wishlistitem:quantity }">
			<MvASSIGN NAME = "l.wishlistoption:subtotal"						VALUE = "{ l.wishlistoption:price * l.wishlistitem:quantity }">

			<MvASSIGN NAME = "l.wishlistoption:formatted_retail"				VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.wishlistoption:retail ) }">
			<MvASSIGN NAME = "l.wishlistoption:formatted_base_price"			VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.wishlistoption:base_price ) }">
			<MvASSIGN NAME = "l.wishlistoption:formatted_price"					VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.wishlistoption:price ) }">
			<MvASSIGN NAME = "l.wishlistoption:formatted_subtotal_retail"		VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.wishlistoption:subtotal_retail ) }">
			<MvASSIGN NAME = "l.wishlistoption:formatted_subtotal_base_price"	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.wishlistoption:subtotal_base_price ) }">
			<MvASSIGN NAME = "l.wishlistoption:formatted_subtotal"				VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.wishlistoption:subtotal ) }">
			<MvASSIGN NAME = "l.item_total"										VALUE = "{ l.item_total + l.wishlistoption:price }">
		</MvFOREACH>

		<MvASSIGN NAME = "l.wishlistitem:total"									VALUE = "{ [ g.Module_Library_Utilities ].Round_Item_Total( l.item_total * l.wishlistitem:quantity ) }">
		<MvASSIGN NAME = "l.wishlistitem:formatted_total"						VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.wishlistitem:total ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:template_filename, l.all_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.working_settings"	VALUE = "{ l.settings }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml, 'code', l.item ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml,	'InventoryMessage',				l.working_settings:invmsg,			'Long,Short,None',	'L,S,N' )				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O', l.provide_xml,	'WishListItemsPerPage',			l.working_settings:per_page )													OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O', l.provide_xml,	'PageCountNumber',				l.working_settings:page_disp_count )											OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'ItemsPerPage',					l.working_settings:items_per_page_filter )										OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'SortBySwitch',					l.working_settings:sort_by:switch )												OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByName',					l.working_settings:sort_by:name )												OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByCode',					l.working_settings:sort_by:code )												OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByPrice',					l.working_settings:sort_by:price )												OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByDateAdded',				l.working_settings:sort_by:dtadded )											OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml,	'DefaultSort',					l.working_settings:default_sort,
																							'id_desc,name_asc,name_desc,code_asc,code_desc,price_asc,price_desc,dtadded_desc,dtadded_asc',
																							'id_desc,name_asc,name_desc,code_asc,code_desc,price_asc,price_desc,dtadded_desc,dtadded_asc' )					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml, 'PriceDisplay',					l.working_settings:price,			'Retail,Base,Sale',	'retail,base,sale' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml, 'AdditionalPriceDisplay',		l.working_settings:additionalprice,	'None,Retail,Base',	',retail,base' )		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml, 'DisplayDiscounts',				l.working_settings:displaydiscounts )											OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml, 'PredictDiscounts',				l.working_settings:predictdiscounts )											OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml, 'ReverseDefaultDisplayOrder',	l.working_settings:reverse_default_disp_order )									OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'O', l.provide_xml,	'Notes',						l.notes ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Fields' ) }">
		<MvIF EXPR = "{ NOT ComponentModule_Provision_Fields( l.module, l.provide_xml:tags:Fields[ 1 ], l.working_settings ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'ImageTypes' ) }">
		<MvIF EXPR = "{ NOT ComponentModule_Provision_ImageTypes( l.module, l.provide_xml:tags:ImageTypes[ 1 ], l.working_settings ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to provision multiple image types' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'ImageDimensions' ) }">
		<MvASSIGN NAME = "l.dimensions" VALUE = "{ l.provide_xml:tags:ImageDimensions[ 1 ] }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Boolean( l.dimensions, 'constrain', l.constrain ) }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvIF EXPR = "{ NOT l.constrain }">
			<MvASSIGN NAME = "l.working_settings:constrain" VALUE = "N">
		<MvELSE>
			<MvASSIGN NAME = "l.working_settings:constrain" VALUE = "B">

			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer( 'R', l.dimensions, 'Width',	l.working_settings:b_width ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer( 'R', l.dimensions, 'Height',	l.working_settings:b_height ) }">
				<MvFUNCTIONRETURN>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Buttons' ) }">
		<MvEVAL EXPR = "{ ComponentModule_Provision_Buttons( l.module, l.provide_xml:tags:Buttons[ 1 ], l.working_settings ) }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
		<MvASSIGN NAME = "l.source"						VALUE = "{ trim( l.provide_xml:tags:Template[ 1 ]:value ) }">
		<MvASSIGN NAME = "l.working_settings:advanced"	VALUE = 1>
	<MvELSE>
		<MvASSIGN NAME = "l.working_settings:advanced"	VALUE = 0>

		<MvEVAL EXPR = "{ ComponentModule_Generate_Code( l.working_settings, l.source ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.source, l.working_settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings" VALUE = "{ l.working_settings }">

	<MvFUNCTIONRETURN>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision_Buttons" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:button_add" VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Add' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision_ImageTypes" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:imagetypes" VALUE = "">

	<MvFOREACH ITERATOR = "l.child" ARRAY = "l.provide_xml:children" COUNT = "{ miva_array_elements( l.provide_xml:children ) }">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].ImageType_Load_Code( l.child:value, l.imagetype ) }">
			<MvASSIGN NAME = "l.type_count" VALUE = "{ miva_array_insert( l.settings:imagetypes, l.imagetype:code, -1 ) }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision_Fields" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:field_code"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_name"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_options"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_dtadded"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_notes"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_quantity"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_price"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:fields_custom"		VALUE = "">
	<MvASSIGN NAME = "l.customfield_count"			VALUE = 0>

	<MvFOREACH ITERATOR = "l.child" ARRAY = "l.provide_xml:children" COUNT = "{ miva_array_elements( l.provide_xml:children ) }">
		<MvASSIGN NAME = "l.name"					VALUE = "{ tolower( l.child:name ) }">

		<MvIF EXPR = "{ l.name EQ 'code' }">			<MvASSIGN NAME = "l.settings:field_code"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'name' }">		<MvASSIGN NAME = "l.settings:field_name"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'options' }">		<MvASSIGN NAME = "l.settings:field_options"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'dateadded' }">	<MvASSIGN NAME = "l.settings:field_dtadded"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'notes' }">		<MvASSIGN NAME = "l.settings:field_notes"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'quantity' }">	<MvASSIGN NAME = "l.settings:field_quantity"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'price' }">		<MvASSIGN NAME = "l.settings:field_price"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'custom' }">
			<MvASSIGN NAME = "l.customfield_count" 	VALUE = "{ l.customfield_count + 1 }">

			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.child, 'module',	l.settings:fields_custom[ l.customfield_count ]:module ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.child, 'code',	l.settings:fields_custom[ l.customfield_count ]:code ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pages" VALUE = "{ [ g.Module_Feature_TUI_DB ].Item_Load_PageList( l.item ) }">

	<MvFUNCTIONRETURN VALUE = "{ 'Exports Wish List Items template from pages: ' $ l.pages }">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item_Runtime( l.item:id, l.pages ) }">
			<MvREFERENCEARRAY NAME = "l.settings" VARIABLE = "l.page:settings">
				<MvMEMBER NAME = "{ l.item:code }">
			</MvREFERENCEARRAY>

			<Page_Update code="{ l.page:code }">
				<Item code="{ l.item:code }">
					<WishListItemsPerPage><MvEVAL EXPR = "{ int( l.settings:per_page ) }"></WishListItemsPerPage>
					<PageCountNumber><MvEVAL EXPR = "{ int( l.settings:page_disp_count ) }"></PageCountNumber>
					<ItemsPerPage><MvIF EXPR = "{ l.settings:items_per_page_filter }">Yes<MvELSE>No</MvIF></ItemsPerPage>
					<SortBySwitch><MvIF EXPR = "{ l.settings:sort_by:switch }">Yes<MvELSE>No</MvIF></SortBySwitch>
					<SortByName><MvIF EXPR = "{ l.settings:sort_by:name }">Yes<MvELSE>No</MvIF></SortByName>
					<SortByCode><MvIF EXPR = "{ l.settings:sort_by:code }">Yes<MvELSE>No</MvIF></SortByCode>
					<SortByPrice><MvIF EXPR = "{ l.settings:sort_by:price }">Yes<MvELSE>No</MvIF></SortByPrice>
					<SortByDateAdded><MvIF EXPR = "{ l.settings:sort_by:dtadded }">Yes<MvELSE>No</MvIF></SortByDateAdded>
					<DefaultSort><MvIF EXPR = "{ ISNULL l.settings:default_sort }">id_desc<MvELSE><MvEVAL EXPR = "{ l.settings:default_sort }"></MvIF></DefaultSort>
					<ReverseDefaultDisplayOrder><MvIF EXPR = "{ l.settings:reverse_default_disp_order }">Yes<MvELSE>No</MvIF></ReverseDefaultDisplayOrder>

					<PriceDisplay><MvIF EXPR = "{ l.settings:price EQ 'retail' }">Retail<MvELSEIF EXPR = "{ l.settings:price EQ 'sale' }">Sale<MvELSE>Base</MvIF></PriceDisplay>
					<AdditionalPriceDisplay><MvIF EXPR = "{ l.settings:additionalprice EQ 'retail' }">Retail<MvELSEIF EXPR = "{ l.settings:additionalprice EQ 'base' }">Base<MvELSE>None</MvIF></AdditionalPriceDisplay>
					<DisplayDiscounts><MvIF EXPR = "{ l.settings:displaydiscounts }">Yes<MvELSE>No</MvIF></DisplayDiscounts>
					<PredictDiscounts><MvIF EXPR = "{ l.settings:predictdiscounts }">Yes<MvELSE>No</MvIF></PredictDiscounts>
					<InventoryMessage><MvIF EXPR = "{ l.settings:invmsg EQ 'L' }">Long<MvELSEIF EXPR = "{ l.settings:invmsg EQ 'S' }">Short<MvELSE>None</MvIF></InventoryMessage>

					<Buttons>
						<MvIF EXPR = "{ l.settings:button_add }"><Add /></MvIF>
					</Buttons>

					<Fields>
						<MvIF EXPR = "{ l.settings:field_code }"><Code /></MvIF>
						<MvIF EXPR = "{ l.settings:field_name }"><Name /></MvIF>
						<MvIF EXPR = "{ l.settings:field_options }"><Options /></MvIF>
						<MvIF EXPR = "{ l.settings:field_dtadded }"><DateAdded /></MvIF>
						<MvIF EXPR = "{ l.settings:field_notes }"><Notes /></MvIF>
						<MvIF EXPR = "{ l.settings:field_quantity }"><Quantity /></MvIF>
						<MvIF EXPR = "{ l.settings:field_price }"><Price /></MvIF>

						<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:fields_custom">
							<Custom module="{ l.customfield:module }" code="{ l.customfield:code }" />
						</MvFOREACH>
					</Fields>

					<ImageTypes>
						<MvFOREACH ITERATOR = "l.imagetype" ARRAY = "l.settings:imagetypes">
							<ImageType><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].CDATA_Encode( l.imagetype ) }"></ImageType>
						</MvFOREACH>
					</ImageTypes>

					<MvIF EXPR = "{ l.settings:constrain NE 'B' }">
						<ImageDimensions constrain="No" />
					<MvELSE>
						<ImageDimensions constrain="Yes">
							<Width><MvEVAL EXPR = "{ int( l.settings:b_width ) }"></Width>
							<Height><MvEVAL EXPR = "{ int( l.settings:b_height ) }"></Height>
						</ImageDimensions>
					</MvIF>

					<MvIF EXPR = "{ l.settings:advanced }">
						<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
							<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.templateversion:source ) }"></Template>
						</MvIF>
					</MvIF>

					<Notes>#Set_Current_Time#</Notes>
				</Item>
			</Page_Update>
		</MvFOREACH>
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Generate_Code" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:price EQ 'retail' }">
		<MvASSIGN NAME = "l.price_variable"			VALUE = "l.settings:wishlistitem:product:retail">
		<MvASSIGN NAME = "l.price_token"			VALUE = "&mvt:wishlistitem:product:formatted_retail;">
		<MvASSIGN NAME = "l.price_variable_option"	VALUE = "l.settings:option:retail">
		<MvASSIGN NAME = "l.price_token_option"		VALUE = "&mvt:option:formatted_retail;">
	<MvELSEIF EXPR = "{ l.settings:price EQ 'sale' }">
		<MvASSIGN NAME = "l.price_variable"			VALUE = "l.settings:wishlistitem:product:price">
		<MvASSIGN NAME = "l.price_token"			VALUE = "&mvt:wishlistitem:product:formatted_price;">
		<MvASSIGN NAME = "l.price_variable_option"	VALUE = "l.settings:option:price">
		<MvASSIGN NAME = "l.price_token_option"		VALUE = "&mvt:option:formatted_price;">
	<MvELSE>
		<MvASSIGN NAME = "l.price_variable"			VALUE = "l.settings:wishlistitem:product:base_price">
		<MvASSIGN NAME = "l.price_token"			VALUE = "&mvt:wishlistitem:product:formatted_base_price;">
		<MvASSIGN NAME = "l.price_variable_option"	VALUE = "l.settings:option:base_price">
		<MvASSIGN NAME = "l.price_token_option"		VALUE = "&mvt:option:formatted_base_price;">
	</MvIF>

	<MvCAPTURE VARIABLE = "l.code">
<MIVA STANDARDOUTPUTLEVEL = "text, html"><mvt:if expr="l.settings:wishlistitems:total_count EQ 0">
	<div class="wishlistitems_container wishlistitems_empty">This wish list is empty</div>
	<mvt:exit />
</mvt:if>

<div class="wishlistitems_container">
	<div id="filter-items-container"><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:sort_by:switch }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="l.settings:wishlistitems:total_count GT 1">
			<div class="sorting">
				<label for="Sort_By">Sort By:</label>
				<select name="Sort_By" id="Sort_By" onchange="window.location.href='&mvtj:urls:_self:auto_sep;WishList_ID=' + encodeURIComponent( '&mvtj:global:WishList_ID;' ) + '&amp;Per_Page=' + encodeURIComponent( '&mvtj:global:Per_Page;' ) + '&amp;Sort_By=' + encodeURIComponent( this.options[ this.selectedIndex ].value );"><MIVA STANDARDOUTPUTLEVEL = "">
				<MvIF EXPR = "{ l.settings:default_sort EQ 'id_desc' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="ISNULL g.Sort_By">
						<option value="id_desc" selected="selected">Default</option>
					<mvt:else>
						<option value="id_desc">Default</option>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF>

				<MvIF EXPR = "{ l.settings:sort_by:code }">
					<MvASSIGN NAME = "l.sort:disp_name_ascending"	VALUE = "Code Ascending">
					<MvASSIGN NAME = "l.sort:disp_code_ascending"	VALUE = "code_asc">
					<MvASSIGN NAME = "l.sort:disp_name_descending"	VALUE = "Code Descending">
					<MvASSIGN NAME = "l.sort:disp_code_descending"	VALUE = "code_desc">

					<MvASSIGN NAME = "l.sort_count"					VALUE = "{ miva_array_insert( l.sorts, l.sort, -1 ) }">
				</MvIF>

				<MvIF EXPR = "{ l.settings:sort_by:name }">
					<MvASSIGN NAME = "l.sort:disp_name_ascending"	VALUE = "Name Ascending">
					<MvASSIGN NAME = "l.sort:disp_code_ascending"	VALUE = "name_asc">
					<MvASSIGN NAME = "l.sort:disp_name_descending"	VALUE = "Name Descending">
					<MvASSIGN NAME = "l.sort:disp_code_descending"	VALUE = "name_desc">

					<MvASSIGN NAME = "l.sort_count"					VALUE = "{ miva_array_insert( l.sorts, l.sort, -1 ) }">
				</MvIF>

				<MvIF EXPR = "{ l.settings:sort_by:price }">
					<MvASSIGN NAME = "l.sort:disp_name_ascending"	VALUE = "Lowest Price">
					<MvASSIGN NAME = "l.sort:disp_code_ascending"	VALUE = "price_asc">
					<MvASSIGN NAME = "l.sort:disp_name_descending"	VALUE = "Highest Price">
					<MvASSIGN NAME = "l.sort:disp_code_descending"	VALUE = "price_desc">

					<MvASSIGN NAME = "l.sort_count"					VALUE = "{ miva_array_insert( l.sorts, l.sort, -1 ) }">
				</MvIF>

				<MvIF EXPR = "{ l.settings:sort_by:dtadded }">
					<MvASSIGN NAME = "l.sort:disp_name_ascending"	VALUE = "Date Added Ascending">
					<MvASSIGN NAME = "l.sort:disp_code_ascending"	VALUE = "dtadded_asc">
					<MvASSIGN NAME = "l.sort:disp_name_descending"	VALUE = "Date Added Descending">
					<MvASSIGN NAME = "l.sort:disp_code_descending"	VALUE = "dtadded_desc">

					<MvASSIGN NAME = "l.sort_count"					VALUE = "{ miva_array_insert( l.sorts, l.sort, -1 ) }">
				</MvIF>

				<MvFOREACH ITERATOR = "l.sort" ARRAY = "l.sorts" COUNT = "{ l.sort_count }">
					<MvIF EXPR = "{ NOT ISNULL l.sort:disp_name_ascending }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="{ 'g.Sort_By EQ \'' $ encodeentities( l.sort:disp_code_ascending ) $ '\'' }">
						<option value="{ encodeentities( l.sort:disp_code_ascending ) }" selected="selected"><MvEVAL EXPR = "{ encodeentities( l.sort:disp_name_ascending ) }"></option>
					<mvt:else>
						<option value="{ encodeentities( l.sort:disp_code_ascending ) }"><MvEVAL EXPR = "{ encodeentities( l.sort:disp_name_ascending ) }"></option>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
					</MvIF>
					
					<MvIF EXPR = "{ NOT ISNULL l.sort:disp_name_descending }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="{ 'g.Sort_By EQ \'' $ encodeentities( l.sort:disp_code_descending ) $ '\'' }">
						<option value="{ encodeentities( l.sort:disp_code_descending ) }" selected="selected"><MvEVAL EXPR = "{ encodeentities( l.sort:disp_name_descending ) }"></option>
					<mvt:else>
						<option value="{ encodeentities( l.sort:disp_code_descending ) }"><MvEVAL EXPR = "{ encodeentities( l.sort:disp_name_descending ) }"></option>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
					</MvIF>
				</MvFOREACH>
				
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				</select>
			</div>
		</mvt:if>
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
		
	<MvIF EXPR = "{ l.settings:items_per_page_filter }">
		<MvIF EXPR = "{ l.settings:per_page EQ 0 OR ISNULL l.settings:per_page }">
			<MvASSIGN NAME = "l.per_page" VALUE = 10>
		<MvELSE>
			<MvASSIGN NAME = "l.per_page" VALUE = "{ l.settings:per_page }">
		</MvIF>
		
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="l.settings:wishlistitems:total_count GT 1">
			<div class="per-page">
				<label for="Per_Page">View:</label>
				<select name="Per_Page" id="Per_Page" onchange="window.location.href='&mvtj:urls:_self:auto_sep;WishList_ID=' + encodeURIComponent( '&mvtj:global:WishList_ID;' ) + '&amp;Per_Page=' + encodeURIComponent( this.options[ this.selectedIndex ].value ) + '&amp;Sort_By=' + encodeURIComponent( '&mvtj:global:Sort_By;' );"><MIVA STANDARDOUTPUTLEVEL = "">
				<MvASSIGN NAME = "l.multiplier" VALUE = 1>
				<MvASSIGN NAME = "l.count" VALUE = 1>
					
				<MvWHILE EXPR = "{ l.count LE 3 }">
					<MvASSIGN NAME = "l.items_per" VALUE = "{ l.per_page * l.multiplier }">
					<MvASSIGN NAME = "l.multiplier" VALUE = "{ l.multiplier + l.count }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="{ 'g.Per_Page EQ ' $ encodeentities( l.items_per ) }">
						<option value="{ encodeentities( l.items_per ) }" selected="selected"><MvEVAL EXPR = "{ encodeentities( l.items_per ) }"></option>
					<mvt:else>
						<option value="{ encodeentities( l.items_per ) }"><MvEVAL EXPR = "{ encodeentities( l.items_per ) }"></option>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
					<MvASSIGN NAME = "l.count" VALUE = "{ l.count + 1 }">
				</MvWHILE>
					
					<MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="g.Per_Page EQ -1">
						<option value="-1" selected="selected">All</option>
					<mvt:else>
						<option value="-1">All</option>
					</mvt:if>
				</select>
			</div>
		</mvt:if>
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="l.settings:wishlistitems:page_links:last_page GT 1">
			<div class="page-links">
				<span class="page-links-title">Page(s):</span>
				<span class="page-links-container">
					<mvt:if expr="l.settings:wishlistitems:page_links:current_page NE 1">
						<a href="&mvte:urls:_self:auto_sep;&mvte:wishlistitems:page_links:prev_link_params;" class="page-links-previous">&lt;</a>
					<mvt:else>
						<span class="page-links-previous page-links-deactivated">&lt;</span>
					</mvt:if>
					<mvt:if expr="l.settings:wishlistitems:page_links:current_page NE l.settings:wishlistitems:page_links:last_page">
						<a href="&mvte:urls:_self:auto_sep;&mvte:wishlistitems:page_links:next_link_params;" class="page-links-next">&gt;</a>
					<mvt:else>
						<span class="page-links-next page-links-deactivated">&gt;</span>
					</mvt:if>
					<span class="page-disp">
						<mvt:if expr="NOT l.settings:wishlistitems:page_links:contains_first">
							<a href="&mvte:urls:_self:auto_sep;&mvte:wishlistitems:page_links:first_link_params;" class="page-links-inactive">1</a>...
						</mvt:if>
						<mvt:foreach iterator="pages" array="wishlistitems:page_links:pages">
							<mvt:if expr="l.settings:wishlistitems:page_links:current_page EQ l.settings:pages:page_num">
								<span class="page-links-active">&mvte:pages:page_num;</span>
							<mvt:else>
								<a href="&mvte:urls:_self:auto_sep;&mvte:pages:link_params;" class="page-links-inactive">&mvte:pages:page_num;</a>
							</mvt:if>
						</mvt:foreach>
						<mvt:if expr="NOT l.settings:wishlistitems:page_links:contains_last">
							...<a href="&mvte:urls:_self:auto_sep;&mvte:wishlistitems:page_links:last_link_params;" class="page-links-inactive"><MvEVAL EXPR = "&mvte:wishlistitems:page_links:last_page;"></a>
						</mvt:if>
					</span>
				</span>
			</div>
		</mvt:if>
	</div>

	<div class="clear"></div>

	<mvt:if expr="( l.settings:wishlistitems:wishlist:cust_id EQ g.Basket:cust_id ) AND ( NOT ISNULL g.Edit_Wish )">
		<form name="edit_wish" method="post" action="&mvte:urls:_self:secure;">
		<input type="hidden" name="Action" value="UPWL" />
		<input type="hidden" name="Sort_By" value="&mvte:global:Sort_By;" />
		<input type="hidden" name="Offset" value="&mvte:global:PrevOffset;" />
		<input type="hidden" name="Per_Page" value="&mvte:global:Per_Page;" />
		<input type="hidden" name="WishList_ID" value="&mvte:global:WishList_ID;" />
		<input type="hidden" name="Wish_ID" value="&mvte:global:Edit_Wish;" />
	</mvt:if>

	<table>
		<tr class="heading"><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ NOT ISNULL l.settings:imagetypes[ 1 ] }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="item-remove">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>

			<MvIF EXPR = "{ l.settings:field_code OR l.settings:field_name OR l.settings:field_options OR l.settings:field_notes OR l.settings:field_dtadded }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="item-name">Item</td><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>

			<MvIF EXPR = "{ l.settings:field_quantity }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="item-quantity">Qty.</td><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>

			<MvIF EXPR = "{ l.settings:field_price }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="item-price">Price</td><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>

			<MvIF EXPR = "{ l.settings:button_add }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="item-action">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
		</tr>

		<mvt:foreach iterator="wishlistitem" array="wishlistitems:items">
			<mvt:if expr="l.settings:wishlistitem:product:option_count">
				<mvt:assign name="l.settings:rowspan" value="l.settings:wishlistitem:product:option_count + 2" />
			<mvt:else>
				<mvt:assign name="l.settings:rowspan" value="2" />
			</mvt:if>

			<mvt:if expr = "l.pos1 MOD 2">
				<tr class="alt_row row_start">
			<mvt:else>
				<tr class="row_start">
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">

				<MvIF EXPR = "{ NOT ISNULL l.settings:imagetypes[ 1 ] }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<td class="item-preview" rowspan="&mvt:rowspan;"><MIVA STANDARDOUTPUTLEVEL = "">
					<MvASSIGN NAME = "l.imagetype"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.settings:imagetypes[ 1 ] ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="{ 'ISNULL l.settings:wishlistitem:product:imagetypes:' $ l.imagetype }">
						&nbsp;
					<mvt:else>
						<div class="item-image"><img src="{ '&mvte:wishlistitem:product:imagetypes:' $ l.imagetype $ ';' }" alt="&mvte:wishlistitem:product:name;" /></div>
					</mvt:if>
				</td><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF>

				<MvIF EXPR = "{ l.settings:field_code OR l.settings:field_name OR l.settings:field_options OR l.settings:field_notes OR l.settings:field_dtadded }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<td class="item-name">
					<table>
						<tr>
							<td width="100%"><MIVA STANDARDOUTPUTLEVEL = "">
							<MvIF EXPR = "{ l.settings:field_name AND NOT l.settings:field_code }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
								<a href="&mvte:wishlistitem:product:link;">&mvt:wishlistitem:product:name;</a><MIVA STANDARDOUTPUTLEVEL = "">
							<MvELSEIF EXPR = "{ l.settings:field_name }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
								&mvt:wishlistitem:product:name; -<MIVA STANDARDOUTPUTLEVEL = "">
							</MvIF>

							<MvIF EXPR = "{ l.settings:field_code }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
								<a href="&mvte:wishlistitem:product:link;">&mvt:wishlistitem:product:code;</a><MIVA STANDARDOUTPUTLEVEL = "">
							</MvIF>

							<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:fields_custom">
								<MvASSIGN NAME = "l.safe_module_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.customfield:module ) }">
								<MvASSIGN NAME = "l.safe_field_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.customfield:code ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
								<mvt:if expr="{ 'NOT ISNULL l.settings:wishlistitem:product:customfield_values:' $ l.safe_module_code $ ':' $ l.safe_field_code }">
									<div class="custom-field">&mvt:customfield_names:<MvEVAL EXPR = "{ l.safe_module_code }">:<MvEVAL EXPR = "{ l.safe_field_code }">;: &mvt:wishlistitem:product:customfield_values:<MvEVAL EXPR = "{ l.safe_module_code }">:<MvEVAL EXPR = "{ l.safe_field_code }">;</div>
								</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
							</MvFOREACH>

							<MvIF EXPR = "{ l.settings:displaydiscounts }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
								<mvt:foreach iterator="discount" array="wishlistitem:product:discounts">
									<mvt:if expr="l.settings:discount:display">
										<div class="item-discount">&mvt:discount:descrip;: &mvt:discount:formatted_discount;</div>
									</mvt:if>
								</mvt:foreach><MIVA STANDARDOUTPUTLEVEL = "">
							</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
							</td>
							<td nowrap>
								<mvt:if expr="( l.settings:wishlistitems:wishlist:cust_id EQ g.Basket:cust_id ) AND ( g.Edit_Wish EQ l.settings:wishlistitem:id ) AND ( l.settings:customerwishlists:wishlist_count GT 1 )">
									<select class="wishlist_move" onchange="document.forms.edit_wish.elements.Action.value='MPWL'; document.forms.edit_wish.elements.WishList_ID.value=this.value; document.forms.edit_wish.submit();">
										<option value="">Move</option>
										
										<mvt:foreach iterator="wishlist" array="customerwishlists:wishlists">
											<option value="&mvte:wishlist:id;">&mvte:wishlist:title;</option>
										</mvt:foreach>
									</select>
								<mvt:else><MIVA STANDARDOUTPUTLEVEL = "">
								<MvIF EXPR = "{ l.settings:field_dtadded }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
									<span style="color: #666;">Added: &mvt:wishlistitem:formatted_dtadded_date;</span><MIVA STANDARDOUTPUTLEVEL = "">
								<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
									&nbsp;<MIVA STANDARDOUTPUTLEVEL = "">
								</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
								</mvt:if>
							</td>
						</tr>
					</table>
				</td><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF>

				<MvIF EXPR = "{ l.settings:field_quantity }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<td class="item-quantity" rowspan="&mvt:rowspan;">
					<mvt:if expr="( l.settings:wishlistitems:wishlist:cust_id EQ g.Basket:cust_id ) AND ( g.Edit_Wish EQ l.settings:wishlistitem:id )">
						<input type="text" name="Quantity" class="textfield qty-input small" value="&mvte:wishlistitem:quantity;" />
					<mvt:else>
						&mvte:wishlistitem:quantity;
					</mvt:if>
				</td><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF>

				<MvIF EXPR = "{ l.settings:field_price }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<td class="item-price">
					<span style="text-decoration: line-through"><MIVA STANDARDOUTPUTLEVEL = "">
					<MvIF EXPR = "{ ( l.settings:additionalprice EQ 'retail' ) AND ( l.settings:price NE 'retail' ) }">
						<MIVA STANDARDOUTPUTLEVEL = "text, html"><mvt:if expr="{ 'l.settings:wishlistitem:product:retail GT ' $ l.price_variable }">&mvt:wishlistitem:product:formatted_retail;</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
					<MvELSEIF EXPR = "{ ( l.settings:additionalprice EQ 'base' ) AND ( l.settings:price NE 'base' ) }">
						<MIVA STANDARDOUTPUTLEVEL = "text, html"><mvt:if expr="{ 'l.settings:wishlistitem:product:base_price GT ' $ l.price_variable }">&mvt:wishlistitem:product:formatted_base_price;</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
					</MvIF>
					<MIVA STANDARDOUTPUTLEVEL = "text, html"></span>

					<MvEVAL EXPR = "{ l.price_token }">
				</td><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF>

				<MvIF EXPR = "{ l.settings:button_add }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<td class="item-action" rowspan="&mvt:rowspan;">
					<mvt:if expr="( l.settings:wishlistitems:wishlist:cust_id EQ g.Basket:cust_id ) AND ( g.Edit_Wish EQ l.settings:wishlistitem:id )">
						<mvt:item name="buttons" param="Update" />
					<mvt:elseif expr="( ISNULL g.Edit_Wish ) OR ( l.settings:wishlistitems:wishlist:cust_id NE g.Basket:cust_id )"><MIVA STANDARDOUTPUTLEVEL = "">
						<MvIF EXPR = "{ l.settings:invmsg NE 'N' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
						<mvt:if expr="l.settings:wishlistitem:product:inv_level EQ 'out'">
							<span class="grey">
								<mvt:if expr="l.settings:wishlistitem:product:inv_active"><MIVA STANDARDOUTPUTLEVEL = "">
								<MvIF EXPR = "{ l.settings:invmsg EQ 'L' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
								&mvt:wishlistitem:product:inv_long;<MIVA STANDARDOUTPUTLEVEL = "">
								<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
								&mvt:wishlistitem:product:inv_short;<MIVA STANDARDOUTPUTLEVEL = "">
								</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
								</mvt:if>
							</span>
						<mvt:else><MIVA STANDARDOUTPUTLEVEL = "">
						<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
						<mvt:if expr="l.settings:wishlistitem:product:inv_level NE 'out'"><MIVA STANDARDOUTPUTLEVEL = "">
						</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
							<form method="post" action="&mvte:urls:BASK:secure;">
								<input type="hidden" name="Action" value="AFWL" />
								<input type="hidden" name="Wish_ID" value="&mvte:wishlistitem:id;" />
								<input type="hidden" name="Quantity" value="&mvte:wishlistitem:quantity;" />
								<mvt:item name="buttons" param="AddToBasketL" />
							</form>
						</mvt:if>
					</mvt:if>
				</td><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
			</tr>
			<MIVA STANDARDOUTPUTLEVEL = "">

			<MvIF EXPR = "{ l.settings:field_options }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:foreach iterator="option" array="wishlistitem:product:options">
				<mvt:if expr = "l.pos1 MOD 2">
					<tr class="alt_row option_row">
				<mvt:else>
					<tr class="option_row">
				</mvt:if>
					<td class="item-name">
						<div class="item-option">
							<mvt:if expr="l.settings:option:option_id">
								&mvt:option:attr_code;: &mvt:option:opt_code;
							<mvt:elseif expr="NOT ISNULL l.settings:option:data">
								&mvt:option:attr_code;: &mvt:option:data;
							<mvt:elseif expr="NOT ISNULL l.settings:option:data_long">
								&mvt:option:attr_code;: &mvt:option:data_long;
							<mvt:else>
								&mvt:option:attr_code;
							</mvt:if>
						</div>
					</td><MIVA STANDARDOUTPUTLEVEL = "">

					<MvIF EXPR = "{ l.settings:field_price }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<td class="item-price">
						<span style="text-decoration: line-through"><MIVA STANDARDOUTPUTLEVEL = "">
						<MvIF EXPR = "{ ( l.settings:additionalprice EQ 'retail' ) AND ( l.settings:price NE 'retail' ) }">
							<MIVA STANDARDOUTPUTLEVEL = "text, html"><mvt:if expr="{ 'l.settings:option:retail GT ' $ l.price_variable_option }">&mvt:option:formatted_retail;</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
						<MvELSEIF EXPR = "{ ( l.settings:additionalprice EQ 'base' ) AND ( l.settings:price NE 'base' ) }">
							<MIVA STANDARDOUTPUTLEVEL = "text, html"><mvt:if expr="{ 'l.settings:option:base_price GT ' $ l.price_variable_option }">&mvt:option:formatted_base_price;</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
						</MvIF>
						<MIVA STANDARDOUTPUTLEVEL = "text, html"></span>

						<mvt:if expr="{ l.price_variable_option }">
							<MvEVAL EXPR = "{ l.price_token_option }">
						<mvt:else>
							&nbsp;
						</mvt:if>
					</td><MIVA STANDARDOUTPUTLEVEL = "">
					</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
				</tr>
			</mvt:foreach><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">

			<mvt:if expr = "l.pos1 MOD 2">
				<tr class="alt_row controls_row">
			<mvt:else>
				<tr class="controls_row">
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">

				<MvIF EXPR = "{ l.settings:field_code OR l.settings:field_name OR l.settings:field_options OR l.settings:field_notes OR l.settings:field_dtadded }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<td class="item-name">
					<table><MIVA STANDARDOUTPUTLEVEL = "">

						<MvIF EXPR = "{ l.settings:field_notes }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
						<mvt:if expr="( l.settings:wishlistitems:wishlist:cust_id EQ g.Basket:cust_id ) AND ( g.Edit_Wish EQ l.settings:wishlistitem:id )">
							<tr>
								<td width="100%" colspan="2">
									<div style="margin-top: 10px;">Notes:</div>
									<div class="editable_note_container">
										<textarea class="editable_note" name="Notes">&mvte:wishlistitem:notes;</textarea>
									</div>
								</td>
							</tr>
						<mvt:elseif expr="NOT ISNULL l.settings:wishlistitem:notes">
							<tr>
								<td width="100%" colspan="2">
									<span class="note">Notes: &mvte:wishlistitem:notes;</span>
								</td>
							</tr>
						</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
						</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">

						<mvt:if expr="l.settings:wishlistitems:wishlist:cust_id EQ g.Basket:cust_id">
							<tr>
								<td width="100%">
									<mvt:if expr="g.Edit_Wish NE l.settings:wishlistitem:id">
										<a class="edit" href="&mvte:urls:_self:auto_sep;WishList_ID=&mvta:global:WishList_ID;&amp;Edit_Wish=&mvta:wishlistitem:id;&amp;Offset=&mvta:global:Offset;&amp;Sort_By=&mvte:global:Sort_By;&amp;Per_Page=&mvte:global:Per_Page;">Edit comment, quantity, & wish list</a>
									</mvt:if>
								</td>
								<td nowrap align="right">
									<mvt:if expr="g.Edit_Wish EQ l.settings:wishlistitem:id">
										<a class="edit" href="&mvte:urls:_self:auto_sep;WishList_ID=&mvta:global:WishList_ID;&amp;Offset=&mvta:global:Offset;&amp;Sort_By=&mvte:global:Sort_By;&amp;Per_Page=&mvte:global:Per_Page;">Cancel</a><MIVA STANDARDOUTPUTLEVEL = "">
									<MvIF EXPR = "{ NOT l.settings:button_add }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
										<span class="grey">|</span>
										<a class="edit" href="#" onclick="document.forms.edit_wish.submit(); return false;">Update</a>
									<MIVA STANDARDOUTPUTLEVEL = "">
									</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
									<mvt:else>
										<a class="edit" href="&mvte:urls:_self:auto_sep;Action=RMWL&amp;WishList_ID=&mvta:global:WishList_ID;&amp;Wish_ID=&mvta:wishlistitem:id;&amp;Offset=&mvta:global:Offset;">Remove</a>
									</mvt:if>
								</td>
							</tr>
						</mvt:if>
					</table>
				</td><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF>

				<MvIF EXPR = "{ l.settings:field_price }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<td class="item-price">
					&nbsp;
				</td><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
			</tr>
		</mvt:foreach>
	</table>

	<mvt:if expr="( l.settings:wishlistitems:wishlist:cust_id EQ g.Basket:cust_id ) AND ( NOT ISNULL g.Edit_Wish )">
		</form>
	</mvt:if>

	<mvt:if expr="NOT l.settings:wishlistitems:page_disp_count GT 0">
		<mvt:if expr="( g.Offset OR g.NextOffset ) AND ( l.settings:wishlistitems:per_page NE 0 )">
			<div class="next-previous">
				<div class="previous-button">
					<mvt:if expr="g.Offset EQ 0">
						&nbsp;
					<mvt:else>
						<form method="post" action="&mvte:urls:_self:auto;" />
							<input type="hidden" name="Sort_By" value="&mvte:global:Sort_By;" />
							<input type="hidden" name="Offset" value="&mvte:global:PrevOffset;" />
							<input type="hidden" name="Per_Page" value="&mvte:global:Per_Page;" />
							<input type="hidden" name="WishList_ID" value="&mvte:global:WishList_ID;" />
							<mvt:item name="buttons" param="Previous" />
						</form>
					</mvt:if>
				</div>
				<div class="next-button">
					<mvt:if expr="g.NextOffset EQ 0">
						&nbsp;
					<mvt:else>
						<form method="post" action="&mvte:urls:_self:auto;">
							<input type="hidden" name="Sort_By" value="&mvte:global:Sort_By;" />
							<input type="hidden" name="Offset" value="&mvte:global:NextOffset;" />
							<input type="hidden" name="Per_Page" value="&mvte:global:Per_Page;" />
							<input type="hidden" name="WishList_ID" value="&mvte:global:WishList_ID;" />
							<mvt:item name="buttons" param="Next" />
						</form>
					</mvt:if>
				</div>
			</div>
	
			<div class="clear"></div>
		</mvt:if>
	</mvt:if>
</div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvCAPTURE>
</MvFUNCTION>

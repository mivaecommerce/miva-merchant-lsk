<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-MV-CMP-IMCHN-
| Next Error Code: 7
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-mv-imagemachine">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Image Machine">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0800">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "10.08">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, component_prov, skins">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Store:ui_mod:code EQ 'cssui' }">
		<MvIF EXPR = "{ l.item EQ 'category_listing_imagemachine' }">		<MvASSIGN NAME = "l.tab_title" VALUE = "Category Product List Image Machine">
		<MvELSEIF EXPR = "{ l.item EQ 'all_products_imagemachine' }">		<MvASSIGN NAME = "l.tab_title" VALUE = "Product List Image Machine">
		<MvELSEIF EXPR = "{ l.item EQ 'related_products_imagemachine' }">	<MvASSIGN NAME = "l.tab_title" VALUE = "Related Product List Image Machine">
		<MvELSEIF EXPR = "{ l.item EQ 'search_results_imagemachine' }">		<MvASSIGN NAME = "l.tab_title" VALUE = "Search Results Image Machine">
		<MvELSEIF EXPR = "{ l.item EQ 'product_display_imagemachine' }">	<MvASSIGN NAME = "l.tab_title" VALUE = "Product Display Layout Image Machine">
		<MvELSE>															<MvASSIGN NAME = "l.tab_title" VALUE = "{ 'Image Machine (' $ l.item $ ')' }">
		</MvIF>
	<MvELSEIF EXPR = "{ g.Store:ui_mod:code EQ 'mmui' }">
		<MvIF EXPR = "{ l.item EQ 'ctgy_imagemachine' }">					<MvASSIGN NAME = "l.tab_title" VALUE = "Category Product List Image Machine">
		<MvELSEIF EXPR = "{ l.item EQ 'plst_imagemachine' }">				<MvASSIGN NAME = "l.tab_title" VALUE = "Product List Image Machine">
		<MvELSEIF EXPR = "{ l.item EQ 'relp_imagemachine' }">				<MvASSIGN NAME = "l.tab_title" VALUE = "Related Product List Image Machine">
		<MvELSEIF EXPR = "{ l.item EQ 'srch_imagemachine' }">				<MvASSIGN NAME = "l.tab_title" VALUE = "Search Results Image Machine">
		<MvELSEIF EXPR = "{ l.item EQ 'prod_imagemachine' }">				<MvASSIGN NAME = "l.tab_title" VALUE = "Product Display Layout Image Machine">
		<MvELSE>															<MvASSIGN NAME = "l.tab_title" VALUE = "{ 'Image Machine (' $ l.item $ ')' }">
		</MvIF>
	<MvELSE>
		<MvASSIGN NAME = "l.tab_title" VALUE = "{ 'Image Machine (' $ l.item $ ')' }">
	</MvIF>

	<MvASSIGN NAME = "l.tabs"							VALUE = "{ 'GT_PAGE/' $ l.item $ ':' $ l.tab_title }">
	<MvIF EXPR = "{ l.settings:enabled }">
		<MvASSIGN NAME = "l.tabs"						VALUE = "{ l.tabs $ ',GT_PAGE/' $ l.item $ '_dimensions:' $ glosub( l.tab_title, 'Image Machine', 'Image Dimensions' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.tabs }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.fields:head:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.fields:enabled"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:enabled ) }">
	<MvASSIGN NAME = "l.fields:preload"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:preload ) }">
	<MvASSIGN NAME = "l.fields:closeup_active"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:closeup_active ) }">
	<MvASSIGN NAME = "l.fields:thumb_active"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:thumb_active ) }">

	<MvIF EXPR = "{ l.fields:image_size EQ 'B' }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:image_width ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'image_width', g.Validation_Message ) }">
		<MvELSEIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:image_height ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'image_height', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.fields:closeup_active }">
		<MvIF EXPR = "{ l.fields:closeup_size EQ 'B' }">
			<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:closeup_width ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'closeup_width', g.Validation_Message ) }">
			<MvELSEIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:closeup_height ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'closeup_height', g.Validation_Message ) }">
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.fields:thumb_active }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:thumb_width ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'thumb_width', g.Validation_Message ) }">
		<MvELSEIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:thumb_height ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'thumb_height', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.fields:head:template_code ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'head:template_code', g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:head:template_filename, l.head_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.settings:enabled"				VALUE = "{ l.fields:enabled }">
	<MvASSIGN NAME = "l.settings:preload"				VALUE = "{ l.fields:preload }">
	<MvASSIGN NAME = "l.settings:main_image"			VALUE = "{ l.fields:main_image }">
	<MvASSIGN NAME = "l.settings:thumbnails"			VALUE = "{ l.fields:thumbnails }">
	<MvASSIGN NAME = "l.settings:closeup_div"			VALUE = "{ l.fields:closeup_div }">
	<MvASSIGN NAME = "l.settings:closeup_image"			VALUE = "{ l.fields:closeup_image }">
	<MvASSIGN NAME = "l.settings:closeup_close"			VALUE = "{ l.fields:closeup_close }">
	<MvASSIGN NAME = "l.settings:image_size"			VALUE = "{ l.fields:image_size }">
	<MvASSIGN NAME = "l.settings:legacy_image"			VALUE = "{ l.fields:legacy_image }">

	<MvIF EXPR = "{ l.settings:image_size EQ 'B' }">
		<MvASSIGN NAME = "l.settings:image_width"		VALUE = "{ l.fields:image_width }">
		<MvASSIGN NAME = "l.settings:image_height"		VALUE = "{ l.fields:image_height }">
	</MvIF>

	<MvASSIGN NAME = "l.settings:thumb_active"			VALUE = "{ l.fields:thumb_active }">

	<MvIF EXPR = "{ l.settings:thumb_active }">
		<MvASSIGN NAME = "l.settings:thumb_width"		VALUE = "{ l.fields:thumb_width }">
		<MvASSIGN NAME = "l.settings:thumb_height"		VALUE = "{ l.fields:thumb_height }">
	</MvIF>

	<MvASSIGN NAME = "l.settings:closeup_active"		VALUE = "{ l.fields:closeup_active }">
	<MvASSIGN NAME = "l.settings:closeup_size"			VALUE = "{ l.fields:closeup_size }">

	<MvIF EXPR = "{ l.settings:closeup_active AND ( l.settings:closeup_size EQ 'B' ) }">
		<MvASSIGN NAME = "l.settings:closeup_width"		VALUE = "{ l.fields:closeup_width }">
		<MvASSIGN NAME = "l.settings:closeup_height"	VALUE = "{ l.fields:closeup_height }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.head_managedtemplate, '', l.fields:head:template_code, l.settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'head:template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update_Requires_Version" PARAMETERS = "module var, page var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_PostUpdate" PARAMETERS = "module var, page var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "text,html,compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:head:template_filename, l.head_templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.load_fields }">
		<MvASSIGN NAME = "l.fields:enabled"							VALUE = "{ l.settings:enabled }">
		<MvASSIGN NAME = "l.fields:preload"							VALUE = "{ l.settings:preload }">
		<MvASSIGN NAME = "l.fields:main_image"						VALUE = "{ l.settings:main_image }">
		<MvASSIGN NAME = "l.fields:thumbnails"						VALUE = "{ l.settings:thumbnails }">
		<MvASSIGN NAME = "l.fields:closeup_div"						VALUE = "{ l.settings:closeup_div }">
		<MvASSIGN NAME = "l.fields:closeup_image"					VALUE = "{ l.settings:closeup_image }">
		<MvASSIGN NAME = "l.fields:closeup_close"					VALUE = "{ l.settings:closeup_close }">
		<MvASSIGN NAME = "l.fields:image_size"						VALUE = "{ l.settings:image_size }">
		<MvASSIGN NAME = "l.fields:image_width"						VALUE = "{ l.settings:image_width }">
		<MvASSIGN NAME = "l.fields:image_height"					VALUE = "{ l.settings:image_height }">
		<MvASSIGN NAME = "l.fields:thumb_active"					VALUE = "{ l.settings:thumb_active }">
		<MvASSIGN NAME = "l.fields:thumb_width"						VALUE = "{ l.settings:thumb_width }">
		<MvASSIGN NAME = "l.fields:thumb_height"					VALUE = "{ l.settings:thumb_height }">
		<MvASSIGN NAME = "l.fields:closeup_active"					VALUE = "{ l.settings:closeup_active }">
		<MvASSIGN NAME = "l.fields:closeup_size"					VALUE = "{ l.settings:closeup_size }">
		<MvASSIGN NAME = "l.fields:closeup_width"					VALUE = "{ l.settings:closeup_width }">
		<MvASSIGN NAME = "l.fields:closeup_height"					VALUE = "{ l.settings:closeup_height }">
		<MvASSIGN NAME = "l.fields:legacy_image"					VALUE = "{ l.settings:legacy_image }">
		<MvASSIGN NAME = "l.fields:head" 							VALUE = "{ l.settings:head }">
		<MvASSIGN NAME = "l.fields:head:template_code" 				VALUE = "{ l.head_templateversion:source }">
		<MvASSIGN NAME = "l.fields:head:template_filename"			VALUE = "{ l.settings:head:template_filename }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Admin ].Tab_Visible( l.tab, l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'head:template_filename' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<tr>
			<td align="left" nowrap>&nbsp;</td>
			<td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:enabled, l.field_prefix $ 'enabled', 'Yes', 'Enabled' ) }">
			</td>
		</tr>

		<tr>
			<td align="left" nowrap>&nbsp;</td>
			<td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:preload, l.field_prefix $ 'preload', 'Yes', 'Preload Images' ) }">
			</td>
		</tr>

		<tr>
			<td valign="top" nowrap>Available Tokens:</td>
			<td nowrap>
				<table border="0" cellspacing="0" cellpadding="2">
				<tr><td nowrap>
					<b>%product_id%</b>
				</td><td nowrap>
					The product's numeric ID
				</td></tr>
				</table>
			</td>
		</tr>

		<tr>
			<td align="left" nowrap>Image Element:</td>
			<td align="left" width="100%">
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'main_image'  }" value= "{ encodeentities( l.fields:main_image ) }" />
			</td>
		</tr>
		<tr>
			<td align="left" nowrap>Thumbnail Container:</td>
			<td align="left" width="100%">
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'thumbnails'  }" value= "{ encodeentities( l.fields:thumbnails ) }" />
			</td>
		</tr>
		<tr>
			<td align="left" nowrap>Closeup Container:</td>
			<td align="left" width="100%">
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'closeup_div'  }" value= "{ encodeentities( l.fields:closeup_div ) }" />
			</td>
		</tr>
		<tr>
			<td align="left" nowrap>Closeup Image Element:</td>
			<td align="left" width="100%">
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'closeup_image'  }" value= "{ encodeentities( l.fields:closeup_image ) }" />
			</td>
		</tr>
		<tr>
			<td align="left" nowrap>Closeup Close Element:</td>
			<td align="left" width="100%">
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'closeup_close'  }" value= "{ encodeentities( l.fields:closeup_close ) }" />
			</td>
		</tr>
		<tr>
			<td colspan="2">&nbsp;</td>
		</tr>
		<tr>
			<td align="left" valign="top">Head Template:</td>
			<td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( l.field_prefix $ 'head:template_code', l.fields:head:template_code, 16, 100, l.head_templateversion:templ_id ) }">
			</td>
		</tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'enabled,defer,preload,main_image,thumbnails,closeup_div,closeup_image,closeup_close,head:template_code,head:template_filename' ) }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Admin ].Tab_Visible( l.tab, l.item $ '_dimensions' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item $ '_dimensions' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%"><tr>
			<td align="left" valign="top" nowrap><b>Main Image:</b></td>
			<td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'image_size', 'N', l.fields:image_size, 'No constraints' ) }"><br>
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'image_size', 'B', l.fields:image_size, 'Resize to fit within bounding box:' ) }">
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'image_width' }" value="{ int( l.fields:image_width ) }" style="text-align:right;" size="5" /> x
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'image_height' }" value="{ int( l.fields:image_height ) }" style="text-align:right;" size="5" /> pixels
			</td>
		</tr>
		<tr>
			<td align="left" valign="top" nowrap>Closeup:</td>
			<td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:closeup_active, l.field_prefix $ 'closeup_active', 'Yes', 'Enable click to closeup' ) }"><br />
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'closeup_size', 'N', l.fields:closeup_size, 'No constraints' ) }"><br>
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'closeup_size', 'B', l.fields:closeup_size, 'Resize to fit within bounding box:' ) }">
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'closeup_width' }" value="{ int( l.fields:closeup_width ) }" style="text-align:right;" size="5" /> x
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'closeup_height' }" value="{ int( l.fields:closeup_height ) }" style="text-align:right;" size="5" /> pixels
			</td>
		</tr>
		<tr>
			<td valign="top" valign="top">Thumbnails:</td>
			<td>
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:thumb_active, l.field_prefix $ 'thumb_active', 'Yes', 'Enable additional thumbnails:' ) }">
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'thumb_width' }" value="{ int( l.fields:thumb_width ) }" style="text-align:right;" size="5" /> x
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'thumb_height' }" value="{ int( l.fields:thumb_height ) }" style="text-align:right;" size="5" /> pixels
			</td>
		</tr>
		<tr>
			<td valign="top" valign="top" nowrap>Legacy Fallback Image:</td>
			<td>
				<select name="{ encodeentities( l.field_prefix ) $ 'legacy_image' }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'N', l.fields:legacy_image, 'None' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'T', l.fields:legacy_image, 'Thumbnail' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'F', l.fields:legacy_image, 'Full-Sized' ) }">
				</select>
			</td>
		</tr></table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'image_size,image_width,image_height,closeup_active,closeup_size,closeup_width,closeup_height,thumb_active,thumb_width,thumb_height,legacy_image' ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:enabled"			VALUE = 0>
	<MvASSIGN NAME = "l.settings:preload"			VALUE = 0>
	<MvASSIGN NAME = "l.settings:image_size"		VALUE = "N">
	<MvASSIGN NAME = "l.settings:image_width"		VALUE = 100>
	<MvASSIGN NAME = "l.settings:image_height"		VALUE = 100>
	<MvASSIGN NAME = "l.settings:thumb_active"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:thumb_width"		VALUE = 42>
	<MvASSIGN NAME = "l.settings:thumb_height"		VALUE = 42>
	<MvASSIGN NAME = "l.settings:closeup_active"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:closeup_size"		VALUE = "N">
	<MvASSIGN NAME = "l.settings:closeup_width"		VALUE = 500>
	<MvASSIGN NAME = "l.settings:closeup_height"	VALUE = 500>
	<MvASSIGN NAME = "l.settings:legacy_image"		VALUE = "N">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ Generate_Head_Code( l.item, l.settings, l.template_source ) }">

	<MvASSIGN NAME = "l.settings:head:template_filename"	VALUE = "{ tolower( l.page:code ) $ '-' $ l.item $ '-head.mvc' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.template_source, l.settings, l.settings:head:template_filename ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:head:template_filename ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:head:template_filename, l.source_templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:head:template_filename, l.dest_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.dest_settings"							VALUE = "{ l.source_settings }">
	<MvASSIGN NAME = "l.dest_settings:head:template_filename"	VALUE = "{ l.dest_managedtemplate:version:settings:head:template_filename }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_managedtemplate, '', l.source_templateversion:source, l.dest_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.source_branch:id EQ l.dest_branch:id }">	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-MV-CMP-IMCHN-00005', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' on branch \'' $ l.dest_branch:name $ '\'' ) }">
	<MvELSE>													<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-MV-CMP-IMCHN-00006', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' from branch \'' $ l.source_branch:name $ '\' to branch \'' $ l.dest_branch:name $ '\'' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT l.settings:enabled }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ l.param EQ 'head' }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_JSON_Variables( l.all_settings ) }">

		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside( l.all_settings, 'ajax.js' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside( l.all_settings, 'runtime.js' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside( l.all_settings, 'MivaEvents.js' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside( l.all_settings, 'ImageMachine.js' ) }">

		<MvCAPTURE VARIABLE = "l.head_content">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:head:template_filename, l.all_settings ) }">
		</MvCAPTURE>

		<MvIF EXPR = "{ ( '<script' CIN l.head_content ) OR
						( '<style' CIN l.head_content ) }">
			<MvEVAL EXPR = "{ l.head_content }">
		<MvELSE>
			<MIVA STANDARDOUTPUTLEVEL = "text, html"><script type="text/javascript"><MvEVAL EXPR = "{ l.head_content }"></script><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
	<MvELSEIF EXPR = "{ l.param EQ 'head_deferred' }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_JSON_Variables( l.all_settings ) }">

		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside_Deferred( l.all_settings, 'ajax.js' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside_Deferred( l.all_settings, 'runtime.js' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside_Deferred( l.all_settings, 'MivaEvents.js' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside_Deferred( l.all_settings, 'ImageMachine.js' ) }">

		<MIVA STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<script type="text/javascript">
			(function( obj, eventType, fn )
			{
				if ( obj.addEventListener )
				{
					obj.addEventListener( eventType, fn, false );
				}
				else if ( obj.attachEvent )
				{
					obj.attachEvent( 'on' + eventType, fn );
				}
			})( window, 'imagemachine_override', function()
			{
				<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:head:template_filename, l.all_settings ) }">
			} );
		</script>
		<MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSEIF EXPR = "{ ( l.param EQ 'body' ) OR ( ( 'body:' IN l.param ) EQ 1 ) OR
						( l.param EQ 'body_deferred' ) OR ( ( 'body_deferred:' IN l.param ) EQ 1 ) }">
		<MvCOMMENT>
		|
		| This code mirrors the product load code in the attribute components so that the attribute machine may also be used multiple times on the same page
		|
		</MvCOMMENT>

		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Component_Prerender( l.all_settings, 'attributemachine', l.param ) }">

		<MvIF EXPR = "{ ( l.param EQ 'body' ) OR ( l.param EQ 'body_deferred' ) }">
			<MvREFERENCE NAME = "l.product" VARIABLE = "l.all_settings:product">
		<MvELSE>
			<MvASSIGN NAME = "l.colon_pos"		VALUE = "{ ':' IN l.param }">
			<MvASSIGN NAME = "l.param_len"		VALUE = "{ len_var( l.param ) }">

			<MvIF EXPR = "{ ( ':id' IN l.param ) EQ ( l.param_len - 2 ) }">
				<MvREFERENCEARRAY NAME = "l.product" VARIABLE = "l.all_settings">
					<MvMEMBER NAME = "{ substring_var( l.param, l.colon_pos + 1, l.param_len - 3 - l.colon_pos ) }">
				</MvREFERENCEARRAY>
			<MvELSE>
				<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_ID( miva_variable_value( 'l.all_settings:' $ substring_var( l.param, l.colon_pos + 1, l.param_len - l.colon_pos ) ), l.product ) }">
					<MvFUNCTIONRETURN>
				</MvIF>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.settings:legacy_image EQ 'F' }">
			<MvASSIGN NAME = "l.legacy_uri"			VALUE = "{ l.product:image }">
		<MvELSEIF EXPR = "{ l.settings:legacy_image EQ 'T' }">
			<MvASSIGN NAME = "l.legacy_uri"			VALUE = "{ l.product:thumbnail }">
		</MvIF>

		<MvASSIGN NAME = "l.main_image"				VALUE = "{ glosub( l.settings:main_image,		'%product_id%', l.product:id ) }">
		<MvASSIGN NAME = "l.thumbnails"				VALUE = "{ glosub( l.settings:thumbnails,		'%product_id%', l.product:id ) }">
		<MvASSIGN NAME = "l.closeup_div"			VALUE = "{ glosub( l.settings:closeup_div,		'%product_id%', l.product:id ) }">
		<MvASSIGN NAME = "l.closeup_image"			VALUE = "{ glosub( l.settings:closeup_image,	'%product_id%', l.product:id ) }">
		<MvASSIGN NAME = "l.closeup_close"			VALUE = "{ glosub( l.settings:closeup_close,	'%product_id%', l.product:id ) }">

		<MvASSIGN NAME = "l.image_sizes"			VALUE = "">
		<MvASSIGN NAME = "l.constraints"			VALUE = "">

		<MvIF EXPR = "{ l.settings:image_size EQ 'N' }">
			<MvASSIGN NAME = "l.image_sizes"		INDEX = 1					VALUE = "original">
		<MvELSEIF EXPR = "{ l.settings:image_size EQ 'B' }">
			<MvASSIGN NAME = "l.image_sizes"		INDEX = 1					VALUE = "{ l.settings:image_width $ 'x' $ l.settings:image_height }">

			<MvASSIGN NAME = "l.constraints"		INDEX = 1 MEMBER = "width"	VALUE = "{ l.settings:image_width }">
			<MvASSIGN NAME = "l.constraints"		INDEX = 1 MEMBER = "height"	VALUE = "{ l.settings:image_height }">
		</MvIF>

		<MvIF EXPR = "{ l.settings:thumb_active }">
			<MvASSIGN NAME = "l.image_sizes"		INDEX = 2					VALUE = "{ l.settings:thumb_width $ 'x' $ l.settings:thumb_height $ ':MultipleOnly' }">
			<MvASSIGN NAME = "l.constraints"		INDEX = 2 MEMBER = "width"	VALUE = "{ l.settings:thumb_width }">
			<MvASSIGN NAME = "l.constraints"		INDEX = 2 MEMBER = "height"	VALUE = "{ l.settings:thumb_height }">
		</MvIF>

		<MvIF EXPR = "{ l.settings:closeup_active }">
			<MvIF EXPR = "{ l.settings:closeup_size EQ 'N' }">
				<MvASSIGN NAME = "l.image_sizes"	INDEX = 3					VALUE = "original">
			<MvELSEIF EXPR = "{ l.settings:closeup_size EQ 'B' }">
				<MvASSIGN NAME = "l.image_sizes"	INDEX = 3					VALUE = "{ l.settings:closeup_width $ 'x' $ l.settings:closeup_height }">
				<MvASSIGN NAME = "l.constraints"	INDEX = 3 MEMBER = "width"	VALUE = "{ l.settings:closeup_width }">
				<MvASSIGN NAME = "l.constraints"	INDEX = 3 MEMBER = "height"	VALUE = "{ l.settings:closeup_height }">
			</MvIF>
		</MvIF>

		<MvASSIGN NAME = "l.imagesize_count"		VALUE = "{ miva_array_collapse( l.image_sizes ) }">
		<MvASSIGN NAME = "l.constraint_count"		VALUE = "{ miva_array_collapse( l.constraints ) }">

		<MvASSIGN NAME = "l.productimagedata_count"	VALUE = "{ [ g.Module_Library_DB ].ProductImageDataList_Load_Product_Variant_Constrained( l.product:id, l.all_settings:attributemachine:variant_id, l.constraints, l.constraint_count, l.productimagedatalist ) }">

		<MvCAPTURE VARIABLE = "l.initialization_js" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
			var image_data<MvEVAL EXPR = "{ int( l.product:id ) }"> = <MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Runtime_ProductImageDataList_Output( l.productimagedatalist, l.productimagedata_count, l.image_sizes, l.imagesize_count ) }">
			var im<MvEVAL EXPR = "{ int( l.product:id ) }"> = new ImageMachine( <MvEVAL EXPR = "{ '\'' $ [ g.Module_Admin ].JavaScriptEncode( l.product:code ) $ '\', ' $ int( l.all_settings:attributemachine:variant_id ) $ ', \'' $ [ g.Module_Admin ].JavaScriptEncode( l.main_image ) $ '\', \'' $ [ g.Module_Admin ].JavaScriptEncode( l.thumbnails ) $ '\', \'' $ [ g.Module_Admin ].JavaScriptEncode( l.closeup_div ) $ '\', \'' $ [ g.Module_Admin ].JavaScriptEncode( l.closeup_image ) $ '\', \'' $ [ g.Module_Admin ].JavaScriptEncode( l.closeup_close ) $ '\', \'\', \'' $ [ g.Module_Admin ].JavaScriptEncode( l.settings:image_size ) $ '\', ' $ int( l.settings:image_width ) $ ', ' $ int( l.settings:image_height ) $ ', ' $ int( l.settings:closeup_active ) $ ', \'' $ [ g.Module_Admin ].JavaScriptEncode( l.settings:closeup_size ) $ '\', ' $ int( l.settings:closeup_width ) $ ', ' $ int( l.settings:closeup_height ) $ ', ' $ int( l.settings:thumb_active ) $ ', ' $ int( l.settings:thumb_width ) $ ', ' $ int( l.settings:thumb_height ) $ ', ' $ int( l.settings:preload ) $ ', \'' $ [ g.Module_Admin ].JavaScriptEncode( l.legacy_uri ) $ '\'' }"> );
			im<MvEVAL EXPR = "{ int( l.product:id ) }">.oninitialize( image_data<MvEVAL EXPR = "{ int( l.product:id ) }"> );
		</MvCAPTURE>

		<MvIF EXPR = "{ ( 'body_deferred' IN l.param ) NE 1 }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html"><script type="text/javascript"><MvEVAL EXPR = "{ l.initialization_js }"></script><MIVA STANDARDOUTPUTLEVEL = "">
		<MvELSE>
			<MIVA STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
			<script type="text/javascript">
				(function( obj, eventType, fn )
				{
					if ( obj.addEventListener )
					{
						obj.addEventListener( eventType, fn, false );
					}
					else if ( obj.attachEvent )
					{
						obj.attachEvent( 'on' + eventType, fn );
					}
				})( window, 'imagemachine_initialize', function()
				{
					<MvEVAL EXPR = "{ l.initialization_js }">
				});
			</script>
			<MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Head_Code" PARAMETERS = "item, settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code" STANDARDOUTPUTLEVEL = "text, html">ImageMachine.prototype.ImageMachine_Generate_Thumbnail = function( thumbnail_image, main_image, closeup_image, type_code )
{
	var thumbnail, span, img;

	thumbnail	= document.createElement( 'li' );
	span		= document.createElement( 'span' ); // to vertically center the thumbnail images

	thumbnail.appendChild( span );

	if ( typeof( thumbnail_image ) == 'string' && thumbnail_image.length > 0 )
	{
		img		= document.createElement( 'img' );
		img.src	= thumbnail_image;

		thumbnail.appendChild( img );
	}

	return thumbnail;
}
</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.working_settings"		VALUE = "{ l.settings }">
	<MvASSIGN NAME = "l.constrain_main_image"	VALUE = "{ l.settings:image_size EQ 'B' }">
	<MvASSIGN NAME = "l.constrain_closeup"		VALUE = "{ l.settings:closeup_size EQ 'B' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'Enabled',				l.working_settings:enabled ) 		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'PreloadImages',		l.working_settings:preload )		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml,	'ImageElement',			l.working_settings:main_image ) 	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml,	'ThumbnailContainer',	l.working_settings:thumbnails ) 	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml,	'CloseupContainer',		l.working_settings:closeup_div )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml,	'CloseupImageElement',	l.working_settings:closeup_image )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml,	'CloseupCloseElement',	l.working_settings:closeup_close )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'ConstrainMainImage',	l.constrain_main_image )			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'o', l.provide_xml,	'MainImageWidth',		l.working_settings:image_width )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'o', l.provide_xml,	'MainImageHeight',		l.working_settings:image_height )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'CloseupActive',		l.working_settings:closeup_active )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'ConstrainCloseup',		l.constrain_closeup )				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'o', l.provide_xml,	'CloseupWidth',			l.working_settings:closeup_width )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'o', l.provide_xml,	'CloseupHeight',		l.working_settings:closeup_height )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'ThumbnailActive',		l.working_settings:thumb_active )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'o', l.provide_xml,	'ThumbnailWidth',		l.working_settings:thumb_width )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'o', l.provide_xml,	'ThumbnailHeight',		l.working_settings:thumb_height )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml, 'Template',				l.template )						OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml, 'Notes',				l.notes )							OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml,	'LegacyImage',			l.working_settings:legacy_image, 'Thumbnail,None,Full-Sized', 'T,N,F' ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ l.constrain_main_image }">	<MvASSIGN NAME = "l.working_settings:image_size"	VALUE = "B">
	<MvELSE>									<MvASSIGN NAME = "l.working_settings:image_size"	VALUE = "N">
	</MvIF>

	<MvIF EXPR = "{ l.constrain_closeup }">		<MvASSIGN NAME = "l.working_settings:closeup_size"	VALUE = "B">
	<MvELSE>									<MvASSIGN NAME = "l.working_settings:closeup_size"	VALUE = "N">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Filename( l.settings:head:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
		<MvASSIGN NAME = "l.template" VALUE = "{ l.managedtemplate:version:source }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion_LowLevel( l.managedtemplate, l.managedtemplate:version, l.notes, l.template, l.working_settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings" VALUE = "{ l.working_settings }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ 'Exports Image Machine settings from pages: ' $ [ g.Module_Feature_TUI_DB ].Item_Load_PageList( l.item ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvCAPTURE VARIABLE = "l.output">
		<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item_Runtime( l.item:id, l.pages ) }">
			<MvREFERENCEARRAY NAME = "l.settings" VARIABLE = "l.page">
				<MvMEMBER NAME = "settings">
				<MvMEMBER NAME = "{ l.item:code }">
			</MvREFERENCEARRAY>

			<Page_Update code="{ encodeentities( l.page:code ) }">
				<Item code="{ encodeentities( l.item:code ) }">
					<Enabled><MvIF EXPR = "{ l.settings:enabled }">Yes<MvELSE>No</MvIF></Enabled>
					<PreloadImages><MvIF EXPR = "{ l.settings:preload }">Yes<MvELSE>No</MvIF></PreloadImages>
					<ImageElement><MvEVAL EXPR = "{ miva_cdata_encode( l.settings:main_image ) }"></ImageElement>
					<ThumbnailContainer><MvEVAL EXPR = "{ miva_cdata_encode( l.settings:thumbnails ) }"></ThumbnailContainer>
					<CloseupContainer><MvEVAL EXPR = "{ miva_cdata_encode( l.settings:closeup_div ) }"></CloseupContainer>
					<CloseupImageElement><MvEVAL EXPR = "{ miva_cdata_encode( l.settings:closeup_image ) }"></CloseupImageElement>
					<CloseupCloseElement><MvEVAL EXPR = "{ miva_cdata_encode( l.settings:closeup_close ) }"></CloseupCloseElement>
					<ConstrainMainImage><MvIF EXPR = "{ l.settings:image_size EQ 'B' }">Yes<MvELSE>No</MvIF></ConstrainMainImage>
					<MainImageWidth><MvEVAL EXPR = "{ int( l.settings:image_width ) }"></MainImageWidth>
					<MainImageHeight><MvEVAL EXPR = "{ int( l.settings:image_height ) }"></MainImageHeight>
					<CloseupActive><MvIF EXPR = "{ l.settings:closeup_active }">Yes<MvELSE>No</MvIF></CloseupActive>
					<ConstrainCloseup><MvIF EXPR = "{ l.settings:closeup_size EQ 'B' }">Yes<MvELSE>No</MvIF></ConstrainCloseup>
					<CloseupWidth><MvEVAL EXPR = "{ int( l.settings:closeup_width ) }"></CloseupWidth>
					<CloseupHeight><MvEVAL EXPR = "{ int( l.settings:closeup_height ) }"></CloseupHeight>
					<ThumbnailActive><MvIF EXPR = "{ l.settings:thumb_active }">Yes<MvELSE>No</MvIF></ThumbnailActive>
					<ThumbnailWidth><MvEVAL EXPR = "{ int( l.settings:thumb_width ) }"></ThumbnailWidth>
					<ThumbnailHeight><MvEVAL EXPR = "{ int( l.settings:thumb_height ) }"></ThumbnailHeight>

					<MvIF EXPR = "{ l.settings:legacy_image EQ 'T' }">
						<LegacyImage>Thumbnail</LegacyImage>
					<MvELSEIF EXPR = "{ l.settings:legacy_image EQ 'F' }">
						<LegacyImage>Full-Sized</LegacyImage>
					<MvELSE>
						<LegacyImage>None</LegacyImage>
					</MvIF>

					<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:head:template_filename, l.templateversion ) }">
						<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.templateversion:source ) }"></Template>
					</MvIF>

					<Notes>#Set_Current_Time#</Notes>
				</Item>
			</Page_Update>
		</MvFOREACH>
	</MvCAPTURE>
</MvFUNCTION>

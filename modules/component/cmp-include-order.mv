<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CMP-INC-ORDR-
| Next Error Code: 6    
|
</MvCOMMENT>

<MvCOMMENT>
|
| Feature: component
|
</MvCOMMENT>

<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ OrderComponentModule_Tabs( l.module, l.item, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "OrderComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.tabs"		VALUE = "{ 'GT_PAGE/' $ l.item $ ':Order Contents' }">

	<MvIF EXPR = "{ miva_array_elements( l.settings:imagetypes ) GT 0 }">
		<MvASSIGN NAME = "l.tabs"	VALUE = "{ l.tabs $ ',GT_PAGE/' $ l.item $ '_dimensions:Order Contents Image Dimensions' }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.tabs }">
</MvFUNCTION>

<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ OrderComponentModule_Validate( l.module, l.item, l.field_prefix, l.fields, l.settings) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "OrderComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.fields:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.fields:groups"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:groups ) }">
	<MvASSIGN NAME = "l.fields:lineitemprice"		VALUE = "{ trim( l.fields:lineitemprice ) }">
	<MvASSIGN NAME = "l.fields:lineitemdiscounts"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:lineitemdiscounts ) }">
	<MvASSIGN NAME = "l.fields:lineitemcoupons"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:lineitemcoupons ) }">
<MvIFDEF NAME = "ORDER_COMPONENT_PAYMENTS">
	<MvASSIGN NAME = "l.fields:payments"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:payments ) }">
</MvIFDEF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:b_width ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item $ '_dimensions', l.field_prefix $ 'b_width', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:b_height ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item $ '_dimensions', l.field_prefix $ 'b_height', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ l.fields:advanced }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.fields:template_code ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ OrderComponentModule_Update( l.module, l.item, l.field_prefix, l.fields, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "OrderComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.settings:advanced"				VALUE = "{ l.fields:advanced }">

	<MvASSIGN NAME = "l.null"							VALUE = "{ [ g.Module_Library_Utilities ].CustomFieldSelect_Selected( l.fields:fields_custom,		l.settings:fields_custom ) }">
	<MvASSIGN NAME = "l.null"							VALUE = "{ [ g.Module_Library_Utilities ].CustomFieldSelect_Selected( l.fields:fields_custom_order, l.settings:fields_custom_order ) }">

	<MvASSIGN NAME = "l.settings:constrain"				VALUE = "{ l.fields:constrain }">
	<MvASSIGN NAME = "l.settings:b_width"				VALUE = "{ l.fields:b_width }">
	<MvASSIGN NAME = "l.settings:b_height"				VALUE = "{ l.fields:b_height }">

<MvIFDEF NAME = "ORDER_COMPONENT_PAYMENTS">
	<MvASSIGN NAME = "l.settings:payments"				VALUE = "{ l.fields:payments }">
</MvIFDEF>

	<MvIF EXPR = "{ l.fields:advanced }">
		<MvASSIGN NAME = "l.imagetype_count"			VALUE = "{ [ g.Module_Library_Utilities ].ImageTypeSelect_Selected( l.fields:imagetypes, l.settings:imagetypes ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:imagetypes"		VALUE = "">
		<MvIF EXPR = "{ NOT ISNULL l.fields:image }">
			<MvIF EXPR = "{ [ g.Module_Library_DB ].ImageType_Load_Code( l.fields:image, l.imagetype ) }">
				<MvASSIGNARRAY NAME = "l.settings"		VALUE = "{ l.imagetype:code }">
					<MvMEMBER NAME = "imagetypes">
					<MvDIMENSION INDEX = 1>
				</MvASSIGNARRAY>
			</MvIF>
		</MvIF>

		<MvASSIGN NAME = "l.settings:groups"			VALUE = "{ l.fields:groups }">
		<MvASSIGN NAME = "l.settings:lineitemprice"		VALUE = "{ l.fields:lineitemprice }">
		<MvASSIGN NAME = "l.settings:lineitemdiscounts"	VALUE = "{ l.fields:lineitemdiscounts }">
		<MvASSIGN NAME = "l.settings:lineitemcoupons"	VALUE = "{ l.fields:lineitemcoupons }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CMP-INC-ORDR-00001', l.module:name $ ' \'' $ l.item $ '\' settings updated' ) }">

	<MvIF EXPR = "{ NOT l.fields:advanced }">
		<MvEVAL EXPR = "{ OrderComponentModule_Generate_Code( l.settings, l.fields:template_code ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, '', l.fields:template_code, l.settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Update_Requires_Version" PARAMETERS = "module var, page var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>
</MvIFNDEF>

<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_PostUpdate" PARAMETERS = "module var, page var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>
</MvIFNDEF>

<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ OrderComponentModule_Content( l.module, l.item, l.tab, l.load_fields, l.field_prefix, l.fields, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "OrderComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvCOMMENT>
	|
	| Load all image types
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.imagetype_count"	VALUE = "{ [ g.Module_Library_DB ].ImageTypeList_Load_All( l.imagetypes ) }">

	<MvIF EXPR = "{ l.load_fields }">
		<MvIF EXPR = "{ ISNULL l.settings:constrain }">
			<MvASSIGN NAME = "l.fields:constrain"				VALUE = "B">
		<MvELSE>
			<MvASSIGN NAME = "l.fields:constrain"				VALUE = "{ l.settings:constrain }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:b_width }">
			<MvASSIGN NAME = "l.fields:b_width"					VALUE = 75>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:b_width"					VALUE = "{ l.settings:b_width }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:b_height }">
			<MvASSIGN NAME = "l.fields:b_height"				VALUE = 75>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:b_height"				VALUE = "{ l.settings:b_height }">
		</MvIF>

		<MvASSIGN NAME = "l.fields:groups"						VALUE = "{ l.settings:groups }">
		<MvASSIGN NAME = "l.fields:lineitemprice"				VALUE = "{ l.settings:lineitemprice }">
		<MvASSIGN NAME = "l.fields:lineitemdiscounts"			VALUE = "{ l.settings:lineitemdiscounts }">
		<MvASSIGN NAME = "l.fields:lineitemcoupons"				VALUE = "{ l.settings:lineitemcoupons }">
<MvIFDEF NAME = "ORDER_COMPONENT_PAYMENTS">
		<MvASSIGN NAME = "l.fields:payments"					VALUE = "{ l.settings:payments }">
</MvIFDEF>

		<MvASSIGN NAME = "l.fields:advanced"					VALUE = "{ ( ISNULL l.settings:advanced ) OR l.settings:advanced }">
		<MvASSIGN NAME = "l.fields:template_code"				VALUE = "{ l.templateversion:source }">
		<MvASSIGN NAME = "l.fields:template_filename"			VALUE = "{ l.settings:template_filename }">

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].InitializeCustomFieldSelect( l.settings:fields_custom,			l.fields:fields_custom ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].InitializeCustomFieldSelect( l.settings:fields_custom_order,	l.fields:fields_custom_order ) }">

		<MvASSIGN NAME = "l.fields:imagetypes"					VALUE = "{ l.settings:imagetypes }">
		<MvASSIGN NAME = "l.fields:image"						VALUE = "{ l.settings:imagetypes[ 1 ] }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'advanced,template_code,template_filename,fields_custom,fields_custom_order,imagetypes,image,groups,lineitemprice,lineitemdiscounts,lineitemcoupons' ) }">
<MvIFDEF NAME = "ORDER_COMPONENT_PAYMENTS">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'payments' ) }">
</MvIFDEF>
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_filename' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<MvIF EXPR = "{ NOT l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_code' ) }">
		<MvELSE>
			<tr><td colspan="2" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( l.field_prefix $ 'template_code', l.fields:template_code, 20, 100, l.templateversion:templ_id ) }">
			</td></tr>

			<tr><td colspan="2">
				&nbsp;
			</td></tr>
		</MvIF>

		<MvASSIGN NAME = "l.product_customfield_count"			VALUE = "{ [ g.Module_Library_Utilities ].ProductCustomFieldList_Load( l.product_customfields ) }">
		<MvASSIGN NAME = "l.order_customfield_count"			VALUE = "{ [ g.Module_Library_Utilities ].OrderCustomFieldList_Load( l.order_customfields ) }">

		<MvIF EXPR = "{ l.product_customfield_count }">
			<tr><td align="left" valign="top" nowrap>
				<b>Product Custom Fields:</b>
			</td><td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCustomFieldSelect( l.field_prefix $ 'fields_custom', l.fields:fields_custom, l.product_customfields, l.product_customfield_count ) }">
			</td></tr>
		</MvIF>

		<MvIF EXPR = "{ l.order_customfield_count }">
			<tr><td align="left" valign="top" nowrap>
				<b>Order Custom Fields:</b>
			</td><td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCustomFieldSelect( l.field_prefix $ 'fields_custom_order', l.fields:fields_custom_order, l.order_customfields, l.order_customfield_count ) }">
			</td></tr>
		</MvIF>

		<MvIF EXPR = "{ l.imagetype_count EQ 0 }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'image,imagetypes' ) }">
		<MvELSE>
			<MvIF EXPR = "{ l.fields:advanced }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'image' ) }">

				<tr><td align="left" valign="top" nowrap>
					<b>Product Images:</b>
				</td><td align="left" width="100%">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawImageTypeSelect( l.field_prefix $ 'imagetypes', l.fields:imagetypes, l.imagetypes, l.imagetype_count ) }">
				</td></tr>
			<MvELSE>
				<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'imagetypes' ) }">

				<tr><td align="left" valign="top" nowrap>
					<b>Image:</b>
				</td><td align="left" width="100%">
					<select name="{ encodeentities( l.field_prefix ) $ 'image' }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '', l.fields:image, 'None' ) }">

						<MvFOREACH ITERATOR = "l.imagetype" ARRAY = "l.imagetypes" COUNT = "{ l.imagetype_count }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( l.imagetype:code, l.fields:image, encodeentities( l.imagetype:descrip ) ) }">
						</MvFOREACH>
					</select>
				</td></tr>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'groups,lineitemdiscounts,lineitemprice,lineitemcoupons' ) }">

<MvIFDEF NAME = "ORDER_COMPONENT_PAYMENTS">
			<tr><td align="left" valign="top" nowrap>
				<b>Payments:</b>
			</td><td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:payments,			l.field_prefix $ 'payments',		1, 'Initialize List of Payment Transactions' ) }"><br />
			</td></tr>
</MvIFDEF>
		<MvELSE>
			<tr><td align="left" valign="top" nowrap>
				<b>Line Item Discounts:</b>
			</td><td align="left" width="100%">
				<select name="{ encodeentities( l.field_prefix ) $ 'groups' }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0', l.fields:groups, 'Display Discounted Line Items Separately' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '1', l.fields:groups, 'Group Discounted Line Items by Product/Attributes' ) }">
				</select>

				<br /><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:lineitemdiscounts, l.field_prefix $ 'lineitemdiscounts',	1, 'Display Line Item Discount Details' ) }">
			</td></tr>

			<tr><td align="left" valign="top" nowrap>
				<b>Additional Price Display:</b>
			</td><td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'lineitemprice', '',		l.fields:lineitemprice,	'None' ) }"><br />
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'lineitemprice', 'retail',	l.fields:lineitemprice, 'Retail Price' ) }"><br />
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'lineitemprice', 'base',	l.fields:lineitemprice, 'Base Price (includes any legacy price group discounts but does not reflect sales, coupons or discounts applied in the basket)' ) }"><br />
			</td></tr>

			<tr><td align="left" valign="top" nowrap>
				<b>Coupons:</b>
			</td><td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:lineitemcoupons,	l.field_prefix $ 'lineitemcoupons',	1, 'Display Line Items for Applied Coupons' ) }"><br />
			</td></tr>

<MvIFDEF NAME = "ORDER_COMPONENT_PAYMENTS">
			<tr><td align="left" valign="top" nowrap>
				<b>Payments:</b>
			</td><td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:payments,			l.field_prefix $ 'payments',		1, 'Display Summary of Payment Transactions' ) }"><br />
			</td></tr>
</MvIFDEF>
		</MvIF>

		<tr><td colspan="2">&nbsp;</td></tr>

		<tr><td>
			&nbsp;
		</td><td width="100%">
			<input type="hidden" name="{ encodeentities( l.field_prefix ) $ 'advanced' }" value="{ encodeentities( l.fields:advanced ) }">
			<MvIF EXPR = "{ l.fields:advanced }">
				<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 0 ); return false;' }">Point + Click Mode</a>
			<MvELSE>
				<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 1 ); return false;' }">Advanced Mode</a>
			</MvIF>
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, ( l.item $ '_dimensions' ) ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'constrain,b_width,b_height' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item $ '_dimensions' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<tr>
		<td align="left" valign="top" nowrap><b>Product Image:</b></td>
		<td align="left" width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'constrain', 'N', l.fields:constrain, 'No constraints' ) }"><br>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'constrain', 'B', l.fields:constrain, 'Resize to fit within bounding box:' ) }">
			<input type="text" name="{ encodeentities( l.field_prefix ) $ 'b_width' }" value="{ encodeentities( l.fields:b_width ) }" style="text-align:right;" size="5" /> x
			<input type="text" name="{ encodeentities( l.field_prefix ) $ 'b_height' }" value="{ encodeentities( l.fields:b_height ) }" style="text-align:right;" size="5" /> pixels
		</td>
		</tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>
</MvFUNCTION>

<MvIFDEF NAME = "ORDER_COMPONENT_DEFAULTS">
<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ OrderComponentModule_Defaults( l.module, l.item, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "OrderComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:advanced"				VALUE = 0>
	<MvASSIGN NAME = "l.settings:fields_custom"			VALUE = "">
	<MvASSIGN NAME = "l.settings:fields_custom_order"	VALUE = "">
	<MvASSIGN NAME = "l.settings:imagetypes"			VALUE = "">
	<MvASSIGN NAME = "l.settings:constrain"				VALUE = "B">
	<MvASSIGN NAME = "l.settings:b_width"				VALUE = "75">
	<MvASSIGN NAME = "l.settings:b_height"				VALUE = "75">
	<MvASSIGN NAME = "l.settings:groups"				VALUE = 1>
	<MvASSIGN NAME = "l.settings:lineitemprice"			VALUE = "base">
	<MvASSIGN NAME = "l.settings:lineitemdiscounts"		VALUE = 1>
	<MvASSIGN NAME = "l.settings:lineitemcoupons"		VALUE = 0>
<MvIFDEF NAME = "ORDER_COMPONENT_PAYMENTS">
	<MvASSIGN NAME = "l.settings:payments"				VALUE = 1>
</MvIFDEF>
</MvFUNCTION>
</MvIFDEF>

<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ OrderComponentModule_Page_Assign( l.module, l.page, l.item, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "OrderComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ OrderComponentModule_Generate_Code( l.settings, l.template_source ) }">

	<MvASSIGN NAME = "l.settings:template_filename"	VALUE = "{ tolower( l.page:code ) $ '-' $ tolower( l.item ) $ '.mvc' }">

	<MvIF EXPR = "{ l.page:admin }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Unsynced_ManagedTemplate_NoDuplicates( l.template_source, l.settings, l.settings:template_filename ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.template_source, l.settings, l.settings:template_filename ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ OrderComponentModule_Page_Unassign( l.module, l.page, l.item, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "OrderComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:template_filename ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ OrderComponentModule_Page_Copy( l.module, l.item, l.source_branch, l.source_page, l.source_settings, l.dest_branch, l.dest_page, l.dest_settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "OrderComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:template_filename, l.source_templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:template_filename, l.dest_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.dest_settings"						VALUE = "{ l.source_settings }">
	<MvASSIGN NAME = "l.dest_settings:template_filename"	VALUE = "{ l.dest_managedtemplate:version:settings:template_filename }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_managedtemplate, '', l.source_templateversion:source, l.dest_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.source_branch:id EQ l.dest_branch:id }">	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CMP-INC-ORDR-00004', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' on branch \'' $ l.dest_branch:name $ '\'' ) }">
	<MvELSE>													<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CMP-INC-ORDR-00005', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' from branch \'' $ l.source_branch:name $ '\' to branch \'' $ l.dest_branch:name $ '\'' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "ORDER_COMPONENT_NO_INITIALIZE">
<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ OrderComponentModule_Initialize( l.module, l.item, l.all_settings, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "OrderComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Initialize_Item( l.all_settings, 'order' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.total"			 	VALUE = 0.00>
	<MvASSIGN NAME = "l.orderitem_count"	VALUE = "{ [ g.Module_Library_DB ].OrderItemList_Load_Order( g.Order:id, l.all_settings:order:items ) }">
	<MvASSIGN NAME = "l.coupon_count"		VALUE = "{ [ g.Module_Feature_PGR_DB ].OrderCouponList_Load_Order( g.Order:id, l.all_settings:order:coupons ) }">
	<MvASSIGN NAME = "l.ordercharge_count"	VALUE = "{ [ g.Module_Library_DB ].OrderChargeList_Load_Order( g.Order:id, l.all_settings:order:charges ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_OrderItems_WithParts( l.all_settings:order:items, l.orderitem_count ) OR
					NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_OrderCharges( l.all_settings:order:charges, l.ordercharge_count ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_CustomField_Lookup( l.all_settings, l.settings:fields_custom ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Order_CustomField_Lookup( l.all_settings, l.settings:fields_custom_order ) OR
					NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Order_CustomFields( l.all_settings:order:id, l.all_settings:order, l.all_settings, l.settings:fields_custom_order ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.all_settings:order:have_custom_order_field_values" VALUE = 0>

	<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:fields_custom_order">
		<MvASSIGN NAME = "l.safe_module_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.customfield:module ) }">
		<MvASSIGN NAME = "l.safe_field_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.customfield:code ) }">

		<MvREFERENCEARRAY NAME = "l.value" VARIABLE = "l.all_settings:order:customfield_order_values">
			<MvMEMBER NAME = "{ l.safe_module_code }">
			<MvMEMBER NAME = "{ l.safe_field_code }">
		</MvREFERENCEARRAY>

		<MvIF EXPR = "{ NOT ISNULL trim( l.value ) }">
			<MvASSIGN NAME = "l.all_settings:order:have_custom_order_field_values" VALUE = 1>
			<MvFOREACHSTOP>
		</MvIF>
	</MvFOREACH>

	<MvFOREACH ITERATOR = "l.orderitem" ARRAY = "l.all_settings:order:items" COUNT = "{ l.orderitem_count }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_CustomFields( l.orderitem:product_id, l.orderitem, l.all_settings, l.settings:fields_custom ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_ImageTypes( l.orderitem:product_id, l.orderitem:variant_id, l.orderitem, l.all_settings, l.settings:imagetypes, l.settings:constrain, l.settings:b_width, l.settings:b_height ) }">

		<MvIF EXPR = "{ NOT l.orderitem:product:id }">	<MvASSIGN NAME = "l.orderitem:link" VALUE = "">
		<MvELSE>										<MvASSIGN NAME = "l.orderitem:link" VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Product_URL( l.orderitem:product, l.null ) }">
		</MvIF>

		<MvASSIGN NAME = "l.orderitem_total"							VALUE = "{ l.orderitem:subtotal }">

		<MvFOREACH ITERATOR = "l.orderoption" ARRAY = "l.orderitem:options" COUNT = "{ l.orderitem:option_count }">
			<MvASSIGN NAME = "l.orderitem_total"						VALUE = "{ l.orderitem_total + l.orderoption:subtotal }">
		</MvFOREACH>

		<MvASSIGN NAME = "l.total"										VALUE = "{ l.total + l.orderitem_total }">
		<MvASSIGN NAME = "l.orderitem:subtotal_comprehensive"			VALUE = "{ [ g.Module_Library_Utilities ].IEEE754_Normalize( 2, l.orderitem_total ) ROUND 2 }">
		<MvASSIGN NAME = "l.orderitem:formatted_subtotal_comprehensive"	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.orderitem:subtotal_comprehensive ) }">
	</MvFOREACH>

	<MvASSIGN NAME = "l.ordergroup_count"								VALUE = "{ [ g.Module_Feature_TUI_UT ].CommonComponentFields_Merge_BasketOrOrderItem_Groups( l.all_settings:order:items, l.orderitem_count, l.all_settings:order:groups ) }">

	<MvASSIGN NAME = "l.all_settings:order:subtotal" 					VALUE = "{ [ g.Module_Library_Utilities ].IEEE754_Normalize( 2, l.total ) ROUND 2 }">
	<MvASSIGN NAME = "l.all_settings:order:formatted_subtotal" 			VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.all_settings:order:subtotal ) }">

<MvIFDEF NAME = "ORDER_COMPONENT_PAYMENTS">
	<MvIF EXPR = "{ l.settings:payments }">
		<MvEVAL EXPR = "{ OrderComponent_Initialize_Payments( g.Order:id, l.all_settings:order:payments ) }">
	</MvIF>
</MvIFDEF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>
</MvIFNDEF>

<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ OrderComponentModule_Prerender( l.module, l.item, l.all_settings, l.settings, l.param ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "OrderComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ OrderComponentModule_Render_Head( l.module, l.item, l.all_settings, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "OrderComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ OrderComponentModule_Render_Start( l.module, l.item, l.all_settings, l.settings, l.param ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "OrderComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:template_filename, l.all_settings ) }">
</MvFUNCTION>

<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ OrderComponentModule_Render_End( l.module, l.item, l.all_settings, l.settings, l.param ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "OrderComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: component_prov
|
</MvCOMMENT>

<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "ComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ OrderComponentModule_Provision( l.module, l.provide_xml, l.settings ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "OrderComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Fields' ) }">
		<MvIF EXPR = "{ NOT OrderComponentModule_Provision_Fields( l.module, l.provide_xml:tags:Fields[ 1 ], l.settings ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'ImageTypes' ) }">
		<MvIF EXPR = "{ NOT OrderComponentModule_Provision_ImageTypes( l.module, l.provide_xml:tags:ImageTypes[ 1 ], l.settings ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to provision multiple image types' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'ImageDimensions' ) }">
		<MvASSIGN NAME = "l.dimensions" VALUE = "{ l.provide_xml:tags:ImageDimensions[ 1 ] }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Boolean( l.dimensions, 'constrain', l.constrain ) }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvIF EXPR = "{ NOT l.constrain }">
			<MvASSIGN NAME = "l.settings:constrain" VALUE = "N">
		<MvELSE>
			<MvASSIGN NAME = "l.settings:constrain" VALUE = "B">

			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer( 'R', l.dimensions, 'Width',	l.settings:b_width ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer( 'R', l.dimensions, 'Height',	l.settings:b_height ) }">
				<MvFUNCTIONRETURN>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml, 'LineItemDiscounts',				l.settings:groups,				'Grouped,Separate',	'1,0' )				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml, 'DisplayLineItemDiscountDetails',	l.settings:lineitemdiscounts )											OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml, 'AdditionalPriceDisplay',			l.settings:lineitemprice,		'Retail,Base,None',	'retail,base,' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml, 'DisplayCouponLineItems',			l.settings:lineitemcoupons ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIFDEF NAME = "ORDER_COMPONENT_PAYMENTS">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean( 'O', l.provide_xml, 'DisplayPayments', l.settings:payments ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	</MvIFDEF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'R', l.provide_xml, 'Template', l.template ) }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvASSIGN NAME = "l.settings:advanced"		VALUE = 1>
	<MvELSE>
		<MvASSIGN NAME = "l.settings:advanced"		VALUE = 0>

		<MvEVAL EXPR = "{ OrderComponentModule_Generate_Code( l.settings, l.template ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml, 'Notes', l.notes ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.template, l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderComponentModule_Provision_Fields" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:fields_custom"				VALUE = "">
	<MvASSIGN NAME = "l.settings:fields_custom_order"		VALUE = "">
	<MvASSIGN NAME = "l.product_customfield_count"			VALUE = 0>
	<MvASSIGN NAME = "l.order_customfield_count"			VALUE = 0>

	<MvASSIGN NAME = "l.child_pos"		VALUE = 1>
	<MvASSIGN NAME = "l.child_count"	VALUE = "{ miva_array_elements( l.provide_xml:children ) }">
	<MvWHILE EXPR = "{ l.child_pos LE l.child_count }">
		<MvASSIGN NAME = "l.name"		VALUE = "{ tolower( l.provide_xml:children[ l.child_pos ]:name ) }">

		<MvIF EXPR = "{ l.name EQ 'custom' }">
			<MvASSIGN NAME = "l.product_customfield_count"	VALUE = "{ l.product_customfield_count + 1 }">

			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml:children[ l.child_pos ], 'module',	l.settings:fields_custom[ l.product_customfield_count ]:module ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml:children[ l.child_pos ], 'code',		l.settings:fields_custom[ l.product_customfield_count ]:code ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		<MvELSEIF EXPR = "{ l.name EQ 'customorder' }">
			<MvASSIGN NAME = "l.order_customfield_count"	VALUE = "{ l.order_customfield_count + 1 }">

			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml:children[ l.child_pos ], 'module',	l.settings:fields_custom_order[ l.order_customfield_count ]:module ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml:children[ l.child_pos ], 'code',		l.settings:fields_custom_order[ l.order_customfield_count ]:code ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvASSIGN NAME = "l.child_pos"	VALUE = "{ l.child_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderComponentModule_Provision_ImageTypes" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:imagetypes" VALUE = "">

	<MvASSIGN NAME = "l.type_pos"		VALUE = 1>
	<MvASSIGN NAME = "l.type_found"		VALUE = 1>
	<MvASSIGN NAME = "l.type_count"		VALUE = "{ miva_array_elements( l.provide_xml:children ) }">

	<MvWHILE EXPR = "{ l.type_pos LE l.type_count }">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].ImageType_Load_Code( l.provide_xml:children[ l.type_pos ]:value, l.imagetype ) }">
			<MvASSIGN NAME = "l.settings:imagetypes" INDEX = "{ l.type_found }" VALUE = "{ l.provide_xml:children[ l.type_pos ]:value }">
			<MvASSIGN NAME = "l.type_found"										VALUE = "{ l.type_found + 1 }">
		</MvIF>

		<MvASSIGN NAME = "l.type_pos" VALUE = "{ l.type_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: skins
|
</MvCOMMENT>

<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ OrderSkinsComponentModule_Description( l.module, l.item ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "OrderSkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pages" VALUE = "{ [ g.Module_Feature_TUI_DB ].Item_Load_PageList( l.item ) }">

	<MvFUNCTIONRETURN VALUE = "{ 'Exports Invoice Order Contents template from pages: ' $ l.pages }">
</MvFUNCTION>

<MvIFNDEF NAME = "ORDER_COMPONENT_NOAPIFUNCTIONS">
<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ OrderSkinsComponentModule_Export_Item( l.module, l.item, l.output ) }">
</MvFUNCTION>
</MvIFNDEF>

<MvFUNCTION NAME = "OrderSkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item_Runtime( l.item:id, l.pages ) }">
			<MvREFERENCEARRAY NAME = "l.settings" VARIABLE = "l.page:settings">
				<MvMEMBER NAME = "{ l.item:code }">
			</MvREFERENCEARRAY>

			<Page_Update code="{ l.page:code }">
				<Item code="{ l.item:code }">
					<Fields>
						<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:fields_custom">
							<Custom module="{ l.customfield:module }" code="{ l.customfield:code }" />
						</MvFOREACH>

						<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:fields_custom_order">
							<CustomOrder module="{ l.customfield:module }" code="{ l.customfield:code }" />
						</MvFOREACH>
					</Fields>

					<ImageTypes>
						<MvFOREACH ITERATOR = "l.imagetype" ARRAY = "l.settings:imagetypes">
							<ImageType><MvEVAL EXPR = "{ miva_cdata_encode( l.imagetype ) }"></ImageType>
						</MvFOREACH>
					</ImageTypes>

					<MvIF EXPR = "{ l.settings:constrain NE 'B' }">
						<ImageDimensions constrain="No" />
					<MvELSE>
						<ImageDimensions constrain="Yes">
							<Width><MvEVAL EXPR = "{ int( l.settings:b_width ) }"></Width>
							<Height><MvEVAL EXPR = "{ int( l.settings:b_height ) }"></Height>
						</ImageDimensions>
					</MvIF>

					<MvIF EXPR = "{ l.settings:groups }">
						<LineItemDiscounts>Grouped</LineItemDiscounts>
					<MvELSE>
						<LineItemDiscounts>Separate</LineItemDiscounts>
					</MvIF>

					<DisplayLineItemDiscountDetails><MvIF EXPR = "{ l.settings:lineitemdiscounts }">Yes<MvELSE>No</MvIF></DisplayLineItemDiscountDetails>

					<MvIF EXPR = "{ l.settings:lineitemprice EQ 'retail' }">
						<AdditionalPriceDisplay>Retail</AdditionalPriceDisplay>
					<MvELSEIF EXPR = "{ l.settings:lineitemprice EQ 'base' }">
						<AdditionalPriceDisplay>Base</AdditionalPriceDisplay>
					<MvELSE>
						<AdditionalPriceDisplay>None</AdditionalPriceDisplay>
					</MvIF>

					<DisplayCouponLineItems><MvIF EXPR = "{ l.settings:lineitemcoupons }">Yes<MvELSE>No</MvIF></DisplayCouponLineItems>

					<MvIFDEF NAME = "ORDER_COMPONENT_PAYMENTS">
						<DisplayPayments><MvIF EXPR = "{ l.settings:payments }">Yes<MvELSE>No</MvIF></DisplayPayments>
					</MvIFDEF>

					<MvIF EXPR = "{ l.settings:advanced }">
						<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
							<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.templateversion:source ) }"></Template>
						</MvIF>
					</MvIF>

					<Notes>#Set_Current_Time#</Notes>
				</Item>
			</Page_Update>
		</MvFOREACH>
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderComponent_Initialize_Payments" PARAMETERS = "order_id, destination var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.orderpayment" ARRAY = "l.orderpayments" COUNT = "{ [ g.Module_Library_DB ].OrderPaymentList_Load_Order( l.order_id, l.orderpayments ) }">
		<MvIF EXPR = "{ l.orderpayment:pay_id LE 0 }">
			<MvFOREACHCONTINUE>
		<MvELSEIF EXPR = "{ ( l.orderpayment:type EQ 2 ) OR
							( l.orderpayment:type EQ 4 ) OR
							( l.orderpayment:type EQ 7 ) }">
			<MvCOMMENT>
			|
			| Ignore capture only and void transactions
			|
			</MvCOMMENT>

			<MvFOREACHCONTINUE>
		</MvIF>

		<MvREFERENCEARRAY NAME = "l.payment_module" VARIABLE = "g.Session:cache:cmp_include_order:modules">
			<MvDIMENSION INDEX = "{ l.orderpayment:pay_id }">
		</MvREFERENCEARRAY>

		<MvIF EXPR = "{ l.payment_module:loaded EQ 0 }">
			<MvIF EXPR = "{ [ g.Module_Library_DB ].Module_Load_ID( l.orderpayment:pay_id, l.payment_module ) }">
				<MvASSIGN NAME = "l.payment_module:loaded"		VALUE = 1>
			<MvELSE>
				<MvASSIGN NAME = "l.payment_module:loaded"		VALUE = -1>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.payment_module:loaded EQ -1 }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.payment:desc"						VALUE = "{ [ g.Module_Feature_TUI_UT ].CommonComponentFields_PaymentMethod_Description( l.payment_module, l.orderpayment:pay_data ) }">

		<MvASSIGN NAME = "l.payment:type"						VALUE = "{ l.orderpayment:type }">

		<MvASSIGN NAME = "l.payment:amount"						VALUE = "{ l.orderpayment:amount }">
		<MvASSIGN NAME = "l.payment:amount_negative"			VALUE = "{ 0 - l.orderpayment:amount }">

		<MvASSIGN NAME = "l.payment:formatted_amount"			VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.payment:amount ) }">
		<MvASSIGN NAME = "l.payment:formatted_amount_negative"	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.payment:amount_negative ) }">

		<MvASSIGN NAME = "l.null"								VALUE = "{ miva_array_insert( l.destination, l.payment, -1 ) }">
	</MvFOREACH>
</MvFUNCTION>

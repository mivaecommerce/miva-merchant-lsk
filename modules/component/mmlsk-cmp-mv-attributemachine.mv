<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-MV-CMP-AMCHN-
| Next Error Code: 5
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-mv-attributemachine">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Attribute Machine">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.1100">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "10.08">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, component_prov, skins">
</MvFUNCTION>

<MvCOMMENT>
|
| Component Feature (component)
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ 'GT_PAGE/' $ l.item $ ':Attribute Machine' }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.fields:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.fields:enabled"							VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:enabled ) }">
	<MvASSIGN NAME = "l.fields:displaydiscounts"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:displaydiscounts ) }">
	<MvASSIGN NAME = "l.fields:inv_element_id"					VALUE = "{ trim( l.fields:inv_element_id ) }">
	<MvASSIGN NAME = "l.fields:price_element_id"				VALUE = "{ trim( l.fields:price_element_id ) }">
	<MvASSIGN NAME = "l.fields:additional_price_element_id"		VALUE = "{ trim( l.fields:additional_price_element_id ) }">
	<MvASSIGN NAME = "l.fields:weight_element_id"				VALUE = "{ trim( l.fields:weight_element_id ) }">
	<MvASSIGN NAME = "l.fields:discount_element_id"				VALUE = "{ trim( l.fields:discount_element_id ) }">
	<MvASSIGN NAME = "l.fields:swatch_element_id"				VALUE = "{ trim( l.fields:swatch_element_id ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].Value_In_List( l.fields:preload, '0,1,2' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'preload', 'Please select a valid value for Initial Attribute State' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].Value_In_List( l.fields:dependency_resolution, 'first,cascade,last' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'dependency_resolution', 'Please select a valid value for Dependency Resolution' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].Value_In_List( l.fields:inv_short, '0,1' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'inv_short', 'Please select a valid value for Inventory Message' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].Value_In_List( l.fields:price, 'retail,base,sale' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'price', 'Please select a valid value for Displayed Price' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].Value_In_List( l.fields:additionalprice, ',retail,base' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'additionalprice', 'Please select a valid value for Additional Price Display' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.fields:template_code ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.settings:enabled"						VALUE = "{ l.fields:enabled }">
	<MvASSIGN NAME = "l.settings:preload"						VALUE = "{ l.fields:preload }">
	<MvASSIGN NAME = "l.settings:dependency_resolution"			VALUE = "{ l.fields:dependency_resolution }">
	<MvASSIGN NAME = "l.settings:inv_element_id"				VALUE = "{ l.fields:inv_element_id }">
	<MvASSIGN NAME = "l.settings:inv_short"						VALUE = "{ l.fields:inv_short }">
	<MvASSIGN NAME = "l.settings:price_element_id"				VALUE = "{ l.fields:price_element_id }">
	<MvASSIGN NAME = "l.settings:additional_price_element_id"	VALUE = "{ l.fields:additional_price_element_id }">
	<MvASSIGN NAME = "l.settings:weight_element_id"				VALUE = "{ l.fields:weight_element_id }">
	<MvASSIGN NAME = "l.settings:discount_element_id"			VALUE = "{ l.fields:discount_element_id }">
	<MvASSIGN NAME = "l.settings:swatch_element_id"				VALUE = "{ l.fields:swatch_element_id }">
	<MvASSIGN NAME = "l.settings:price"							VALUE = "{ l.fields:price }">
	<MvASSIGN NAME = "l.settings:additionalprice"				VALUE = "{ l.fields:additionalprice }">
	<MvASSIGN NAME = "l.settings:displaydiscounts"				VALUE = "{ l.fields:displaydiscounts }">
	<MvASSIGN NAME = "l.settings:invalid_msg"					VALUE = "{ l.fields:invalid_msg }">
	<MvASSIGN NAME = "l.settings:missing_text_msg"				VALUE = "{ l.fields:missing_text_msg }">
	<MvASSIGN NAME = "l.settings:missing_radio_msg"				VALUE = "{ l.fields:missing_radio_msg }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, '', l.fields:template_code, l.settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update_Requires_Version" PARAMETERS = "module var, page var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_PostUpdate" PARAMETERS = "module var, page var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "text,html,compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.load_fields }">
		<MvASSIGN NAME = "l.fields:enabled"						VALUE = "{ l.settings:enabled }">
		<MvASSIGN NAME = "l.fields:preload"						VALUE = "{ l.settings:preload }">
		<MvASSIGN NAME = "l.fields:dependency_resolution"		VALUE = "{ l.settings:dependency_resolution }">
		<MvASSIGN NAME = "l.fields:inv_element_id"				VALUE = "{ l.settings:inv_element_id }">
		<MvASSIGN NAME = "l.fields:inv_short"					VALUE = "{ l.settings:inv_short }">
		<MvASSIGN NAME = "l.fields:price_element_id"			VALUE = "{ l.settings:price_element_id }">
		<MvASSIGN NAME = "l.fields:additional_price_element_id"	VALUE = "{ l.settings:additional_price_element_id }">
		<MvASSIGN NAME = "l.fields:weight_element_id"			VALUE = "{ l.settings:weight_element_id }">
		<MvASSIGN NAME = "l.fields:discount_element_id"			VALUE = "{ l.settings:discount_element_id }">
		<MvASSIGN NAME = "l.fields:swatch_element_id"			VALUE = "{ l.settings:swatch_element_id }">

		<MvIF EXPR = "{ ISNULL l.settings:price }">
			<MvASSIGN NAME = "l.fields:price"					VALUE = "base">
		<MvELSE>
			<MvASSIGN NAME = "l.fields:price"					VALUE = "{ l.settings:price }">
		</MvIF>

		<MvASSIGN NAME = "l.fields:additionalprice"				VALUE = "{ l.settings:additionalprice }">
		<MvASSIGN NAME = "l.fields:displaydiscounts"			VALUE = "{ l.settings:displaydiscounts }">

		<MvASSIGN NAME = "l.fields:invalid_msg"					VALUE = "{ l.settings:invalid_msg }">
		<MvASSIGN NAME = "l.fields:missing_text_msg"			VALUE = "{ l.settings:missing_text_msg }">
		<MvASSIGN NAME = "l.fields:missing_radio_msg"			VALUE = "{ l.settings:missing_radio_msg }">
		<MvASSIGN NAME = "l.fields:template_code" 				VALUE = "{ l.templateversion:source }">
		<MvASSIGN NAME = "l.fields:template_filename"			VALUE = "{ l.settings:template_filename }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Admin ].Tab_Visible( l.tab, l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_filename' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<tr>
			<td align="left" nowrap>&nbsp;</td>
			<td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox_With_Label( l.fields:enabled, encodeentities( l.field_prefix ) $ 'enabled', '1', 'Enabled' ) }">
			</td>
		</tr>

		<tr>
			<td align="left" valign="top" nowrap>Initial Attribute State:</td>
			<td align="left" width="100%">
				<select name="{ encodeentities( l.field_prefix ) $ 'preload' }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '0', l.fields:preload, 'Do Not Preload (Reduces Initial Page Size)' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '1', l.fields:preload, 'Preload - Drop-Down Lists Do Not Contain Select One' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '2', l.fields:preload, 'Preload - Drop-Down Lists Contain Select One' ) }">
				</select>
			</td>
		</tr>

		<tr>
			<td align="left" nowrap>Dependency Resolution:</td>
			<td align="left" width="100%">
				<select name="{ encodeentities( l.field_prefix ) $ 'dependency_resolution' }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'first',	l.fields:dependency_resolution, 'First Selected Attribute Shows all Options' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'cascade',	l.fields:dependency_resolution, 'Cascade' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'last',	l.fields:dependency_resolution, 'Most Recently Selected Attribute Shows all Options' ) }">
				</select>
			</td>
		</tr>

		<tr><td colspan="2">&nbsp;</td></tr>

		<tr>
			<td align="left" nowrap>Inventory Element:</td>
			<td align="left" width="100%">
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'inv_element_id' }" value="{ encodeentities( l.fields:inv_element_id ) }" />
			</td>
		</tr>

		<tr>
			<td align="left" nowrap>Inventory Message:</td>
			<td align="left" width="100%">
				<select name="{ encodeentities( l.field_prefix ) $ 'inv_short' }" />
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 0, l.fields:inv_short, 'Short' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 1, l.fields:inv_short, 'Long' ) }">
				</select>
			</td>
		</tr>

		<tr><td colspan="2">&nbsp;</td></tr>

		<tr>
			<td align="left" nowrap>Price Element:</td>
			<td align="left" width="100%">
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'price_element_id' }" value="{ encodeentities( l.fields:price_element_id ) }" />
			</td>
		</tr>

		<tr>
			<td align="left" nowrap>Additional Price Element:</td>
			<td align="left" width="100%">
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'additional_price_element_id' }" value="{ encodeentities( l.fields:additional_price_element_id ) }" />
			</td>
		</tr>

		<tr>
			<td align="left" nowrap>Weight Element:</td>
			<td align="left" width="100%">
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'weight_element_id' }" value="{ encodeentities( l.fields:weight_element_id ) }" />
			</td>
		</tr>

		<tr>
			<td align="left" nowrap>Discount Element:</td>
			<td align="left" width="100%">
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'discount_element_id' }" value="{ encodeentities( l.fields:discount_element_id ) }" />
			</td>
		</tr>

		<tr><td align="left" nowrap valign="top">
			<b>Displayed Price:</b>
		</td><td align="left" nowrap width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'price', 'retail',	l.fields:price, 'Retail Price' ) }"><br />
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'price', 'base',	l.fields:price, 'Base Price (includes any legacy price group discounts but does not reflect sales, coupons or discounts applied in the basket)' ) }"><br />
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'price', 'sale',	l.fields:price, 'Sale Price (includes legacy price groups and predicted discounts)' ) }"><br />
		</td></tr>

		<tr><td align="left" nowrap valign="top">
			<b>Additional Price Display:</b>
		</td><td align="left" nowrap width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'additionalprice', '',			l.fields:additionalprice, 'None' ) }"><br />
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'additionalprice', 'retail',	l.fields:additionalprice, 'Retail Price' ) }"><br />
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'additionalprice', 'base',		l.fields:additionalprice, 'Base Price (includes any legacy price group discounts but does not reflect sales, coupons or discounts applied in the basket)' ) }"><br />
		</td></tr>

		<tr><td>
			&nbsp;
		</td><td width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:displaydiscounts, l.field_prefix $ 'displaydiscounts', 1, 'Display Predicted Discounts' ) }">
		</td></tr>

		<tr><td colspan="2">&nbsp;</td></tr>

		<tr>
			<td align="left" nowrap>Swatch Element:</td>
			<td align="left" width="100%">
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'swatch_element_id' }" value="{ encodeentities( l.fields:swatch_element_id ) }" />
			</td>
		</tr>

		<tr><td colspan="2">&nbsp;</td></tr>

		<tr>
			<td align="left" valign="top" nowrap>Invalid Attribute Combination Message:</td>
			<td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea( l.field_prefix $ 'invalid_msg', l.fields:invalid_msg, 10, 58 ) }">
			</td>
		</tr>

		<tr>
			<td align="left" valign="top" nowrap>Missing Text Field Value Message:</td>
			<td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea( l.field_prefix $ 'missing_text_msg', l.fields:missing_text_msg, 10, 58 ) }">
			</td>
		</tr>

		<tr>
			<td align="left" valign="top" nowrap>Missing Radio Selection Message:</td>
			<td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea( l.field_prefix $ 'missing_radio_msg', l.fields:missing_radio_msg, 10, 58 ) }">
			</td>
		</tr>

		<tr><td valign="top" nowrap>
			Available Tokens:
		</td><td nowrap>
			<table border="0" cellspacing="0" cellpadding="2">
			<tr><td nowrap>
				<b>%product_id%</b>
			</td><td nowrap>
				The product's numeric ID 
			</td></tr>

			<tr><td nowrap>
				<b>%attribute_code%</b>
			</td><td nowrap>
				The attribute's code
			</td></tr>

			<tr><td nowrap>
				<b>%attribute_prompt%</b>
			</td><td nowrap>
				The attribute's prompt
			</td></tr>
			</table>
		</td></tr>

		<tr><td colspan="2">&nbsp;</td></tr>

		<tr>
			<td align="left" valign="top">Head Template:</td> 
			<td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( l.field_prefix $ 'template_code', l.fields:template_code, 13, 100, l.templateversion:templ_id ) }">
			</td>
		</tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, '' ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:enabled"						VALUE = 1>
	<MvASSIGN NAME = "l.settings:preload"						VALUE = 1>
	<MvASSIGN NAME = "l.settings:dependency_resolution"			VALUE = "first">
	<MvASSIGN NAME = "l.settings:inv_element_id"				VALUE = "">
	<MvASSIGN NAME = "l.settings:inv_short"						VALUE = 0>
	<MvASSIGN NAME = "l.settings:price_element_id"				VALUE = "">
	<MvASSIGN NAME = "l.settings:additional_price_element_id"	VALUE = "">
	<MvASSIGN NAME = "l.settings:weight_element_id"				VALUE = "">
	<MvASSIGN NAME = "l.settings:discount_element_id"			VALUE = "">
	<MvASSIGN NAME = "l.settings:price"							VALUE = "base">
	<MvASSIGN NAME = "l.settings:additionalprice"				VALUE = "">
	<MvASSIGN NAME = "l.settings:displaydiscounts"				VALUE = 0>
	<MvASSIGN NAME = "l.settings:swatch_element_id"				VALUE = "">
	<MvASSIGN NAME = "l.settings:invalid_msg"					VALUE = "Please select a valid combination of attributes.">
	<MvASSIGN NAME = "l.settings:missing_text_msg"				VALUE = "<br>A value must be entered for <b>%attribute_prompt%</b>.">
	<MvASSIGN NAME = "l.settings:missing_radio_msg"				VALUE = "<br>An option must be selected for <b>%attribute_prompt%</b>.">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ Generate_Code( l.settings, l.template_source ) }">

	<MvASSIGN NAME = "l.settings:template_filename"	VALUE = "{ tolower( l.page:code ) $ '-' $ l.item $ '.mvc' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.template_source, l.settings, l.settings:template_filename ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:template_filename ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:template_filename, l.source_templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:template_filename, l.dest_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.dest_settings"						VALUE = "{ l.source_settings }">
	<MvASSIGN NAME = "l.dest_settings:template_filename"	VALUE = "{ l.dest_managedtemplate:version:settings:template_filename }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_managedtemplate, '', l.source_templateversion:source, l.dest_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.source_branch:id EQ l.dest_branch:id }">	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-MV-CMP-AMCHN-00003', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' on branch \'' $ l.dest_branch:name $ '\'' ) }">
	<MvELSE>													<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-MV-CMP-AMCHN-00004', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' from branch \'' $ l.source_branch:name $ '\' to branch \'' $ l.dest_branch:name $ '\'' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT l.settings:enabled }">														<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ ( l.param NE 'body' ) AND ( ( 'body:' IN l.param ) NE 1 ) AND
					( l.param NE 'body_deferred' ) AND ( ( 'body_deferred:' IN l.param ) NE 1 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvCOMMENT>
	|
	| This code mirrors the product load code in the attribute components so that the attribute machine may also be used multiple times on the same page
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ( l.param EQ 'body' ) OR ( l.param EQ 'body_deferred' ) }">
		<MvREFERENCE NAME = "l.product"		VARIABLE = "l.all_settings:product">
	<MvELSE>
		<MvASSIGN NAME = "l.colon_pos"		VALUE = "{ ':' IN l.param }">
		<MvASSIGN NAME = "l.param_len"		VALUE = "{ len_var( l.param ) }">

		<MvIF EXPR = "{ ( ':id' IN l.param ) EQ ( l.param_len - 2 ) }">
			<MvREFERENCEARRAY NAME = "l.product" VARIABLE = "l.all_settings">
				<MvMEMBER NAME = "{ substring_var( l.param, l.colon_pos + 1, l.param_len - 3 - l.colon_pos ) }">
			</MvREFERENCEARRAY>
		<MvELSE>
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_ID( miva_variable_value( 'l.all_settings:' $ substring_var( l.param, l.colon_pos + 1, l.param_len - l.colon_pos ) ), l.product ) }">
				<MvFUNCTIONRETURN>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.settings:variant_id"		VALUE = 0>

	<MvIF EXPR = "{ l.product:id GT 0 }">
		<MvASSIGN NAME = "l.settings:product"		VALUE = "{ l.product }">
		<MvASSIGN NAME = "l.settings:product:price"	VALUE = "{ l.product:retail }">
	<MvELSE>
		<MvREFERENCE NAME = "l.settings:product"	VARIABLE = "l.empty_product">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Reference_Cache( l.all_settings, l.module:code, l.cache ) }">

	<MvIF EXPR = "{ miva_element_exists( l.cache:data, l.product:id ) }">
		<MvASSIGN NAME = "l.settings:variant_id"	VALUE = "{ l.cache:data[ l.product:id ]:variant_id }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ ( l.param EQ 'body' ) OR ( l.param EQ 'body_deferred' ) }">	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Component_Prerender( l.all_settings, 'product_attributes', '' ) }">
	<MvELSE>																	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Component_Prerender( l.all_settings, 'product_attributes', substring( l.param, l.colon_pos + 1, l.param_len - l.colon_pos ) ) }"> <MvCOMMENT> This line uses substring() instead of substring_var() due to engine bug 6712 </MvCOMMENT>
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.cache_data" VARIABLE = "l.cache:data">
		<MvDIMENSION INDEX = "{ l.product:id }">
	</MvREFERENCEARRAY>

	<MvASSIGN NAME = "l.cache_data:subscription"	VALUE = "{ l.all_settings:subscription }">
	<MvASSIGN NAME = "l.cache_data:attributes"		VALUE = "{ l.all_settings:attributes }">
	<MvASSIGN NAME = "l.cache_data:attribute_count"	VALUE = "{ miva_array_elements( l.cache_data:attributes ) }">
	<MvASSIGN NAME = "l.cache_data:inventory_count"	VALUE = 0>
	<MvASSIGN NAME = "l.cache_data:swatch_count"	VALUE = 0>
	<MvASSIGN NAME = "l.selected_count"				VALUE = 0>
	<MvASSIGN NAME = "l.unselected_count"			VALUE = 0>

	<MvIF EXPR = "{ l.cache_data:subscription:enabled AND l.cache_data:subscription:mandatory AND l.cache_data:subscription:term_count }">
		<MvASSIGN NAME = "l.cache_data:initial_term" VALUE = "{ l.cache_data:subscription:terms[ 1 ] }">
	</MvIF>

	<MvFOREACH ITERATOR = "l.attribute" ARRAY = "l.cache_data:attributes" COUNT = "{ l.cache_data:attribute_count }">
		<MvIF EXPR = "{ ( l.attribute:type EQ 'text' ) OR ( l.attribute:type EQ 'memo' ) OR ( l.attribute:type EQ 'checkbox' ) }">
			<MvASSIGN NAME = "l.selected_count"															VALUE = "{ l.selected_count + 1 }">
			<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "attr_id"				VALUE = "{ l.attribute:id }">
			<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "attmpat_id"			VALUE = "{ l.attribute:attmpat_id }">
			<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "option_id"			VALUE = "{ len( l.attribute:value ) NE 0 }">
			<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "type"				VALUE = "{ l.attribute:type }">
			<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "inventory"			VALUE = "{ l.attribute:inventory }">
			<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "price"				VALUE = "{ l.attribute:price }">
			<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "cost"				VALUE = "{ l.attribute:cost }">
			<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "weight"				VALUE = "{ l.attribute:weight }">
		<MvELSEIF EXPR = "{ ( l.attribute:type EQ 'radio' ) OR ( l.attribute:type EQ 'select' ) OR ( l.attribute:type EQ 'swatch-select' ) }">
			<MvASSIGN NAME = "l.have_selection"															VALUE = 0>

			<MvFOREACH ITERATOR = "l.option" ARRAY = "l.attribute:options">
				<MvIF EXPR = "{ ( ( NOT ISNULL l.attribute:value ) AND ( strcmp( l.option:code, l.attribute:value ) EQ 0 ) ) OR
								( ( ISNULL l.attribute:value ) AND ( l.option:id EQ l.attribute:default_id ) ) }">
					<MvASSIGN NAME = "l.have_selection"													VALUE = 1>
					<MvASSIGN NAME = "l.selected_count"													VALUE = "{ l.selected_count + 1 }">
					<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "attr_id"		VALUE = "{ l.attribute:id }">
					<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "attmpat_id"	VALUE = "{ l.attribute:attmpat_id }">
					<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "option_id"	VALUE = "{ l.option:id }">
					<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "type"		VALUE = "{ l.attribute:type }">
					<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "inventory"	VALUE = "{ l.attribute:inventory }">
					<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "price"		VALUE = "{ l.option:price }">
					<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "cost"		VALUE = "{ l.option:cost }">
					<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "weight"		VALUE = "{ l.option:weight }">

					<MvFOREACHSTOP>
				</MvIF>
			</MvFOREACH>

			<MvCOMMENT>
			|
			| If a select has no default and no existing value, the browser will automatically select the first option, unless there
			| is a select-one choice (preload value of "2").
			|
			| If a radio has no default and no existing value, it will have no selection, and must be added to the unselected list for
			| proper processing.
			|
			</MvCOMMENT>

			<MvIF EXPR = "{ NOT l.have_selection }">
				<MvIF EXPR = "{ ( l.attribute:type EQ 'radio' ) OR ( ( l.settings:preload EQ 2 ) AND
																	 ( ( l.attribute:type EQ 'select' ) OR
																	   ( l.attribute:type EQ 'swatch-select' ) ) ) }">
					<MvASSIGN NAME = "l.unselected_count"													VALUE = "{ l.unselected_count + 1 }">
					<MvASSIGN NAME = "l.unselected" INDEX = "{ l.unselected_count }" MEMBER = "attr_id"		VALUE = "{ l.attribute:id }">
					<MvASSIGN NAME = "l.unselected" INDEX = "{ l.unselected_count }" MEMBER = "attmpat_id"	VALUE = "{ l.attribute:attmpat_id }">
					<MvASSIGN NAME = "l.unselected" INDEX = "{ l.unselected_count }" MEMBER = "inventory"	VALUE = "{ l.attribute:inventory }">
				<MvELSEIF EXPR = "{ ( l.attribute:type EQ 'select' ) OR ( l.attribute:type EQ 'swatch-select' ) }">
					<MvASSIGN NAME = "l.selected_count"														VALUE = "{ l.selected_count + 1 }">
					<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "attr_id"			VALUE = "{ l.attribute:id }">
					<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "attmpat_id"		VALUE = "{ l.attribute:attmpat_id }">
					<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "option_id"		VALUE = "{ l.attribute:options[ 1 ]:id }">
					<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "type"			VALUE = "{ l.attribute:type }">
					<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "inventory"		VALUE = "{ l.attribute:inventory }">
					<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "price"			VALUE = "{ l.attribute:options[ 1 ]:price }">
					<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "cost"			VALUE = "{ l.attribute:options[ 1 ]:cost }">
					<MvASSIGN NAME = "l.selected" INDEX = "{ l.selected_count }" MEMBER = "weight"			VALUE = "{ l.attribute:options[ 1 ]:weight }">
				</MvIF>
			</MvIF>
		</MvIF>
	</MvFOREACH>

	<MvIF EXPR = "{ NOT l.settings:preload }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvCOMMENT>
	|
	| This code is roughly copied from JSON_Runtime_AttributeList_Load_ProductVariant_Possible in json/runtime.mv, and calls functions implemented there.
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.have_incomplete_variants"								VALUE = 0>
	<MvASSIGN NAME = "l.cache_data:have_complete_variant"						VALUE = 0>
	<MvASSIGN NAME = "l.cache_data:output_attribute_count"						VALUE = 0>
	
	<MvREFERENCE NAME = "l.cache_data:selected_attribute_count"					VARIABLE = "l.selected_count">
	<MvREFERENCE NAME = "l.cache_data:selected_attributes"						VARIABLE = "l.selected">

	<MvASSIGN NAME = "l.selected_inventory_count"								VALUE = "{ miva_array_filter( l.selected, 1, l.selected_attribute, 'l.selected_attribute:inventory', l.selected_inventory_attributes ) }">
	<MvASSIGN NAME = "l.unselected_inventory_count"								VALUE = "{ miva_array_filter( l.unselected, 1, l.unselected_attribute, 'l.unselected_attribute:inventory', l.unselected_inventory_attributes ) }">

	<MvASSIGN NAME = "l.applied_facet_count"									VALUE = "{ [ g.Module_JSON ].JSON_Runtime_Product_AppliedFacets( l.applied_facets ) }">

	<MvIF EXPR = "{ l.selected_inventory_count + l.unselected_inventory_count GT 0 }">
		<MvASSIGN NAME = "l.last_selected_count"								VALUE = 0>
		<MvASSIGN NAME = "l.last_selected"										VALUE = "">

		<MvIF EXPR = "{ l.selected_inventory_count GT 0 }">
			<MvASSIGN NAME = "l.last_selected_count"							VALUE = "{ miva_array_insert_var( l.last_selected, l.selected_inventory_attributes[ 1 ], -1 ) }">
		</MvIF>

		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_JSON ].JSON_Possible( l.product, l.settings:dependency_resolution,
																			  l.last_selected, l.last_selected_count,
																			  l.selected_inventory_attributes, l.selected_inventory_count,
																			  l.unselected_inventory_attributes, l.unselected_inventory_count,
																			  l.applied_facets, l.applied_facet_count,
																			  l.have_incomplete_variants, l.cache_data:have_complete_variant, l.cache_data:complete_variant,
																			  l.cache_data:output_attributes, l.cache_data:output_attribute_count ) }">
	</MvIF>

	<MvIF EXPR = "{ l.cache_data:have_complete_variant }">	<MvASSIGN NAME = "l.cache_data:variant_id" VALUE = "{ l.cache_data:complete_variant:variant_id }">
	<MvELSE>												<MvASSIGN NAME = "l.cache_data:variant_id" VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.settings:variant_id"									VALUE = "{ l.cache_data:variant_id }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT l.settings:enabled }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ l.param EQ 'head' }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_JSON_Variables( l.all_settings ) }">

		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside( l.all_settings, 'ajax.js' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside( l.all_settings, 'runtime.js' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside( l.all_settings, 'MivaEvents.js' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside( l.all_settings, 'AttributeMachine.js' ) }">

		<MvCAPTURE VARIABLE = "l.head_content">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:template_filename, l.all_settings ) }">
		</MvCAPTURE>

		<MvIF EXPR = "{ ( '<script' CIN l.head_content ) OR
						( '<style' CIN l.head_content ) }">
			<MvEVAL EXPR = "{ l.head_content }">
		<MvELSE>
			<MIVA STANDARDOUTPUTLEVEL = "text, html"><script type="text/javascript"><MvEVAL EXPR = "{ l.head_content }"></script><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
	<MvELSEIF EXPR = "{ l.param EQ 'head_deferred' }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_JSON_Variables( l.all_settings ) }">

		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside_Deferred( l.all_settings, 'ajax.js' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside_Deferred( l.all_settings, 'runtime.js' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside_Deferred( l.all_settings, 'MivaEvents.js' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Clientside_Deferred( l.all_settings, 'AttributeMachine.js' ) }">

		<MIVA STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<script type="text/javascript">
			(function( obj, eventType, fn )
			{
				if ( obj.addEventListener )
				{
					obj.addEventListener( eventType, fn, false );
				}
				else if ( obj.attachEvent )
				{
					obj.attachEvent( 'on' + eventType, fn );
				}
			})( window, 'attributemachine_override', function()
			{
				<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:template_filename, l.all_settings ) }">
			} );
		</script>
		<MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSEIF EXPR = "{ ( l.param EQ 'body' ) OR ( ( 'body:' IN l.param ) EQ 1 ) OR
						( l.param EQ 'body_deferred' ) OR ( ( 'body_deferred:' IN l.param ) EQ 1 ) }">
		<MvIF EXPR = "{ l.settings:product:id LE 0 }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Reference_Cache( l.all_settings, l.module:code, l.cache ) }">

		<MvREFERENCEARRAY NAME = "l.cache_data" VARIABLE = "l.cache:data">
			<MvDIMENSION INDEX = "{ l.settings:product:id }">
		</MvREFERENCEARRAY>

		<MvCOMMENT>
		|
		| If there are no attributes, do not create an attribute machine.
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ ( l.cache_data:attribute_count GT 0 ) OR
						( l.cache_data:subscription:enabled AND l.cache_data:subscription:term_count GT 0 ) }">
			<MvCOMMENT>
			|
			| Parse tokens in element ids
			|
			</MvCOMMENT>

			<MvASSIGN NAME = "l.search" INDEX = 1 VALUE = "%product_id%">	<MvASSIGN NAME = "l.replace" INDEX = 1 VALUE = "{ l.settings:product:id }">
			<MvASSIGN NAME = "l.search" INDEX = 2 VALUE = "%product_code%">	<MvASSIGN NAME = "l.replace" INDEX = 2 VALUE = "{ l.settings:product:code }">

			<MvASSIGN NAME = "l.inv_element_id"					VALUE = "{ glosub_array( l.settings:inv_element_id,					l.search, l.replace ) }">
			<MvASSIGN NAME = "l.price_element_id"				VALUE = "{ glosub_array( l.settings:price_element_id,				l.search, l.replace ) }">
			<MvASSIGN NAME = "l.additional_price_element_id"	VALUE = "{ glosub_array( l.settings:additional_price_element_id,	l.search, l.replace ) }">
			<MvASSIGN NAME = "l.weight_element_id"				VALUE = "{ glosub_array( l.settings:weight_element_id,				l.search, l.replace ) }">
			<MvASSIGN NAME = "l.discount_element_id"			VALUE = "{ glosub_array( l.settings:discount_element_id,			l.search, l.replace ) }">
			<MvASSIGN NAME = "l.swatch_element_id"				VALUE = "{ glosub_array( l.settings:swatch_element_id,				l.search, l.replace ) }">

			<MvCAPTURE VARIABLE = "l.initialization_js" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
				window.am<MvEVAL EXPR = "{ int( l.settings:product:id ) }"> = new AttributeMachine(
				{
					"product_code":					"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.settings:product:code ) }">",
					"dependency_resolution":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.settings:dependency_resolution ) }">",
					"inventory_element_id":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.inv_element_id ) }">",
					"inv_long":						<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Boolean( l.settings:inv_short ) }">,
					"price_element_id":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.price_element_id ) }">",
					"additional_price_element_id":	"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.additional_price_element_id ) }">",
					"weight_element_id":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.weight_element_id ) }">",
					"discount_element_id":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.discount_element_id ) }">",
					"price":						"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.settings:price ) }">",
					"additionalprice":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.settings:additionalprice ) }">",
					"displaydiscounts":				<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Boolean( l.settings:displaydiscounts ) }">,
					"swatch_element_id":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.swatch_element_id ) }">",
					"invalid_msg":					"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.settings:invalid_msg ) }">",
					"missing_text_msg":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.settings:missing_text_msg ) }">",
					"missing_radio_msg":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.settings:missing_radio_msg ) }">"
				} );

				<MvIF EXPR = "{ l.settings:preload }">
					window.amAttributes<MvEVAL EXPR = "{ int( l.settings:product:id ) }"> = <MvASSIGN NAME = "l.null" VALUE = "{ AttributeMachine_Attributes( l.cache_data:attributes, l.cache_data:attribute_count ) }">;
					window.amPossible<MvEVAL EXPR = "{ int( l.settings:product:id ) }"> = <MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_JSON ].JSON_Possible_Output( l.settings:product, l.cache_data:initial_term, l.cache_data:have_complete_variant, l.cache_data:complete_variant, l.settings:displaydiscounts, l.settings:price EQ 'sale', l.cache_data:selected_attributes, l.cache_data:selected_attribute_count, l.cache_data:output_attributes, l.cache_data:output_attribute_count ) }">;
					window.am<MvEVAL EXPR = "{ int( l.settings:product:id ) }">.Initialize( window.amAttributes<MvEVAL EXPR = "{ int( l.settings:product:id ) }">, window.amPossible<MvEVAL EXPR = "{ int( l.settings:product:id ) }"> );
				<MvELSE>
					window.am<MvEVAL EXPR = "{ int( l.settings:product:id ) }">.Initialize( null, null );
				</MvIF>
			</MvCAPTURE>

			<MvIF EXPR = "{ ( 'body_deferred' IN l.param ) NE 1 }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html"><script type="text/javascript"><MvEVAL EXPR = "{ l.initialization_js }"></script><MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSE>
				<MIVA STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
				<script type="text/javascript">
					(function( obj, eventType, fn )
					{
						if ( obj.addEventListener )
						{
							obj.addEventListener( eventType, fn, false );
						}
						else if ( obj.attachEvent )
						{
							obj.attachEvent( 'on' + eventType, fn );
						}
					})( window, 'attributemachine_initialize', function()
					{
						<MvEVAL EXPR = "{ l.initialization_js }">
					});
				</script>
				<MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>
		</MvIF>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeMachine_Attributes" PARAMETERS = "attributes var, attribute_count" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvCOMMENT>
	|
	| This code outputs a JSON variable for the attributes in roughly the same format as JSON_Runtime_AttributeAndOptionList_Load_Product.  Some fields
	| are omitted in order to avoid reloading data.
	|
	</MvCOMMENT>

    <MvASSIGN NAME = "l.attribute_pos"							VALUE = 1>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	[
	<MvWHILE EXPR = "{ l.attribute_pos LE l.attribute_count }">
		<MvREFERENCEARRAY NAME = "l.attribute" VARIABLE = "l.attributes">
			<MvDIMENSION INDEX = "{ l.attribute_pos }">
		</MvREFERENCEARRAY>

		<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.attribute_output_count ) }">
		"id":				<MvEVAL EXPR = "{ l.attribute:id }">,
		"code":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.attribute:code ) }">",

		<MvIF EXPR = "{ len( l.attribute:template_code ) EQ 0 }">
			<MvASSIGN NAME = "l.prompt"							VALUE = "{ AttributeMachine_Strip_Colon( l.attribute:prompt ) }">

			"product_id":		<MvEVAL EXPR = "{ l.attribute:product_id }">,
			"default_id":		<MvEVAL EXPR = "{ l.attribute:default_id }">,
			"disp_order":		<MvEVAL EXPR = "{ l.attribute:disp_order }">,
			"type":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.attribute:type ) }">",
			"prompt":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.prompt ) }">",
			"price":			<MvEVAL EXPR = "{ encodejavascriptnumber( l.attribute:price ) }">,
			"formatted_price":	"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.attribute:formatted_price ) }">",
			"weight":			<MvEVAL EXPR = "{ encodejavascriptnumber( l.attribute:weight ) }">,
			"formatted_weight":	"<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Format_Weight( l.attribute:weight ) }">",
			"required":			<MvEVAL EXPR = "{ l.attribute:required }">,
			"inventory":		<MvEVAL EXPR = "{ l.attribute:inventory }">,
			"image":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.attribute:image ) }">"

			<MvIF EXPR = "{ ( l.attribute:type EQ 'radio' ) OR ( l.attribute:type EQ 'select' ) OR ( l.attribute:type EQ 'swatch-select' ) }">
				<MvASSIGN NAME = "l.option_output_count"		VALUE = 0>
				<MvASSIGN NAME = "l.option_pos"					VALUE = 1>
				<MvASSIGN NAME = "l.option_count"				VALUE = "{ miva_array_elements( l.attribute:options ) }">

				,"options":
				[
				<MvWHILE EXPR = "{ l.option_pos LE l.option_count }">
					<MvREFERENCEARRAY NAME = "l.option" VARIABLE = "l.attribute">
						<MvMEMBER NAME = "options">
						<MvDIMENSION INDEX = "{ l.option_pos }">
					</MvREFERENCEARRAY>

					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.option_output_count ) }">
					"id":				<MvEVAL EXPR = "{ l.option:id }">,
					"product_id":		<MvEVAL EXPR = "{ l.option:product_id }">,
					"attr_id":			<MvEVAL EXPR = "{ l.option:attr_id }">,
					"disp_order":		<MvEVAL EXPR = "{ l.option:disp_order }">,
					"code":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:code ) }">",
					"prompt":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:prompt ) }">",
					"price":			<MvEVAL EXPR = "{ encodejavascriptnumber( l.option:price ) }">,
					"formatted_price":  "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:formatted_price ) }">",
					"weight":			<MvEVAL EXPR = "{ encodejavascriptnumber( l.option:weight ) }">,
					"formatted_weight":	"<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Format_Weight( l.option:weight ) }">",
					"image":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:image ) }">"
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">

					<MvASSIGN NAME = "l.option_pos"				VALUE = "{ l.option_pos + 1 }">
				</MvWHILE>
				]
			</MvIF>

			<MvASSIGN NAME = "l.attribute_pos"					VALUE = "{ l.attribute_pos + 1 }">
		<MvELSE>
			"type":				"template",
			"attributes":
			[
			<MvASSIGN NAME = "l.template_attr_output_count"		VALUE = 0>

			<MvWHILE EXPR = "{ ( l.attribute_pos LE l.attribute_count ) AND ( l.attributes[ l.attribute_pos ]:code EQ l.attribute:code ) }">
				<MvREFERENCEARRAY NAME = "l.template_attribute" VARIABLE = "l.attributes">
					<MvDIMENSION INDEX = "{ l.attribute_pos }">
				</MvREFERENCEARRAY>

				<MvASSIGN NAME = "l.prompt"						VALUE = "{ AttributeMachine_Strip_Colon( l.template_attribute:prompt ) }">

				<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.template_attr_output_count ) }">
				"id":				<MvEVAL EXPR = "{ l.template_attribute:attmpat_id }">,
				"default_id":		<MvEVAL EXPR = "{ l.template_attribute:default_id }">,
				"code":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.template_attribute:template_code ) }">",
				"type":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.template_attribute:type ) }">",
				"prompt":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.prompt ) }">",
				"price":			<MvEVAL EXPR = "{ encodejavascriptnumber( l.template_attribute:price ) }">,
				"formatted_price":	"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.template_attribute:formatted_price ) }">",
				"weight":			<MvEVAL EXPR = "{ encodejavascriptnumber( l.template_attribute:weight ) }">,
				"formatted_weight":	"<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Format_Weight( l.template_attribute:weight ) }">",
				"required":			<MvEVAL EXPR = "{ l.template_attribute:required }">,
				"inventory":		<MvEVAL EXPR = "{ l.template_attribute:inventory }">,
				"image":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.template_attribute:image ) }">"

				<MvIF EXPR = "{ ( l.template_attribute:type EQ 'radio' ) OR ( l.template_attribute:type EQ 'select' ) OR ( l.template_attribute:type EQ 'swatch-select' ) }">
					<MvASSIGN NAME = "l.option_output_count"		VALUE = 0>
					<MvASSIGN NAME = "l.option_pos"					VALUE = 1>
					<MvASSIGN NAME = "l.option_count"				VALUE = "{ miva_array_elements( l.template_attribute:options ) }">

					,"options":
					[
					<MvWHILE EXPR = "{ l.option_pos LE l.option_count }">
						<MvREFERENCEARRAY NAME = "l.option" VARIABLE = "l.template_attribute">
							<MvMEMBER NAME = "options">
							<MvDIMENSION INDEX = "{ l.option_pos }">
						</MvREFERENCEARRAY>

						<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.option_output_count ) }">
						"id":				<MvEVAL EXPR = "{ l.option:id }">,
						"attemp_id":		<MvEVAL EXPR = "{ l.option:attemp_id }">,
						"attmpat_id":		<MvEVAL EXPR = "{ l.option:attmpat_id }">,
						"disp_order":		<MvEVAL EXPR = "{ l.option:disporder }">,
						"code":				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:code ) }">",
						"prompt":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:prompt ) }">",
						"price":			<MvEVAL EXPR = "{ encodejavascriptnumber( l.option:price ) }">,
						"formatted_price":	"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:formatted_price ) }">",
						"weight":			<MvEVAL EXPR = "{ encodejavascriptnumber( l.option:weight ) }">,
						"formatted_weight":	"<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Format_Weight( l.option:weight ) }">",
						"image":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.option:image ) }">"
						<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">

						<MvASSIGN NAME = "l.option_pos"				VALUE = "{ l.option_pos + 1 }">
					</MvWHILE>
					]
				</MvIF>

				<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">

				<MvASSIGN NAME = "l.attribute_pos"			VALUE = "{ l.attribute_pos + 1 }">
			</MvWHILE>
			]
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
	</MvWHILE>
	]

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeMachine_Strip_Colon" PARAMETERS = "prompt" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.length"	VALUE = "{ len( l.prompt ) }">

	<MvIF EXPR = "{ substring( l.prompt, l.length, 1 ) NE ':' }">
		<MvFUNCTIONRETURN VALUE = "{ l.prompt }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ substring( l.prompt, 1, l.length - 1 ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Code" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code" STANDARDOUTPUTLEVEL = "text, html"><MvIF EXPR = "{ g.Store:ui_mod:code EQ 'mmui' }"><script>
</MvIF>AttributeMachine.prototype.Generate_Discount = function( discount )
{
	var discount_div;

	discount_div		= document.createElement( 'div' );
	discount_div.innerHTML	= discount.descrip + ': ' + discount.formatted_discount;

	return discount_div;
}

AttributeMachine.prototype.Generate_Swatch = function( product_code, attribute, option )
{
	var swatch	= document.createElement( 'li' );
	var span	= document.createElement( 'span' ); // to vertically center the swatch images
	var img		= document.createElement( 'img' );
	img.src		= option.image;

	swatch.appendChild( span );
	swatch.appendChild( img );

	return swatch;
}<MvIF EXPR = "{ g.Store:ui_mod:code EQ 'mmui' }">
</script>
<style type="text/css">
#swatches{
	display:inline;
	margin:0;
	padding:0;
}

#swatches li{
	display:block;
	float:left;
	margin:4px 4px 0 0;
	padding:0;
	width:50px;
	height:50px;
	text-align:center;
	border:1px solid #eeeeee;
	cursor:pointer;
	list-style-type:none;
}

#swatches li span{
	display:inline-block;
	height:100%;
	text-align:center;
	vertical-align:middle;
}

#swatches li img{
	vertical-align:middle;
}
</style></MvIF></MvCAPTURE>
</MvFUNCTION>

<MvCOMMENT>
|
| Component Provision Feature (component_prov)
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.working_settings" VALUE = "{ l.settings }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'Enabled',								l.working_settings:enabled )																					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml,	'InventoryElement',						l.working_settings:inv_element_id )																				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml,	'PriceElement',							l.working_settings:price_element_id )																			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml,	'AdditionalPriceElement',				l.working_settings:additional_price_element_id ) 																OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml,	'WeightElement',						l.working_settings:weight_element_id )																			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml,	'DiscountElement',						l.working_settings:discount_element_id )																		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml,	'PriceDisplay',							l.working_settings:price,					'Retail,Base,Sale',							'retail,base,sale' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml,	'AdditionalPriceDisplay',				l.working_settings:additionalprice,			'None,Retail,Base',							',retail,base' )		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'DisplayDiscounts',						l.working_settings:displaydiscounts )																			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml,	'SwatchElement',						l.working_settings:swatch_element_id )																			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml,	'InvalidAttributeCombinationMessage',	l.working_settings:invalid_msg )																				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml,	'MissingTextFieldValueMessage',			l.working_settings:missing_text_msg )			 																OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml,	'MissingRadioSelectionMessage',			l.working_settings:missing_radio_msg )																			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'o', l.provide_xml,	'DependencyResolution',					l.working_settings:dependency_resolution,	'First,Cascade,Last',						'first,cascade,last' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'o', l.provide_xml,	'InventoryMessage',						l.working_settings:inv_short,				'Short,Long',								'0,1' )					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml, 'Template',								l.template )																									OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml, 'Notes',								l.notes ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'PreloadInitialAttributeState' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean( 'O', 	l.provide_xml,	'PreloadInitialAttributeState',		l.working_settings:preload ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	<MvELSEIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List( 'O',		l.provide_xml,	'InitialAttributeState',			l.working_settings:preload,					'NoPreload,Preload,PreloadWithSelectOne',	'0,1,2' ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
		<MvASSIGN NAME = "l.template" VALUE = "{ l.managedtemplate:version:source }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion_LowLevel( l.managedtemplate, l.managedtemplate:version, l.notes, l.template, l.working_settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings" VALUE = "{ l.working_settings }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Framework Support Feature (skins)
|
</MvCOMMENT>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ 'Exports Attribute Machine settings from pages: ' $ [ g.Module_Feature_TUI_DB ].Item_Load_PageList( l.item ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvCAPTURE VARIABLE = "l.output">
		<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item_Runtime( l.item:id, l.pages ) }">
			<MvREFERENCEARRAY NAME = "l.settings" VARIABLE = "l.page">
				<MvMEMBER NAME = "settings">
				<MvMEMBER NAME = "{ l.item:code }">
			</MvREFERENCEARRAY>

			<Page_Update code="{ encodeentities( l.page:code ) }">
				<Item code="{ encodeentities( l.item:code ) }">
					<Enabled><MvIF EXPR = "{ l.settings:enabled }">Yes<MvELSE>No</MvIF></Enabled>

					<MvIF EXPR = "{ l.settings:preload EQ 0 }">
						<InitialAttributeState>NoPreload</InitialAttributeState>
					<MvELSEIF EXPR = "{ l.settings:preload EQ 1 }">
						<InitialAttributeState>Preload</InitialAttributeState>
					<MvELSEIF EXPR = "{ l.settings:preload EQ 2 }">
						<InitialAttributeState>PreloadWithSelectOne</InitialAttributeState>
					</MvIF>

					<MvIF EXPR = "{ l.settings:dependency_resolution EQ 'first' }">
						<DependencyResolution>First</DependencyResolution>
					<MvELSEIF EXPR = "{ l.settings:dependency_resolution EQ 'cascade' }">
						<DependencyResolution>Cascade</DependencyResolution>
					<MvELSEIF EXPR = "{ l.settings:dependency_resolution EQ 'last' }">
						<DependencyResolution>Last</DependencyResolution>
					</MvIF>

					<InventoryElement><MvEVAL EXPR = "{ miva_cdata_encode( l.settings:inv_element_id ) }"></InventoryElement>
					<InventoryMessage><MvIF EXPR = "{ l.settings:inv_short }">Long<MvELSE>Short</MvIF></InventoryMessage>

					<PriceElement><MvEVAL EXPR = "{ miva_cdata_encode( l.settings:price_element_id ) }"></PriceElement>
					<AdditionalPriceElement><MvEVAL EXPR = "{ miva_cdata_encode( l.settings:additional_price_element_id ) }"></AdditionalPriceElement>
					<WeightElement><MvEVAL EXPR = "{ miva_cdata_encode( l.settings:weight_element_id ) }"></WeightElement>
					<DiscountElement><MvEVAL EXPR = "{ miva_cdata_encode( l.settings:discount_element_id ) }"></DiscountElement>

					<MvIF EXPR = "{ l.settings:price EQ 'retail' }">
						<PriceDisplay>Retail</PriceDisplay>
					<MvELSEIF EXPR = "{ l.settings:price EQ 'sale' }">
						<PriceDisplay>Sale</PriceDisplay>
					<MvELSE>
						<PriceDisplay>Base</PriceDisplay>
					</MvIF>

					<MvIF EXPR = "{ l.settings:additionalprice EQ 'retail' }">
						<AdditionalPriceDisplay>Retail</AdditionalPriceDisplay>
					<MvELSEIF EXPR = "{ l.settings:additionalprice EQ 'base' }">
						<AdditionalPriceDisplay>Base</AdditionalPriceDisplay>
					<MvELSE>
						<AdditionalPriceDisplay>None</AdditionalPriceDisplay>
					</MvIF>

					<DisplayDiscounts><MvIF EXPR = "{ l.settings:displaydiscounts }">Yes<MvELSE>No</MvIF></DisplayDiscounts>
					<PredictDiscounts><MvIF EXPR = "{ l.settings:displaydiscounts }">Yes<MvELSE>No</MvIF></PredictDiscounts>

					<SwatchElement><MvEVAL EXPR = "{ miva_cdata_encode( l.settings:swatch_element_id ) }"></SwatchElement>

					<InvalidAttributeCombinationMessage><MvEVAL EXPR = "{ miva_cdata_encode( l.settings:invalid_msg ) }"></InvalidAttributeCombinationMessage>
					<MissingTextFieldValueMessage><MvEVAL EXPR = "{ miva_cdata_encode( l.settings:missing_text_msg ) }"></MissingTextFieldValueMessage>
					<MissingRadioSelectionMessage><MvEVAL EXPR = "{ miva_cdata_encode( l.settings:missing_radio_msg ) }"></MissingRadioSelectionMessage>

					<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
						<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.templateversion:source ) }"></Template>
					</MvIF>

					<Notes>#Set_Current_Time#</Notes>
				</Item>
			</Page_Update>
		</MvFOREACH>
	</MvCAPTURE>
</MvFUNCTION>

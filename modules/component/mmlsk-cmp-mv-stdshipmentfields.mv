<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CMP-SOF-
| Next Error Code: 1    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-mv-stdshipmentfields">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Standard Shipment Fields">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0800">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "10.08">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, component_prov, skins">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.ordershipment_count"		VALUE = "{ miva_array_elements( l.settings:shipments ) }">

	<MvIF EXPR = "{ l.ordershipment_count GT 0 }">
		<MvASSIGN NAME = "l.ordershipment_pos"		VALUE = 1>
		<MvWHILE EXPR = "{ l.ordershipment_pos LE l.ordershipment_count }">
			<MvIF EXPR = "{ NOT StandardShipmentFields_Initialize_Shipment( l.all_settings, l.settings, l.settings:shipments[ l.ordershipment_pos ] ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.ordershipment_pos"	VALUE = "{ l.ordershipment_pos + 1 }">
		</MvWHILE>
	<MvELSEIF EXPR = "{ g.OrderShipment_ID }">
		<MvFUNCTIONRETURN VALUE = "{ StandardShipmentFields_Load_Shipment( l.all_settings, l.settings, g.OrderShipment_ID, l.settings ) }">
	<MvELSEIF EXPR = "{ len( g.OrderShipment_IDs ) }">
		<MvASSIGN NAME = "l.ordershipment_pos"		VALUE = 1>
		<MvASSIGN NAME = "l.ordershipment_id"		VALUE = "{ gettoken( g.OrderShipment_IDs, '|', l.ordershipment_pos ) }">

		<MvWHILE EXPR = "{ l.ordershipment_id }">
			<MvIF EXPR = "{ NOT StandardShipmentFields_Load_Shipment( l.all_settings, l.settings, l.ordershipment_id, l.settings:shipments[ l.ordershipment_pos ] ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.ordershipment_pos"	VALUE = "{ l.ordershipment_pos + 1 }">
			<MvASSIGN NAME = "l.ordershipment_id"	VALUE = "{ gettoken( g.OrderShipment_IDs, '|', l.ordershipment_pos ) }">
		</MvWHILE>
	</MvIF>

	<MvASSIGN NAME = "l.settings:shipment_count" VALUE = "{ miva_array_elements( l.settings:shipments ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StandardShipmentFields_Load_Shipment" PARAMETERS = "all_settings var, settings var, shipment_id, shipment var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].OrderShipment_Load_ID( l.shipment_id, l.shipment ) OR
					NOT [ g.Module_Library_DB ].Order_Load_ID( l.shipment:order_id, l.shipment:order ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ StandardShipmentFields_Initialize_Shipment( l.all_settings, l.settings, l.shipment ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "StandardShipmentFields_Initialize_Shipment" PARAMETERS = "all_settings var, settings var, shipment var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT StandardShipmentFields_Initialize_OrderFields( l.shipment ) }">									<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ NOT StandardShipmentFields_Initialize_Shipping( l.shipment ) }">									<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ NOT StandardShipmentFields_Initialize_OrderItems( l.all_settings, l.settings, l.shipment ) }">		<MvFUNCTIONRETURN VALUE = 0>	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StandardShipmentFields_Initialize_OrderFields" PARAMETERS = "shipment var" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCE NAME = "l.order" VARIABLE = "l.shipment:order">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Order_Load_ID( l.shipment:order_id, l.order ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Order( l.order ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "StandardShipmentFields_Initialize_Shipping" PARAMETERS = "shipment var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_OrderShipment( l.shipment ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.shipment:order:ship_id EQ 0 }">
		<MvASSIGN NAME = "l.shipment:method"	VALUE = "Unknown">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.module" VARIABLE = "g.Session:cache:cmp_mv_stdshipmentfields:modules">
		<MvDIMENSION INDEX = "{ l.shipment:order:ship_id }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ l.module:loaded EQ 0 }">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].Module_Load_ID( l.shipment:order:ship_id, l.module ) }">
			<MvASSIGN NAME = "l.module:loaded"	VALUE = 1>
		<MvELSE>
			<MvASSIGN NAME = "l.module:loaded"	VALUE = -1>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.module:loaded EQ -1 }">
		<MvASSIGN NAME = "l.shipment:method"	VALUE = "Unknown">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_SHP_DB ].ShippingMethodRules_Load_MethodOrDefault( l.module:code, l.shipment:order:ship_data, l.shippingmethodrules ) AND
					NOT ISNULL l.shippingmethodrules:descrip }">
		<MvASSIGN NAME = "l.shipment:method" 	VALUE = "{ l.shippingmethodrules:descrip }">
	<MvELSE>
		<MvASSIGN NAME = "l.shipment:method"	VALUE = "{ [ g.Module_Root $ l.module:module ].ShippingModule_Description( l.module, l.shipment:order:ship_data ) }">
	</MvIF>

	<MvASSIGN NAME = "l.fields"					VALUE = "{ [ g.Module_Root $ l.module:module ].ShippingModule_Report_Fields( l.module ) }">
	<MvASSIGN NAME = "l.field_pos"				VALUE = 1>
	<MvASSIGN NAME = "l.field"					VALUE = "{ trim( gettoken( l.fields, ',', l.field_pos ) ) }">

	<MvWHILE EXPR = "{ len( l.field ) }">
		<MvASSIGN NAME = "l.label"				VALUE = "{ [ g.Module_Root $ l.module:module ].ShippingModule_Report_Label( l.module, l.field ) }">
		<MvASSIGN NAME = "l.value"				VALUE = "{ [ g.Module_Root $ l.module:module ].ShippingModule_Report_Value( l.module, l.field, l.shipment:order:ship_data ) }">

		<MvASSIGNARRAY NAME = "l.shipment" VALUE = "{ l.value }">
			<MvMEMBER NAME = "field">
			<MvMEMBER NAME = "{ l.field }">
		</MvASSIGNARRAY>
	
		<MvASSIGN NAME = "l.shipment:fields" INDEX = "{ l.field_pos }" MEMBER = "code"	VALUE = "{ l.field }">
		<MvASSIGN NAME = "l.shipment:fields" INDEX = "{ l.field_pos }" MEMBER = "label"	VALUE = "{ l.label }">
		<MvASSIGN NAME = "l.shipment:fields" INDEX = "{ l.field_pos }" MEMBER = "value"	VALUE = "{ l.value }">

		<MvASSIGN NAME = "l.field_pos"			VALUE = "{ l.field_pos + 1 }">
		<MvASSIGN NAME = "l.field"				VALUE = "{ trim( gettoken( l.fields, ',', l.field_pos ) ) }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StandardShipmentFields_Initialize_OrderItems" PARAMETERS = "all_settings var, settings var, shipment var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.orderitem_count"	VALUE = "{ [ g.Module_Library_DB ].OrderItemList_Load_Shipment( l.shipment:id, l.shipment:items ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_OrderItems_WithParts( l.shipment:items, l.orderitem_count ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.item" ARRAY = "l.shipment:items">
		<MvASSIGN NAME = "l.null"			VALUE = "{ [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_ImageTypes( l.item:product_id, l.item:variant_id, l.item, l.all_settings, l.settings:imagetypes, l.settings:constrain, l.settings:b_width, l.settings:b_height ) }">
	</MvFOREACH>

	<MvASSIGN NAME = "l.ordergroup_count"	VALUE = "{ [ g.Module_Feature_TUI_UT ].CommonComponentFields_Merge_BasketOrOrderItem_Groups( l.shipment:items, l.orderitem_count, l.shipment:groups ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvINCLUDE FILE = "modules/component/cmp-include-imagetypes.mv">

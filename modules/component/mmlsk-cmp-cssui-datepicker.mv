<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-DTPK-
| Next Error Code: 5    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-datepicker">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Date Picker">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0800">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "10.08">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, component_prov, skins">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ 'GT_PAGE/' $ l.item $ ':Date Picker Layout' }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.fields:template_code ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, '', l.fields:template_code, l.settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.load_fields }">
		<MvASSIGN NAME = "l.fields:template_code" VALUE = "{ l.templateversion:source }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Admin ].Tab_Visible( l.tab, l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<tr><td colspan="2" width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( l.field_prefix $ 'template_code', l.fields:template_code, 20, 100, l.templateversion:templ_id ) }">
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, '' ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ Generate_Code( l.settings, l.template_source ) }">

	<MvASSIGN NAME = "l.settings:template_filename"	VALUE = "{ tolower( l.page:code ) $ '-' $ tolower( l.item ) $ '.mvc' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.template_source, l.settings, l.settings:template_filename ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:template_filename ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:template_filename, l.source_templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:template_filename, l.dest_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.dest_settings"						VALUE = "{ l.source_settings }">
	<MvASSIGN NAME = "l.dest_settings:template_filename"	VALUE = "{ l.dest_managedtemplate:version:settings:template_filename }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_managedtemplate, '', l.source_templateversion:source, l.dest_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.source_branch:id EQ l.dest_branch:id }">	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-DTPK-00003', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' on branch \'' $ l.dest_branch:name $ '\'' ) }">
	<MvELSE>													<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-DTPK-00004', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' from branch \'' $ l.source_branch:name $ '\' to branch \'' $ l.dest_branch:name $ '\'' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Secure AND len_var( g.Domain:base_surl ) }">	<MvASSIGN NAME = "l.settings:image_base" VALUE = "{ g.Domain:base_surl $ g.AdminGraphics_Path }">
	<MvELSE> 														<MvASSIGN NAME = "l.settings:image_base" VALUE = "{ g.Domain:base_url $ g.AdminGraphics_Path }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.param }">
		<MvASSIGN NAME = "l.settings:id_suffix" VALUE = "{ MakeSessionID() }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:id_suffix" VALUE = "{ l.param }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:template_filename, l.all_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update_Requires_Version" PARAMETERS = "module var, page var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_PostUpdate" PARAMETERS = "module var, page var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Code" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code">
<MIVA STANDARDOUTPUTLEVEL = "text, html"><span id="miva_datepicker_&mvte:datepicker:id_suffix;" class="miva_datepicker">
	<span class="miva_datepicker_calendar_container">
		<div class="miva_datepicker_calendar_month_selector">
			<div id="miva_datepicker_calendar_month_selector_prev_&mvte:datepicker:id_suffix;" class="miva_datepicker_calendar_month_selector_prev"></div>
			<div id="miva_datepicker_calendar_month_selector_title_&mvte:datepicker:id_suffix;" class="miva_datepicker_calendar_month_selector_title">
				<span id="miva_datepicker_calendar_month_selector_title_month_&mvte:datepicker:id_suffix;" class="miva_datepicker_calendar_month_selector_title_month"></span>
				<span id="miva_datepicker_calendar_month_selector_title_year_&mvte:datepicker:id_suffix;" class="miva_datepicker_calendar_month_selector_title_year"></span>
			</div>
			<div id="miva_datepicker_calendar_month_selector_next_&mvte:datepicker:id_suffix;" class="miva_datepicker_calendar_month_selector_next"></div>
		</div>

		<div class="miva_datepicker_calendar_weekday_container">
			<span class="miva_datepicker_calendar_weekday_title miva_datepicker_calendar_day_0">Su</span>
			<span class="miva_datepicker_calendar_weekday_title miva_datepicker_calendar_day_1">Mo</span>
			<span class="miva_datepicker_calendar_weekday_title miva_datepicker_calendar_day_2">Tu</span>
			<span class="miva_datepicker_calendar_weekday_title miva_datepicker_calendar_day_3">We</span>
			<span class="miva_datepicker_calendar_weekday_title miva_datepicker_calendar_day_4">Th</span>
			<span class="miva_datepicker_calendar_weekday_title miva_datepicker_calendar_day_5">Fr</span>
			<span class="miva_datepicker_calendar_weekday_title miva_datepicker_calendar_day_6">Sa</span>
		</div>

		<div id="miva_datepicker_calendar_week_container_&mvte:datepicker:id_suffix;" class="miva_datepicker_calendar_week_container"></div>
		<a href="#" id="miva_datepicker_cancel_&mvte:datepicker:id_suffix;" class="miva_datepicker_cancel">Cancel</a>
	</span>
</span>

<script type="text/javascript">
	function MivaDatePicker( initial_unixtimestamp )
	{
		var self = this;

		this.element_parent											= document.getElementById( 'miva_datepicker_&mvtj:datepicker:id_suffix;' );
		this.element_week_container									= document.getElementById( 'miva_datepicker_calendar_week_container_&mvtj:datepicker:id_suffix;' );
		this.element_month_selector_prev							= document.getElementById( 'miva_datepicker_calendar_month_selector_prev_&mvtj:datepicker:id_suffix;' );
		this.element_month_selector_title_month						= document.getElementById( 'miva_datepicker_calendar_month_selector_title_month_&mvtj:datepicker:id_suffix;' );
		this.element_month_selector_title_year						= document.getElementById( 'miva_datepicker_calendar_month_selector_title_year_&mvtj:datepicker:id_suffix;' );
		this.element_month_selector_next							= document.getElementById( 'miva_datepicker_calendar_month_selector_next_&mvtj:datepicker:id_suffix;' );
		this.element_time_period_am									= document.getElementById( 'miva_datepicker_time_am_&mvtj:datepicker:id_suffix;' );
		this.element_time_period_pm									= document.getElementById( 'miva_datepicker_time_pm_&mvtj:datepicker:id_suffix;' );
		this.element_time_container									= document.getElementById( 'miva_datepicker_time_container_&mvtj:datepicker:id_suffix;' );
		this.element_cancel											= document.getElementById( 'miva_datepicker_cancel_&mvtj:datepicker:id_suffix;' );

		this.element_month_selector_prev.style.backgroundImage		= 'url(\'&mvtj:datepicker:image_base;mm9_calendar_prev.png\')';
		this.element_month_selector_next.style.backgroundImage		= 'url(\'&mvtj:datepicker:image_base;mm9_calendar_next.png\')';
		this.element_month_selector_prev.onclick					= function( event ) { self.Month_Prev(); };
		this.element_month_selector_next.onclick					= function( event ) { self.Month_Next(); };

		this.element_cancel.onclick									= function( event ) { self.Hide(); return false; };

		this.current_date											= initial_unixtimestamp ? new Date( initial_unixtimestamp * 1000 ) : new Date();
		this.current_date.setMilliseconds( 0 );

		this.visible_month											= this.current_date.getMonth();
		this.visible_year											= this.current_date.getFullYear();
		this.picked_day												= this.current_date.getDate();
		this.picked_month											= this.current_date.getMonth();
		this.picked_year											= this.current_date.getFullYear();
		this.datelimit_past											= null;
		this.datelimit_future										= null;
		this.monthlist												= [ 'January', 'February', 'March', 'April', 'May', 'June', 'July', 'August', 'September', 'October', 'November', 'December' ];
	}

	MivaDatePicker.prototype.Calendar_Refresh = function()
	{
		var self = this;
		var i, i_len, element_week, element_day, loaded_date, picked_date, current_date, visible_date_last;

		this.element_week_container.innerHTML				= '';
		this.element_month_selector_title_month.innerHTML	= '';
		this.element_month_selector_title_year.innerHTML	= '';

		visible_date_last									= new Date( this.visible_year, this.visible_month + 1, 0 );
		current_date										= new Date();
		picked_date											= this.GetPickedDate();

		element_week										= document.createElement( 'div' );
		element_week.className								= 'miva_datepicker_calendar_week';

		this.element_week_container.appendChild( element_week );

		for ( i = 0, i_len = visible_date_last.getDate(); i < i_len; i++ )
		{
			loaded_date										= new Date( visible_date_last.getFullYear(), visible_date_last.getMonth(), i + 1, picked_date.getHours(), picked_date.getMinutes(), picked_date.getSeconds() );

			if ( loaded_date.getDay() == 0 && i != 0 )
			{
				element_week								= document.createElement( 'div' );
				element_week.className						= 'miva_datepicker_calendar_week';

				this.element_week_container.appendChild( element_week );
			}

			element_day										= document.createElement( 'span' );
			element_day.className							= 'miva_datepicker_calendar_day miva_datepicker_calendar_day_' + loaded_date.getDay();
			element_day.innerHTML							= loaded_date.getDate();
			element_day.date_year							= loaded_date.getFullYear();
			element_day.date_month							= loaded_date.getMonth();
			element_day.date_day							= loaded_date.getDate();

			element_week.appendChild( element_day );

			if ( this.datelimit_past && ( this.datelimit_past instanceof Date ) && this.DateLimit_Past_DateDisabled( loaded_date, this.datelimit_past ) )
			{
				element_day.className						+= ' miva_datepicker_calendar_day_noselect';
			}
			else if ( this.datelimit_future && ( this.datelimit_future instanceof Date ) && this.DateLimit_Future_DateDisabled( loaded_date, this.datelimit_future ) )
			{
				element_day.className						+= ' miva_datepicker_calendar_day_noselect';
			}
			else
			{
				if ( this.getDayStart( picked_date ).getTime() == this.getDayStart( loaded_date ).getTime() )
				{
					element_day.className					+= ' miva_datepicker_calendar_day_active';
				}

				element_day.onclick							= function( event )
				{
					self.picked_year	= this.date_year;
					self.picked_month	= this.date_month;
					self.picked_day		= this.date_day;

					self.Calendar_Refresh();
					self.Complete();

					if ( event.stopPropagation )
					{
						return event.stopPropagation();
					}

					event.cancelBubble = true;

					if ( event.preventDefault )
					{
						return event.preventDefault();
					}

					event.returnValue = false;

					return false;
				}
			}

			if ( this.getDayStart( loaded_date ).getTime() == this.getDayStart( current_date ).getTime() )
			{
				element_day.className						+= ' miva_datepicker_calendar_day_current';
			}
		}

		this.element_month_selector_title_month.appendChild( document.createTextNode( this.monthlist[ this.visible_month ] ) );
		this.element_month_selector_title_year.appendChild( document.createTextNode( visible_date_last.getFullYear() ) );
	}

	MivaDatePicker.prototype.getDayStart = function( date )
	{
		var day_start;

		day_start = new Date( date.getTime() );
		day_start.setMilliseconds( 0 );
		day_start.setSeconds( 0 );
		day_start.setMinutes( 0 );
		day_start.setHours( 0 );

		return day_start;
	}

	MivaDatePicker.prototype.Show = function()
	{
		this.Calendar_Refresh();
		this.element_parent.style.display = 'block';
	}

	MivaDatePicker.prototype.Hide = function()
	{
		this.element_parent.style.display = 'none';
	}

	MivaDatePicker.prototype.SetDateLimit_Past = function( dateObject )
	{
		this.datelimit_past = dateObject;
		this.datelimit_past.setMilliseconds( 0 );
	}

	MivaDatePicker.prototype.DateLimit_Past_DateDisabled = function( date, datelimit )
	{
		if ( this.getDayStart( date ).getTime() <= this.getDayStart( datelimit ).getTime() )
		{
			return true;
		}

		return false;
	}

	MivaDatePicker.prototype.SetDateLimit_Future = function( dateObject )
	{
		this.datelimit_future = dateObject;
		this.datelimit_future.setMilliseconds( 0 );
	}

	MivaDatePicker.prototype.DateLimit_Future_DateDisabled = function( date, datelimit )
	{
		if ( this.getDayStart( date ).getTime() >= this.getDayStart( datelimit ).getTime() )
		{
			return true;
		}

		return false;
	}

	MivaDatePicker.prototype.Month_Prev = function()
	{
		var date = new Date( this.visible_year, this.visible_month, 0 );

		if ( ( this.datelimit_past && ( date < this.datelimit_past ) ) ||
			 ( this.datelimit_future && ( date > this.datelimit_future ) ) )
		{
			return;
		}

		this.visible_month--;

		if ( this.visible_month == -1 )
		{
			this.visible_month	= 11;
			this.visible_year	= this.visible_year - 1;
		}

		this.Calendar_Refresh();
	}

	MivaDatePicker.prototype.Month_Next = function()
	{
		var date = new Date( this.visible_year, this.visible_month + 1, 1 );

		if ( ( this.datelimit_past && ( date < this.datelimit_past ) ) ||
			 ( this.datelimit_future && ( date > this.datelimit_future ) ) )
		{
			return;
		}

		this.visible_month++;

		if ( this.visible_month == 12 )
		{
			this.visible_month	= 0;
			this.visible_year	= this.visible_year + 1;
		}

		this.Calendar_Refresh();
	}

	MivaDatePicker.prototype.GetPickedDate = function()
	{
		return new Date( this.picked_year, this.picked_month, this.picked_day, 0, 0, 0 );
	}

	MivaDatePicker.prototype.Complete = function()
	{
		this.oncomplete( this.GetPickedDate() );
		this.Hide();
	}

	MivaDatePicker.prototype.oncomplete = function( date ) { ; };
</script><MIVA STANDARDOUTPUTLEVEL = "">
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'r', l.provide_xml, 'Template',	l.template ) OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml, 'Notes',		l.notes ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.template, l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pages" VALUE = "{ [ g.Module_Feature_TUI_DB ].Item_Load_PageList( l.item ) }">
	
	<MvFUNCTIONRETURN VALUE = "{ 'Exports Date Picker Layout template from pages: ' $ l.pages }">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item_Runtime( l.item:id, l.pages ) }">
			<MvREFERENCEARRAY NAME = "l.settings" VARIABLE = "l.page:settings">
				<MvMEMBER NAME = "{ l.item:code }">
			</MvREFERENCEARRAY>

			<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
				<Page_Update code="{ l.page:code }">
					<Item code="{ l.item:code }">
						<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.templateversion:source ) }"></Template>
						<Notes>#Set_Current_Time#</Notes>
					</Item>
				</Page_Update>
			</MvIF>
		</MvFOREACH>
	</MvCAPTURE>
</MvFUNCTION>

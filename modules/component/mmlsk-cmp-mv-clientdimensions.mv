<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CMP-CLD-
| Next Error Code: 8
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-mv-clientdimensions">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Client Dimensions">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0800">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.72">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, data_store, provision_store, vis_store, skins">
</MvFUNCTION>

<MvCOMMENT>
|
| Store-level Module Data Support Feature (data_store)
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'cmp-mv-clientdimensions.mvc', l.null_managedtemplate ) }">
		<MvEVAL EXPR = "{ Generate_Code( l.code ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.code, l.null, 'cmp-mv-clientdimensions.mvc' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT Register_CacheSettings() }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.module:version EQ l.version }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CMP-CLD-00007', 'Module \'' $ l.module:name $ '\' does not support manual upgrade.  New versions may only be obtained through the streaming update system.' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_GlobalDelete_ManagedTemplate_Filename( 'cmp-mv-clientdimensions.mvc' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Edit Store Screen Feature (vis_store)
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Store_Tabs" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "GT_UISETTINGS/CMP-MV-CLIENTDIMENSIONS:Client Dimensions">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( g.ClientDimensions:template_code ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-MV-CLIENTDIMENSIONS', 'ClientDimensions:template_code', g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'cmp-mv-clientdimensions.mvc', l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, '', g.ClientDimensions:template_code, l.null_settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-MV-CLIENTDIMENSIONS', 'ClientDimensions:template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Head" PARAMETERS = "module var, tab" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "text,html,compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( 'cmp-mv-clientdimensions.mvc', l.templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.load_fields }">
		<MvASSIGN NAME = "g.ClientDimensions:template_code" VALUE = "{ l.templateversion:source }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, 'CMP-MV-CLIENTDIMENSIONS' ) }">
		<MvHIDE FIELDS = "g.ClientDimensions">

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_UISETTINGS', 'CMP-MV-CLIENTDIMENSIONS' ) }">
	<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<tr>
			<td valign="top" nowrap>Client Dimensions:</td>
			<td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( 'ClientDimensions:template_code', g.ClientDimensions:template_code, 10, 58, l.templateversion:templ_id ) }">
			</td>
		</tr>
	</table>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Module Provisioning Feature (provision_store)
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Provision_Store" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'r', l.provide_xml, 'Template',	l.template ) OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml, 'Notes',		l.notes ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'cmp-mv-clientdimensions.mvc', l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.template, l.null_settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvCOMMENT>
|
| Component Feature (component)
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].ParseCookies( l.cookies ) }">

	<MvASSIGN NAME = "l.width" 		VALUE = "{ l.cookies:mm\-clientdimensions\-width }">
	<MvASSIGN NAME = "l.height" 	VALUE = "{ l.cookies:mm\-clientdimensions\-height }">
	<MvASSIGN NAME = "l.invalid" 	VALUE = "{ ( len_var( l.width ) EQ 0 ) OR ( len_var( l.height ) EQ 0 ) OR ( NOT isdigit( l.width ) ) OR ( NOT isdigit( l.height ) ) }">

	<MvIF EXPR = "{ l.invalid }">
		<MvASSIGN NAME = "l.settings:detected" 		VALUE = 0>
		<MvASSIGN NAME = "l.settings:width" 		VALUE = "">
		<MvASSIGN NAME = "l.settings:height" 		VALUE = "">
		<MvASSIGN NAME = "l.settings:orientation" 	VALUE = "">

		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.settings:detected" 	VALUE = 1>
	<MvASSIGN NAME = "l.settings:width" 	VALUE = "{ int( l.width ) }">
	<MvASSIGN NAME = "l.settings:height" 	VALUE = "{ int( l.height ) }">

	<MvIF EXPR = "{ l.settings:width GT l.settings:height }">	<MvASSIGN NAME = "l.settings:orientation" VALUE = "landscape">
	<MvELSE>													<MvASSIGN NAME = "l.settings:orientation" VALUE = "portrait">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( 'cmp-mv-clientdimensions.mvc', l.all_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Framework Support Feature (skins)
|
</MvCOMMENT>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "Exports Client Diemnsions">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item var, output var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( 'cmp-mv-clientdimensions.mvc', l.managedtemplateversion ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<Module code="{ encodeentities( l.module:code ) }" feature="component">
			<Template><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].CDATA_Encode( l.managedtemplateversion:source ) }"></Template>
			<Notes>Exported Client Dimensions</Notes>
		</Module>
	</MvCAPTURE>
</MvFUNCTION>

<MvCOMMENT>
|
| Helper Functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "Generate_Code" PARAMETERS = "code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code">
<MIVA STANDARDOUTPUTLEVEL = "text, html"><mvt:if expr="s.request_method NE 'POST'">
	<script language="JavaScript">
		function clientdimensions_cookieIsSet( name )
		{
			var i, cookies, cookie_name, values;

			cookies = document.cookie.split( ';' );

			for ( i = 0; i < cookies.length; i++ )
			{
				values = cookies[ i ].split( '=' );

				if ( values.length )
				{
					cookie_name = values[ 0 ].replace( /^\s+|\s+$/g, '' );

					if ( name == cookie_name )
					{
						return true;
					}
				}
			}

			return false;
		}

		function clientdimensions_getWindowWidth()
		{
			if ( window.innerWidth )														return window.innerWidth;
			else if ( document.documentElement && document.documentElement.clientWidth )	return document.documentElement.clientWidth;
			else if ( document.body && document.body.clientWidth )							return document.body.clientWidth;
		}

		function clientdimensions_getWindowHeight()
		{
			if ( window.innerHeight )														return window.innerHeight;
			else if ( document.documentElement && document.documentElement.clientHeight )	return document.documentElement.clientHeight;
			else if ( document.body && document.body.clientHeight )							return document.body.clientHeight;
		}

		setTimeout( function()
		{
			var clientdimensions_reload;

			clientdimensions_reload = !clientdimensions_cookieIsSet( 'mm-clientdimensions-width' ) || !clientdimensions_cookieIsSet( 'mm-clientdimensions-height' );
			document.cookie 		= 'mm-clientdimensions-width=' + clientdimensions_getWindowWidth();
			document.cookie 		= 'mm-clientdimensions-height=' + clientdimensions_getWindowHeight();

			if ( clientdimensions_reload && clientdimensions_cookieIsSet( 'mm-clientdimensions-width' ) && clientdimensions_cookieIsSet( 'mm-clientdimensions-height' ) )
			{
				if ( window.stop )
				{
					window.stop();
				}

				location.reload( true );
			}

			window.onresize = function( event )
			{
				document.cookie = 'mm-clientdimensions-width=' + clientdimensions_getWindowWidth();
				document.cookie = 'mm-clientdimensions-height=' + clientdimensions_getWindowHeight();
			}
		}, 0 );
	</script>
</mvt:if>

<mvt:if expr="l.settings:clientdimensions:detected">
	<mvt:if expr="l.settings:clientdimensions:width GE 1170">
		<mvt:assign name="l.settings:clientdimensions:class" value="'x-large'" />
	<mvt:elseif expr="l.settings:clientdimensions:width GE 960">
		<mvt:assign name="l.settings:clientdimensions:class" value="'large'" />
	<mvt:elseif expr="l.settings:clientdimensions:width GE 768">
		<mvt:assign name="l.settings:clientdimensions:class" value="'medium'" />
	<mvt:elseif expr="l.settings:clientdimensions:width GE 640">
		<mvt:assign name="l.settings:clientdimensions:class" value="'small'" />
	<mvt:else>
		<mvt:assign name="l.settings:clientdimensions:class" value="'x-small'" />
	</mvt:if>
</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "Register_CacheSettings" STANDARDOUTPUTLEVEL = "">
	<MvLOCKFILE FILE = "{ [ g.Module_Feature_CHE_UT ].Store_CacheSettings_LockFile( g.Store ) }">
		<MvASSIGN NAME = "l.result" VALUE = "{ Register_CacheSettings_LowLevel() }">
	</MvLOCKFILE>

	<MvFUNCTIONRETURN VALUE = "{ l.result }">
</MvFUNCTION>

<MvFUNCTION NAME = "Register_CacheSettings_LowLevel" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Store_Load_ID( g.Store:id, l.store ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.width_cookie" VARIABLE = "l.store:cacheset:cookie">
		<MvMEMBER NAME = "mm-clientdimensions-width">
	</MvREFERENCEARRAY>

	<MvREFERENCEARRAY NAME = "l.height_cookie" VARIABLE = "l.store:cacheset:cookie">
		<MvMEMBER NAME = "mm-clientdimensions-height">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ ISNULL l.width_cookie }">
		<MvASSIGN NAME = "l.width_cookie:mode"	VALUE = "key">
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.height_cookie }">
		<MvASSIGN NAME = "l.height_cookie:mode"	VALUE = "key">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Store_Update_CacheSettings( l.store ) }">
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-PROD-
| Next Error Code: 6
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-prodlayo">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI Product Display Layout">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.1100">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "10.08">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, component_prov, skins">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:imagemachine_enabled"	VALUE = "{ CSSUIProductLayout_AssociatedImageMachine_IsCurrentlyEnabled( g.Edit_Page, l.item ) }">

	<MvASSIGN NAME = "l.tabs"							VALUE = "{ 'GT_PAGE/' $ l.item $ ':Product Display Layout' }">
	<MvIF EXPR = "{ ( NOT l.settings:imagemachine_enabled ) AND ( miva_array_elements( l.settings:imagetypes ) GT 0 ) }">
		<MvASSIGN NAME = "l.tabs"						VALUE = "{ l.tabs $ ',GT_PAGE/' $ l.item $ '_dimensions' $ ':Product Display Image Dimensions' }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.tabs }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.fields:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.fields:price"					VALUE = "{ trim( l.fields:price ) }">
	<MvASSIGN NAME = "l.fields:additionalprice"			VALUE = "{ trim( l.fields:additionalprice ) }">
	<MvASSIGN NAME = "l.fields:displaydiscounts"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:displaydiscounts ) }">
	<MvASSIGN NAME = "l.fields:predictdiscounts"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:predictdiscounts ) }">
	<MvASSIGN NAME = "l.fields:button_wish"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:button_wish ) }">
	<MvASSIGN NAME = "l.fields:button_wish_nodisable"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:button_wish_nodisable ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:b_width ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item $ '_dimensions', l.field_prefix $ 'b_width', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:b_height ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item $ '_dimensions', l.field_prefix $ 'b_height', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ l.fields:advanced }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.fields:template_code ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.settings:advanced"						VALUE = "{ l.fields:advanced }">
	<MvASSIGN NAME = "l.settings:imagemachine_enabled"			VALUE = "{ CSSUIProductLayout_AssociatedImageMachine_WillBeEnabled( g.Edit_Page, l.item ) }">

	<MvIF EXPR = "{ l.fields:advanced }">
		<MvASSIGN NAME = "l.settings:predictdiscounts"			VALUE = "{ l.fields:predictdiscounts }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:field_name"				VALUE = "{ l.fields:field_name }">
		<MvASSIGN NAME = "l.settings:field_code"				VALUE = "{ l.fields:field_code }">
		<MvASSIGN NAME = "l.settings:field_sku"					VALUE = "{ l.fields:field_sku }">
		<MvASSIGN NAME = "l.settings:field_price"				VALUE = "{ l.fields:field_price }">
		<MvASSIGN NAME = "l.settings:field_weight"				VALUE = "{ l.fields:field_weight }">
		<MvASSIGN NAME = "l.settings:field_desc"				VALUE = "{ l.fields:field_desc }">
		<MvASSIGN NAME = "l.settings:price"						VALUE = "{ l.fields:price }">
		<MvASSIGN NAME = "l.settings:additionalprice"			VALUE = "{ l.fields:additionalprice }">
		<MvASSIGN NAME = "l.settings:displaydiscounts"			VALUE = "{ l.fields:displaydiscounts }">
		<MvASSIGN NAME = "l.settings:predictdiscounts"			VALUE = "{ l.settings:displaydiscounts OR ( l.settings:price EQ 'sale' ) }">
		<MvASSIGN NAME = "l.settings:button_wish"				VALUE = "{ l.fields:button_wish }">
		<MvASSIGN NAME = "l.settings:button_wish_nodisable"		VALUE = "{ l.fields:button_wish_nodisable }">
		<MvASSIGN NAME = "l.settings:image"						VALUE = "{ l.fields:image }">
		<MvASSIGN NAME = "l.settings:invmsg"					VALUE = "{ l.fields:invmsg }">
	</MvIF>

	<MvASSIGN NAME = "l.selectedfield_count"					VALUE = "{ [ g.Module_Library_Utilities ].CustomFieldSelect_Selected( l.fields:fields_custom, l.settings:fields_custom ) }">

	<MvCOMMENT>
	|
	| If Point + Click, add the single selected image type (if any) to the list
	| of image types to be initialized.  Otherwise, add all selected image types.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.fields:advanced }">
		<MvASSIGN NAME = "l.imagetype_count"					VALUE = "{ [ g.Module_Library_Utilities ].ImageTypeSelect_Selected( l.fields:imagetypes, l.settings:imagetypes ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:imagetypes"				VALUE = "">

		<MvIF EXPR = "{ ( 'type:' IN l.fields:image ) EQ 1 }">	
			<MvASSIGN NAME = "l.settings:imagetypes" INDEX = 1	VALUE = "{ substring( l.fields:image, 6, len( l.fields:image ) - 5 ) }">
		</MvIF>
	</MvIF>

	<MvCOMMENT>
	|
	| These settings are common between advanced and point + click mode
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.settings:constrain"		VALUE = "{ l.fields:constrain }">
	<MvASSIGN NAME = "l.settings:b_width"		VALUE = "{ l.fields:b_width }">
	<MvASSIGN NAME = "l.settings:b_height"		VALUE = "{ l.fields:b_height }">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-PROD-00003', l.module:name $ ' settings updated' ) }">

	<MvIF EXPR = "{ NOT l.fields:advanced }">
		<MvEVAL EXPR = "{ Generate_Code( l.settings, l.fields:template_code ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, '', l.fields:template_code, l.settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update_Requires_Version" PARAMETERS = "module var, page var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_PostUpdate" PARAMETERS = "module var, page var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvCOMMENT>
	|
	| Load all image types
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.imagetype_count"	VALUE = "{ [ g.Module_Library_DB ].ImageTypeList_Load_All( l.imagetypes ) }">

	<MvIF EXPR = "{ l.load_fields }">
		<MvASSIGN NAME = "l.fields:field_name"					VALUE = "{ l.settings:field_name }">
		<MvASSIGN NAME = "l.fields:field_code"					VALUE = "{ l.settings:field_code }">
		<MvASSIGN NAME = "l.fields:field_sku"					VALUE = "{ l.settings:field_sku }">
		<MvASSIGN NAME = "l.fields:field_price"					VALUE = "{ l.settings:field_price }">
		<MvASSIGN NAME = "l.fields:field_weight"				VALUE = "{ l.settings:field_weight }">
		<MvASSIGN NAME = "l.fields:field_desc"					VALUE = "{ l.settings:field_desc }">

		<MvIF EXPR = "{ ISNULL l.settings:price }">
			<MvASSIGN NAME = "l.fields:price"					VALUE = "base">
		<MvELSE>
			<MvASSIGN NAME = "l.fields:price"					VALUE = "{ l.settings:price }">
		</MvIF>

		<MvASSIGN NAME = "l.fields:additionalprice"				VALUE = "{ l.settings:additionalprice }">
		<MvASSIGN NAME = "l.fields:displaydiscounts"			VALUE = "{ l.settings:displaydiscounts }">
		<MvASSIGN NAME = "l.fields:predictdiscounts"			VALUE = "{ l.settings:predictdiscounts }">
		<MvASSIGN NAME = "l.fields:button_wish"					VALUE = "{ l.settings:button_wish }">
		<MvASSIGN NAME = "l.fields:button_wish_nodisable"		VALUE = "{ l.settings:button_wish_nodisable }">
		<MvASSIGN NAME = "l.fields:image"						VALUE = "{ l.settings:image }">
		<MvASSIGN NAME = "l.fields:invmsg"						VALUE = "{ l.settings:invmsg }">

		<MvIF EXPR = "{ ISNULL l.settings:constrain }">
			<MvASSIGN NAME = "l.fields:constrain"				VALUE = "B">
		<MvELSE>
			<MvASSIGN NAME = "l.fields:constrain"				VALUE = "{ l.settings:constrain }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:b_width }">
			<MvASSIGN NAME = "l.fields:b_width"					VALUE = 274>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:b_width"					VALUE = "{ l.settings:b_width }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:b_height }">
			<MvASSIGN NAME = "l.fields:b_height"				VALUE = 274>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:b_height"				VALUE = "{ l.settings:b_height }">
		</MvIF>

		<MvASSIGN NAME = "l.fields:advanced"					VALUE = "{ l.settings:advanced }">
		<MvASSIGN NAME = "l.fields:template_code"				VALUE = "{ l.templateversion:source }">
		<MvASSIGN NAME = "l.fields:template_filename"			VALUE = "{ l.settings:template_filename }">

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].InitializeCustomFieldSelect( l.settings:fields_custom, l.fields:fields_custom ) }">

		<MvASSIGN NAME = "l.fields:imagetypes"					VALUE = "{ l.settings:imagetypes }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'advanced,field_name,field_code,field_sku,field_price,field_weight,field_desc,price,additionalprice,displaydiscounts,predictdiscounts,button_wish,button_wish_nodisable,image,invmsg,template_filename,template_code,fields_custom,imagetypes' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_filename' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<MvIF EXPR = "{ NOT l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_code' ) }">
		<MvELSE>
			<tr><td colspan="2" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( l.field_prefix $ 'template_code', l.fields:template_code, 20, 100, l.templateversion:templ_id ) }">
			</td></tr>

			<MvIF EXPR = "{ l.customfield_count }">
				<tr><td colspan="2">&nbsp;</td></tr>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'field_name,field_code,field_sku,field_price,field_weight,field_desc' ) }">
		<MvELSE>
			<tr><td align="left" valign="top" nowrap>
				<b>Product Fields:</b>
			</td><td align="left" width="100%">
				<table border="0" cellpadding="0" cellspacing="0">
				<tr><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_name, l.field_prefix $ 'field_name', 1, 'Product Name' ) }">
				</td><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_code, l.field_prefix $ 'field_code', 1, 'Product Code' ) }">
				</td></tr>

				<tr><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_sku, l.field_prefix $ 'field_sku', 1, 'Product SKU' ) }">
				</td><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_price, l.field_prefix $ 'field_price', 1, 'Price' ) }">
				</td></tr>

				<tr><td align="left" nowrap>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_weight, l.field_prefix $ 'field_weight', 1, 'Weight' ) }">
				</td><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_desc, l.field_prefix $ 'field_desc', 1, 'Description' ) }">
				</td></tr>
				</table>
			</td></tr>
		</MvIF>

		<MvASSIGN NAME = "l.customfield_count"	VALUE = "{ [ g.Module_Library_Utilities ].ProductCustomFieldList_Load( l.customfields ) }">
		<MvIF EXPR = "{ l.customfield_count }">
			<tr><td align="left" valign="top" nowrap>
				<b>Custom Fields:</b>
			</td><td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCustomFieldSelect( l.field_prefix $ 'fields_custom', l.fields:fields_custom, l.customfields, l.customfield_count ) }">
			</td></tr>
		</MvIF>

		<MvIF EXPR = "{ l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'price,additionalprice,discounts,button_wish' ) }">

			<tr><td>
				&nbsp;
			</td><td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:predictdiscounts, l.field_prefix $ 'predictdiscounts', 1, 'Calculate Predicted Discounts and Sale Price' ) }">
			</td></tr>
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'predictdiscounts' ) }">

			<tr><td colspan="2">&nbsp;</td></tr>

			<tr><td align="left" nowrap valign="top">
				<b>Displayed Price:</b>
			</td><td align="left" nowrap width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'price', 'retail',	l.fields:price, 'Retail Price' ) }"><br />
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'price', 'base',	l.fields:price, 'Base Price (includes any legacy price group discounts but does not reflect sales, coupons or discounts applied in the basket)' ) }"><br />
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'price', 'sale',	l.fields:price, 'Sale Price (includes legacy price groups and predicted discounts)' ) }"><br />
			</td></tr>

			<tr><td align="left" nowrap valign="top">
				<b>Additional Price Display:</b>
			</td><td align="left" nowrap width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'additionalprice', '',			l.fields:additionalprice, 'None' ) }"><br />
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'additionalprice', 'retail',	l.fields:additionalprice, 'Retail Price' ) }"><br />
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'additionalprice', 'base',		l.fields:additionalprice, 'Base Price (includes any legacy price group discounts but does not reflect sales, coupons or discounts applied in the basket)' ) }"><br />
			</td></tr>

			<tr><td>
				&nbsp;
			</td><td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:displaydiscounts, l.field_prefix $ 'displaydiscounts', 1, 'Display Predicted Discounts' ) }">
			</td></tr>

			<tr><td>
				Buttons:
			</td><td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:button_wish, l.field_prefix $ 'button_wish', 1, 'Add To Wish List' ) }">
			</td></tr>

			<tr><td colspan="2">&nbsp;</td></tr>
		</MvIF>

		<MvIF EXPR = "{ NOT l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'imagetypes' ) }">
		<MvELSE>
			<MvIF EXPR = "{ ( NOT l.settings:imagemachine_enabled ) AND ( l.imagetype_count GT 0 ) }">
				<tr><td align="left" valign="top" nowrap>
					<b>Product Images:</b>
				</td><td align="left" width="100%">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawImageTypeSelect( l.field_prefix $ 'imagetypes', l.fields:imagetypes, l.imagetypes, l.imagetype_count ) }">
				</td></tr>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'image,invmsg,button_wish_nodisable' ) }">
		<MvELSE>
			<MvIF EXPR = "{ l.settings:imagemachine_enabled }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'image' ) }">
			<MvELSE>
				<tr><td align="left" nowrap>
					<b>Image:</b>
				</td><td align="left" width="100%">
					<select name="{ encodeentities( l.field_prefix ) $ 'image' }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'N', l.fields:image, 'None' ) }">

						<MvFOREACH ITERATOR = "l.imagetype" ARRAY = "l.imagetypes" COUNT = "{ l.imagetype_count }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'type:' $ l.imagetype:code, l.fields:image, encodeentities( l.imagetype:descrip ) ) }">
						</MvFOREACH>

						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'T', l.fields:image, 'Legacy Thumbnail' ) }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'F', l.fields:image, 'Legacy Full-Sized' ) }">
					</select>
				</td></tr>
			</MvIF>

			<tr><td align="left" nowrap>
				<b>Inventory Level Message:</b>
			</td><td width="100%">
				<select name="{ encodeentities( l.field_prefix ) $ 'invmsg' }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'N', l.fields:invmsg, 'None' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'S', l.fields:invmsg, 'Short' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'L', l.fields:invmsg, 'Long' ) }">
				</select>
			</td></tr>

			<tr><td>
				&nbsp;
			</td><td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:button_wish_nodisable, l.field_prefix $ 'button_wish_nodisable', 1, 'Do not disable wish list button based on inventory level' ) }">
			</td></tr>
		</MvIF>

		<tr><td colspan="2">&nbsp;</td></tr>

		<tr><td>
			&nbsp;
		</td><td width="100%">
			<input type="hidden" name="{ encodeentities( l.field_prefix ) $ 'advanced' }" value="{ encodeentities( l.fields:advanced ) }">
			<MvIF EXPR = "{ l.fields:advanced }">
				<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 0 ); return false;' }">Point + Click Mode</a>
			<MvELSE>
				<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 1 ); return false;' }">Advanced Mode</a>
			</MvIF>
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, l.item $ '_dimensions' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'constrain,b_width,b_height' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item $ '_dimensions' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<tr>
		<td align="left" valign="top" nowrap><b>Product Image:</b></td>
		<td align="left" width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'constrain', 'N', l.fields:constrain, 'No constraints' ) }"><br>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'constrain', 'B', l.fields:constrain, 'Resize to fit within bounding box:' ) }"> 
			<input type="text" name="{ encodeentities( l.field_prefix ) $ 'b_width' }" value="{ encodeentities( l.fields:b_width ) }" style="text-align:right;" size="5" /> x 
			<input type="text" name="{ encodeentities( l.field_prefix ) $ 'b_height' }" value="{ encodeentities( l.fields:b_height ) }" style="text-align:right;" size="5" /> pixels
		</td>
		</tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:field_name"				VALUE = "1">
	<MvASSIGN NAME = "l.settings:field_code"				VALUE = "1">
	<MvASSIGN NAME = "l.settings:field_sku"					VALUE = "0">
	<MvASSIGN NAME = "l.settings:field_price"				VALUE = "1">
	<MvASSIGN NAME = "l.settings:field_desc"				VALUE = "1">
	<MvASSIGN NAME = "l.settings:field_weight"				VALUE = "1">
	<MvASSIGN NAME = "l.settings:price"						VALUE = "base">
	<MvASSIGN NAME = "l.settings:additionalprice"			VALUE = "">
	<MvASSIGN NAME = "l.settings:displaydiscounts"			VALUE = 0>
	<MvASSIGN NAME = "l.settings:predictdiscounts"			VALUE = 0>
	<MvASSIGN NAME = "l.settings:button_wish"				VALUE = 1>
	<MvASSIGN NAME = "l.settings:button_wish_nodisable"		VALUE = 1>
	<MvASSIGN NAME = "l.settings:image"						VALUE = "F">
	<MvASSIGN NAME = "l.settings:invmsg"					VALUE = "L">
	<MvASSIGN NAME = "l.settings:constrain"					VALUE = "N">
	<MvASSIGN NAME = "l.settings:b_width"					VALUE = "274">
	<MvASSIGN NAME = "l.settings:b_height"					VALUE = "274">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:imagemachine_enabled"	VALUE = "{ CSSUIProductLayout_AssociatedImageMachine_EnabledAtAssign( l.page, l.item, l.settings ) }">

	<MvEVAL EXPR = "{ Generate_Code( l.settings, l.template_source ) }">

	<MvASSIGN NAME = "l.settings:template_filename"	VALUE = "{ tolower( l.page:code ) $ '-' $ tolower( l.item ) $ '.mvc' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.template_source, l.settings, l.settings:template_filename ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'product_attributes' )  OR
					NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'buttons' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:template_filename ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:template_filename, l.source_templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:template_filename, l.dest_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.dest_template_filename" 			VALUE = "{ l.dest_settings:template_filename }">
	<MvASSIGN NAME = "l.dest_settings"						VALUE = "{ l.source_settings }">
	<MvASSIGN NAME = "l.dest_settings:template_filename"	VALUE = "{ l.dest_template_filename }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_managedtemplate, '', l.source_templateversion:source, l.dest_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.source_branch:id EQ l.dest_branch:id }">	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-PROD-00004', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' on branch \'' $ l.dest_branch:name $ '\'' ) }">
	<MvELSE>													<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-PROD-00005', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' from branch \'' $ l.source_branch:name $ '\' to branch \'' $ l.dest_branch:name $ '\'' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT l.all_settings:product:id  }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_CustomField_Lookup( l.all_settings, l.settings:fields_custom ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_CustomFields( l.all_settings:product:id, l.all_settings:product, l.all_settings, l.settings:fields_custom ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.null"							VALUE = "{ [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_ImageTypes( l.all_settings:product:id, 0, l.all_settings:product, l.all_settings, l.settings:imagetypes, l.settings:constrain, l.settings:b_width, l.settings:b_height ) }">
	<MvASSIGN NAME = "l.have_unprecalculated_product"	VALUE = 1>

	<MvIF EXPR = "{ ( l.settings:price EQ 'sale' ) OR l.settings:advanced }">
		<MvIF EXPR = "{ [ g.Module_Feature_PGR_UT ].DiscountSignature_Calculate_Cached( g.Basket:cust_id, l.discount_signature ) }">
			<MvIF EXPR = "{ NOT ISNULL l.discount_signature }">
				<MvIF EXPR = "{ [ g.Module_Feature_PGR_DB ].DiscountPrice_Load( l.discount_signature, l.all_settings:product:id, l.discountprice ) }">
					<MvASSIGN NAME = "l.all_settings:product:price"				VALUE = "{ l.discountprice:price }">
					<MvASSIGN NAME = "l.all_settings:product:formatted_price"	VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.all_settings:product:price ) }">
					<MvASSIGN NAME = "l.have_unprecalculated_product" 			VALUE = 0>
				</MvIF>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.settings:predictdiscounts OR ( ( ( l.settings:price EQ 'sale' ) OR l.settings:advanced ) AND l.have_unprecalculated_product ) }">
		<MvASSIGN NAME = "l.subterm_id" VALUE = 0>

		<MvIF EXPR = "{ [ g.Module_Feature_SUB_DB ].ProductSubscriptionSettings_Load_Product_Cached( l.all_settings:product:id, l.subscription ) }">
			<MvIF EXPR = "{ l.subscription:enabled AND l.subscription:mandatory }">
				<MvASSIGN NAME = "l.productsubscriptionterm_count" VALUE = "{ [ g.Module_Feature_SUB_DB ].ProductSubscriptionTermList_Load_Product_Cached( l.all_settings:product:id, l.productsubscriptionterms ) }">

				<MvIF EXPR = "{ l.productsubscriptionterm_count }">
					<MvASSIGN NAME = "l.subterm_id" VALUE = "{ l.productsubscriptionterms[ 1 ]:id }">
				</MvIF>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_Discounts_Runtime_WithProductSubscriptionTerm( l.all_settings:product, l.subterm_id ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:template_filename, l.all_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUIProductLayout_AssociatedImageMachine_IsCurrentlyEnabled" PARAMETERS = "page_code, item_code" STANDARDOUTPUTLEVEL = "">
    <MvASSIGN NAME = "l.imagemachine_item_code" VALUE = "{ l.item_code $ '_imagemachine' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Item_Load_Code( l.imagemachine_item_code, l.item ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_Code( l.page_code, l.page ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].PageXItem_Load( l.page:id, l.item:id, l.pagexitem ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.imagemachine_settings" VARIABLE = "l.page">
		<MvMEMBER NAME = "settings">
		<MvMEMBER NAME = "{ l.imagemachine_item_code }">
	</MvREFERENCEARRAY>

	<MvFUNCTIONRETURN VALUE = "{ l.imagemachine_settings:enabled }">
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUIProductLayout_AssociatedImageMachine_EnabledAtAssign" PARAMETERS = "page var, item_code, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:imagemachine_enabled }">
		<MvFUNCTIONRETURN VALUE = "{ l.settings:imagemachine_enabled }">
	</MvIF>

    <MvASSIGN NAME = "l.imagemachine_item_code" VALUE = "{ l.item_code $ '_imagemachine' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Item_Load_Code( l.imagemachine_item_code, l.item ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].PageXItem_Load( l.page:id, l.item:id, l.pagexitem ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.imagemachine_settings" VARIABLE = "l.page">
		<MvMEMBER NAME = "settings">
		<MvMEMBER NAME = "{ l.imagemachine_item_code }">
	</MvREFERENCEARRAY>

	<MvFUNCTIONRETURN VALUE = "{ l.imagemachine_settings:enabled }">
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUIProductLayout_AssociatedImageMachine_WillBeEnabled" PARAMETERS = "page_code, item_code" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.imagemachine_item_code"	VALUE = "{ l.item_code $ '_imagemachine' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Item_Load_Code( l.imagemachine_item_code, l.item ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_Code( l.page_code, l.page ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| Because removes are processed before this function will be called, if the imagemachine was unassigned on
	| this update, PageXItem_Load will return 0, which simplifies this function (slightly).
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].PageXItem_Load( l.page:id, l.item:id, l.pagexitem ) }">
		<MvCOMMENT>
		|
		| If the imagemachine item is already assigned, then look at its global "fields" variable
		| to see if it will be enabled after this update
		|
		</MvCOMMENT>

		<MvREFERENCEARRAY NAME = "l.imagemachine_settings" VARIABLE = "g.Page_Settings">
			<MvMEMBER NAME = "{ l.imagemachine_item_code }">
		</MvREFERENCEARRAY>

		<MvFUNCTIONRETURN VALUE = "{ l.imagemachine_settings:enabled }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| If the imagemachine item is not currently assigned, then we have to see if it IS being assigned as
	| part of this update, and, if so, if it has existing settings that say it is enabled
	|
	</MvCOMMENT>

	<MvFOREACH INDEX = "l.item_pos" ITERATOR = "l.item" ARRAY = "g.Page_Item">
		<MvIF EXPR = "{ l.item EQ l.imagemachine_item_code }">
			<MvIF EXPR = "{ g.Page_Item_Assigned[ l.item_pos ] }">
				<MvCOMMENT>
				|
				| The item is being assigned, so the enabled status depends on whether
				| there are existing settings on the page already, and the enabled flag
				| is set to 1.
				|
				</MvCOMMENT>

				<MvIF EXPR = "{ NOT miva_member_exists( l.page:settings, l.imagemachine_item_code ) }">
					<MvFUNCTIONRETURN VALUE = 0>
				</MvIF>

				<MvREFERENCEARRAY NAME = "l.imagemachine_settings" VARIABLE = "l.page">
					<MvMEMBER NAME = "settings">
					<MvMEMBER NAME = "{ l.imagemachine_item_code }">
				</MvREFERENCEARRAY>

				<MvFUNCTIONRETURN VALUE = "{ l.imagemachine_settings:enabled }">
			</MvIF>

			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvCOMMENT>
	|
	| The item is not assigned, and is not being assigned
	|
	</MvCOMMENT>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Code" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:price EQ 'retail' }">
		<MvASSIGN NAME = "l.price_variable"	VALUE = "l.settings:product:retail">
		<MvASSIGN NAME = "l.price_token"	VALUE = "&mvt:product:formatted_retail;">
	<MvELSEIF EXPR = "{ l.settings:price EQ 'sale' }">
		<MvASSIGN NAME = "l.price_variable"	VALUE = "l.settings:product:price">
		<MvASSIGN NAME = "l.price_token"	VALUE = "&mvt:product:formatted_price;">
	<MvELSE>
		<MvASSIGN NAME = "l.price_variable"	VALUE = "l.settings:product:base_price">
		<MvASSIGN NAME = "l.price_token"	VALUE = "&mvt:product:formatted_base_price;">
	</MvIF>

	<MvCAPTURE VARIABLE = "l.code">
	<MvIF EXPR = "{ l.settings:field_name }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
<h1>&mvt:product:name;</h1><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
<div class="product-details-1"><MIVA STANDARDOUTPUTLEVEL = "">
	<MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:imagemachine_enabled }"><MIVA STANDARDOUTPUTLEVEL = "text, html"><div class="product-image"><img id="main_image" src="graphics/en-US/cssui/blank.gif" alt="&mvte:product:name;" /></div>
	<MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSEIF EXPR = "{ l.settings:image EQ 'T' }"><MIVA STANDARDOUTPUTLEVEL = "text, html"><mvt:if expr="NOT ISNULL l.settings:product:thumbnail">
	<div class="product-thumbnail"><img id="main_image" src="&mvte:product:thumbnail;" alt="&mvte:product:name;" /></div>
	<mvt:else>
	<div class="image-not-available product-thumbnail"><span></span><img id="main_image" alt="&mvte:product:name;" /></div>
	</mvt:if>
	<MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSEIF EXPR = "{ l.settings:image EQ 'F' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<mvt:if expr="NOT ISNULL l.settings:product:image">
	<div class="product-image"><img id="main_image" src="&mvte:product:image;" alt="&mvte:product:name;" /></div>
	<mvt:else>
	<div class="image-not-available product-image"></div>
	</mvt:if>
	<MvELSEIF EXPR = "{ ( 'type:' IN l.settings:image ) EQ 1 }">
	<MvASSIGN NAME = "l.imagetype"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( substring( l.settings:image, 6, len( l.settings:image ) - 5 ) ) }">
	<div class="product-image"><img id="main_image" src="{ '&mvte:product:imagetypes:' $ l.imagetype $ ';' }" alt="&mvte:product:name;" /></div>
	</MvIF>
	<MvIF EXPR = "{ l.settings:imagemachine_enabled }"><MIVA STANDARDOUTPUTLEVEL = "text, html"><ul id="thumbnails" class="thumbnails"></ul>
	<div id="closeup_div" class="closeup"><img id="closeup_image" src="graphics/en-US/cssui/blank.gif" alt="" /><div><a id="closeup_close">close</a></div></div>
	<mvt:item name="product_display_imagemachine" param="body_deferred:product:id" />
	<div class="clear product-image-margin"></div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MvIF EXPR = "{ l.settings:field_code }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<div class="product-code">Code: <span class="bold">&mvt:product:code;</span></div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MvIF EXPR = "{ l.settings:field_sku }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<div class="product-sku">SKU: <span class="bold">&mvt:product:sku;</span></div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MvIF EXPR = "{ l.settings:field_price }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<div class="product-price">
	Price: 
	<span id="price-value-additional" style="text-decoration: line-through"><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( l.settings:additionalprice EQ 'retail' ) AND ( l.settings:price NE 'retail' ) }">
	<MIVA STANDARDOUTPUTLEVEL = "text, html"><mvt:if expr="{ 'l.settings:product:retail GT ' $ l.price_variable }">&mvt:product:formatted_retail;</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSEIF EXPR = "{ ( l.settings:additionalprice EQ 'base' ) AND ( l.settings:price NE 'base' ) }">
	<MIVA STANDARDOUTPUTLEVEL = "text, html"><mvt:if expr="{ 'l.settings:product:base_price GT ' $ l.price_variable }">&mvt:product:formatted_base_price;</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>

	<MIVA STANDARDOUTPUTLEVEL = "text, html"></span>

	<span id="price-value" class="bold"><MvEVAL EXPR = "{ l.price_token }"></span>

	<div id="product-discounts"><MIVA STANDARDOUTPUTLEVEL = "">

	<MvIF EXPR = "{ l.settings:displaydiscounts }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<mvt:foreach iterator="discount" array="product:discounts">
		<div class="product-discount">&mvt:discount:descrip;: &mvt:discount:formatted_discount;</div>
	</mvt:foreach><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
	</div>
	</div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MvIF EXPR = "{ l.settings:field_weight }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<mvt:if expr="l.settings:product:weight NE 0">
	<div class="product-weight">Shipping Weight: <span id="product-weight-value" class="bold" data-mm-formatted>&mvt:product:formatted_weight;</span></div>
	</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>

	<MvASSIGN NAME = "l.customfield_pos"		VALUE = 1>
	<MvASSIGN NAME = "l.customfield_count"		VALUE = "{ miva_array_elements( l.settings:fields_custom ) }">
	<MvWHILE EXPR = "{ l.customfield_pos LE l.customfield_count }">
		<MvASSIGN NAME = "l.safe_module_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.settings:fields_custom[ l.customfield_pos ]:module ) }">
		<MvASSIGN NAME = "l.safe_field_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.settings:fields_custom[ l.customfield_pos ]:code ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="{ 'NOT ISNULL l.settings:product:customfield_values:' $ l.safe_module_code $ ':' $ l.safe_field_code }">
		<div class="custom-field" id="{ l.safe_module_code $ '-' $ l.safe_field_code }">&mvt:customfield_names:<MvEVAL EXPR = "{ l.safe_module_code }">:<MvEVAL EXPR = "{ l.safe_field_code }">;: <span class="bold">&mvt:product:customfield_values:<MvEVAL EXPR = "{ l.safe_module_code }">:<MvEVAL EXPR = "{ l.safe_field_code }">;</span></div>
		</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
		<MvASSIGN NAME = "l.customfield_pos"	VALUE = "{ l.customfield_pos + 1 }">
	</MvWHILE>

	<MvIF EXPR = "{ l.settings:invmsg NE 'N' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<div id="inventory-message">
		<mvt:if expr="l.settings:product:inv_active"><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ l.settings:invmsg EQ 'L' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		&mvt:product:inv_long;<MIVA STANDARDOUTPUTLEVEL = "">
		<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
		&mvt:product:inv_short;<MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
		</mvt:if>
	</div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<div class="product-quantity">Quantity in Basket:
		<mvt:if expr="l.settings:product:quantity EQ 0">
		<span class="italic">None</span>
		<mvt:else>
		<span class="bold">&mvt:product:quantity;</span>
		</mvt:if>
	</div>
</div>
<div class="product-details-2"><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:field_desc }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<div class="product-description">&mvt:product:descrip;</div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<form name="add" method="post" action="&mvte:urls:BASK:auto;">
	<div id="swatches" class="swatches"></div>
	<div class="clear"></div>
	<input type="hidden" name="Old_Screen" value="&mvte:global:Screen;" />
	<input type="hidden" name="Old_Search" value="&mvte:global:Search;" />
	<input type="hidden" name="Action" value="ADPR" />
	<input type="hidden" name="Product_Code" value="&mvte:product:code;" />
	<input type="hidden" name="Category_Code" value="&mvte:global:category_code;" />
	<input type="hidden" name="Offset" value="&mvte:global:Offset;" />
	<input type="hidden" name="AllOffset" value="&mvte:global:AllOffset;" />
	<input type="hidden" name="CatListingOffset" value="&mvte:global:CatListingOffset;" />
	<input type="hidden" name="RelatedOffset" value="&mvte:global:RelatedOffset;" />
	<input type="hidden" name="SearchOffset" value="&mvte:global:SearchOffset;" />
	<div class="product-attributes"><mvt:item name="product_attributes" param="product:id" /></div><MIVA STANDARDOUTPUTLEVEL = "">

	<MvIF EXPR = "{ l.settings:button_wish }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<mvt:if expr="l.settings:subscription:enabled AND l.settings:subscription:mandatory">
		<mvt:if expr="l.settings:product:inv_level NE 'out'"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<div class="purchase-buttons">
				Quantity: <input type="text" name="Quantity" value="1" class="product-quantity-input textfield" /> <mvt:item name="buttons" param="AddToBasket" />
			</div>
		</mvt:if>
	<mvt:else>
		<div class="purchase-buttons">
			Quantity: <input type="text" name="Quantity" value="1" class="product-quantity-input textfield" />

			<div class="purchase-buttons-spacer">
				<mvt:if expr="l.settings:product:inv_level NE 'out'">
					<span onclick="document.forms.add.action = '&mvtj:urls:BASK:auto;'; document.forms.add.elements.Action.value = 'ADPR';">
						<mvt:item name="buttons" param="AddToBasket" />
					</span>
				</mvt:if>

				<MvIF EXPR = "{ l.settings:button_wish_nodisable }"><span data-mmnodisable="true"></MvIF><span onclick="document.forms.add.action = '&mvtj:urls:WISH:secure;'; document.forms.add.elements.Action.value = 'ATWL';"><mvt:item name="buttons" param="AddToWishList" /></span><MvIF EXPR = "{ l.settings:button_wish_nodisable }"></span></MvIF>
			</div>
		</div>
	</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<mvt:if expr="l.settings:product:inv_level NE 'out'"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<div class="purchase-buttons">
			Quantity: <input type="text" name="Quantity" value="1" class="product-quantity-input textfield" /> <mvt:item name="buttons" param="AddToBasket" />
		</div>
	</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
	</form>
</div>
<div class="clear"></div><MIVA STANDARDOUTPUTLEVEL = "">
</MvCAPTURE>
</MvFUNCTION>

<MvCOMMENT>
|
| Component Provision Feature (component_prov)
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.working_settings"	VALUE = "{ l.settings }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml,	'Image',						l.working_settings:image,			'Thumbnail,None,Full-Sized,Type',	'T,N,F,TYPE' )			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml,	'InventoryMessage',				l.working_settings:invmsg,			'Long,Short,None',					'L,S,N' )				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O', l.provide_xml,	'ProductsPerPage',				l.working_settings:per_page )																	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml, 'PriceDisplay',					l.working_settings:price,			'Retail,Base,Sale',					'retail,base,sale' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml, 'AdditionalPriceDisplay',		l.working_settings:additionalprice,	'None,Retail,Base',					',retail,base' )		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml, 'DisplayDiscounts',				l.working_settings:displaydiscounts )															OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml, 'PredictDiscounts',				l.working_settings:predictdiscounts )															OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml, 'AddToWishListIgnoreInventory',	l.working_settings:button_wish_nodisable )														OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'O', l.provide_xml,	'Notes',						l.notes ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_List( 'O', l.provide_xml, 'ImageMachineEnabled', l.working_settings:imagemachine_enabled,	'Yes,No',	'1,0' ) }">

	<MvIF EXPR = "{ l.working_settings:image EQ 'TYPE' }">
		<MvASSIGN NAME = "l.working_settings:imagetypes"			VALUE = "">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Code( 'R', l.provide_xml, 'TypeCode', l.working_settings:image ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Must provide image type code' ) }">
		</MvIF>

		<MvIF EXPR = "{ [ g.Module_Library_DB ].ImageType_Load_Code( l.working_settings:image, l.imagetype ) }">
			<MvASSIGN NAME = "l.working_settings:image"				VALUE = "{ 'type:' $ l.imagetype:code }">
			<MvASSIGN NAME = "l.working_settings:imagetypes[ 1 ]"	VALUE = "{ l.imagetype:code }">
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Image type does not exist' ) }">
			<MvASSIGN NAME = "l.working_settings:image"				VALUE = "N">
		</MvIF>
	<MvELSEIF EXPR = "{ l.working_settings:image EQ 'N' }">
		<MvASSIGN NAME = "l.working_settings:imagetypes" VALUE = "">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'ImageTypes' ) }">
		<MvIF EXPR = "{ NOT ComponentModule_Provision_ImageTypes( l.module, l.provide_xml:tags:ImageTypes[ 1 ], l.working_settings ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to provision multiple image types' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'ImageDimensions' ) }">
		<MvASSIGN NAME = "l.dimensions" VALUE = "{ l.provide_xml:tags:ImageDimensions[ 1 ] }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Boolean( l.dimensions, 'constrain', l.constrain ) }">	
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvIF EXPR = "{ NOT l.constrain }">
			<MvASSIGN NAME = "l.working_settings:constrain" VALUE = "N">
		<MvELSE>
			<MvASSIGN NAME = "l.working_settings:constrain" VALUE = "B">

			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer( 'R', l.dimensions, 'Width',	l.working_settings:b_width ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer( 'R', l.dimensions, 'Height',	l.working_settings:b_height ) }">
				<MvFUNCTIONRETURN>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Fields' ) }">
		<MvIF EXPR = "{ NOT ComponentModule_Provision_Fields( l.module, l.provide_xml:tags:Fields[ 1 ], l.working_settings ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Buttons' ) }">
		<MvEVAL EXPR = "{ ComponentModule_Provision_Buttons( l.module, l.provide_xml:tags:Buttons[ 1 ], l.working_settings ) }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
		<MvASSIGN NAME = "l.source"						VALUE = "{ trim( l.provide_xml:tags:Template[ 1 ]:value ) }">
		<MvASSIGN NAME = "l.working_settings:advanced"	VALUE = 1>
	<MvELSE>
		<MvASSIGN NAME = "l.working_settings:advanced"	VALUE = 0>

		<MvEVAL EXPR = "{ Generate_Code( l.working_settings, l.source ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.source, l.working_settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings" VALUE = "{ l.working_settings }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision_Buttons" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:button_wish" VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'AddToWishList' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision_Fields" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:field_name"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_code"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_sku"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_price"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_weight"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_desc"	VALUE = 0>

	<MvASSIGN NAME = "l.settings:fields_custom"	VALUE = "">
	<MvASSIGN NAME = "l.customfield_count"		VALUE = 0>

	<MvASSIGN NAME = "l.child_pos"				VALUE = 1>
	<MvASSIGN NAME = "l.child_count"			VALUE = "{ miva_array_elements( l.provide_xml:children ) }">

	<MvWHILE EXPR = "{ l.child_pos LE l.child_count }">
		<MvASSIGN NAME = "l.name"		VALUE = "{ tolower( l.provide_xml:children[ l.child_pos ]:name ) }">

		<MvIF EXPR = "{ l.name EQ 'name' }">			<MvASSIGN NAME = "l.settings:field_name"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'code' }">		<MvASSIGN NAME = "l.settings:field_code"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'sku' }">			<MvASSIGN NAME = "l.settings:field_sku"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'price' }">		<MvASSIGN NAME = "l.settings:field_price"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'weight' }">		<MvASSIGN NAME = "l.settings:field_weight"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'description' }">	<MvASSIGN NAME = "l.settings:field_desc"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'custom' }">
			<MvASSIGN NAME = "l.customfield_count"	VALUE = "{ l.customfield_count + 1 }">

			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml:children[ l.child_pos ], 'module',	l.settings:fields_custom[ l.customfield_count ]:module ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml:children[ l.child_pos ], 'code',		l.settings:fields_custom[ l.customfield_count ]:code ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvASSIGN NAME = "l.child_pos"	VALUE = "{ l.child_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision_ImageTypes" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:imagetypes" VALUE = "">

	<MvASSIGN NAME = "l.type_pos"		VALUE = 1>
	<MvASSIGN NAME = "l.type_found"		VALUE = 1>
	<MvASSIGN NAME = "l.type_count"		VALUE = "{ miva_array_elements( l.provide_xml:children ) }">

	<MvWHILE EXPR = "{ l.type_pos LE l.type_count }">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].ImageType_Load_Code( l.provide_xml:children[ l.type_pos ]:value, l.imagetype ) }">
			<MvASSIGN NAME = "l.settings:imagetypes" INDEX = "{ l.type_found }" VALUE = "{ l.provide_xml:children[ l.type_pos ]:value }">
			<MvASSIGN NAME = "l.type_found"										VALUE = "{ l.type_found + 1 }">
		</MvIF>

		<MvASSIGN NAME = "l.type_pos" VALUE = "{ l.type_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Framework Support Feature (skins)
|
</MvCOMMENT>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pages" VALUE = "{ [ g.Module_Feature_TUI_DB ].Item_Load_PageList( l.item ) }">

	<MvFUNCTIONRETURN VALUE = "{ 'Exports Product Layout design/template from pages: ' $ l.pages }">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.output"  STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item_Runtime( l.item:id, l.pages ) }">
			<MvREFERENCEARRAY NAME = "l.settings" VARIABLE = "l.page:settings">
				<MvMEMBER NAME = "{ l.item:code }">
			</MvREFERENCEARRAY>

			<Page_Update code="{ l.page:code }">
				<Item code="{ l.item:code }">
					<MvIF EXPR = "{ CSSUIProductLayout_AssociatedImageMachine_IsCurrentlyEnabled( l.page:code, l.item:code ) }">
						<ImageMachineEnabled>Yes</ImageMachineEnabled>
					<MvELSE>
						<ImageMachineEnabled>No</ImageMachineEnabled>
					</MvIF>

					<MvIF EXPR = "{ l.settings:invmsg EQ 'L' }">					<InventoryMessage>Long</InventoryMessage>
					<MvELSEIF EXPR = "{ l.settings:invmsg EQ 'S' }">				<InventoryMessage>Short</InventoryMessage>
					<MvELSE>														<InventoryMessage>None</InventoryMessage>
					</MvIF>

					<Buttons>
						<MvIF EXPR = "{ l.settings:button_wish }">
							<AddToWishList />
						</MvIF>
					</Buttons>

					<Fields>
						<MvIF EXPR = "{ l.settings:field_name }">	<Name />		</MvIF>
						<MvIF EXPR = "{ l.settings:field_code }">	<Code />		</MvIF>
						<MvIF EXPR = "{ l.settings:field_sku }">	<SKU />			</MvIF>
						<MvIF EXPR = "{ l.settings:field_price }">	<Price />		</MvIF>
						<MvIF EXPR = "{ l.settings:field_desc }">	<Description />	</MvIF>
						<MvIF EXPR = "{ l.settings:field_weight }">	<Weight />		</MvIF>

						<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:fields_custom">
							<Custom module="{ l.customfield:module }" code="{ l.customfield:code }" />
						</MvFOREACH>
					</Fields>

					<MvIF EXPR = "{ l.settings:price EQ 'retail' }">				<PriceDisplay>Retail</PriceDisplay>
					<MvELSEIF EXPR = "{ l.settings:price EQ 'sale' }">				<PriceDisplay>Sale</PriceDisplay>
					<MvELSE>														<PriceDisplay>Base</PriceDisplay>
					</MvIF>

					<MvIF EXPR = "{ l.settings:additionalprice EQ 'retail' }">		<AdditionalPriceDisplay>Retail</AdditionalPriceDisplay>
					<MvELSEIF EXPR = "{ l.settings:additionalprice EQ 'base' }">	<AdditionalPriceDisplay>Base</AdditionalPriceDisplay>
					<MvELSE>														<AdditionalPriceDisplay>None</AdditionalPriceDisplay>
					</MvIF>

					<MvIF EXPR = "{ l.settings:displaydiscounts }">					<DisplayDiscounts>Yes</DisplayDiscounts>
					<MvELSE>														<DisplayDiscounts>No</DisplayDiscounts>
					</MvIF>

					<MvIF EXPR = "{ l.settings:button_wish_nodisable }">			<AddToWishListIgnoreInventory>Yes</AddToWishListIgnoreInventory>
					<MvELSE>														<AddToWishListIgnoreInventory>No</AddToWishListIgnoreInventory>
					</MvIF>

					<MvIF EXPR = "{ l.settings:predictdiscounts }">					<PredictDiscounts>Yes</PredictDiscounts>
					<MvELSE>														<PredictDiscounts>No</PredictDiscounts>
					</MvIF>

					<MvIF EXPR = "{ l.settings:advanced AND NOT ISNULL l.settings:template_filename }">
						<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
							<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.templateversion:source ) }"></Template>
						</MvIF>

						<ImageTypes>
							<MvFOREACH ITERATOR = "l.imagetype" ARRAY = "l.settings:imagetypes">
								<ImageType><MvEVAL EXPR = "{ miva_cdata_encode( l.imagetype ) }"></ImageType>
							</MvFOREACH>
						</ImageTypes>
					<MvELSE>
						<MvIF EXPR = "{ l.settings:image EQ 'T' }">		<Image>Thumbnail</Image>
						<MvELSEIF EXPR = "{ l.settings:image EQ 'F' }">	<Image>Full-Sized</Image>
						<MvELSEIF EXPR = "{ l.settings:image EQ 'N' }">	<Image>None</Image>
						<MvELSE>
							<MvASSIGN NAME = "l.type_code" VALUE = "{ substring_var( l.settings:image, 6, len_var( l.settings:image ) ) }">

							<Image>TYPE</Image>
							<TypeCode><MvEVAL EXPR = "{ miva_cdata_encode( l.type_code ) }"></TypeCode>
						</MvIF>

					</MvIF>

					<Notes>#Set_Current_Time#</Notes>

					<MvIF EXPR = "{ l.settings:constrain NE 'B' }">
						<ImageDimensions constrain="No" />
					<MvELSE>
						<ImageDimensions constrain="Yes">
							<Width><MvEVAL EXPR = "{ int( l.settings:b_width ) }"></Width>
							<Height><MvEVAL EXPR = "{ int( l.settings:b_height ) }"></Height>
						</ImageDimensions>
					</MvIF>
				</Item>
			</Page_Update>
		</MvFOREACH>
	</MvCAPTURE>
</MvFUNCTION>

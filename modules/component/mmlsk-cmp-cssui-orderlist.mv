<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-OLST-
| Next Error Code: 25   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-orderlist">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI Order History List">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0800">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "10.08">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, skins, component_prov">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ 'GT_PAGE/' $ l.item $ ':Order History List Layout' }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModuleValidate_SortMethod" PARAMETERS = "item, default_sort, value, sort_by, sorting_option, sorting_method" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( l.default_sort EQ l.value ) AND ( NOT l.sort_by ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, '', 'Sorting option \'' $ l.sorting_option $ '\' must be enabled to use the Default Sort Method of ' $ l.sorting_method ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.fields:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.fields:per_page"				VALUE = "{ trim( l.fields:per_page ) }">
	<MvASSIGN NAME = "l.fields:page_disp_count"			VALUE = "{ trim( l.fields:page_disp_count ) }">
	<MvASSIGN NAME = "l.fields:default_sort"			VALUE = "{ trim( l.fields:default_sort ) }">
	<MvASSIGN NAME = "l.fields:paginate_count"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:paginate_count ) }">
	<MvASSIGN NAME = "l.fields:paginate"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:paginate ) }">
	<MvASSIGN NAME = "l.fields:advanced"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:advanced ) }">
	<MvASSIGN NAME = "l.fields:items_per_page_filter"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:items_per_page_filter ) }">
	<MvASSIGN NAME = "l.fields:sort_by:switch"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:sort_by:switch ) }">
	<MvASSIGN NAME = "l.fields:sort_by:invoice"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:sort_by:invoice ) }">
	<MvASSIGN NAME = "l.fields:sort_by:order_date"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:sort_by:order_date ) }">
	<MvASSIGN NAME = "l.fields:sort_by:status"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:sort_by:status ) }">
	<MvASSIGN NAME = "l.fields:sort_by:order_total"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:sort_by:order_total ) }">

	<MvIF EXPR = "{ l.fields:paginate }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:per_page ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'per_page', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.fields:paginate_count }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:page_disp_count ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'page_disp_count', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.fields:default_sort EQ '' }">
		<MvASSIGN NAME = "l.settings:default_sort"	VALUE = "OrdDtDesc">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:default_sort"	VALUE = "{ l.fields:default_sort }">
	</MvIF>

	<MvIF EXPR = "{ ( ( 'customfield:' IN l.fields:default_sort ) EQ 1 ) OR
					( ( 'customfield_desc:' IN l.fields:default_sort ) EQ 1 ) }">
		<MvASSIGN NAME = "l.module_code"				VALUE = "{ gettoken( l.fields:default_sort, ':', 2 ) }">
		<MvASSIGN NAME = "l.field_code"					VALUE = "{ gettoken( l.fields:default_sort, ':', 3 ) }">

		<MvASSIGN NAME = "l.sortable_customfield_count"	VALUE = "{ [ g.Module_Library_Utilities ].CustomFieldSelect_Selected( l.fields:sort_custom, l.sortable_customfields ) }">

		<MvIF EXPR = "{ NOT miva_array_search( l.sortable_customfields, 1, l.sortable_customfield, '( l.sortable_customfield:module EQ l.module_code ) AND ( l.sortable_customfield:code EQ l.field_code )' ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Module_Load_Code_Cached( l.module_code, l.customfield_module ) }">	<MvASSIGN NAME = "l.field_name" VALUE = "Unknown">
			<MvELSE>																										<MvASSIGN NAME = "l.field_name" VALUE = "{ [ g.Module_Root $ l.customfield_module:module ].Module_Order_Field_Name( l.customfield_module, l.field_code ) }">
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, '', 'Sorting by custom field \'' $ l.field_name $ '\' must be enabled to use it as the Default Sort Method' ) }">
		</MvIF>
	<MvELSE>
		<MvIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( 	l.item, l.fields:default_sort, 'InvAsc', 		l.fields:sort_by:invoice,		'Order ID',		'Order ID Ascending' ) }">		<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.fields:default_sort, 'InvDesc', 		l.fields:sort_by:invoice,		'Order ID',		'Order ID Descending' ) }">		<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.fields:default_sort, 'OrdDtAsc',		l.fields:sort_by:order_date,	'Order Date',	'Order Date Ascending' ) }">	<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.fields:default_sort, 'OrdDtDesc',		l.fields:sort_by:order_date,	'Order Date',	'Order Date Descending' ) }">	<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.fields:default_sort, 'StusAsc',		l.fields:sort_by:status,		'Status',		'Status Ascending' ) }">		<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.fields:default_sort, 'StusDesc',		l.fields:sort_by:status,		'Status',		'Status Descending' ) }">		<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.fields:default_sort, 'OrdTotAsc',		l.fields:sort_by:order_total,	'Order Total',	'Order Total Ascending' ) }">	<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.fields:default_sort, 'OrdTotDesc',	l.fields:sort_by:order_total,	'Order Total',	'Order Total Descending' ) }">	<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.sort_field_count"			VALUE = "{ miva_splitstring( 'InvAsc,InvDesc,OrdDtAsc,OrdDtDesc,StusAsc,StusDesc,OrdTotAsc,OrdTotDesc', ',', l.sort_fields, '' ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].Value_In_List( l.fields:default_sort, l.sort_fields ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'default_sort', 'Invalid default sort selection' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.fields:advanced }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.fields:template_code ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.settings:advanced"				VALUE = "{ l.fields:advanced }">

	<MvIF EXPR = "{ l.fields:default_sort EQ '' }">
		<MvASSIGN NAME = "l.settings:default_sort"		VALUE = "OrdDtDesc">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:default_sort"		VALUE = "{ l.fields:default_sort }">
	</MvIF>

	<MvIF EXPR = "{ l.fields:paginate }">
		<MvASSIGN NAME = "l.settings:per_page"			VALUE = "{ l.fields:per_page }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:per_page"			VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.fields:paginate_count }">
		<MvASSIGN NAME = "l.settings:page_disp_count"	VALUE = "{ l.fields:page_disp_count }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:page_disp_count"	VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.settings:items_per_page_filter"	VALUE = "{ l.fields:items_per_page_filter }">

	<MvASSIGN NAME = "l.settings:sort_by:switch"		VALUE = "{ l.fields:sort_by:switch }">
	<MvASSIGN NAME = "l.settings:sort_by:invoice"		VALUE = "{ l.fields:sort_by:invoice }">
	<MvASSIGN NAME = "l.settings:sort_by:order_date"	VALUE = "{ l.fields:sort_by:order_date }">
	<MvASSIGN NAME = "l.settings:sort_by:status"		VALUE = "{ l.fields:sort_by:status }">
	<MvASSIGN NAME = "l.settings:sort_by:order_total"	VALUE = "{ l.fields:sort_by:order_total }">

	<MvASSIGN NAME = "l.null"							VALUE = "{ [ g.Module_Library_Utilities ].CustomFieldSelect_Selected( l.fields:sort_custom, l.settings:sort_custom ) }">
	<MvASSIGN NAME = "l.null"							VALUE = "{ [ g.Module_Library_Utilities ].CustomFieldSelect_Selected( l.fields:fields_custom, l.settings:fields_custom ) }">

	<MvIF EXPR = "{ NOT l.fields:advanced }">
		<MvASSIGN NAME = "l.settings:invoice"			VALUE = "{ l.fields:invoice }">
		<MvASSIGN NAME = "l.settings:order_date"		VALUE = "{ l.fields:order_date }">
		<MvASSIGN NAME = "l.settings:products_bought"	VALUE = "{ l.fields:products_bought }">
		<MvASSIGN NAME = "l.settings:quantity_bought"	VALUE = "{ l.fields:quantity_bought }">
		<MvASSIGN NAME = "l.settings:status"			VALUE = "{ l.fields:status }">
		<MvASSIGN NAME = "l.settings:tracking"			VALUE = "{ l.fields:tracking }">
		<MvASSIGN NAME = "l.settings:order_total"		VALUE = "{ l.fields:order_total }">

		<MvEVAL EXPR = "{ Generate_Code( l.settings, l.fields:template_code ) }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-OLST-00009', l.module:name $ ' settings updated' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, '', l.fields:template_code, l.settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.load_fields }">
		<MvASSIGN NAME = "l.fields:invoice"						VALUE = "{ l.settings:invoice }">
		<MvASSIGN NAME = "l.fields:order_date"					VALUE = "{ l.settings:order_date }">
		<MvASSIGN NAME = "l.fields:products_bought"				VALUE = "{ l.settings:products_bought }">
		<MvASSIGN NAME = "l.fields:quantity_bought"				VALUE = "{ l.settings:quantity_bought }">
		<MvASSIGN NAME = "l.fields:status"						VALUE = "{ l.settings:status }">
		<MvASSIGN NAME = "l.fields:tracking"					VALUE = "{ l.settings:tracking }">
		<MvASSIGN NAME = "l.fields:order_total"					VALUE = "{ l.settings:order_total }">

		<MvASSIGN NAME = "l.fields:advanced"					VALUE = "{ l.settings:advanced }">
		<MvASSIGN NAME = "l.fields:template_code"				VALUE = "{ l.templateversion:source }">
		<MvASSIGN NAME = "l.fields:template_filename"			VALUE = "{ l.settings:template_filename }">

		<MvIF EXPR = "{ ISNULL l.settings:sort_by:switch }">
			<MvASSIGN NAME = "l.fields:sort_by:switch"			VALUE = 1>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:sort_by:switch"			VALUE = "{ l.settings:sort_by:switch }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:sort_by:invoice }">
			<MvASSIGN NAME = "l.fields:sort_by:invoice"			VALUE = 1>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:sort_by:invoice"			VALUE = "{ l.settings:sort_by:invoice }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:sort_by:order_date }">
			<MvASSIGN NAME = "l.fields:sort_by:order_date"		VALUE = 1>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:sort_by:order_date"		VALUE = "{ l.settings:sort_by:order_date }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:sort_by:status }">
			<MvASSIGN NAME = "l.fields:sort_by:status"			VALUE = 1>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:sort_by:status"			VALUE = "{ l.settings:sort_by:status }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:sort_by:order_total }">
			<MvASSIGN NAME = "l.fields:sort_by:order_total"		VALUE = 1>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:sort_by:order_total"		VALUE = "{ l.settings:sort_by:order_total }">
		</MvIF>

		<MvIF EXPR = "{ ISNULL l.settings:default_sort }">
			<MvASSIGN NAME = "l.fields:default_sort"			VALUE = "OrdDtDesc">
		<MvELSE>
			<MvASSIGN NAME = "l.fields:default_sort"			VALUE = "{ l.settings:default_sort }">
		</MvIF>

		<MvIF EXPR = "{ l.settings:per_page EQ 0 }">
			<MvASSIGN NAME = "l.fields:paginate"				VALUE = 0>
			<MvASSIGN NAME = "l.fields:per_page"				VALUE = 10>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:paginate"				VALUE = 1>
			<MvASSIGN NAME = "l.fields:per_page"				VALUE = "{ l.settings:per_page }">
		</MvIF>

		<MvIF EXPR = "{ l.settings:page_disp_count EQ 0 }">
			<MvASSIGN NAME = "l.fields:paginate_count"			VALUE = 0>
			<MvASSIGN NAME = "l.fields:page_disp_count"			VALUE = 3>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:paginate_count"			VALUE = 1>
			<MvASSIGN NAME = "l.fields:page_disp_count"			VALUE = "{ l.settings:page_disp_count }">
		</MvIF>

		<MvIF EXPR = "{ l.settings:items_per_page_filter EQ 0 }">
			<MvASSIGN NAME = "l.fields:items_per_page_filter"	VALUE = 0>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:items_per_page_filter"	VALUE = "{ l.settings:items_per_page_filter }">
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].InitializeCustomFieldSelect( l.settings:fields_custom, l.fields:fields_custom ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].InitializeCustomFieldSelect( l.settings:sort_custom, l.fields:sort_custom ) }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Admin ].Tab_Visible( l.tab, l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_filename' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<MvIF EXPR = "{ l.fields:advanced }">
			<tr><td colspan="2" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( l.field_prefix $ 'template_code', l.fields:template_code, 20, 100, l.templateversion:templ_id ) }">
			</td></tr>

			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'invoice, order_date, products_bought, quantity_bought, status, tracking, order_total' ) }">
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_code' ) }">

			<tr><td align="left" valign="top" nowrap>
				<b>Order Fields:</b>
			</td><td align="left" width="100%">
				<table border="0" cellpadding="0" cellspacing="0">
				<tr><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:invoice, l.field_prefix $ 'invoice', 1, 'Order ID' ) }">
				</td><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:order_date, l.field_prefix $ 'order_date', 1, 'Order Date' ) }">
				</td></tr>

				<tr><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:products_bought, l.field_prefix $ 'products_bought', 1, 'Products Purchased' ) }">
				</td><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:quantity_bought, l.field_prefix $ 'quantity_bought', 1, 'Quantity Purchased' ) }">
				</td></tr>

				<tr><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:status, l.field_prefix $ 'status', 1, 'Order Status' ) }">
				</td><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:tracking, l.field_prefix $ 'tracking', 1, 'Order Tracking' ) }">
				</td></tr>

				<tr><td align="left" nowrap>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:order_total, l.field_prefix $ 'order_total', 1, 'Order Total' ) }">
				</td><td>
					&nbsp;
				</td></tr>
				</table>
			</td></tr>
		</MvIF>

		<MvASSIGN NAME = "l.order_customfield_count"	VALUE = "{ [ g.Module_Library_Utilities ].OrderCustomFieldList_Load( l.order_customfields ) }">
		<MvASSIGN NAME = "l.sortable_customfield_count"	VALUE = "{ miva_array_filter_ref( l.order_customfields, 1, l.customfield, 'l.customfield:capabilities:orderby', l.sortable_customfields ) }">

		<MvIF EXPR = "{ l.order_customfield_count GT 0 }">
			<tr><td align="left" valign="top" nowrap>
				<b>Custom Fields:</b>
			</td><td align="left" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCustomFieldSelect( l.field_prefix $ 'fields_custom', l.fields:fields_custom, l.order_customfields, l.order_customfield_count ) }">
			</td></tr>
		</MvIF>

		<MvIF EXPR = "{ l.fields:advanced }">
			<tr>
				<td align="left" valign="top" nowrap>
					<b>Sorting Options:</b>
				</td>
				<td valign="top" width="100%">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio_With_Label( l.field_prefix $ 'sort_by:switch', 0, l.fields:sort_by:switch, 'Off' ) }"><br />
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio_With_Label( l.field_prefix $ 'sort_by:switch', 1, l.fields:sort_by:switch, 'On' ) }">
				</td>
			</tr>

			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'sort_by:invoice,sort_by:order_date,sort_by:status,sort_by:order_total,sort_custom' ) }">
		<MvELSE>
			<tr><td colspan="2">&nbsp;</td></tr>

			<tr><td align="left" valign="top" nowrap>
				<b>Sorting Options:</b>
			</td><td width="100%">
				<table width="100%">
					<tr>
						<td align="left" style="padding-right: 15px;" valign="top" nowrap>
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio_With_Label( l.field_prefix $ 'sort_by:switch', 0, l.fields:sort_by:switch, 'Off' ) }"><br />
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio_With_Label( l.field_prefix $ 'sort_by:switch', 1, l.fields:sort_by:switch, 'On' ) }">
						</td>
						<td width="100%">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox_With_Label( l.fields:sort_by:invoice,		l.field_prefix $ 'sort_by:invoice',		'1', 'Order ID' ) }"><br />
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox_With_Label( l.fields:sort_by:order_date,	l.field_prefix $ 'sort_by:order_date',	'1', 'Order Date' ) }"><br />
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox_With_Label( l.fields:sort_by:status,		l.field_prefix $ 'sort_by:status',		'1', 'Order Status' ) }"><br />
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox_With_Label( l.fields:sort_by:order_total,	l.field_prefix $ 'sort_by:order_total',	'1', 'Order Total' ) }">
						</td>
					</tr>
				</table>
			</td></tr>

			<MvIF EXPR = "{ l.sortable_customfield_count GT 0 }">
				<tr><td align="left" valign="top" nowrap>
					<b>Sort By Custom Field:</b>
				</td><td align="left" width="100%">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCustomFieldSelect( l.field_prefix $ 'sort_custom', l.fields:sort_custom, l.sortable_customfields, l.sortable_customfield_count ) }">
				</td></tr>
			</MvIF>
		</MvIF>

		<tr><td align="left" nowrap valign="middle">
			<b>Default Sort Method:</b>
		</td><td align="left" width="100%">
			<select name="{ encodeentities( l.field_prefix ) $ 'default_sort' }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'InvAsc', 		l.fields:default_sort, 'Order ID Ascending' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'InvDesc', 	l.fields:default_sort, 'Order ID Descending' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'OrdDtAsc', 	l.fields:default_sort, 'Order Date Ascending' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'OrdDtDesc', 	l.fields:default_sort, 'Order Date Descending' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'StusAsc', 	l.fields:default_sort, 'Status Ascending' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'StusDesc', 	l.fields:default_sort, 'Status Descending' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'OrdTotAsc', 	l.fields:default_sort, 'Order Total Ascending' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'OrdTotDesc', 	l.fields:default_sort, 'Order Total Descending' ) }">

				<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.sortable_customfields" COUNT = "{ l.sortable_customfield_count }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption_Encode( 'customfield:' $ l.customfield:module:code $ ':' $ l.customfield:code,		l.fields:default_sort, l.customfield:name $ ' Ascending' ) }">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption_Encode( 'customfield_desc:' $ l.customfield:module:code $ ':' $ l.customfield:code,	l.fields:default_sort, l.customfield:name $ ' Descending' ) }">
				</MvFOREACH>
			</select>
		</td></tr>

		<tr><td colspan="2">&nbsp;</td></tr>

		<tr><td align="left" nowrap valign="top">
			<b>Pagination:</b>
		</td><td align="left" width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate', 0, l.fields:paginate, 'Do not paginate orders' ) }"><br>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate', 1, l.fields:paginate, 'Display' ) }"><input type="text" name="{ encodeentities( l.field_prefix ) $ 'per_page' }" value="{ encodeentities( l.fields:per_page ) }" size="5"> orders per page
		</td></tr>

		<tr><td align="left" nowrap valign="top">
			<b>Page Numbers:</b>
		</td><td align="left" nowrap width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate_count', 0, l.fields:paginate_count, 'Do not show page count' ) }"><br>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate_count', 1, l.fields:paginate_count, 'Display' ) }"> <input type="text" name="{ encodeentities( l.field_prefix ) $ 'page_disp_count' }" value="{ encodeentities( l.fields:page_disp_count ) }" size="5"> pages at a time
		</td></tr>

		<tr><td align="left" nowrap valign="top">
			<b>Items Per Page Filter:</b>
		</td><td align="left" nowrap width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'items_per_page_filter', 0, l.fields:items_per_page_filter, 'Off' ) }"><br>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'items_per_page_filter', 1, l.fields:items_per_page_filter, 'On' ) }">
		</td></tr>

		<tr><td colspan="2">&nbsp;</td></tr>

		<tr><td nowrap>
			&nbsp;
		</td><td width="100%">
			<input type="hidden" name="{ encodeentities( l.field_prefix ) $ 'advanced' }" value="{ encodeentities( l.fields:advanced ) }">
			<MvIF EXPR = "{ l.fields:advanced }">
				<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 0 ); return false;' }">Point + Click Mode</a>
			<MvELSE>
				<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 1 ); return false;' }">Advanced Mode</a>
			</MvIF>
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, '' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:invoice"				VALUE = "1">
	<MvASSIGN NAME = "l.settings:order_date"			VALUE = "1">
	<MvASSIGN NAME = "l.settings:products_bought"		VALUE = "1">
	<MvASSIGN NAME = "l.settings:quantity_bought"		VALUE = "1">
	<MvASSIGN NAME = "l.settings:status"				VALUE = "1">
	<MvASSIGN NAME = "l.settings:tracking"				VALUE = "1">
	<MvASSIGN NAME = "l.settings:order_total"			VALUE = "1">
	<MvASSIGN NAME = "l.settings:paginate"				VALUE = "1">
	<MvASSIGN NAME = "l.settings:per_page"				VALUE = "10">
	<MvASSIGN NAME = "l.settings:page_disp_count"		VALUE = "3">
	<MvASSIGN NAME = "l.settings:items_per_page_filter"	VALUE = "1">
	<MvASSIGN NAME = "l.settings:default_sort"			VALUE = "OrdDtDesc">
	<MvASSIGN NAME = "l.settings:fields_custom"			VALUE = "">
	<MvASSIGN NAME = "l.settings:sort_custom"			VALUE = "">
	<MvASSIGN NAME = "l.settings:sort_by:switch"		VALUE = "1">
	<MvASSIGN NAME = "l.settings:sort_by:invoice"		VALUE = "1">
	<MvASSIGN NAME = "l.settings:sort_by:order_date"	VALUE = "1">
	<MvASSIGN NAME = "l.settings:sort_by:status"		VALUE = "1">
	<MvASSIGN NAME = "l.settings:sort_by:order_total"	VALUE = "1">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ Generate_Code( l.settings, l.template_source ) }">

	<MvASSIGN NAME = "l.settings:template_filename"	VALUE = "{ tolower( l.page:code ) $ '-' $ tolower( l.item ) $ '.mvc' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.template_source, l.settings, l.settings:template_filename ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Create_Pagination" PARAMETERS = "settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:page_disp_count EQ 0 OR l.settings:per_page EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.url"										VALUE = "">
	<MvASSIGN NAME = "l.additional_params"							VALUE = "">
	<MvASSIGN NAME = "l.screen"										VALUE = "{ toupper( g.Screen ) }">

	<MvIF EXPR = "{ ISNULL g.Session:uri:uri }">
		<MvASSIGN NAME = "l.flags:sep"								VALUE = 1>
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Feature_URI_DB ].URISettings_Load_Cached( l.urisettings ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ g.Secure }">	<MvASSIGN NAME = "l.flags"	VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Flags( 'secure,sep' ) }">
		<MvELSE> 						<MvASSIGN NAME = "l.flags"	VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Flags( 'sep' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.url"									VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.urisettings, l.flags, g.Session:uri:uri, '' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.next_offset"								VALUE = "{ g.NextOffset }">

	<MvIF EXPR = "{ ISNULL l.url }">
		<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code_Cached( l.screen, l.page ) }">
			<MvASSIGN NAME = "l.url"								VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Page_URL( l.page, l.flags ) }">
		<MvELSE>
			<MvASSIGN NAME = "l.url"								VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Screen_URL( l.screen, l.flags ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL g.Per_Page }">	<MvASSIGN NAME = "l.additional_params" VALUE = "{ l.additional_params $ '&Per_Page=' $ encodeattribute( g.Per_Page ) }">	</MvIF>
	<MvIF EXPR = "{ NOT ISNULL g.SortBy }">		<MvASSIGN NAME = "l.additional_params" VALUE = "{ l.additional_params $ '&SortBy=' $ encodeattribute( g.SortBy ) }">		</MvIF>

	<MvASSIGN NAME = "l.page_count"						VALUE = "{ ceil( l.settings:total_ord_count / l.settings:per_page ) }">
	<MvASSIGN NAME = "l.settings:page_links:last_page"	VALUE = "{ l.page_count }">

	<MvIF EXPR = "{ l.next_offset }">	<MvASSIGN NAME = "l.settings:page_links:current_page" VALUE = "{ ceil( l.next_offset / l.settings:per_page ) }">
	<MvELSE>							<MvASSIGN NAME = "l.settings:page_links:current_page" VALUE = "{ ceil( l.settings:total_ord_count / l.settings:per_page ) }">
	</MvIF>

	<MvASSIGN NAME = "l.params" 								VALUE = "{ 'Offset=' $ encodeattribute( ( l.settings:page_links:current_page ) * l.settings:per_page ) $ l.additional_params }">
	<MvASSIGN NAME = "l.settings:page_links:next_link_params"	VALUE = "{ l.params }">
	<MvASSIGN NAME = "l.settings:page_links:next_link"			VALUE = "{ l.url $ l.params }">

	<MvASSIGN NAME = "l.params" 								VALUE = "{ 'Offset=' $ encodeattribute( ( l.settings:page_links:current_page - 2 ) * l.settings:per_page ) $ l.additional_params }">
	<MvASSIGN NAME = "l.settings:page_links:prev_link_params"	VALUE = "{ l.params }">
	<MvASSIGN NAME = "l.settings:page_links:prev_link"			VALUE = "{ l.url $ l.params }">

	<MvASSIGN NAME = "l.params" 								VALUE = "{ 'Offset=0' $ l.additional_params }">
	<MvASSIGN NAME = "l.settings:page_links:first_link_params"	VALUE = "{ l.params }">
	<MvASSIGN NAME = "l.settings:page_links:first_link"			VALUE = "{ l.url $ l.params }">

	<MvIF EXPR = "{ floor( l.settings:total_ord_count / l.settings:per_page ) * l.settings:per_page EQ l.settings:total_ord_count }">
		<MvASSIGN NAME = "l.last_page_offset"	VALUE = "{ floor( ( l.settings:total_ord_count - 1 ) / l.settings:per_page ) * l.settings:per_page }">
		<MvASSIGN NAME = "l.params" 			VALUE = "{ 'Offset=' $ encodeattribute( floor( ( l.settings:total_ord_count - 1 ) / l.settings:per_page ) * l.settings:per_page ) $ l.additional_params }">
	<MvELSE>
		<MvASSIGN NAME = "l.last_page_offset"	VALUE = "{ floor( ( l.settings:total_ord_count ) / l.settings:per_page ) * l.settings:per_page }">
		<MvASSIGN NAME = "l.params" 			VALUE = "{ 'Offset=' $ encodeattribute( floor( l.settings:total_ord_count / l.settings:per_page ) * l.settings:per_page ) $ l.additional_params }">
	</MvIF>

	<MvASSIGN NAME = "l.settings:page_links:last_link_params"	VALUE = "{ l.params }">
	<MvASSIGN NAME = "l.settings:page_links:last_link"			VALUE = "{ l.url $ l.params }">

	<MvIF EXPR = "{ l.page_count LT l.settings:page_disp_count }">
		<MvASSIGN NAME = "l.left"	VALUE = "{ floor( ( l.page_count - 1 ) / 2 ) }">
		<MvASSIGN NAME = "l.right"	VALUE = "{ ceil( ( l.page_count - 1 ) / 2 ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.left"	VALUE = "{ floor( ( l.settings:page_disp_count - 1 ) / 2 ) }">
		<MvASSIGN NAME = "l.right"	VALUE = "{ ceil( ( l.settings:page_disp_count - 1 ) / 2 ) }">
	</MvIF>

	<MvASSIGN NAME = "l.counter"	VALUE = 0>
	<MvASSIGN NAME = "l.left_final"	VALUE = 0>

	<MvWHILE EXPR = "{ l.counter LT l.left }">
		<MvIF EXPR = "{ ( ( l.settings:page_links:current_page - l.left + 1 ) + l.counter ) GT 1 }">
			<MvASSIGN NAME = "l.left_final"	VALUE = "{ l.left_final + 1 }">
		<MvELSE>
			<MvASSIGN NAME = "l.right"		VALUE = "{ l.right + 1 }">
		</MvIF>

		<MvASSIGN NAME = "l.counter"		VALUE = "{ l.counter + 1 }">
	</MvWHILE>

	<MvIF EXPR = "{ l.left_final EQ l.left }">
		<MvASSIGN NAME = "l.counter"				VALUE = 0>
		<MvASSIGN NAME = "l.right_final"			VALUE = 0>

		<MvWHILE EXPR = "{ l.counter LT l.right }">
			<MvIF EXPR = "{ ( ( l.settings:page_links:current_page + l.right - 1 ) - l.counter ) LT l.page_count }">
				<MvASSIGN NAME = "l.right_final"	VALUE = "{ l.right_final + 1 }">
			<MvELSE>
				<MvASSIGN NAME = "l.left_final"		VALUE = "{ l.left_final + 1 }">
			</MvIF>

			<MvASSIGN NAME = "l.counter"			VALUE = "{ l.counter + 1 }">
		</MvWHILE>

		<MvASSIGN NAME = "l.pages"					VALUE = "{ l.left_final + l.right_final + 1 }">
	<MvELSE>
		<MvASSIGN NAME = "l.pages"					VALUE = "{ l.left_final + l.right + 1 }">
	</MvIF>

	<MvASSIGN NAME = "l.counter" VALUE = 0>

	<MvWHILE EXPR = "{ l.counter LT l.pages }">
		<MvASSIGN NAME = "l.params" 																		VALUE = "{ 'Offset=' $ encodeattribute( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) $ l.additional_params }">
		<MvASSIGN NAME = "l.settings:page_links:pages" INDEX = "{ l.counter + 1 }" MEMBER = "page_num"		VALUE = "{ ( l.settings:page_links:current_page - l.left_final ) + l.counter }">
		<MvASSIGN NAME = "l.settings:page_links:pages" INDEX = "{ l.counter + 1 }" MEMBER = "link_params"	VALUE = "{ l.params }">
		<MvASSIGN NAME = "l.settings:page_links:pages" INDEX = "{ l.counter + 1 }" MEMBER = "link" 			VALUE = "{ l.url $ l.params }">

		<MvIF EXPR = "{ ( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) EQ 0 }">					<MvASSIGN NAME = "l.settings:page_links:contains_first"	VALUE = "YES">	</MvIF>
		<MvIF EXPR = "{ ( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) EQ l.last_page_offset }">	<MvASSIGN NAME = "l.settings:page_links:contains_last"	VALUE = "YES">	</MvIF>

		<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:template_filename ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:template_filename, l.source_templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:template_filename, l.dest_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.dest_settings"						VALUE = "{ l.source_settings }">
	<MvASSIGN NAME = "l.dest_settings:template_filename"	VALUE = "{ l.dest_managedtemplate:version:settings:template_filename }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_managedtemplate, '', l.source_templateversion:source, l.dest_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.source_branch:id EQ l.dest_branch:id }">	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-OLST-00023', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' on branch \'' $ l.dest_branch:name $ '\'' ) }">
	<MvELSE>													<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-OLST-00024', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' from branch \'' $ l.source_branch:name $ '\' to branch \'' $ l.dest_branch:name $ '\'' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:items_per_page_filter }">
		<MvIF EXPR = "{ ISNULL g.Per_Page OR NOT [ g.Module_Admin ].Validate_WholeNumber_Required( g.Per_Page ) }">
			<MvIF EXPR = "{ l.settings:per_page EQ 0 }">
				<MvASSIGN NAME = "g.Per_Page"				VALUE = -1>
			<MvELSE>
				<MvASSIGN NAME = "g.Per_Page"				VALUE = "{ l.settings:per_page }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ g.Per_Page GE 0 }">
				<MvASSIGN NAME = "l.settings:per_page"		VALUE = "{ g.Per_Page }">
			<MvELSE>
				<MvASSIGN NAME = "g.Per_Page"				VALUE = -1>
				<MvASSIGN NAME = "l.settings:per_page"		VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.SortBy }">
		<MvASSIGN NAME = "l.sort_by"						VALUE = "{ g.SortBy }">
	<MvELSE>
		<MvASSIGN NAME = "l.sort_by"						VALUE = "{ l.settings:default_sort }">
		<MvASSIGN NAME = "g.SortBy"							VALUE = "{ l.settings:default_sort }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_OrderList_Build_Query( l.query ) }">

	<MvIF EXPR = "{ ( NOT ISNULL g.Order_BillEmail ) AND
					( NOT ISNULL g.Order_BillZip ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_OrderList_Build_Query_WHERE_BillEmail_BillZip( l.query, g.Order_BillEmail, g.Order_BillZip ) }">
	<MvELSEIF EXPR = "{ ( NOT ISNULL g.Order_ID ) AND
						( NOT ISNULL g.Order_BillEmail ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_OrderList_Build_Query_WHERE_Order_ID_BillEmail( l.query, g.Order_ID, g.Order_BillEmail ) }">
	<MvELSEIF EXPR = "{ g.Basket:cust_id }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_OrderList_Build_Query_WHERE_Customer_ID( l.query, g.Basket:cust_id ) }">
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_OrderList_Build_Query_OutputCustomFields( l.query, l.settings:fields_custom, miva_array_elements( l.settings:fields_custom ), l.all_settings:customfield_names ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_OrderList_Build_Query_ORDER_BY( l.query, l.sort_by ) }">

	<MvREFERENCE NAME = "l.offset"		VARIABLE = "g.Offset">
	<MvREFERENCE NAME = "l.next_offset"	VARIABLE = "g.NextOffset">
	<MvREFERENCE NAME = "l.prev_offset"	VARIABLE = "g.PrevOffset">

	<MvASSIGN NAME = "l.prev_offset"						VALUE = "{ l.offset - l.settings:per_page }">

	<MvIF EXPR = "{ l.prev_offset LT 0 }">
		<MvASSIGN NAME = "l.prev_offset"					VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.all_settings:orders_on_page_count"	VALUE = "{ [ g.Module_Library_DB ].Runtime_OrderList_Load( l.query, l.offset, l.settings:per_page, l.next_offset, l.settings:total_ord_count, l.all_settings:orders ) }">
	<MvASSIGN NAME = "l.all_settings:orders_found"			VALUE = "{ l.settings:total_ord_count NE 0 }">

	<MvASSIGN NAME = "l.null"								VALUE = "{ Create_Pagination( l.settings ) }">

	<MvASSIGN NAME = "l.all_settings:page_links"			VALUE = "{ l.settings:page_links }">

	<MvFOREACH ITERATOR = "l.order" ARRAY = "l.all_settings:orders" COUNT = "{ l.all_settings:orders_on_page_count }">
		<MvCOMMENT>
		|
		| Initialize common order tokens
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Order( l.order ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvCOMMENT>
		|
		| These tokens are here for compatibility with old versions of this module
		|
		</MvCOMMENT>

		<MvASSIGN NAME = "l.order:order_status"			VALUE = "{ l.order:formatted_status }">
		<MvASSIGN NAME = "l.order:date_formatted"		VALUE = "{ l.order:date }">

		<MvIF EXPR = "{ NOT OrderItem_Load_Totals( l.order:id, l.order:prod_num, l.order:prod_qty ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvCOMMENT>
		|
		| Initialize order shipments
		|
		</MvCOMMENT>

		<MvFOREACH ITERATOR = "l.shipment" ARRAY = "l.order:shipments" COUNT = "{ [ g.Module_Library_DB ].OrderShipmentList_Load_Order( l.order:id, l.order:shipments ) }">
			<MvCOMMENT>
			|
			| Common shipment tokens
			|
			</MvCOMMENT>

			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_OrderShipment( l.shipment ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvCOMMENT>
			|
			| Old versions of this module set up "tracklink" as a full <a> tag.  Move the common value into tracklink_raw and
			| emulate the old behavior.
			|
			</MvCOMMENT>

			<MvASSIGN NAME = "l.shipment:tracklink_raw"	VALUE = "{ l.shipment:tracklink }">

			<MvIF EXPR = "{ ISNULL l.shipment:tracklink_raw }">	<MvASSIGN NAME = "l.shipment:tracklink"	VALUE = "{ l.shipment:tracknum }">
			<MvELSE>											<MvASSIGN NAME = "l.shipment:tracklink" VALUE = "{ '<a href="' $ encodeentities( l.shipment:tracklink_raw ) $ '" target="_blank">' $ encodeentities( l.shipment:tracknum ) $ '</a>' }">
			</MvIF>
		</MvFOREACH>

		<MvCOMMENT>
		|
		| Remove PII from orders for non-logged in customers / order's not associated with the logged-in customer
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ ( NOT g.Customer_Session_Verified ) OR ( g.Basket:cust_id NE l.order:cust_id ) }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Order_RemovePII( l.order ) }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:template_filename, l.all_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update_Requires_Version" PARAMETERS = "module var, page var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_PostUpdate" PARAMETERS = "module var, page var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Code" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code">
	<MIVA STANDARDOUTPUTLEVEL = "text, html"><div id="filter-items-container"><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:sort_by:switch }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<mvt:if expr="{ 'l.settings:orders_on_page_count GT 0' }">
		<div class="sorting">
			<form method="get" action="&mvte:urls:_self:auto_noparams;">
				<mvt:item name="urls" param="hidden_params:_self:auto" />
				<input type="hidden" name="Per_Page" value="&mvte:global:Per_Page;" />
				<input type="hidden" name="Offset" value="&mvte:global:Offset;" />

				<label for="SortBy">Sort By:</label>
				<select id="SortBy" name="SortBy" onchange="window.location.href='&mvtj:urls:_self:auto_sep;Offset=' + encodeURIComponent( '&mvtj:global:Offset;' ) + '&amp;Per_Page=' + encodeURIComponent( '&mvtj:global:Per_Page;' ) + '&amp;SortBy=' + encodeURIComponent( this.options[ this.selectedIndex ].value ) + '&amp;Order_BillEmail=' + encodeURIComponent( '&mvtj:global:Order_BillEmail;' ) + '&amp;Order_BillZip=' + encodeURIComponent( '&mvtj:global:Order_BillZip;' );">
<MIVA STANDARDOUTPUTLEVEL = "">

				<MvIF EXPR = "{ l.settings:invoice AND l.settings:sort_by:invoice }">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea"		INDEX = "1" VALUE = "Invoice Ascending">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea_code"	INDEX = "1" VALUE = "InvAsc">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_named"		INDEX = "1" VALUE = "Invoice Descending">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_named_code"	INDEX = "1" VALUE = "InvDesc">
				</MvIF>

				<MvIF EXPR = "{ l.settings:order_date AND l.settings:sort_by:order_date }">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea"		INDEX = "2" VALUE = "Order Date Ascending">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea_code"	INDEX = "2" VALUE = "OrdDtAsc">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_named"		INDEX = "2" VALUE = "Order Date Descending">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_named_code"	INDEX = "2" VALUE = "OrdDtDesc">
				</MvIF>

				<MvIF EXPR = "{ l.settings:status AND l.settings:sort_by:status }">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea"		INDEX = "3" VALUE = "Order Status Ascending">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea_code"	INDEX = "3" VALUE = "StusAsc">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_named"		INDEX = "3" VALUE = "Order Status Descending">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_named_code"	INDEX = "3" VALUE = "StusDesc">
				</MvIF>

				<MvIF EXPR = "{ l.settings:order_total AND l.settings:sort_by:order_total }">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea"		INDEX = "4" VALUE = "Total Price Ascending">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_namea_code"	INDEX = "4" VALUE = "OrdTotAsc">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_named"		INDEX = "4" VALUE = "Total Price Descending">
					<MvASSIGN NAME = "l.sorts" MEMBER = "disp_named_code"	INDEX = "4" VALUE = "OrdTotDesc">
				</MvIF>

				<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:sort_custom">
					<MvIF EXPR = "{ [ g.Module_Library_DB ].Module_Load_Code_Cached( l.customfield:module, l.customfield_module ) }">
						<MvASSIGN NAME = "l.name"					VALUE = "{ [ g.Module_Root $ l.customfield_module:module ].Module_Order_Field_Name( l.customfield_module, l.customfield:code ) }">

						<MvASSIGN NAME = "l.sort:disp_namea"		VALUE = "{ l.name $ ' Ascending' }">
						<MvASSIGN NAME = "l.sort:disp_namea_code"	VALUE = "{ 'customfield:' $ l.customfield:module $ ':' $ l.customfield:code }">
						<MvASSIGN NAME = "l.sort:disp_named"		VALUE = "{ l.name $ ' Descending' }">
						<MvASSIGN NAME = "l.sort:disp_named_code"	VALUE = "{ 'customfield_desc:' $ l.customfield:module $ ':' $ l.customfield:code }">

						<MvASSIGN NAME = "l.null"					VALUE = "{ miva_array_insert_var( l.sorts, l.sort, -1 ) }">
					</MvIF>
				</MvFOREACH>

				<MvASSIGN NAME = "l.null" VALUE = "{ miva_array_collapse( l.sorts ) }">

				<MvFOREACH ITERATOR = "l.sort" ARRAY = "l.sorts">
					<MvIF EXPR = "{ NOT ISNULL l.sort:disp_namea }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="{ 'g.SortBy EQ \'' $ encodeentities( l.sort:disp_namea_code ) $ '\'' }">
						<option value="{ encodeentities( l.sort:disp_namea_code ) }" selected="selected"><MvEVAL EXPR = "{ encodeentities( l.sort:disp_namea ) }"></option>
					<mvt:else>
						<option value="{ encodeentities( l.sort:disp_namea_code ) }"><MvEVAL EXPR = "{ encodeentities( l.sort:disp_namea ) }"></option>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
					</MvIF>

					<MvIF EXPR = "{ NOT ISNULL l.sort:disp_named }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="{ 'g.SortBy EQ \'' $ encodeentities( l.sort:disp_named_code ) $ '\'' }">
						<option value="{ encodeentities( l.sort:disp_named_code ) }" selected="selected"><MvEVAL EXPR = "{ encodeentities( l.sort:disp_named ) }"></option>
					<mvt:else>
						<option value="{ encodeentities( l.sort:disp_named_code ) }"><MvEVAL EXPR = "{ encodeentities( l.sort:disp_named ) }"></option>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
					</MvIF>
				</MvFOREACH>

				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				</select>
			</form>
		</div>
	</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>

	<MvIF EXPR = "{ l.settings:items_per_page_filter }">
		<MvIF EXPR = "{ l.settings:per_page EQ 0 OR ISNULL l.settings:per_page }">
			<MvASSIGN NAME = "l.per_page" VALUE = 10>
		<MvELSE>
			<MvASSIGN NAME = "l.per_page" VALUE = "{ l.settings:per_page }">
		</MvIF>

<MIVA STANDARDOUTPUTLEVEL = "text, html">
	<mvt:if expr="l.settings:orders_on_page_count GT 1">
		<div class="per-page">
			<form method="get" action="&mvte:urls:_self:auto_noparams;">
				<mvt:item name="urls" param="hidden_params:_self:auto" />
				<input type="hidden" name="SortBy" value="&mvte:global:SortBy;" />
				<input type="hidden" name="Offset" value="&mvte:global:Offset;" />

				<label for="Per_Page">View:</label>
				<select id="Per_Page" name="Per_Page" onchange="window.location.href='&mvtj:urls:_self:auto_sep;Offset=' + encodeURIComponent( '&mvtj:global:Offset;' ) + '&amp;Per_Page=' + encodeURIComponent( this.options[ this.selectedIndex ].value ) + '&amp;SortBy=' + encodeURIComponent( '&mvtj:global:SortBy;' ) + '&amp;Order_BillEmail=' + encodeURIComponent( '&mvtj:global:Order_BillEmail;' ) + '&amp;Order_BillZip=' + encodeURIComponent( '&mvtj:global:Order_BillZip;' );">
<MIVA STANDARDOUTPUTLEVEL = "">

				<MvASSIGN NAME = "l.multiplier"	VALUE = 1>
				<MvASSIGN NAME = "l.count"		VALUE = 1>

				<MvWHILE EXPR = "{ l.count LE 3 }">
					<MvASSIGN NAME = "l.items_per" VALUE = "{ l.per_page * l.multiplier }">
					<MvASSIGN NAME = "l.multiplier" VALUE = "{ l.multiplier + l.count }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="{ 'g.Per_Page EQ ' $ encodeentities( l.items_per ) }">
						<option value="{ encodeentities( l.items_per ) }" selected="selected"><MvEVAL EXPR = "{ encodeentities( l.items_per ) }"></option>
					<mvt:else>
						<option value="{ encodeentities( l.items_per ) }"><MvEVAL EXPR = "{ encodeentities( l.items_per ) }"></option>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
					<MvASSIGN NAME = "l.count" VALUE = "{ l.count + 1 }">
				</MvWHILE>

					<MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="g.Per_Page EQ -1">
						<option value="-1" selected="selected">All</option>
					<mvt:else>
						<option value="-1">All</option>
					</mvt:if>
				</select>
			</form>
		</div>
	</mvt:if>
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>

<MIVA STANDARDOUTPUTLEVEL = "text, html">
	<mvt:if expr="l.settings:page_links:last_page GT 1">
		<div class="page-links">
			<span class="page-links-title">Page(s):</span>
			<span class="page-links-container">
				<mvt:if expr="l.settings:page_links:current_page NE 1">
					<a href="&mvte:urls:_self:auto_sep;&mvte:page_links:prev_link_params;&Order_BillEmail=&mvta:global:Order_BillEmail;&Order_BillZip=&mvta:global:Order_BillZip;" class="page-links-previous">&lt;</a>
				<mvt:else>
					<span class="page-links-previous page-links-deactivated">&lt;</span>
				</mvt:if>
				<mvt:if expr="l.settings:page_links:current_page NE l.settings:page_links:last_page">
					<a href="&mvte:urls:_self:auto_sep;&mvte:page_links:next_link_params;&Order_BillEmail=&mvta:global:Order_BillEmail;&Order_BillZip=&mvta:global:Order_BillZip;" class="page-links-next">&gt;</a>
				<mvt:else>
					<span class="page-links-next page-links-deactivated">&gt;</span>
				</mvt:if>
				<span class="page-disp">
					<mvt:if expr="NOT l.settings:page_links:contains_first">
						<a href="&mvte:urls:_self:auto_sep;&mvte:page_links:first_link_params;&Order_BillEmail=&mvta:global:Order_BillEmail;&Order_BillZip=&mvta:global:Order_BillZip;" class="page-links-inactive">1</a>...
					</mvt:if>
					<mvt:foreach iterator="pages" array="page_links:pages">
						<mvt:if expr="l.settings:page_links:current_page EQ l.settings:pages:page_num">
							<span class="page-links-active">&mvte:pages:page_num;</span>
						<mvt:else>
							<a href="&mvte:urls:_self:auto_sep;&mvte:pages:link_params;&Order_BillEmail=&mvta:global:Order_BillEmail;&Order_BillZip=&mvta:global:Order_BillZip;" class="page-links-inactive">&mvte:pages:page_num;</a>
						</mvt:if>
					</mvt:foreach>
					<mvt:if expr="NOT l.settings:page_links:contains_last">
						...<a href="&mvte:urls:_self:auto_sep;&mvte:page_links:last_link_params;&Order_BillEmail=&mvta:global:Order_BillEmail;&Order_BillZip=&mvta:global:Order_BillZip;" class="page-links-inactive"><MvEVAL EXPR = "{ '&mvte:page_links:last_page;' }"></a>
					</mvt:if>
				</span>
			</span>
		</div>
	</mvt:if>
	</div><div class="clear"></div>
</div>

<div class="line-item">
	<table>
		<tr class="heading"><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ l.settings:invoice }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="order-invoice">
				Invoice
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:order_date }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="order-date">
				Order Date
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:products_bought }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="order-purchased">
				Products
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:quantity_bought }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="order-quantity">
				Quantity
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:status }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="order-status">
				Order Status
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:tracking }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="order-tracking">
				Shipments
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:order_total }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="order-total">
				Order Total
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="order-view">
			&nbsp;
		</td><MIVA STANDARDOUTPUTLEVEL = "">

		<MvIF EXPR = "{ l.settings:fields_custom }">
			<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:fields_custom">
				<MvASSIGN NAME = "l.safe_module_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.customfield:module ) }">
				<MvASSIGN NAME = "l.safe_field_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.customfield:code ) }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				<td class="customfield-header">
					&mvt:customfield_names:<MvEVAL EXPR = "{ l.safe_module_code $ ':' $ l.safe_field_code }">;
				</td><MIVA STANDARDOUTPUTLEVEL = "">
			</MvFOREACH>
		</MvIF>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		</tr>
		<mvt:foreach iterator="order" array="orders">
		<mvt:if expr = "l.pos1 MOD 2">
		<tr class="alt_row">
		<mvt:else>
		<tr>
		</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">

		<MvIF EXPR = "{ l.settings:invoice }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="order-invoice">
				&mvt:order:id;
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:order_date }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="order-date">
				&mvt:order:date_formatted;
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:products_bought }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="order-purchased">
				&mvt:order:prod_num;
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:quantity_bought }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="order-quantity">
				&mvt:order:prod_qty;
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:status }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="order-status">
				&mvt:order:order_status;
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:tracking }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="order-tracking">
				<mvt:foreach iterator="shipment" array="order:shipments">
				<a target="_blank" href="&mvt:shipment:tracklink_raw;">&mvt:shipment:tracknum;</a><br />
				</mvt:foreach>
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:order_total }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="order-total">
				&mvt:order:formatted_total;
			</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="order-view">
			<a href="&mvte:global:secure_sessionurl;Screen=ORDS&amp;Store_Code=&mvta:global:Store_Code;&amp;SortBy=&mvta:global:SortBy;&amp;Offset=&mvta:global:Offset;&amp;Order_ID=&mvt:order:id;&amp;Order_BillEmail=&mvta:global:Order_BillEmail;&amp;Order_BillZip=&mvta:global:Order_BillZip;">View</a>
		</td><MIVA STANDARDOUTPUTLEVEL = "">

		<MvIF EXPR = "{ l.settings:fields_custom }">
			<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:fields_custom">
				<MvASSIGN NAME = "l.safe_module_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.customfield:module ) }">
				<MvASSIGN NAME = "l.safe_field_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.customfield:code ) }">
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				<mvt:if expr="{ 'NOT ISNULL l.settings:order:customfield_values:' $ l.safe_module_code $ ':' $ l.safe_field_code }">
					<td class="order-customfields">
						&mvt:order:customfield_values:<MvEVAL EXPR = "{ l.safe_module_code $ ':' $ l.safe_field_code }">;
					</td>
				<mvt:else>
					<td>
						&nbsp;
					</td>
				</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			</MvFOREACH>
		</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
		</tr>
		</mvt:foreach>
	</table>

	<mvt:if expr="l.settings:page_disp_count LE 0">
		<mvt:if expr="g.Offset OR g.NextOffset">
		<div class="next-previous">
			<mvt:if expr="g.Offset GT 0">
			<div class="previous-button">
				<form method="get" action="&mvte:urls:_self:auto_noparams;">
					<mvt:item name="urls" param="hidden_params:_self:auto" />
					<input type="hidden" name="Per_Page" value="&mvte:global:Per_Page;" />
					<input type="hidden" name="SortBy" value="&mvte:global:SortBy;" />
					<input type="hidden" name="Order_BillEmail" value="&mvte:global:Order_BillEmail;" />
					<input type="hidden" name="Order_BillZip" value="&mvte:global:Order_BillZip;" />
					<input type="hidden" name="Offset" value="&mvte:global:PrevOffset;" />
					<mvt:item name="buttons" param="Previous" />
				</form>
			</div>
			</mvt:if>

			<mvt:if expr="g.NextOffset GT 0">
			<div class="next-button">
				<form method="get" action="&mvte:urls:_self:auto_noparams;">
					<mvt:item name="urls" param="hidden_params:_self:auto" />
					<input type="hidden" name="Per_Page" value="&mvte:global:Per_Page;" />
					<input type="hidden" name="SortBy" value="&mvte:global:SortBy;" />
					<input type="hidden" name="Order_BillEmail" value="&mvte:global:Order_BillEmail;" />
					<input type="hidden" name="Order_BillZip" value="&mvte:global:Order_BillZip;" />
					<input type="hidden" name="Offset" value="&mvte:global:NextOffset;" />
					<mvt:item name="buttons" param="Next" />
				</form>
			</div>
			</mvt:if>
		</div>
		</mvt:if>
	</mvt:if>
</div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "OrderItem_Load_Totals" PARAMETERS = "order_id, orderitem_count var, orderitem_total_quantity var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderItems"
				QUERY	= "{ 'SELECT COUNT( * ) AS orderitem_count, SUM( quantity ) AS total_quantity FROM ' $ g.Store_Table_Prefix $ 'OrderItems WHERE order_id = ?' }"
				FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CSSUI-CMP-OLST-00021', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ OrderItems.d.EOF }">
		<MvASSIGN NAME = "l.orderitem_count"			VALUE = 0>
		<MvASSIGN NAME = "l.orderitem_total_quantity"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.orderitem_count"			VALUE = "{ int( OrderItems.d.orderitem_count ) }">
		<MvASSIGN NAME = "l.orderitem_total_quantity"	VALUE = "{ int( OrderItems.d.total_quantity ) }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Validate_Default_Sort_Method" PARAMETERS = "default_sort, working_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( l.default_sort EQ 'InvAsc' OR l.default_sort EQ 'InvDesc' ) AND ( l.working_settings:sort_by:invoice ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ ( l.default_sort EQ 'OrdDtAsc' OR l.default_sort EQ 'OrdDtDesc' ) AND ( l.working_settings:sort_by:order_date ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ ( l.default_sort EQ 'StusAsc' OR l.default_sort EQ 'StusDesc' ) AND ( l.working_settings:sort_by:status ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ ( l.default_sort EQ 'OrdTotAsc' OR l.default_sort EQ 'OrdTotDesc' ) AND ( l.working_settings:sort_by:order_total ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.working_settings"				VALUE = "{ l.settings }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'O', l.provide_xml,	'Notes',			l.notes ) 										OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O', l.provide_xml,	'OrdersPerPage',	l.working_settings:per_page )					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O', l.provide_xml,	'PageCountNumber',	l.working_settings:page_disp_count )			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'ItemsPerPage',		l.working_settings:items_per_page_filter )		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'SortBySwitch',		l.working_settings:sort_by:switch )				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByInvoice',	l.working_settings:sort_by:invoice )			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByOrderDate',	l.working_settings:sort_by:order_date )			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByStatus',		l.working_settings:sort_by:status )				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByOrderTotal',	l.working_settings:sort_by:order_total ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'SortableCustomFields' ) }">
		<MvIF EXPR = "{ NOT ComponentModule_Provision_SortableCustomFields( l.module, l.provide_xml:tags:SortableCustomFields[ 1 ], l.working_settings ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Fields' ) }">
		<MvIF EXPR = "{ NOT ComponentModule_Provision_Fields( l.module, l.provide_xml:tags:Fields[ 1 ], l.working_settings ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'DefaultSort' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(	'O', l.provide_xml,	'DefaultSort', l.default_sort,
																			 'InvAsc,InvDesc,OrdDtAsc,OrdDtDesc,StusAsc,StusDesc,OrdTotAsc,OrdTotDesc,customfield,customfield_desc',
																			 'InvAsc,InvDesc,OrdDtAsc,OrdDtDesc,StusAsc,StusDesc,OrdTotAsc,OrdTotDesc,customfield,customfield_desc' ) }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvIF EXPR = "{ ( l.default_sort NE 'customfield' ) AND ( l.default_sort NE 'customfield_desc' ) }">
			<MvIF EXPR = "{ Validate_Default_Sort_Method( l.default_sort, l.working_settings ) }">
				<MvASSIGN NAME = "l.working_settings:default_sort"	VALUE = "{ l.default_sort }">
			<MvELSE>
				<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Cannot set default sort to \'' $ l.default_sort $ '\' without enabling the column to be sortable' ) }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml:tags:DefaultSort[ 1 ], 'module',	l.module_code ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml:tags:DefaultSort[ 1 ], 'code',	l.field_code ) }">
				<MvFUNCTIONRETURN>
			</MvIF>

			<MvIF EXPR = "{ miva_array_search( l.working_settings:sort_custom, 1, l.custom_field, '( l.custom_field:module EQ l.module_code ) AND ( l.custom_field:code EQ l.field_code )' ) }">
				<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ModuleAndFeatures_Load_Code_Cached( l.module_code, l.customfield_module ) }">
					<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
						<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
					</MvIF>

					<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Module not found' ) }">
				<MvELSEIF EXPR = "{ NOT l.customfield_module:active }">
					<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Module \'' $ l.customfield_module:code $ '\' has been deactivated' ) }">
				<MvELSEIF EXPR = "{ NOT l.customfield_module:feature_hash:fields_ordr }">
					<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Module \'' $ l.customfield_module:code $ '\' does not provide the \'fields_ordr\' feature' ) }">
				<MvELSEIF EXPR = "{ l.customfield_module:api_ver LT 9.07 }">
					<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Field \'' $ l.field_code $ '\' does not support sorting' ) }">
				<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreModule_Count_Module_Cached( l.customfield_module:id ) }">
					<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Module \'' $ l.customfield_module:code $ '\' is not installed in store \'' $ g.Store:name $ '\'' ) }">
				<MvELSE>
					<MvEVAL EXPR = "{ [ g.Module_Root $ l.customfield_module:module ].Module_Order_Field_Capabilities( l.customfield_module, l.field_code, l.capabilities ) }">

					<MvIF EXPR = "{ l.capabilities:orderby }">
						<MvASSIGN NAME = "l.working_settings:default_sort"	VALUE = "{ l.default_sort $ ':' $ l.module_code $ ':' $ l.field_code }">
					<MvELSE>
						<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Field \'' $ l.field_code $ '\' does not support sorting' ) }">
					</MvIF>
				</MvIF>
			<MvELSE>
				<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Cannot set default sort to \'' $ l.default_sort $ ':' $ l.module_code $ ':' $ l.field_code $ '\' without enabling the column to be sortable' ) }">
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
		<MvASSIGN NAME = "l.source"						VALUE = "{ trim( l.provide_xml:tags:Template[ 1 ]:value ) }">
		<MvASSIGN NAME = "l.working_settings:advanced"	VALUE = 1>
	<MvELSE>
		<MvASSIGN NAME = "l.working_settings:advanced"	VALUE = 0>

		<MvEVAL EXPR = "{ Generate_Code( l.working_settings, l.source ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.source, l.working_settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings" VALUE = "{ l.working_settings }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision_SortableCustomFields" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:sort_custom"	VALUE = "">
	<MvASSIGN NAME = "l.customfield_count"		VALUE = 0>

	<MvFOREACH ITERATOR = "l.child_xml" ARRAY = "l.provide_xml:children">
		<MvASSIGN NAME = "l.name"					VALUE = "{ tolower( l.child_xml:name ) }">

		<MvIF EXPR = "{ l.name EQ 'custom' }">
			<MvASSIGN NAME = "l.customfield_count"	VALUE = "{ l.customfield_count + 1 }">

			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.child_xml, 'module',	l.settings:sort_custom[ l.customfield_count ]:module ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.child_xml, 'code',	l.settings:sort_custom[ l.customfield_count ]:code ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.child_xml, 'Unknown tag' ) }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision_Fields" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:invoice"			VALUE = 0>
	<MvASSIGN NAME = "l.settings:products_bought"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:status"			VALUE = 0>
	<MvASSIGN NAME = "l.settings:order_total"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:order_date"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:quantity_bought"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:tracking"			VALUE = 0>

	<MvASSIGN NAME = "l.settings:fields_custom"		VALUE = "">

	<MvASSIGN NAME = "l.child_pos"		VALUE = 1>
	<MvASSIGN NAME = "l.child_count"	VALUE = "{ miva_array_elements( l.provide_xml:children ) }">
	<MvWHILE EXPR = "{ l.child_pos LE l.child_count }">
		<MvASSIGN NAME = "l.name"		VALUE = "{ tolower( l.provide_xml:children[ l.child_pos ]:name ) }">

		<MvIF EXPR = "{ l.name EQ 'invoice' }">					<MvASSIGN NAME = "l.settings:invoice"			VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'productspurchased' }">	<MvASSIGN NAME = "l.settings:products_bought"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'orderstatus' }">			<MvASSIGN NAME = "l.settings:status"			VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'ordertotal' }">			<MvASSIGN NAME = "l.settings:order_total"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'orderdate' }">			<MvASSIGN NAME = "l.settings:order_date"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'quantitypurchased' }">	<MvASSIGN NAME = "l.settings:quantity_bought"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'ordertracking' }">		<MvASSIGN NAME = "l.settings:tracking"			VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'custom' }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml:children[ l.child_pos ], 'module',	l.field_module ) OR
							NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml:children[ l.child_pos ], 'code',		l.field_code ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ModuleAndFeatures_Load_Code_Cached( l.field_module, l.customfield_module ) }">
				<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
					<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml:children[ l.child_pos ] ) }">
				</MvIF>

				<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml:children[ l.child_pos ], 'Module not found' ) }">
			<MvELSEIF EXPR = "{ NOT l.customfield_module:active }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml:children[ l.child_pos ], 'Module \'' $ l.customfield_module:code $ '\' has been deactivated' ) }">
			<MvELSEIF EXPR = "{ NOT l.customfield_module:feature_hash:fields_ordr }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml:children[ l.child_pos ], 'Module \'' $ l.customfield_module:code $ '\' does not provide the \'fields_ordr\' feature' ) }">
			<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreModule_Count_Module_Cached( l.customfield_module:id ) }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml:children[ l.child_pos ], 'Module \'' $ l.customfield_module:code $ '\' is not installed in store \'' $ g.Store:name $ '\'' ) }">
			<MvELSE>
				<MvASSIGN NAME = "l.custom_field"			VALUE = "">
				<MvASSIGN NAME = "l.custom_field:module"	VALUE = "{ l.field_module }">
				<MvASSIGN NAME = "l.custom_field:code"		VALUE = "{ l.field_code }">

				<MvASSIGN NAME = "l.null"					VALUE = "{ miva_array_insert_var( l.settings:fields_custom, l.custom_field, -1 ) }">
			</MvIF>
		</MvIF>

		<MvASSIGN NAME = "l.child_pos"	VALUE = "{ l.child_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pages" VALUE = "{ [ g.Module_Feature_TUI_DB ].Item_Load_PageList( l.item ) }">
	
	<MvFUNCTIONRETURN VALUE = "{ 'Exports Order History List design/template from pages: ' $ l.pages }">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item_Runtime( l.item:id, l.pages ) }">
			<MvREFERENCEARRAY NAME = "l.settings" VARIABLE = "l.page:settings">
				<MvMEMBER NAME = "{ l.item:code }">
			</MvREFERENCEARRAY>

			<Page_Update code="{ l.page:code }">
				<Item code="{ l.item:code }">
					<OrdersPerPage><MvEVAL EXPR = "{ int( l.settings:per_page ) }"></OrdersPerPage>
					<PageCountNumber><MvEVAL EXPR = "{ int( l.settings:page_disp_count ) }"></PageCountNumber>
					<ItemsPerPage><MvIF EXPR = "{ l.settings:items_per_page_filter }">Yes<MvELSE>No</MvIF></ItemsPerPage>
					<SortBySwitch><MvIF EXPR = "{ l.settings:sort_by:switch }">Yes<MvELSE>No</MvIF></SortBySwitch>
					<SortByInvoice><MvIF EXPR = "{ l.settings:sort_by:invoice }">Yes<MvELSE>No</MvIF></SortByInvoice>
					<SortByOrderDate><MvIF EXPR = "{ l.settings:sort_by:order_date }">Yes<MvELSE>No</MvIF></SortByOrderDate>
					<SortByStatus><MvIF EXPR = "{ l.settings:sort_by:status }">Yes<MvELSE>No</MvIF></SortByStatus>
					<SortByOrderTotal><MvIF EXPR = "{ l.settings:sort_by:order_total }">Yes<MvELSE>No</MvIF></SortByOrderTotal>

					<SortableCustomFields>
						<MvFOREACH ITERATOR = "l.custom_field" ARRAY = "l.settings:sort_custom">
							<Custom module="{ encodeentities( l.custom_field:module ) }" code="{ encodeentities( l.custom_field:code ) }" />
						</MvFOREACH>
					</SortableCustomFields>

					<MvIF EXPR = "{ ( ( 'customfield:' IN l.settings:default_sort ) EQ 1 ) OR
										( ( 'customfield_desc:' IN l.settings:default_sort ) EQ 1 ) }">
						<MvASSIGN NAME = "l.sort_type"		VALUE = "{ gettoken( l.settings:default_sort, ':', 1 ) }">
						<MvASSIGN NAME = "l.module_code"	VALUE = "{ gettoken( l.settings:default_sort, ':', 2 ) }">
						<MvASSIGN NAME = "l.field_code"		VALUE = "{ gettoken( l.settings:default_sort, ':', 3 ) }">

						<DefaultSort module="{ encodeentities( l.module_code ) }" code="{ encodeentities( l.field_code ) }"><MvEVAL EXPR = "{ miva_cdata_encode( l.sort_type ) }"></DefaultSort>
					<MvELSE>
						<DefaultSort><MvEVAL EXPR = "{ miva_cdata_encode( l.settings:default_sort ) }"></DefaultSort>
					</MvIF>

					<MvIF EXPR = "{ l.settings:advanced }">
						<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
							<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.templateversion:source ) }"></Template>
						</MvIF>
					<MvELSE>
						<Fields>
							<MvIF EXPR = "{ l.settings:invoice }">			<Invoice />				</MvIF>
							<MvIF EXPR = "{ l.settings:products_bought }">	<ProductsPurchased />	</MvIF>
							<MvIF EXPR = "{ l.settings:status }">			<OrderStatus />			</MvIF>
							<MvIF EXPR = "{ l.settings:order_total }">		<OrderTotal />			</MvIF>
							<MvIF EXPR = "{ l.settings:order_date }">		<OrderDate />			</MvIF>
							<MvIF EXPR = "{ l.settings:quantity_bought }">	<QuantityPurchased />	</MvIF>
							<MvIF EXPR = "{ l.settings:tracking }">			<OrderTracking />		</MvIF>

							<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:fields_custom">
								<Custom module="{ encodeentities( l.customfield:module ) }" code="{ encodeentities( l.customfield:code ) }" />
							</MvFOREACH>
						</Fields>
					</MvIF>

					<Notes>#Set_Current_Time#</Notes>
				</Item>
			</Page_Update>
		</MvFOREACH>
	</MvCAPTURE>
</MvFUNCTION>

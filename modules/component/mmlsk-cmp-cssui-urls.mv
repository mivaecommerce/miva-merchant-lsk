<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-URI-
| Next Error Code: 1
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-urls">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI URLs">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.1001">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.03">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, component_frag">
</MvFUNCTION>

<MvCOMMENT>
|
| Component Feature (component)
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.data"						VALUE = "">
	<MvASSIGN NAME = "l.data:auto_flags"			VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Flags( '' ) }">
	<MvASSIGN NAME = "l.data:auto_sep_flags"		VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Flags( 'sep' ) }">
	<MvASSIGN NAME = "l.data:auto_noparams_flags"	VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Flags( 'nostore,nosession' ) }">
	<MvASSIGN NAME = "l.data:secure_flags"			VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Flags( 'secure' ) }">
	<MvASSIGN NAME = "l.data:secure_sep_flags"		VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Flags( 'secure,sep' ) }">
	<MvASSIGN NAME = "l.data:secure_noparams_flags"	VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Flags( 'secure,nostore,nosession' ) }">
	<MvASSIGN NAME = "l.data:rr_flags"				VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Flags( 'rr' ) }">
	<MvASSIGN NAME = "l.data:rr_sep_flags"			VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Flags( 'rr,sep' ) }">
	<MvASSIGN NAME = "l.data:rr_noparams_flags"		VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Flags( 'rr,nostore,nosession' ) }">
	<MvASSIGN NAME = "l.data:uri_settings"			VALUE = "">
	<MvASSIGN NAME = "l.data:seo_settings"			VALUE = "">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_URI_DB ].URISettings_Load_Cached( l.data:uri_settings ) OR
					NOT [ g.Module_Library_DB ].SEOSettings_Load_Cached( l.data:seo_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.all_settings:urls"			VALUE = "">

	<MvEVAL EXPR = "{ miva_struct_member_callback( l.all_settings:urls, 'CSSUI_URL_Callback', l.data ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ ( 'hidden_params:' IN l.param ) NE 1 }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_URI_DB ].URISettings_Load_Cached( l.urisettings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.screen"	VALUE = "{ gettoken( l.param, ':', 2 ) }">
	<MvASSIGN NAME = "l.type"	VALUE = "{ gettoken( l.param, ':', 3 ) }">

	<MvIF EXPR = "{ len_var( l.screen ) EQ 0 OR len_var( l.type ) EQ 0 }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ l.screen EQ '_self' }">
		<MvASSIGN NAME = "l.screen" VALUE = "{ g.Screen }">
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.url" VARIABLE = "l.all_settings:urls">
		<MvMEMBER NAME = "{ l.screen }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ l.url:source EQ 'long' }">
		<input type="hidden" name="Screen" value="{ encodeentities( l.screen ) }" />

		<MvIF EXPR = "{ g.Store_Count GT 1 }">
			<input type="hidden" name="Store_Code" value="{ encodeentities( g.Store:code ) }" />
		</MvIF>

		<MvIF EXPR = "{ toupper( l.screen ) EQ 'PROD' AND NOT ISNULL g.Product_Code }">
			<input type="hidden" name="Product_Code" value="{ encodeentities( g.Product_Code ) }" />
		<MvELSEIF EXPR = "{ toupper( l.screen ) EQ 'CTGY' AND NOT ISNULL g.Category_Code }">
			<input type="hidden" name="Category_Code" value="{ encodeentities( g.Category_Code ) }" />
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.Domain:mm_params EQ 'always' }">
		<MvIF EXPR = "{ NOT ISNULL g.Session_ID }">
			<input type="hidden" name="Session_ID" value="{ encodeentities( g.Session_ID ) }" />
		</MvIF>

		<MvIF EXPR = "{ ( ISNULL l.urisettings:surlprefix ) OR ( g.Secure AND ( l.type EQ 'secure' OR l.type EQ 'rr' ) ) }">
			<MvIF EXPR = "{ NOT ISNULL g.Customer_Session_ID }">	<input type="hidden" name="Customer_Session_ID" value="{ encodeentities( g.Customer_Session_ID ) }" />		</MvIF>
			<MvIF EXPR = "{ NOT ISNULL g.Affiliate_Session_ID }">	<input type="hidden" name="Affiliate_Session_ID" value="{ encodeentities( g.Affiliate_Session_ID ) }" />	</MvIF>
			<MvIF EXPR = "{ NOT ISNULL g.Checkout_Session_ID }">	<input type="hidden" name="Checkout_Session_ID" value="{ encodeentities( g.Checkout_Session_ID ) }" />		</MvIF>
		</MvIF>
	<MvELSEIF EXPR = "{ g.Domain:mm_params EQ 'switch' }">
		<MvIF EXPR = "{ ( g.Secure OR l.type EQ 'secure' ) AND ( NOT ISNULL l.urisettings:surlprefix ) AND NOT ISNULL g.Session_ID }">
			<input type="hidden" name="Session_ID" value="{ encodeentities( g.Session_ID ) }" />
		</MvIF>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvCOMMENT>
|
| Component Fragment Feature (component_frag)
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Initialize_Fragment" PARAMETERS = "module var, item, page_all_settings var, page_item_settings var, fragment_all_settings var, fragment_item_settings var, merged_all_settings var, merged_item_settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ ComponentModule_Initialize( l.module, l.item, l.merged_all_settings, l.merged_item_settings ) }">
</MvFUNCTION>

<MvCOMMENT>
|
| Helper Functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "CSSUI_URL_Callback" PARAMETERS = "urls var, screen, data var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvREFERENCEARRAY NAME = "l.url" VARIABLE = "l.urls">
		<MvMEMBER NAME = "{ l.screen }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ tolower( l.screen ) EQ '_self' }">
		<MvIF EXPR = "{ NOT ISNULL g.Session:uri:uri }">
			<MvASSIGN NAME = "l.url"						VALUE = "">
			<MvASSIGN NAME = "l.url:source"					VALUE = "uri">
			<MvASSIGN NAME = "l.url:secure"					VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:secure_flags,				g.Session:uri:uri, '' ) }">
			<MvASSIGN NAME = "l.url:secure_sep"				VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:secure_sep_flags,			g.Session:uri:uri, '' ) }">
			<MvASSIGN NAME = "l.url:secure_noparams"		VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:secure_noparams_flags,	g.Session:uri:uri, '' ) }">

			<MvIF EXPR = "{ g.Secure }">
				<MvASSIGN NAME = "l.url:auto"				VALUE = "{ l.url:secure }">
				<MvASSIGN NAME = "l.url:auto_sep"			VALUE = "{ l.url:secure_sep }">
				<MvASSIGN NAME = "l.url:auto_noparams"		VALUE = "{ l.url:secure_noparams }">
				<MvASSIGN NAME = "l.url:rr"					VALUE = "{ l.url:secure }">
				<MvASSIGN NAME = "l.url:rr_sep"				VALUE = "{ l.url:secure_sep }">
				<MvASSIGN NAME = "l.url:rr_noparams"		VALUE = "{ l.url:secure_noparams }">
			<MvELSE>
				<MvASSIGN NAME = "l.url:auto"				VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:auto_flags,				g.Session:uri:uri, '' ) }">
				<MvASSIGN NAME = "l.url:auto_sep"			VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:auto_sep_flags,			g.Session:uri:uri, '' ) }">
				<MvASSIGN NAME = "l.url:auto_noparams"		VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:auto_noparams_flags,		g.Session:uri:uri, '' ) }">
				<MvASSIGN NAME = "l.url:rr"					VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:rr_flags,					g.Session:uri:uri, '' ) }">
				<MvASSIGN NAME = "l.url:rr_sep"				VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:rr_sep_flags,				g.Session:uri:uri, '' ) }">
				<MvASSIGN NAME = "l.url:rr_noparams"		VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:rr_noparams_flags,		g.Session:uri:uri, '' ) }">
			</MvIF>
		<MvELSEIF EXPR = "{ toupper( g.Screen ) EQ 'CTGY' }">
			<MvIF EXPR = "{ l.data:uri_settings:source EQ 'legacy_seo' AND l.data:seo_settings:sl_active }">
				<MvASSIGN NAME = "l.url"					VALUE = "">
				<MvASSIGN NAME = "l.url:source"				VALUE = "legacy_seo">
				<MvASSIGN NAME = "l.url:secure"				VALUE = "{ Legacy_SEO_Category_URL_Secure( l.data:seo_settings, g.Category_Code ) }">
				<MvASSIGN NAME = "l.url:secure_noparams"	VALUE = "{ l.url:secure }">

				<MvIF EXPR = "{ g.Secure }">
					<MvASSIGN NAME = "l.url:auto"			VALUE = "{ l.url:secure }">
					<MvASSIGN NAME = "l.url:auto_noparams"	VALUE = "{ l.url:secure_noparams }">
					<MvASSIGN NAME = "l.url:rr"				VALUE = "{ l.url:secure }">
					<MvASSIGN NAME = "l.url:rr_noparams"	VALUE = "{ l.url:secure_noparams }">
				<MvELSE>
					<MvASSIGN NAME = "l.url:auto"			VALUE = "{ Legacy_SEO_Category_URL( l.data:seo_settings, g.Category_Code ) }">
					<MvASSIGN NAME = "l.url:auto_noparams"	VALUE = "{ l.url:auto }">
					<MvASSIGN NAME = "l.url:rr"				VALUE = "{ l.url:auto }">
					<MvASSIGN NAME = "l.url:rr_noparams"	VALUE = "{ l.url:auto_noparams }">
				</MvIF>

				<MvASSIGN NAME = "l.url:secure_sep"			VALUE = "{ l.url:secure	$ '?' }">
				<MvASSIGN NAME = "l.url:auto_sep"			VALUE = "{ l.url:auto	$ '?' }">
				<MvASSIGN NAME = "l.url:rr_sep"				VALUE = "{ l.url:rr		$ '?' }">
			<MvELSEIF EXPR = "{ [ g.Module_Feature_URI_UT ].URISettings_Source_Is_Valid_Module( l.data:uri_settings ) }">
				<MvASSIGN NAME = "l.url"					VALUE = "">
				<MvASSIGN NAME = "l.url:source"				VALUE = "module">
				<MvASSIGN NAME = "l.url:secure"				VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Category_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Category_Code, l.data:secure_flags ) }">
				<MvASSIGN NAME = "l.url:secure_sep"			VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Category_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Category_Code, l.data:secure_sep_flags ) }">
				<MvASSIGN NAME = "l.url:secure_noparams"	VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Category_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Category_Code, l.data:secure_noparams_flags ) }">

				<MvIF EXPR = "{ g.Secure }">
					<MvASSIGN NAME = "l.url:auto"			VALUE = "{ l.url:secure }">
					<MvASSIGN NAME = "l.url:auto_sep"		VALUE = "{ l.url:secure_sep }">
					<MvASSIGN NAME = "l.url:auto_noparams"	VALUE = "{ l.url:secure_noparams }">
					<MvASSIGN NAME = "l.url:rr"				VALUE = "{ l.url:secure }">
					<MvASSIGN NAME = "l.url:rr_sep"			VALUE = "{ l.url:secure_sep }">
					<MvASSIGN NAME = "l.url:rr_noparams"	VALUE = "{ l.url:secure_noparams }">
				<MvELSE>
					<MvASSIGN NAME = "l.url:auto"			VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Category_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Category_Code, l.data:auto_flags ) }">
					<MvASSIGN NAME = "l.url:auto_sep"		VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Category_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Category_Code, l.data:auto_sep_flags ) }">
					<MvASSIGN NAME = "l.url:auto_noparams"	VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Category_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Category_Code, l.data:auto_noparams_flags ) }">
					<MvASSIGN NAME = "l.url:rr"				VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Category_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Category_Code, l.data:rr_flags ) }">
					<MvASSIGN NAME = "l.url:rr_sep"			VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Category_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Category_Code, l.data:rr_sep_flags ) }">
					<MvASSIGN NAME = "l.url:rr_noparams"	VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Category_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Category_Code, l.data:rr_noparams_flags ) }">
				</MvIF>
			<MvELSE>
				<MvASSIGN NAME = "l.params"					VALUE = "{ 'Category_Code=' $ encodeattribute( g.Category_Code ) }">

				<MvASSIGN NAME = "l.url"					VALUE = "{ CSSUI_URL_Callback( l.urls, 'CTGY', l.data ) }">
				<MvASSIGN NAME = "l.url:secure"				VALUE = "{ l.url:secure		$ '&' $ l.params }">
				<MvASSIGN NAME = "l.url:auto"				VALUE = "{ l.url:auto		$ '&' $ l.params }">
				<MvASSIGN NAME = "l.url:rr"					VALUE = "{ l.url:rr			$ '&' $ l.params }">
				<MvASSIGN NAME = "l.url:secure_sep"			VALUE = "{ l.url:secure_sep	$ l.params $ '&' }">
				<MvASSIGN NAME = "l.url:auto_sep"			VALUE = "{ l.url:auto_sep	$ l.params $ '&' }">
				<MvASSIGN NAME = "l.url:rr_sep"				VALUE = "{ l.url:rr_sep		$ l.params $ '&' }">
			</MvIF>
		<MvELSEIF EXPR = "{ toupper( g.Screen ) EQ 'PROD' }">
			<MvIF EXPR = "{ l.data:uri_settings:source EQ 'legacy_seo' AND l.data:seo_settings:sl_active }">
				<MvASSIGN NAME = "l.url"					VALUE = "">
				<MvASSIGN NAME = "l.url:source"				VALUE = "legacy_seo">
				<MvASSIGN NAME = "l.url:secure"				VALUE = "{ Legacy_SEO_Product_URL_Secure( l.data:seo_settings, g.Product_Code ) }">
				<MvASSIGN NAME = "l.url:secure_noparams"	VALUE = "{ l.url:secure }">

				<MvIF EXPR = "{ g.Secure }">
					<MvASSIGN NAME = "l.url:auto"			VALUE = "{ l.url:secure }">
					<MvASSIGN NAME = "l.url:auto_noparams"	VALUE = "{ l.url:secure_noparams }">
					<MvASSIGN NAME = "l.url:rr"				VALUE = "{ l.url:secure }">
					<MvASSIGN NAME = "l.url:rr_noparams"	VALUE = "{ l.url:secure_noparams }">
				<MvELSE>
					<MvASSIGN NAME = "l.url:auto"			VALUE = "{ Legacy_SEO_Product_URL( l.data:seo_settings, g.Product_Code ) }">
					<MvASSIGN NAME = "l.url:auto_noparams"	VALUE = "{ l.url:auto }">
					<MvASSIGN NAME = "l.url:rr"				VALUE = "{ l.url:auto }">
					<MvASSIGN NAME = "l.url:rr_noparams"	VALUE = "{ l.url:auto_noparams }">
				</MvIF>

				<MvASSIGN NAME = "l.url:secure_sep"			VALUE = "{ l.url:secure	$ '?' }">
				<MvASSIGN NAME = "l.url:auto_sep"			VALUE = "{ l.url:auto	$ '?' }">
				<MvASSIGN NAME = "l.url:rr_sep"				VALUE = "{ l.url:rr		$ '?' }">
			<MvELSEIF EXPR = "{ [ g.Module_Feature_URI_UT ].URISettings_Source_Is_Valid_Module( l.data:uri_settings ) }">
				<MvASSIGN NAME = "l.url"					VALUE = "">
				<MvASSIGN NAME = "l.url:secure"				VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Product_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Product_Code, l.data:secure_flags ) }">
				<MvASSIGN NAME = "l.url:secure_sep"			VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Product_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Product_Code, l.data:secure_sep_flags ) }">
				<MvASSIGN NAME = "l.url:secure_noparams"	VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Product_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Product_Code, l.data:secure_noparams_flags ) }">

				<MvIF EXPR = "{ g.Secure }">
					<MvASSIGN NAME = "l.url:auto"			VALUE = "{ l.url:secure }">
					<MvASSIGN NAME = "l.url:auto_sep"		VALUE = "{ l.url:secure_sep }">
					<MvASSIGN NAME = "l.url:auto_noparams"	VALUE = "{ l.url:secure_noparams }">
					<MvASSIGN NAME = "l.url:rr"				VALUE = "{ l.url:secure }">
					<MvASSIGN NAME = "l.url:rr_sep"			VALUE = "{ l.url:secure_sep }">
					<MvASSIGN NAME = "l.url:rr_noparams"	VALUE = "{ l.url:secure_noparams }">
				<MvELSE>
					<MvASSIGN NAME = "l.url:auto"			VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Product_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Product_Code, l.data:auto_flags ) }">
					<MvASSIGN NAME = "l.url:auto_sep"		VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Product_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Product_Code, l.data:auto_sep_flags ) }">
					<MvASSIGN NAME = "l.url:auto_noparams"	VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Product_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Product_Code, l.data:auto_noparams_flags ) }">
					<MvASSIGN NAME = "l.url:rr"				VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Product_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Product_Code, l.data:rr_flags ) }">
					<MvASSIGN NAME = "l.url:rr_sep"			VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Product_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Product_Code, l.data:rr_sep_flags ) }">
					<MvASSIGN NAME = "l.url:rr_noparams"	VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Product_Code_URL( l.data:uri_settings:module, l.data:uri_settings, g.Product_Code, l.data:rr_noparams_flags ) }">
				</MvIF>
			<MvELSE>
				<MvASSIGN NAME = "l.params"					VALUE = "{ 'Product_Code=' $ encodeattribute( g.Product_Code ) }">

				<MvASSIGN NAME = "l.url"					VALUE = "{ CSSUI_URL_Callback( l.urls, 'PROD', l.data ) }">
				<MvASSIGN NAME = "l.url:secure"				VALUE = "{ l.url:secure 	$ '&' $ l.params }">
				<MvASSIGN NAME = "l.url:auto"				VALUE = "{ l.url:auto		$ '&' $ l.params }">
				<MvASSIGN NAME = "l.url:rr"					VALUE = "{ l.url:rr			$ '&' $ l.params }">
				<MvASSIGN NAME = "l.url:secure_sep"			VALUE = "{ l.url:secure_sep	$ l.params $ '&' }">
				<MvASSIGN NAME = "l.url:auto_sep"			VALUE = "{ l.url:auto_sep	$ l.params $ '&' }">
				<MvASSIGN NAME = "l.url:rr_sep"				VALUE = "{ l.url:rr_sep		$ l.params $ '&' }">
			</MvIF>
		<MvELSEIF EXPR = "{ NOT ISNULL g.Screen }">
			<MvASSIGN NAME = "l.url"						VALUE = "{ CSSUI_URL_Callback( l.urls, g.Screen, l.data ) }">
		</MvIF>
	<MvELSEIF EXPR = "{ l.data:uri_settings:source EQ 'legacy_seo' AND l.data:seo_settings:sl_active }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_Code_Cached( l.screen, l.page ) }">
			<MvASSIGN NAME = "l.secure"						VALUE = "{ URL_Screen_Secure( l.screen ) }">
		<MvELSE>
			<MvASSIGN NAME = "l.secure"						VALUE = "{ l.page:secure }">
			<MvASSIGN NAME = "l.screen"						VALUE = "{ l.page:code }">
		</MvIF>

		<MvASSIGN NAME = "l.url"							VALUE = "">
		<MvASSIGN NAME = "l.url:source"						VALUE = "legacy_seo">
		<MvASSIGN NAME = "l.url:secure"						VALUE = "{ Legacy_SEO_URL_Secure( l.data:seo_settings, l.screen ) }">
		<MvASSIGN NAME = "l.url:secure_noparams"			VALUE = "{ l.url:secure }">

		<MvIF EXPR = "{ l.secure }">
			<MvASSIGN NAME = "l.url:auto"					VALUE = "{ l.url:secure }">
			<MvASSIGN NAME = "l.url:auto_noparams"			VALUE = "{ l.url:secure_noparams }">
			<MvASSIGN NAME = "l.url:rr"						VALUE = "{ l.url:secure }">
			<MvASSIGN NAME = "l.url:rr_noparams"			VALUE = "{ l.url:secure_noparams }">
		<MvELSE>
			<MvASSIGN NAME = "l.url:auto"					VALUE = "{ Legacy_SEO_URL( l.data:seo_settings, l.screen ) }">
			<MvASSIGN NAME = "l.url:auto_noparams"			VALUE = "{ l.url:auto }">

			<MvIF EXPR = "{ g.Secure }">
				<MvASSIGN NAME = "l.url:rr"					VALUE = "{ l.url:secure }">
				<MvASSIGN NAME = "l.url:rr_noparams"		VALUE = "{ l.url:secure_noparams }">
			<MvELSE>
				<MvASSIGN NAME = "l.url:rr"					VALUE = "{ l.url:auto }">
				<MvASSIGN NAME = "l.url:rr_noparams"		VALUE = "{ l.url:auto_noparams }">
			</MvIF>
		</MvIF>

		<MvASSIGN NAME = "l.url:secure_sep"					VALUE = "{ l.url:secure	$ '?' }">
		<MvASSIGN NAME = "l.url:auto_sep"					VALUE = "{ l.url:auto	$ '?' }">
		<MvASSIGN NAME = "l.url:rr_sep"						VALUE = "{ l.url:rr		$ '?' }">
	<MvELSEIF EXPR = "{ [ g.Module_Feature_URI_UT ].URISettings_Source_Is_Valid_Module( l.data:uri_settings ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_Code_Cached( l.screen, l.page ) }">
			<MvASSIGN NAME = "l.secure"						VALUE = "{ URL_Screen_Secure( l.screen ) }">
			<MvASSIGN NAME = "l.page_loaded"				VALUE = 0>
		<MvELSE>
			<MvASSIGN NAME = "l.page_loaded"				VALUE = 1>
			<MvASSIGN NAME = "l.secure"						VALUE = "{ l.page:secure }">
			<MvASSIGN NAME = "l.screen"						VALUE = "{ l.page:code }">
		</MvIF>

		<MvASSIGN NAME = "l.url"							VALUE = "">
		<MvASSIGN NAME = "l.url:source"						VALUE = "module">

		<MvIF EXPR = "{ l.page_loaded }">
			<MvASSIGN NAME = "l.url:secure"					VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Page_URL( l.data:uri_settings:module, l.data:uri_settings, l.page, l.data:secure_flags ) }">
			<MvASSIGN NAME = "l.url:secure_sep"				VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Page_URL( l.data:uri_settings:module, l.data:uri_settings, l.page, l.data:secure_sep_flags ) }">
			<MvASSIGN NAME = "l.url:secure_noparams"		VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Page_URL( l.data:uri_settings:module, l.data:uri_settings, l.page, l.data:secure_noparams_flags ) }">
		<MvELSE>
			<MvASSIGN NAME = "l.url:secure"					VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Screen_URL( l.data:uri_settings:module, l.data:uri_settings, l.screen, l.data:secure_flags ) }">
			<MvASSIGN NAME = "l.url:secure_sep"				VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Screen_URL( l.data:uri_settings:module, l.data:uri_settings, l.screen, l.data:secure_sep_flags ) }">
			<MvASSIGN NAME = "l.url:secure_noparams"		VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Screen_URL( l.data:uri_settings:module, l.data:uri_settings, l.screen, l.data:secure_noparams_flags ) }">
		</MvIF>

		<MvIF EXPR = "{ l.secure }">
			<MvASSIGN NAME = "l.url:auto"					VALUE = "{ l.url:secure }">
			<MvASSIGN NAME = "l.url:auto_sep"				VALUE = "{ l.url:secure_sep }">
			<MvASSIGN NAME = "l.url:auto_noparams"			VALUE = "{ l.url:secure_noparams }">
			<MvASSIGN NAME = "l.url:rr"						VALUE = "{ l.url:secure }">
			<MvASSIGN NAME = "l.url:rr_sep"					VALUE = "{ l.url:secure_sep }">
			<MvASSIGN NAME = "l.url:rr_noparams"			VALUE = "{ l.url:secure_noparams }">
		<MvELSE>
			<MvIF EXPR = "{ l.page_loaded }">
				<MvASSIGN NAME = "l.url:auto"				VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Page_URL( l.data:uri_settings:module, l.data:uri_settings, l.page, l.data:auto_flags ) }">
				<MvASSIGN NAME = "l.url:auto_sep"			VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Page_URL( l.data:uri_settings:module, l.data:uri_settings, l.page, l.data:auto_sep_flags ) }">
				<MvASSIGN NAME = "l.url:auto_noparams"		VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Page_URL( l.data:uri_settings:module, l.data:uri_settings, l.page, l.data:auto_noparams_flags ) }">
				<MvASSIGN NAME = "l.url:rr"					VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Page_URL( l.data:uri_settings:module, l.data:uri_settings, l.page, l.data:rr_flags ) }">
				<MvASSIGN NAME = "l.url:rr_sep"				VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Page_URL( l.data:uri_settings:module, l.data:uri_settings, l.page, l.data:rr_sep_flags ) }">
				<MvASSIGN NAME = "l.url:rr_noparams"		VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Page_URL( l.data:uri_settings:module, l.data:uri_settings, l.page, l.data:rr_noparams_flags ) }">
			<MvELSE>
				<MvASSIGN NAME = "l.url:auto"				VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Screen_URL( l.data:uri_settings:module, l.data:uri_settings, l.screen, l.data:auto_flags ) }">
				<MvASSIGN NAME = "l.url:auto_sep"			VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Screen_URL( l.data:uri_settings:module, l.data:uri_settings, l.screen, l.data:auto_sep_flags ) }">
				<MvASSIGN NAME = "l.url:auto_noparams"		VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Screen_URL( l.data:uri_settings:module, l.data:uri_settings, l.screen, l.data:auto_noparams_flags ) }">
				<MvASSIGN NAME = "l.url:rr"					VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Screen_URL( l.data:uri_settings:module, l.data:uri_settings, l.screen, l.data:rr_flags ) }">
				<MvASSIGN NAME = "l.url:rr_sep"				VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Screen_URL( l.data:uri_settings:module, l.data:uri_settings, l.screen, l.data:rr_sep_flags ) }">
				<MvASSIGN NAME = "l.url:rr_noparams"		VALUE = "{ [ g.Module_Root $ l.data:uri_settings:module:module ].URIModule_Screen_URL( l.data:uri_settings:module, l.data:uri_settings, l.screen, l.data:rr_noparams_flags ) }">
			</MvIF>
		</MvIF>
	<MvELSEIF EXPR = "{ l.data:uri_settings:source NE 'uri' AND l.data:uri_settings:source NE 'module' }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_Code_Cached( l.screen, l.page ) }">
			<MvASSIGN NAME = "l.secure"						VALUE = "{ URL_Screen_Secure( l.screen ) }">
		<MvELSE>
			<MvASSIGN NAME = "l.secure"						VALUE = "{ l.page:secure }">
			<MvASSIGN NAME = "l.screen"						VALUE = "{ l.page:code }">
		</MvIF>

		<MvASSIGN NAME = "l.params"							VALUE = "{ 'Screen=' $ encodeattribute( l.screen ) }">

		<MvASSIGN NAME = "l.url"							VALUE = "">
		<MvASSIGN NAME = "l.url:source"						VALUE = "long">
		<MvASSIGN NAME = "l.url:secure"						VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:secure_flags,				l.params ) }">
		<MvASSIGN NAME = "l.url:secure_sep"					VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:secure_sep_flags,			l.params ) }">
		<MvASSIGN NAME = "l.url:secure_noparams"			VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:secure_noparams_flags,	'' ) }">

		<MvIF EXPR = "{ l.secure }">
			<MvASSIGN NAME = "l.url:auto"					VALUE = "{ l.url:secure }">
			<MvASSIGN NAME = "l.url:auto_sep"				VALUE = "{ l.url:secure_sep }">
			<MvASSIGN NAME = "l.url:auto_noparams"			VALUE = "{ l.url:secure_noparams }">
			<MvASSIGN NAME = "l.url:rr"						VALUE = "{ l.url:secure }">
			<MvASSIGN NAME = "l.url:rr_sep"					VALUE = "{ l.url:secure_sep }">
			<MvASSIGN NAME = "l.url:rr_noparams"			VALUE = "{ l.url:secure_noparams }">
		<MvELSE>
			<MvASSIGN NAME = "l.url:auto"					VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:auto_flags,				l.params ) }">
			<MvASSIGN NAME = "l.url:auto_sep"				VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:auto_sep_flags,			l.params ) }">
			<MvASSIGN NAME = "l.url:auto_noparams"			VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:auto_noparams_flags,		'' ) }">
			<MvASSIGN NAME = "l.url:rr"						VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:rr_flags,					l.params ) }">
			<MvASSIGN NAME = "l.url:rr_sep"					VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:rr_sep_flags,				l.params ) }">
			<MvASSIGN NAME = "l.url:rr_noparams"			VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:rr_noparams_flags,		'' ) }">
		</MvIF>
	<MvELSE>
		<MvASSIGN NAME = "l.working_branch_id"				VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Working_Branch_ID() }">

		<MvOPENVIEW NAME	= "Merchant"
					VIEW 	= "URIs"
					QUERY 	= "{ 'SELECT
									0 AS secure,
									u.uri,
									u.screen,
									u.page_code
								  FROM
								  	URIs u
								  WHERE
								  	u.store_id																	= ?																			AND
								  	' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'u.screen' )	$ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) $ ' 		AND
								  	u.page_code																	= \'\'																		AND
								  	u.cat_id																	= 0																			AND
								  	u.product_id																= 0																			AND
								  	u.canonical																	= 1
								  UNION ALL
								  SELECT
								  	pv.secure,
								  	u.uri,
								  	u.screen,
								  	u.page_code
								  FROM
								  	URIs u,
									' $ g.Store_Table_Prefix $ 'PageBranchVersions pbv,
									' $ g.Store_Table_Prefix $ 'PageVersions pv
								  WHERE
									pbv.branch_id																= ?																			AND
								  	' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'pbv.code' )	$ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) $ ' 		AND
									pbv.head																	= 1																			AND
									pv.id																		= pbv.version_id															AND
								  	pv.admin																	= 0																			AND
								  	u.store_id																	= ?																			AND
								  	u.screen																	= \'\'																		AND
								  	' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'u.page_code' )	$ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'pv.code' ) $ '	AND
								  	u.cat_id																	= 0																			AND
								  	u.product_id																= 0																			AND
								  	u.canonical																	= 1
								  ORDER BY page_code' }"
					FIELDS	= "g.Store:id, l.screen,
							   l.working_branch_id, l.screen, g.Store:id">
		<MvIF EXPR = "{ g.MvOPENVIEW_Error OR URIs.d.EOF }">
			<MvIF EXPR = "{ URIS.d.EOF }">
				<MvCLOSEVIEW NAME = "Merchant" VIEW = "URIs">
			</MvIF>

			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_Code_Cached( l.screen, l.page ) }">
				<MvASSIGN NAME = "l.secure" 				VALUE = "{ URL_Screen_Secure( l.screen ) }">
			<MvELSE>
				<MvASSIGN NAME = "l.secure" 				VALUE = "{ l.page:secure }">
				<MvASSIGN NAME = "l.screen" 				VALUE = "{ l.page:code }">
			</MvIF>

			<MvASSIGN NAME = "l.params"						VALUE = "{ 'Screen=' $ encodeattribute( l.screen ) }">

			<MvASSIGN NAME = "l.url"						VALUE = "">
			<MvASSIGN NAME = "l.url:source"					VALUE = "long">
			<MvASSIGN NAME = "l.url:secure"					VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:secure_flags,				l.params ) }">
			<MvASSIGN NAME = "l.url:secure_sep"				VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:secure_sep_flags,			l.params ) }">
			<MvASSIGN NAME = "l.url:secure_noparams"		VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:secure_noparams_flags,	'' ) }">

			<MvIF EXPR = "{ l.secure }">
				<MvASSIGN NAME = "l.url:auto"				VALUE = "{ l.url:secure }">
				<MvASSIGN NAME = "l.url:auto_sep"			VALUE = "{ l.url:secure_sep }">
				<MvASSIGN NAME = "l.url:auto_noparams"		VALUE = "{ l.url:secure_noparams }">
				<MvASSIGN NAME = "l.url:rr"					VALUE = "{ l.url:secure }">
				<MvASSIGN NAME = "l.url:rr_sep"				VALUE = "{ l.url:secure_sep }">
				<MvASSIGN NAME = "l.url:rr_noparams"		VALUE = "{ l.url:secure_noparams }">
			<MvELSE>
				<MvASSIGN NAME = "l.url:auto"				VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:auto_flags,				l.params ) }">
				<MvASSIGN NAME = "l.url:auto_sep"			VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:auto_sep_flags,			l.params ) }">
				<MvASSIGN NAME = "l.url:auto_noparams"		VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:auto_noparams_flags,		'' ) }">
				<MvASSIGN NAME = "l.url:rr"					VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:rr_flags,					l.params ) }">
				<MvASSIGN NAME = "l.url:rr_sep"				VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:rr_sep_flags,				l.params ) }">
				<MvASSIGN NAME = "l.url:rr_noparams"		VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_Long( l.data:rr_noparams_flags,		'' ) }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ NOT ISNULL URIs.d.screen }">	<MvASSIGN NAME = "l.secure" VALUE = "{ URL_Screen_Secure( URIs.d.screen ) }">
			<MvELSE>										<MvASSIGN NAME = "l.secure" VALUE = "{ URIs.d.secure }">
			</MvIF>

			<MvASSIGN NAME = "l.url"						VALUE = "">
			<MvASSIGN NAME = "l.url:source"					VALUE = "uri">
			<MvASSIGN NAME = "l.url:secure"					VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:secure_flags,				URIs.d.uri, '' ) }">
			<MvASSIGN NAME = "l.url:secure_sep"				VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:secure_sep_flags,			URIs.d.uri, '' ) }">
			<MvASSIGN NAME = "l.url:secure_noparams"		VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:secure_noparams_flags,	URIs.d.uri, '' ) }">

			<MvIF EXPR = "{ l.secure }">
				<MvASSIGN NAME = "l.url:auto"				VALUE = "{ l.url:secure }">
				<MvASSIGN NAME = "l.url:auto_sep"			VALUE = "{ l.url:secure_sep }">
				<MvASSIGN NAME = "l.url:auto_noparams"		VALUE = "{ l.url:secure_noparams }">
				<MvASSIGN NAME = "l.url:rr"					VALUE = "{ l.url:secure }">
				<MvASSIGN NAME = "l.url:rr_sep"				VALUE = "{ l.url:secure_sep }">
				<MvASSIGN NAME = "l.url:rr_noparams"		VALUE = "{ l.url:secure_noparams }">
			<MvELSE>
				<MvASSIGN NAME = "l.url:auto"				VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:auto_flags,				URIs.d.uri, '' ) }">
				<MvASSIGN NAME = "l.url:auto_sep"			VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:auto_sep_flags,			URIs.d.uri, '' ) }">
				<MvASSIGN NAME = "l.url:auto_noparams"		VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:auto_noparams_flags,		URIs.d.uri, '' ) }">
				<MvASSIGN NAME = "l.url:rr"					VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:rr_flags,					URIs.d.uri, '' ) }">
				<MvASSIGN NAME = "l.url:rr_sep"				VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:rr_sep_flags,				URIs.d.uri, '' ) }">
				<MvASSIGN NAME = "l.url:rr_noparams"		VALUE = "{ [ g.Module_Feature_URI_UT ].URL_Build_URI( l.data:uri_settings, l.data:rr_noparams_flags,		URIs.d.uri, '' ) }">
			</MvIF>

			<MvCLOSEVIEW NAME = "Merchant" VIEW = "URIs">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.url }">
</MvFUNCTION>

<MvFUNCTION NAME = "URL_Screen_Secure" PARAMETERS = "screen" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Store:ui_mod:api_ver GE 9.04 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Root $ g.Store:ui_mod:module ].StoreUIModule_Screen_Secure( g.Store:ui_mod, l.screen ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvCOMMENT>
|
| Includes
|
</MvCOMMENT>

<MvINCLUDE FILE = "modules/component/seo_include.mv">

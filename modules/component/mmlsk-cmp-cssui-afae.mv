<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-AFAE-
| Next Error Code: 6    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-afae">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI Affiliate Add/Edit">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0800">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "10.08">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, component_prov, skins">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ 'GT_PAGE/' $ l.item $ ':Affiliate Fields' }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.fields:template_code ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, '', l.fields:template_code, l.settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update_Requires_Version" PARAMETERS = "module var, page var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_PostUpdate" PARAMETERS = "module var, page var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.load_fields }">
		<MvASSIGN NAME = "l.fields:template_code" VALUE = "{ l.templateversion:source }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Admin ].Tab_Visible( l.tab, l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item ) }">
		<table border=0 cellpadding=2 cellspacing=0 width="100%">
		<tr><td colspan=2 width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( l.field_prefix $ 'template_code', l.fields:template_code, 20, 100, l.templateversion:templ_id ) }">
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, '' ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ Generate_Code( l.settings, l.template_source ) }">

	<MvASSIGN NAME = "l.settings:template_filename"	VALUE = "{ tolower( l.page:code ) $ '-' $ tolower( l.item ) $ '.mvc' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.template_source, l.settings, l.settings:template_filename ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'buttons' )	OR
					NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'states' )	OR
					NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'countries' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:template_filename ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:template_filename, l.source_templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:template_filename, l.dest_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.dest_settings"						VALUE = "{ l.source_settings }">
	<MvASSIGN NAME = "l.dest_settings:template_filename"	VALUE = "{ l.dest_managedtemplate:version:settings:template_filename }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_managedtemplate, '', l.source_templateversion:source, l.dest_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.source_branch:id EQ l.dest_branch:id }">	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-AFAE-00004', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' on branch \'' $ l.dest_branch:name $ '\'' ) }">
	<MvELSE>													<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-AFAE-00005', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' from branch \'' $ l.source_branch:name $ '\' to branch \'' $ l.dest_branch:name $ '\'' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.AffiliateOptions:active }">
		<MvDO FILE="{ g.Module_Feature_AFF_DB }" NAME="l.affiliate_found" VALUE="{ Affiliate_Load_Session( g.Basket:session_id, g.Affiliate ) }">

		<MvIF EXPR = "{ ( NOT g.Error_Message_Count ) AND ( l.affiliate_found ) AND g.Affiliate_Session_Verified }">
			<MvASSIGN NAME="g.Affiliate_Code" 				VALUE="{ g.Affiliate:code }">
			<MvASSIGN NAME="g.Affiliate_CurrentPassword"	VALUE="">
			<MvASSIGN NAME="g.Affiliate_Password" 			VALUE="">
			<MvASSIGN NAME="g.Affiliate_VerifyPassword" 	VALUE="">
			<MvASSIGN NAME="g.Affiliate_LostPass_Email" 	VALUE="{ g.Affiliate:lpasseml }">

			<MvASSIGN NAME="g.Affiliate_SiteName" 			VALUE="{ g.Affiliate:site_name }">
			<MvASSIGN NAME="g.Affiliate_SiteURL" 			VALUE="{ g.Affiliate:site_url }">
			<MvASSIGN NAME="g.Affiliate_ContactName" 		VALUE="{ g.Affiliate:con_name }">

			<MvASSIGN NAME="g.Affiliate_Company" 			VALUE="{ g.Affiliate:company }">
			<MvASSIGN NAME="g.Affiliate_Email" 				VALUE="{ g.Affiliate:email }">
			<MvASSIGN NAME="g.Affiliate_Phone" 				VALUE="{ g.Affiliate:phone }">
			<MvASSIGN NAME="g.Affiliate_Fax" 				VALUE="{ g.Affiliate:fax }">
			<MvASSIGN NAME="g.Affiliate_Address" 			VALUE="{ g.Affiliate:addr }">
			<MvASSIGN NAME="g.Affiliate_City" 				VALUE="{ g.Affiliate:city }">
			<MvASSIGN NAME="g.Affiliate_State" 				VALUE="{ g.Affiliate:state }">
			<MvASSIGN NAME="g.Affiliate_Zip" 				VALUE="{ g.Affiliate:zip }">
			<MvASSIGN NAME="g.Affiliate_Country" 			VALUE="{ g.Affiliate:cntry }">

			<MvASSIGN NAME="l.temp_balance" 				VALUE="{ int( g.Affiliate:balance * 100 ) / 100 }">
			<MvASSIGN NAME="g.Affiliate:balance" 			VALUE="{ [ g.Module_Root $ g.Store_Module_Currency:module ].CurrencyModule_AddFormatting( g.Store_Module_Currency, l.temp_balance ) }">

			<MvASSIGN NAME="g.Affiliate:hits"				VALUE="{ g.Affiliate:hits ROUND 4 }">
			<MvASSIGN NAME="g.Affiliate:order_pct" 			VALUE="{ ( g.Affiliate:order_pct * 1 ) ROUND 2 }">

			<MvIF EXPR = "{ g.Affiliate:order_flat }">
				<MvASSIGN NAME="g.Affiliate:order_flat"		VALUE="{ [ g.Module_Root $ g.Store_Module_Currency:module ].CurrencyModule_AddFormatting( g.Store_Module_Currency, g.Affiliate:order_flat ) }">
			</MvIF>

			<MvIF EXPR = "{ g.Affiliate:lstpay_amt }">
				<MvASSIGN NAME="g.Affiliate:lstpay_amt" 	VALUE="{ [ g.Module_Root $ g.Store_Module_Currency:module ].CurrencyModule_AddFormatting( g.Store_Module_Currency, g.Affiliate:lstpay_amt ) }">
			</MvIF>
			
			<MvIF EXPR = "{ g.Affiliate:lstpay_dt }">
				<MvASSIGN NAME="g.Affiliate:lstpay_dt" 		VALUE="{ [ g.Module_Library_Utilities ].Format_Date( g.Affiliate:lstpay_dt, s.miva_language ) }">
			</MvIF>

			<MvIF EXPR = "{ len( g.AffiliateOptions:link_image ) }">
				<MvASSIGN NAME = "g.Affiliate_Full_Image_Path" 	VALUE = "{ g.Domain:base_url $ g.AffiliateOptions:link_image }">
			</MvIF>
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_Country"			VALUE="{ g.Store:country }">
		</MvIF>

		<MvIF EXPR = "{ len( miva_variable_value( 'g.Affiliate_State' ) ) AND
						[ g.Module_Library_DB ].State_Load_Code( miva_variable_value( 'g.Affiliate_State' ), l.state ) }">
			<MvASSIGN NAME = "g.Affiliate_StateSelect"		VALUE = "{ l.state:code }">
			<MvASSIGN NAME = "g.Affiliate_State"			VALUE = "">
		</MvIF>

		<MvASSIGN NAME = "g.State_Count"					VALUE = "{ [ g.Module_Library_DB ].State_Count() }">

		<MvIF EXPR = "{ g.Affiliate_Code_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_Code_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_Code_Row" VALUE = "form_row">
		</MvIF>

		<MvIF EXPR = "{ g.Affiliate_LostPass_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_LostPass_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_LostPass_Row" VALUE = "form_row">
		</MvIF>

		<MvIF EXPR = "{ g.Affiliate_CurrentPassword_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_CurrentPassword_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_CurrentPassword_Row" VALUE = "form_row">
		</MvIF>

		<MvIF EXPR = "{ g.Affiliate_Password_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_Password_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_Password_Row" VALUE = "form_row">
		</MvIF>

		<MvIF EXPR = "{ g.Affiliate_VerifyPassword_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_VerfiyPassword_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_VerifyPassword_Row" VALUE = "form_row">
		</MvIF>
		
		<MvIF EXPR = "{ g.Affiliate_SiteName_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_SiteName_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_SiteName_Row" VALUE = "form_row">
		</MvIF>

		<MvIF EXPR = "{ g.Affiliate_SiteURL_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_SiteURL_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_SiteURL_Row" VALUE = "form_row">
		</MvIF>

		<MvIF EXPR = "{ g.Affiliate_ContactName_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_ContactName_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_ContactName_Row" VALUE = "form_row">
		</MvIF>

		<MvIF EXPR = "{ g.Affiliate_Email_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_Email_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_Email_Row" VALUE = "form_row">
		</MvIF>

		<MvIF EXPR = "{ g.Affiliate_Phone_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_Phone_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_Phone_Row" VALUE = "form_row">
		</MvIF>

		<MvIF EXPR = "{ g.Affiliate_Fax_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_Fax_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_Fax_Row" VALUE = "form_row">
		</MvIF>

		<MvIF EXPR = "{ g.Affiliate_Company_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_Company_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_Company_Row" VALUE = "form_row">
		</MvIF>

		<MvIF EXPR = "{ g.Affiliate_Address_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_Address_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_Address_Row" VALUE = "form_row">
		</MvIF>

		<MvIF EXPR = "{ g.Affiliate_City_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_City_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_City_Row" VALUE = "form_row">
		</MvIF>

		<MvIF EXPR = "{ g.Affiliate_StateSelect_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_StateSelect_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_StateSelect_Row" VALUE = "form_row">
		</MvIF>

		<MvIF EXPR = "{ g.Affiliate_State_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_State_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_State_Row" VALUE = "form_row">
		</MvIF>

		<MvIF EXPR = "{ g.Affiliate_Zip_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_Zip_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_Zip_Row" VALUE = "form_row">
		</MvIF>

		<MvIF EXPR = "{ g.Affiliate_Country_Invalid }">
			<MvASSIGN NAME = "g.Affiliate_Country_Row" VALUE = "form_row invalid">
		<MvELSE>
			<MvASSIGN NAME = "g.Affiliate_Country_Row" VALUE = "form_row">
		</MvIF>
	</MvIF>
	
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:template_filename, l.all_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Code" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code">
		<MvIF EXPR = "{ NOT l.settings:edit }">
			<MvEVAL EXPR = "{ Generate_Code_Add( l.settings ) }">
		<MvELSE>
			<MvEVAL EXPR = "{ Generate_Code_Edit( l.settings ) }">
		</MvIF>
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Code_Add" PARAMETERS = "settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<div id="fields-key">
		<div><span class="required">Bold</span> = Required</div>
		<div><span class="italic">Italic</span> = Optional</div>
		<mvt:if expr="g.Error_Message_Count">
		<div><span class="red">Red</span> = Invalid</div>
		</mvt:if>
	</div>

	<div id="login-details">
		<div class="&mvte:global:Affiliate_Code_Row;">
			<label for="Affiliate_Code" class="required">Login:</label>
			<input type="text" name="Affiliate_Code" id="Affiliate_Code" value="&mvte:global:Affiliate_Code;" class="textfield" autocomplete="off" />
		</div>

		<div class="&mvte:global:Affiliate_LostPass_Row;">
			<label for="Affiliate_LostPass_Email" class="required">Email Lost Passwords To:</label>
			<input type="text" name="Affiliate_LostPass_Email" id="Affiliate_LostPass_Email" value="&mvte:global:Affiliate_LostPass_Email;" class="textfield" autocomplete="off" />
		</div>

		<div class="&mvte:global:Affiliate_Password_Row;">
			<label for="Affiliate_Password" class="required">Password:</label>
			<input type="password" name="Affiliate_Password" id="Affiliate_Password" value="&mvte:global:Affiliate_Password;" class="textfield" autocomplete="off" />
		</div>

		<div class="&mvte:global:Affiliate_VerfiyPassword_Row;">
			<label for="Affiliate_VerifyPassword" class="required">Confirm Password:</label>
			<input type="password" name="Affiliate_VerifyPassword" id="Affiliate_VerifyPassword" value="&mvte:global:Affiliate_VerifyPassword;" class="textfield" autocomplete="off" />
		</div>
	</div>
		
	<div id="affiliate-information">
		<h2 class="fields-heading">Affiliate Information:</h2>
		
		<div class="&mvte:global:Affiliate_SiteName_Row;">
			<label for="Affiliate_SiteName" class="required">Site Name:</label>
			<input type="text" name="Affiliate_SiteName" id="Affiliate_SiteName" value="&mvte:global:Affiliate_SiteName;" class="textfield" />
		</div>

		<div class="&mvte:global:Affiliate_SiteURL_Row;">
			<label for="Affiliate_SiteURL" class="required">Site URL:</label>
			<input type="text" name="Affiliate_SiteURL" id="Affiliate_SiteURL" value="&mvte:global:Affiliate_SiteURL;" class="textfield" />
		</div>
	</div>
	
	<div id="affiliate-contact">
		<h2 class="fields-heading">Contact:</h2>
		
		<div class="&mvte:global:Affiliate_ContactName_Row;">
			<label for="Affiliate_ContactName" class="required">Name:</label>
			<input type="text" name="Affiliate_ContactName" id="Affiliate_SiteURL" value="&mvte:global:Affiliate_ContactName;" class="textfield" />
		</div>
					
		<div class="form_row">
			<label for="Affiliate_Company" class="italic">Company:</label>
			<input type="text" name="Affiliate_Company" id="Affiliate_Company" id="Affiliate_Company" value="&mvte:global:Affiliate_Company;" class="textfield" />
		</div>

		<div class="&mvte:global:Affiliate_Email_Row;">
			<label for="Affiliate_Email" class="required">Email:</label>
			<input type="text" name="Affiliate_Email" id="Affiliate_Email" value="&mvte:global:Affiliate_Email;" class="textfield" />
		</div>

		<div class="&mvte:global:Affiliate_Phone_Row;">
			<label for="Affiliate_Phone" class="required">Phone:</label>
			<input type="text" name="Affiliate_Phone" id="Affiliate_Phone" value="&mvte:global:Affiliate_Phone;" class="textfield" />
		</div>
		
		<div class="form_row">
			<label for="Affiliate_Fax" class="italic">Fax:</label>
			<input type="text" name="Affiliate_Fax" id="Affiliate_Fax" value="&mvte:global:Affiliate_Fax;" class="textfield" />
		</div>		
			
		<div class="&mvte:global:Affiliate_Address_Row;">
			<label for="Affiliate_Address" class="required">Address:</label>
			<input type="text" name="Affiliate_Address" id="Affiliate_Address" value="&mvte:global:Affiliate_Address;" class="textfield" />
		</div>

		<div class="&mvte:global:Affiliate_City_Row;">
			<label for="Affiliate_City" class="required">City:</label>
			<input type="text" name="Affiliate_City" id="Affiliate_City" value="&mvte:global:Affiliate_City;" class="textfield" />
		</div>

		<mvt:if expr="g.States_Empty">
			<div class="&mvte:global:Affiliate_State_Row;">
				<label for="Affiliate_State" class="required">State/Province:</label>
				<input type="text" name="Affiliate_State" id="Affiliate_State" value="&mvte:global:Affiliate_State;" class="textfield" />
			</div>
		<mvt:else>				
			<div class="&mvte:global:Affiliate_StateSelect_Row;">
				<label for="Affiliate_StateSelect" class="required">State/Province:</label>
				<mvt:item name="states" param="Affiliate_StateSelect" />
			</div>
			
			<div class="&mvte:global:Affiliate_State_Row;">
				<label for="Affiliate_State" class="italic">Other State/Province:</label>
				<input type="text" name="Affiliate_State" id="Affiliate_State" value="&mvte:global:Affiliate_State;" class="textfield" />
			</div>
		</mvt:if>

		<div class="&mvte:global:Affiliate_Zip_Row;">
			<label for="Affiliate_State" class="required">Zip Code:</label>
			<input type="text" name="Affiliate_Zip" id="Affiliate_Zip" value="&mvte:global:Affiliate_Zip;" class="textfield" />
		</div>
			
		<div class="&mvte:global:Affiliate_Country_Row;">
			<label for="Affiliate_State" class="required">Country:</label>
			<mvt:item name="countries" param="Affiliate_Country" />
		</div>
			
		<mvt:if expr="NOT ISNULL g.AffiliateOptions:terms">
			<div class="form_row">
			<label class="required">Terms:</label>
			<div id="affiliate-terms">&mvt:global:AffiliateOptions:terms;</div>
		</div>
		</mvt:if>
	</div>			
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Code_Edit" PARAMETERS = "settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<div id="fields-key">
		<div><span class="required">Bold</span> = Required</div>
		<div><span class="italic">Italic</span> = Optional</div>
		<mvt:if expr="g.Error_Message_Count">
			<div><span class="red">Red</span> = Invalid</div>
		</mvt:if>
	</div>

	<div id="login-details">
		<div class="&mvte:global:Affiliate_Code_Row;">
			<label for="Affiliate_Code" class="required">Login:</label>
			<div class="non-editable">&mvte:global:Affiliate_Code;</div><input type="hidden" name="Affiliate_Code" value="&mvte:global:Affiliate_Code;" />
		</div>

		<div class="&mvte:global:Affiliate_LostPass_Row;">
			<label for="Affiliate_LostPass_Email" class="required">Email Lost Passwords To:</label>
			<input type="text" name="Affiliate_LostPass_Email" id="Affiliate_LostPass_Email" value="&mvte:global:Affiliate_LostPass_Email;" class="textfield" autocomplete="off" />
		</div>

		<mvt:if expr="g.AffiliateOptions:pw_chgauth">
			<div class="&mvte:global:Affiliate_CurrentPassword_Row;">
				<label for="Affiliate_CurrentPassword" class="required">Current Password:</label>
				<input type="password" name="Affiliate_CurrentPassword" id="Affiliate_CurrentPassword" value="&mvte:global:Affiliate_CurrentPassword;" class="textfield" autocomplete="off" />
			</div>
		</mvt:if>
		<div class="&mvte:global:Affiliate_Password_Row;">
			<label for="Affiliate_Password" class="required">Password:</label>
			<input type="password" name="Affiliate_Password" id="Affiliate_Password" value="&mvte:global:Affiliate_Password;" class="textfield" autocomplete="off" />
		</div>

		<div class="&mvte:global:Affiliate_VerifyPassword_Row;">
			<label for="Affiliate_VerifyPassword" class="required">Confirm Password:</label>
			<input type="password" name="Affiliate_VerifyPassword" id="Affiliate_VerifyPassword" value="&mvte:global:Affiliate_VerifyPassword;" class="textfield" autocomplete="off" />
		</div>
	</div>

	<div id="affiliate-information">
		<h2 class="fields-heading">Affiliate Information:</h2>
		<div class="&mvte:global:Affiliate_SiteName_Row;">
			<label for="Affiliate_SiteName" class="required">Site Name:</label>
			<input type="text" name="Affiliate_SiteName" id="Affiliate_SiteName" value="&mvte:global:Affiliate_SiteName;" class="textfield" />
		</div>

		<div class="&mvte:global:Affiliate_SiteURL_Row;">
			<label for="Affiliate_SiteURL" class="required">Site URL:</label>
			<input type="text" name="Affiliate_SiteURL" id="Affiliate_SiteURL" value="&mvte:global:Affiliate_SiteURL;" class="textfield" />
		</div>
	</div>

	<div id="affiliate-contact">
		<h2 class="fields-heading">Contact:</h2>
		<div class="&mvte:global:Affiliate_ContactName_Row;">
			<label for="Affiliate_ContactName" class="required">Name:</label>
			<input type="text" name="Affiliate_ContactName" id="Affiliate_SiteURL" value="&mvte:global:Affiliate_ContactName;" class="textfield" />
		</div>

		<div class="form_row">
			<label for="Affiliate_Company" class="italic">Company:</label>
			<input type="text" name="Affiliate_Company" id="Affiliate_Company" id="Affiliate_Company" value="&mvte:global:Affiliate_Company;" class="textfield" />
		</div>

		<div class="&mvte:global:Affiliate_Email_Row;">
			<label for="Affiliate_Email" class="required">Email:</label>
			<input type="text" name="Affiliate_Email" id="Affiliate_Email" value="&mvte:global:Affiliate_Email;" class="textfield" />
		</div>

		<div class="&mvte:global:Affiliate_Phone_Row;">
			<label for="Affiliate_Phone" class="required">Phone:</label>
			<input type="text" name="Affiliate_Phone" id="Affiliate_Phone" value="&mvte:global:Affiliate_Phone;" class="textfield" />
		</div>

		<div class="form_row">
			<label for="Affiliate_Fax" class="italic">Fax:</label>
			<input type="text" name="Affiliate_Fax" id="Affiliate_Fax" value="&mvte:global:Affiliate_Fax;" class="textfield">
		</div>

		<div class="&mvte:global:Affiliate_Address_Row;">
			<label for="Affiliate_Address" class="required">Address:</label>
			<input type="text" name="Affiliate_Address" id="Affiliate_Address" value="&mvte:global:Affiliate_Address;" class="textfield" />
		</div>

		<div class="&mvte:global:Affiliate_City_Row;">
			<label for="Affiliate_City" class="required">City:</label>
			<input type="text" name="Affiliate_City" id="Affiliate_City" value="&mvte:global:Affiliate_City;" class="textfield" />
		</div>

		<mvt:if expr="g.States_Empty">
			<div class="&mvte:global:Affiliate_State_Row;">
				<label for="Affiliate_State" class="required">State/Province:</label>
				<input type="text" name="Affiliate_State" id="Affiliate_State" value="&mvte:global:Affiliate_State;" class="textfield" />
			</div>
			<mvt:else>
				<div class="&mvte:global:Affiliate_StateSelect_Row;">
					<label for="Affiliate_StateSelect" class="required">State/Province:</label>
					<mvt:item name="states" param="Affiliate_StateSelect" />
				</div>
				<div class="&mvte:global:Affiliate_State_Row;">
					<label for="Affiliate_State" class="italic">Other State/Province:</label>
					<input type="text" name="Affiliate_State" id="Affiliate_State" value="&mvte:global:Affiliate_State;" class="textfield" />
				</div>
		</mvt:if>

		<div class="&mvte:global:Affiliate_Zip_Row;">
			<label for="Affiliate_State" class="required">Zip Code:</label>
			<input type="text" name="Affiliate_Zip" id="Affiliate_Zip" value="&mvte:global:Affiliate_Zip;" class="textfield" />
		</div>

		<div class="&mvte:global:Affiliate_Country_Row;">
			<label for="Affiliate_State" class="required">Country:</label>
			<mvt:item name="countries" param="Affiliate_Country" />
		</div>
	</div>

	<div id="affiliate-commissions">
		<h2 class="fields-heading">Commissions:</h2>

		<div class="form_row">
			<label>Application Status:</label>
			<div>&mvte:global:Affiliate:Status;</div>
		</div>

		<div class="form_row">
			<label>Commission:</label>
			<div>
				<mvt:if expr="g.Affiliate:hits NE 0">
					&mvte:global:affiliate:hits; per referral
				</mvt:if>
				<mvt:if expr="g.Affiliate:order_flat NE 0">
					<mvt:if expr="g.Affiliate:hits NE 0">
						<br />
					</mvt:if>
					&mvte:global:Affiliate:order_flat; per sale
				</mvt:if>
				<mvt:if expr="g.Affiliate:order_pct NE 0">
					<mvt:if expr="g.Affiliate:hits NE 0 OR g.Affiliate:order_flat NE 0 ">
						<br />
					</mvt:if>
					&mvte:global:Affiliate:order_pct;% per sale
				</mvt:if>
				<mvt:if expr="g.Affiliate:hits EQ 0 AND g.Affiliate:orderflat EQ 0 AND g.Affiliate:order_pct EQ 0">
					No Commission specified, please contact the store administrator
				</mvt:if>
				<mvt:if expr="g.AffiliateOptions:terms">
					<br /> See Terms Below
				</mvt:if>
			</div>
		</div>

		<div class="form_row">
			<label>Current Earnings:</label>
			<div>&mvte:global:Affiliate:Balance;</div>
		</div>

		<div class="form_row">
			<label>Last Payment:</label>
			<div>
				<mvt:if expr="g.Affiliate:lstpay_amt NE 0">
					&mvte:global:Affiliate:lstpay_amt;
				<mvt:else>
					<span class="italic">None</span>
				</mvt:if>
			</div>
		</div>

		<div class="form_row">
			<label>Payment Date:</label>
			<div>
				<mvt:if expr="g.Affiliate:lstpay_dt NE 0">
					&mvte:global:Affiliate:lstpay_dt;
				<mvt:else>
					<span class="italic">None</span>
				</mvt:if>
			</div>
		</div>

		<div class="form_row">
			<label>Link URL:</label>
			<div><input type="text" readonly class="textfield affiliate-code" value="&mvte:urls:SFNT:auto_sep;Affiliate=&mvta:global:Affiliate:code;" /></div>
		</div>

		<div class="form_row">
			<mvt:if expr="g.AffiliateOptions:link_image">
				<label>Image Link HTML:</label>
				<div>
					<input type="text" readonly class="textfield affiliate-code" value="&lt;a href=&quot;&mvte:global:Domain:mm_url;Screen=SFNT&amp;Affiliate=&mvta:global:Affiliate:code;&quot;&gt;&lt;img src=&quot;&mvte:global:Affiliate_Full_Image_Path;&quot; alt=&quot;&mvte:global:AffiliateOptions:link_text;&quot; border=&quot;0&quot;&gt;&lt;/a&gt;" /><br />
					
					<div id="affiliate-image">
						<img src="&mvte:global:AffiliateOptions:link_image;" alt="&mvte:global:AffiliateOptions:link_text;" border="0" />
					</div>
				</div>
			<mvt:elseif expr="g.AffiliateOptions:link_text">
				<label>Link Text:</label>
				<div>
					<input type="text" readonly class="textfield affiliate-code" value="&lt;a href=&quot;&mvte:global:Domain:mm_url;Screen=SFNT&amp;Affiliate=&mvta:global:Affiliate:code;&quot;&gt;&mvte:global:AffiliateOptions:link_text;&lt;/a&gt;" /><br>
				</div>
			</mvt:if>
		</div>

		<mvt:if expr="NOT ISNULL g.AffiliateOptions:terms">
			<div class="form_row">
				<label class="required">Terms:</label>
				<div id="affiliate-terms">&mvt:global:AffiliateOptions:terms;</div>
			</div>
		</mvt:if>
	</div>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Row" PARAMETERS = "invalid_var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<mvt:if expr="{ 'NOT g.' $ l.invalid_var }">
		<div class="form_row">
	<mvt:else>
		<div class="form_row invalid">
	</mvt:if>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'r', l.provide_xml, 'Template',	l.template ) OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml, 'Notes',		l.notes ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.template, l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pages" VALUE = "{ [ g.Module_Feature_TUI_DB ].Item_Load_PageList( l.item ) }">
	
	<MvFUNCTIONRETURN VALUE = "{ 'Exports Affiliate Add/Edit Content template from pages: ' $ l.pages }">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item_Runtime( l.item:id, l.pages ) }">
			<MvREFERENCEARRAY NAME = "l.settings" VARIABLE = "l.page:settings">
				<MvMEMBER NAME = "{ l.item:code }">
			</MvREFERENCEARRAY>

			<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
				<Page_Update code="{ l.page:code }">
					<Item code="{ l.item:code }">
						<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.templateversion:source ) }"></Template>
						<Notes>#Set_Current_Time#</Notes>
					</Item>
				</Page_Update>
			</MvIF>
		</MvFOREACH>
	</MvCAPTURE>
</MvFUNCTION>

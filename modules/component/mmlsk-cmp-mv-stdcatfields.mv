<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CMP-SCF-
| Next Error Code: 1    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-mv-stdcatfields">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Standard Category Fields">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0800">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.00">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.settings:id }">
		<MvIF EXPR = "{ NOT ISNULL l.settings:category_code }">								<MvASSIGN NAME = "l.explicit_category_notfound" VALUE = "{ NOT [ g.Module_Library_DB ].Runtime_Category_Load_Code( l.settings:category_code, l.settings ) }">
		<MvELSEIF EXPR = "{ NOT ISNULL g.Category_Code }">									<MvASSIGN NAME = "l.explicit_category_notfound" VALUE = "{ NOT [ g.Module_Library_DB ].Runtime_Category_Load_Code( g.Category_Code, l.settings ) }">
		<MvELSEIF EXPR = "{ NOT Load_Implicit_Category( l.all_settings, l.settings ) }">	<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.explicit_category_notfound }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Throw_Exception( 'category_not_found' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings:link"	VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Category_URL( l.settings, l.null ) }">
	<MvASSIGN NAME = "g.Category_Code"	VALUE = "{ l.settings:code }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Load_Implicit_Category" PARAMETERS = "all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Cached() ) AND ( NOT ISNULL s.http_referer ) AND ( NOT miva_array_search( g.Session:parameters, 1, l.parameter, 'l.parameter EQ \'category_code\'' ) ) }">
		<MvIF EXPR = "{ Referer_Parser( s.http_referer, l.category_code ) }">
			<MvIF EXPR = "{ [ g.Module_Library_DB ].Runtime_Category_Load_Code( l.category_code, l.settings ) }">
				<MvFUNCTIONRETURN VALUE = 1>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Initialize_Item( l.all_settings, 'product' ) }">
		<MvIF EXPR = "{ l.all_settings:product:cancat_id }">
			<MvIF EXPR = "{ [ g.Module_Library_DB ].Runtime_Category_Load_ID_Cached( l.all_settings:product:cancat_id, l.settings ) }">
				<MvFUNCTIONRETURN VALUE = 1>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.all_settings:product:id }">
			<MvIF EXPR = "{ [ g.Module_Library_DB ].Runtime_Category_Load_Product_First( l.all_settings:product:id, l.settings ) }">
				<MvFUNCTIONRETURN VALUE = 1>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "Referer_Parser" PARAMETERS = "referer, category_code var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Feature_URI_DB ].URISettings_Load_Cached( l.urisettings ) AND l.urisettings:source EQ 'uri' }">
		<MvASSIGN NAME = "l.uri" VALUE = "{ [ g.Module_Feature_URI_UT ].URL_To_Normalized_URI( l.referer ) }">

		<MvIF EXPR = "{ [ g.Module_Feature_URI_DB ].URI_Load_URI( l.uri, l.existing_uri ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Category_Load_ID( l.existing_uri:cat_id, l.category ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.category_code"	VALUE = "{ l.category:code }">
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.position"	 		VALUE = "{ 'Category_Code=' CIN l.referer }">
	<MvIF EXPR = "{ l.position GT 0 }">
		<MvASSIGN NAME = "l.source"			VALUE = "{ substring_var( l.referer, l.position, len_var( l.referer ) ) }">
		<MvASSIGN NAME = "l.category_code" 	VALUE = "{ gettoken( gettoken( l.source, '&', 1 ), '=', 2 ) }">
		
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Library_DB ].SEOSettings_Load_Cached( l.seosettings ) }">
		<MvASSIGN NAME = "l.seo_url"		VALUE = "{ Legacy_SEO_Category_URL( l.seosettings, '%%' ) }">
		<MvASSIGN NAME = "l.start"			VALUE = "{ indexof( '%25%25', l.seo_url, 1 ) }">
		<MvASSIGN NAME = "l.end_match"		VALUE = "{ substring_var( l.seo_url, l.start + 6, len_var( l.seo_url ) - l.start - 5 ) }">
		<MvASSIGN NAME = "l.end"			VALUE = "{ indexof( l.end_match, l.referer, l.start + 1 ) }">
		
		<MvIF EXPR = "{ ( substring_var( l.seo_url, 1, l.start - 1 ) CIN l.referer EQ 1 ) AND l.start GT 0 AND l.end GT l.start }">
			<MvASSIGN NAME = "l.category_code" VALUE = "{ substring_var( l.referer, l.start, l.end - l.start ) }">
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvINCLUDE FILE = "modules/component/seo_include.mv">

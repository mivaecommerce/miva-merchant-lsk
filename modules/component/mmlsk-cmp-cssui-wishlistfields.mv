<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-WSHLF-
| Next Error Code: 5
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-wishlistfields">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI Wish List Fields">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0800">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "10.08">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, component_prov, skins">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ 'GT_PAGE/' $ l.item $ ':Wish List Fields' }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.fields:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.fields:mode" VALUE = "{ trim( l.fields:mode ) }">

	<MvEVAL EXPR = "{ ModeToSettings_Convert( l.fields:mode, l.mode_settings ) }">

	<MvIF EXPR = "{ l.mode_settings:mode EQ l.settings:mode }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.fields:template_code ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvEVAL EXPR = "{ ModeToSettings_Convert( l.fields:mode, l.mode_settings ) }">

	<MvIF EXPR = "{ l.mode_settings:mode NE l.settings:mode }">
		<MvASSIGN NAME = "l.settings:mode" VALUE = "{ l.mode_settings:mode }">

		<MvEVAL EXPR = "{ Generate_Code( l.settings, l.fields:template_code ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, '', l.fields:template_code, l.settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update_Requires_Version" PARAMETERS = "module var, page var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_PostUpdate" PARAMETERS = "module var, page var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.load_fields }">
		<MvASSIGN NAME = "l.fields:template_code"		VALUE = "{ l.templateversion:source }">
		<MvASSIGN NAME = "l.fields:template_filename"	VALUE = "{ l.settings:template_filename }">

		<MvEVAL EXPR = "{ SettingsToMode_Convert( l.settings, l.fields:mode ) }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Admin ].Tab_Visible( l.tab, l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_filename' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">

		<MvIF EXPR = "{ l.fields:mode EQ 'tokens_only_nodisplay' }">
			<MvHIDE FIELDS = "{ l.field_prefix $ 'template_code' }">
		<MvELSE>
			<tr><td colspan="2" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( l.field_prefix $ 'template_code', l.fields:template_code, 20, 100, l.templateversion:templ_id ) }">
			</td></tr>

			<tr><td colspan="2">&nbsp;</td></tr>
		</MvIF>

		<tr><td>
			Mode:
		</td><td>
			<select name="{ encodeentities( l.field_prefix ) $ 'mode' }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'wishlist_add',			l.fields:mode, 'Wish List: Add' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'wishlist_edit',			l.fields:mode, 'Wish List: Edit' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'tokens_only_nodisplay',	l.fields:mode, 'Tokens Only - No Display' ) }">
			</select>
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, '' ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:mode" VALUE = "wishlist_add">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ Generate_Code( l.settings, l.template_source ) }">

	<MvASSIGN NAME = "l.settings:template_filename"	VALUE = "{ tolower( l.page:code ) $ '-' $ tolower( l.item ) $ '.mvc' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.template_source, l.settings, l.settings:template_filename ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:template_filename ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:template_filename, l.source_templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:template_filename, l.dest_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.dest_settings"						VALUE = "{ l.source_settings }">
	<MvASSIGN NAME = "l.dest_settings:template_filename"	VALUE = "{ l.dest_managedtemplate:version:settings:template_filename }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_managedtemplate, '', l.source_templateversion:source, l.dest_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.source_branch:id EQ l.dest_branch:id }">	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-WSHLF-00003', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' on branch \'' $ l.dest_branch:name $ '\'' ) }">
	<MvELSE>													<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-WSHLF-00004', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' from branch \'' $ l.source_branch:name $ '\' to branch \'' $ l.dest_branch:name $ '\'' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:mode EQ 'wishlist_edit' OR l.settings:mode EQ 'tokens_only_nodisplay' }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_WSH_DB ].WishList_Load_ID_Cached( g.WishList_ID, l.wishlist ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Throw_Exception( 'wishlist_invalid' ) }">
		<MvELSEIF EXPR = "{ ( ( l.settings:mode EQ 'tokens_only_nodisplay' ) AND ( NOT l.wishlist:shared ) AND ( l.wishlist:cust_id NE g.Basket:cust_id ) ) OR
							( ( l.settings:mode EQ 'wishlist_edit' ) AND ( l.wishlist:cust_id NE g.Basket:cust_id ) ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Throw_Exception( 'wishlist_invalid' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.settings:id"			VALUE = "{ l.wishlist:id }">
		<MvASSIGN NAME = "l.settings:cust_id"		VALUE = "{ l.wishlist:cust_id }">
		<MvASSIGN NAME = "l.settings:title"			VALUE = "{ l.wishlist:title }">
		<MvASSIGN NAME = "l.settings:notes"			VALUE = "{ l.wishlist:notes }">
		<MvASSIGN NAME = "l.settings:shared"		VALUE = "{ l.wishlist:shared }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:id"			VALUE = 0>
		<MvASSIGN NAME = "l.settings:cust_id"		VALUE = 0>
		<MvASSIGN NAME = "l.settings:title"			VALUE = "">
		<MvASSIGN NAME = "l.settings:notes"			VALUE = "">
		<MvASSIGN NAME = "l.settings:shared"		VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.settings:mode EQ 'tokens_only_nodisplay' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ ( NOT g.Error_Message_Count ) AND ( g.Basket:cust_id EQ l.settings:cust_id ) AND g.Customer_Session_Verified }">
		<MvASSIGN NAME = "g.WishList_Title"			VALUE = "{ l.settings:title }">
		<MvASSIGN NAME = "g.WishList_Notes"			VALUE = "{ l.settings:notes }">
		<MvASSIGN NAME = "g.WishList_Shared"		VALUE = "{ l.settings:shared }">
	</MvIF>

	<MvIF EXPR = "{ g.WishList_Title_Invalid }">
		<MvASSIGN NAME = "g.WishList_Title_Row" 	VALUE = "form_row invalid">
	<MvELSE>
		<MvASSIGN NAME = "g.WishList_Title_Row" 	VALUE = "form_row">
	</MvIF>

	<MvIF EXPR = "{ g.WishList_Notes_Invalid }">
		<MvASSIGN NAME = "g.WishList_Notes_Row" 	VALUE = "form_row invalid">
	<MvELSE>
		<MvASSIGN NAME = "g.WishList_Notes_Row" 	VALUE = "form_row">
	</MvIF>

	<MvIF EXPR = "{ g.WishList_Shared_Invalid }">
		<MvASSIGN NAME = "g.WishList_Shared_Row" 	VALUE = "form_row invalid">
	<MvELSE>
		<MvASSIGN NAME = "g.WishList_Shared_Row" 	VALUE = "form_row">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:template_filename, l.all_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Code" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code">
		<MvIF EXPR = "{ l.settings:mode EQ 'tokens_only_nodisplay' }">
			<MvCOMMENT> Empty Template </MvCOMMENT>
		<MvELSE>
<MIVA STANDARDOUTPUTLEVEL = "text, html"><div id="fields-key">
	<div><span class="required">Bold</span> = Required</div>
	<div><span class="italic">Italic</span> = Optional</div>
	<mvt:if expr="g.Error_Message_Count">
	<div><span class="red">Red</span> = Invalid</div>
	</mvt:if>
</div>
<MIVA STANDARDOUTPUTLEVEL = "">

			<MvIF EXPR = "{ l.settings:mode EQ 'wishlist_edit' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
<input type="hidden" name="WishList_ID" value="&mvte:global:WishList_ID;" />
<MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
<div class="&mvte:global:WishList_Title_Row;">
	<span class="prompt">
		<label for="WishList_Title" class="prompt required">Title:</label>
	</span>
	<span class="field">
		<input type="text" name="WishList_Title" id="WishList_Title" value="&mvte:global:WishList_Title;" class="textfield" />
	</span>
</div>

<div class="&mvte:global:WishList_Notes_Row;">
	<span class="prompt">
		<label for="WishList_Notes" class="prompt italic">Notes:</label>
	</span>
	<span class="field">
		<textarea name="WishList_Notes" id="WishList_Notes" class="textarea">&mvte:global:WishList_Notes;</textarea>
	</span>
</div>

<div class="&mvte:global:WishList_Shared_Row;">
	<span class="prompt"></span>
	<span class="field">
		<label>
		<mvt:if expr="g.WishList_Shared">
			<input type="checkbox" name="WishList_Shared" id="WishList_Shared" value="Yes" checked />
		<mvt:else>
			<input type="checkbox" name="WishList_Shared" id="WishList_Shared" value="Yes" />
		</mvt:if>

		Public</label>
	</span>
</div><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) AND
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Mode' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Either Template or Mode must be specified' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'o', l.provide_xml, 'Template',	l.template )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml, 'Notes',		l.notes )		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List( 'O', l.provide_xml, 'Mode',		l.mode,
																	'tokens_only_nodisplay, wishlist_add, wishlist_edit',
																	'tokens_only_nodisplay, wishlist_add, wishlist_edit' ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.mode }">
		<MvEVAL EXPR = "{ ModeToSettings_Convert( l.mode, l.settings ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
			<MvEVAL EXPR = "{ Generate_Code( l.settings, l.template ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.template, l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pages" VALUE = "{ [ g.Module_Feature_TUI_DB ].Item_Load_PageList( l.item ) }">

	<MvFUNCTIONRETURN VALUE = "{ 'Exports Wish List View/Add/Edit Content template from pages: ' $ l.pages }">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item_Runtime( l.item:id, l.pages ) }">
			<MvREFERENCEARRAY NAME = "l.settings" VARIABLE = "l.page:settings">
				<MvMEMBER NAME = "{ l.item:code }">
			</MvREFERENCEARRAY>

			<MvEVAL EXPR = "{ SettingsToMode_Convert( l.settings, l.mode ) }">

			<Page_Update code="{ l.page:code }">
				<Item code="{ l.item:code }">
					<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
						<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.templateversion:source ) }"></Template>
					</MvIF>

					<Mode><MvEVAL EXPR = "{ miva_cdata_encode( l.mode ) }"></Mode>
					<Notes>#Set_Current_Time#</Notes>
				</Item>
			</Page_Update>
		</MvFOREACH>
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "SettingsToMode_Convert" PARAMETERS = "settings var, mode var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:mode EQ 'wishlist_add' }">       		<MvASSIGN NAME = "l.mode" VALUE = "wishlist_add">
	<MvELSEIF EXPR = "{ l.settings:mode EQ 'wishlist_edit' }">      	<MvASSIGN NAME = "l.mode" VALUE = "wishlist_edit">
	<MvELSEIF EXPR = "{ l.settings:mode EQ 'tokens_only_nodisplay' }">	<MvASSIGN NAME = "l.mode" VALUE = "tokens_only_nodisplay">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ModeToSettings_Convert" PARAMETERS = "mode var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.mode EQ 'wishlist_add' }">				<MvASSIGN NAME = "l.settings:mode" VALUE = "wishlist_add">
	<MvELSEIF EXPR = "{ l.mode EQ 'wishlist_edit' }">			<MvASSIGN NAME = "l.settings:mode" VALUE = "wishlist_edit">
	<MvELSEIF EXPR = "{ l.mode EQ 'tokens_only_nodisplay' }">	<MvASSIGN NAME = "l.settings:mode" VALUE = "tokens_only_nodisplay">
	</MvIF>
</MvFUNCTION>

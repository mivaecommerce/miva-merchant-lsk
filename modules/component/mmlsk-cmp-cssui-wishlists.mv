<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-WSHLL-
| Next Error Code: 6    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-wishlists">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI Wish Lists">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0800">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "10.08">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, component_prov, skins">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ 'GT_PAGE/' $ l.item $ ':Wish Lists Layout' }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModuleValidate_SortMethod" PARAMETERS = "item, default_sort, value, sort_by, sorting_option, sorting_method" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( l.default_sort EQ l.value ) AND ( NOT l.sort_by ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, '', 'Sorting option \'' $ l.sorting_option $ '\' must be enabled to use the Default Sort Method of ' $ l.sorting_method ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.fields:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.fields:per_page"					VALUE = "{ trim( l.fields:per_page ) }">
	<MvASSIGN NAME = "l.fields:page_disp_count"				VALUE = "{ trim( l.fields:page_disp_count ) }">
	<MvASSIGN NAME = "l.fields:reverse_default_disp_order"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:reverse_default_disp_order ) }">

	<MvIF EXPR = "{ l.fields:paginate }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:per_page ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'per_page', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.fields:paginate_count }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:page_disp_count ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'page_disp_count', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.fields:advanced AND l.fields:sort_by:switch }">
		<MvASSIGN NAME = "l.ds" VALUE = "{ l.fields:default_sort }">
		<MvASSIGN NAME = "l.sb" VALUE = "{ l.fields:sort_by }">

		<MvIF EXPR = "{ NOT ComponentModuleValidate_SortMethod(		l.item, l.ds, 'title_asc', 		l.sb:title,		'Title',		'Title Ascending' ) }">			<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'title_desc', 	l.sb:title,		'Title', 		'Title Descending' ) }">		<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'fname_asc', 		l.sb:fname, 	'FirstName', 	'First Name Ascending' ) }">	<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'fname_desc', 	l.sb:fname, 	'FirstName', 	'First Name Descending' ) }">	<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'lname_asc', 		l.sb:lname, 	'LastName', 	'Last Name Ascending' ) }">		<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'lname_desc', 	l.sb:lname, 	'LastName', 	'Last Name Descending' ) }">	<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.fields:advanced }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.fields:template_code ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.settings:advanced"						VALUE = "{ l.fields:advanced }">
	<MvASSIGN NAME = "l.settings:reverse_default_disp_order" 	VALUE = "{ l.fields:reverse_default_disp_order }">

	<MvIF EXPR = "{ NOT l.fields:advanced }">
		<MvASSIGN NAME = "l.settings:field_title"		VALUE = "{ l.fields:field_title }">
		<MvASSIGN NAME = "l.settings:field_name"		VALUE = "{ l.fields:field_name }">
		<MvASSIGN NAME = "l.settings:field_notes"		VALUE = "{ l.fields:field_notes }">
	</MvIF>

	<MvCOMMENT>
	|
	| These settings are common between advanced and point + click mode
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.fields:paginate }">
		<MvASSIGN NAME = "l.settings:per_page"			VALUE = "{ l.fields:per_page }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:per_page"			VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.fields:paginate_count }">
		<MvASSIGN NAME = "l.settings:page_disp_count"	VALUE = "{ l.fields:page_disp_count }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:page_disp_count"	VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.settings:items_per_page_filter"	VALUE = "{ l.fields:items_per_page_filter }">

	<MvASSIGN NAME = "l.settings:sort_by:switch"		VALUE = "{ l.fields:sort_by:switch }">
	<MvASSIGN NAME = "l.settings:sort_by:title"			VALUE = "{ l.fields:sort_by:title }">
	<MvASSIGN NAME = "l.settings:sort_by:fname"			VALUE = "{ l.fields:sort_by:fname }">
	<MvASSIGN NAME = "l.settings:sort_by:lname"			VALUE = "{ l.fields:sort_by:lname }">

	<MvIF EXPR = "{ l.fields:default_sort EQ '' }">
		<MvASSIGN NAME = "l.settings:default_sort"		VALUE = "id">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:default_sort"		VALUE = "{ l.fields:default_sort }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-WSHLL-00001', l.module:name $ ' \'' $ l.item $ '\' settings updated' ) }">

	<MvIF EXPR = "{ NOT l.fields:advanced }">
		<MvEVAL EXPR = "{ ComponentModule_Generate_Code( l.settings, l.fields:template_code ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, '', l.fields:template_code, l.settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update_Requires_Version" PARAMETERS = "module var, page var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_PostUpdate" PARAMETERS = "module var, page var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.load_fields }">
		<MvASSIGN NAME = "l.fields:field_title"					VALUE = "{ l.settings:field_title }">
		<MvASSIGN NAME = "l.fields:field_name"					VALUE = "{ l.settings:field_name }">
		<MvASSIGN NAME = "l.fields:field_notes"					VALUE = "{ l.settings:field_notes }">

		<MvASSIGN NAME = "l.fields:advanced"					VALUE = "{ l.settings:advanced }">
		<MvASSIGN NAME = "l.fields:template_code"				VALUE = "{ l.templateversion:source }">
		<MvASSIGN NAME = "l.fields:template_filename"			VALUE = "{ l.settings:template_filename }">

		<MvIF EXPR = "{ l.settings:per_page EQ 0 }">
			<MvASSIGN NAME = "l.fields:paginate"				VALUE = 0>
			<MvASSIGN NAME = "l.fields:per_page"				VALUE = 10>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:paginate"				VALUE = 1>
			<MvASSIGN NAME = "l.fields:per_page"				VALUE = "{ l.settings:per_page }">
		</MvIF>

		<MvIF EXPR = "{ l.settings:page_disp_count EQ 0 }">
			<MvASSIGN NAME = "l.fields:paginate_count"			VALUE = 0>
			<MvASSIGN NAME = "l.fields:page_disp_count"			VALUE = 3>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:paginate_count"			VALUE = 1>
			<MvASSIGN NAME = "l.fields:page_disp_count"			VALUE = "{ l.settings:page_disp_count }">
		</MvIF>

		<MvIF EXPR = "{ l.settings:items_per_page_filter EQ 0 }">
			<MvASSIGN NAME = "l.fields:items_per_page_filter"	VALUE = 0>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:items_per_page_filter"	VALUE = "{ l.settings:items_per_page_filter }">
		</MvIF>

		<MvASSIGN NAME = "l.fields:sort_by:switch"				VALUE = "{ l.settings:sort_by:switch }">
		<MvASSIGN NAME = "l.fields:sort_by:title"				VALUE = "{ l.settings:sort_by:title }">
		<MvASSIGN NAME = "l.fields:sort_by:fname"				VALUE = "{ l.settings:sort_by:fname }">
		<MvASSIGN NAME = "l.fields:sort_by:lname"				VALUE = "{ l.settings:sort_by:lname }">

		<MvIF EXPR = "{ l.settings:default_sort EQ '' }">
			<MvASSIGN NAME = "l.fields:default_sort"			VALUE = "id">
		<MvELSE>
			<MvASSIGN NAME = "l.fields:default_sort"			VALUE = "{ l.settings:default_sort }">
		</MvIF>

		<MvASSIGN NAME = "l.fields:reverse_default_disp_order" 	VALUE = "{ l.settings:reverse_default_disp_order }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'field_title,field_name,field_notes,paginate,per_page,paginate_count,items_per_page_filter,page_disp_count,default_sort,sort_by:switch,sort_by:title,sort_by:fname,sort_by:lname,reverse_default_disp_order,template_code,template_filename,advanced' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_filename' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<MvIF EXPR = "{ NOT l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_code' ) }">
		<MvELSE>
			<tr><td colspan="2" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( l.field_prefix $ 'template_code', l.fields:template_code, 20, 100, l.templateversion:templ_id ) }">
			</td></tr>

			<MvIF EXPR = "{ l.customfield_count }">
				<tr><td colspan="2">&nbsp;</td></tr>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'field_title,field_name,field_notes' ) }">
		<MvELSE>
			<tr><td align="left" valign="top" nowrap>
				<b>Displayed Fields:</b>
			</td><td align="left" width="100%">
				<table border="0" cellpadding="0" cellspacing="0">
				<tr><td>
					<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_title, l.field_prefix $ 'field_title', 1, 'Title' ) }"></label>
				</td></tr>

				<tr><td>
					<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_name, l.field_prefix $ 'field_name', 1, 'Name' ) }"></label>
				</td></tr>

				<tr><td>
					<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_notes, l.field_prefix $ 'field_notes', 1, 'Notes' ) }"></label>
				</td></tr>
				</table>
			</td></tr>
		</MvIF>

		<tr>
			<td align="left" nowrap>
				<b>Sorting Options:</b>
			</td>
			<td width="100%">
				<table width="100%">
					<tr>
						<td align="left" style="padding-right: 15px;" nowrap>
							<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'sort_by:switch', 0, l.fields:sort_by:switch, 'Off' ) }"></label><br />
							<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'sort_by:switch', 1, l.fields:sort_by:switch, 'On' ) }"></label>
						</td>
						<td width="100%">
							<MvIF EXPR = "{ l.fields:advanced }">
								<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'sort_by:title,sort_by:fname,sort_by:lname' ) }">
							<MvELSE>
								<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:title, 	l.field_prefix $ 'sort_by:title', 	'1', 'Title' ) }"></label><br />
								<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:fname,	l.field_prefix $ 'sort_by:fname',	'1', 'First Name' ) }"></label><br />
								<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:lname, 	l.field_prefix $ 'sort_by:lname', 	'1', 'Last Name' ) }"></label><br />
							</MvIF>
						</td>
					</tr>
				</table>
			</td>
		</tr>

		<tr>
			<td align="left" style="padding-right: 15px;" nowrap>
				<b>Default Sort Method:</b>
			</td>
			<td width="100%">
				<table width="100%">
					<tr>
						<td align="left" style="padding-right: 15px;" nowrap>
							<select name="{ encodeentities( l.field_prefix ) $ 'default_sort' }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'id',				l.fields:default_sort, 'Default' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'title_asc', 		l.fields:default_sort, 'Title Ascending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'title_desc',	 	l.fields:default_sort, 'Title Descending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'fname_asc',		l.fields:default_sort, 'First Name Ascending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'fname_desc',		l.fields:default_sort, 'First Name Descending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'lname_asc', 		l.fields:default_sort, 'Last Name Ascending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'lname_desc', 		l.fields:default_sort, 'Last Name Descending' ) }">
							</select>
						</td>
						<td width="100%">
							<font color="red"><b>WARNING:</b></font>&nbsp;&nbsp;Changing the default sorting option to any value other than "default" will increase page load times and increase the load on your server. Be fully aware of the effect this will have on your store before changing this option!
						</td>
					</tr>
				</table>
			</td>
		</tr>

		<tr>
			<td align="left" nowrap>
				&nbsp;
			</td>
			<td width="100%">
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:reverse_default_disp_order, l.field_prefix $ 'reverse_default_disp_order', '1', 'Reverse Default Display Order' ) }"></label>
			</td>
		</tr>

		<tr><td align="left" nowrap valign="top">
			<b>Pagination:</b>
		</td><td align="left" nowrap width="100%">
			<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate', 0, l.fields:paginate, 'Do not paginate wish lists' ) }"></label><br>
			<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate', 1, l.fields:paginate, 'Display' ) }"></label> <input type="text" name="{ encodeentities( l.field_prefix ) $ 'per_page' }" value="{ encodeentities( l.fields:per_page ) }" size="5"> wish lists per page<br />
		</td></tr>

		<tr><td align="left" nowrap valign="top">
			<b>Page Numbers:</b>
		</td><td align="left" nowrap width="100%">
			<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate_count', 0, l.fields:paginate_count, 'Do not show page count' ) }"></label><br>
			<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate_count', 1, l.fields:paginate_count, 'Display' ) }"></label> <input type="text" name="{ encodeentities( l.field_prefix ) $ 'page_disp_count' }" value="{ encodeentities( l.fields:page_disp_count ) }" size="5"> pages at a time
		</td></tr>

		<tr><td align="left" nowrap valign="top">
			<b>Items Per Page Filter:</b>
		</td><td align="left" nowrap width="100%">
			<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'items_per_page_filter', 0, l.fields:items_per_page_filter, 'Off' ) }"></label><br>
			<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'items_per_page_filter', 1, l.fields:items_per_page_filter, 'On' ) }"></label>
		</td></tr>

		<tr><td colspan="2">&nbsp;</td></tr>

		<tr><td>
			&nbsp;
		</td><td width="100%">
			<input type="hidden" name="{ encodeentities( l.field_prefix ) $ 'advanced' }" value="{ encodeentities( l.fields:advanced ) }">
			<MvIF EXPR = "{ l.fields:advanced }">
				<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 0 ); return false;' }">Point + Click Mode</a>
			<MvELSE>
				<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 1 ); return false;' }">Advanced Mode</a>
			</MvIF>
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:field_title"					VALUE = "1">
	<MvASSIGN NAME = "l.settings:field_name"					VALUE = "1">
	<MvASSIGN NAME = "l.settings:field_notes"					VALUE = "0">
	<MvASSIGN NAME = "l.settings:per_page"						VALUE = "10">
	<MvASSIGN NAME = "l.settings:page_disp_count"				VALUE = "3">
	<MvASSIGN NAME = "l.settings:items_per_page_filter"			VALUE = "1">
	<MvASSIGN NAME = "l.settings:sort_by:switch"				VALUE = "1">
	<MvASSIGN NAME = "l.settings:sort_by:title"					VALUE = "1">
	<MvASSIGN NAME = "l.settings:sort_by:fname"					VALUE = "0">
	<MvASSIGN NAME = "l.settings:sort_by:lname"					VALUE = "0">
	<MvASSIGN NAME = "l.settings:default_sort"					VALUE = "id">
	<MvASSIGN NAME = "l.settings:reverse_default_disp_order" 	VALUE = "0">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ ComponentModule_Generate_Code( l.settings, l.template_source ) }">

	<MvASSIGN NAME = "l.settings:template_filename"	VALUE = "{ tolower( l.page:code ) $ '-' $ tolower( l.item ) $ '.mvc' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.template_source, l.settings, l.settings:template_filename ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'buttons' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:template_filename ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:template_filename, l.source_templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:template_filename, l.dest_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.dest_settings"						VALUE = "{ l.source_settings }">
	<MvASSIGN NAME = "l.dest_settings:template_filename"	VALUE = "{ l.dest_managedtemplate:version:settings:template_filename }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_managedtemplate, '', l.source_templateversion:source, l.dest_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.source_branch:id EQ l.dest_branch:id }">	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-WSHLL-00004', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' on branch \'' $ l.dest_branch:name $ '\'' ) }">
	<MvELSE>													<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-WSHLL-00005', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' from branch \'' $ l.source_branch:name $ '\' to branch \'' $ l.dest_branch:name $ '\'' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Create_Pagination" PARAMETERS = "all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT ISNULL g.WishListSearch }">		<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Feature_WSH_DB ].WishList_Count_Customer_Search( g.WishListSearch, g.Basket:cust_id, l.settings:total_count ) }">
	<MvELSE>											<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Feature_WSH_DB ].WishList_Count_Customer( g.Basket:cust_id, l.settings:total_count ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT l.success }">
		<MvASSIGN NAME = "l.settings:page_disp_count" VALUE = 0>
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.settings:page_disp_count EQ 0 OR l.settings:per_page EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.link_additions" VALUE = "">

	<MvIF EXPR = "{ NOT ISNULL g.Per_Page }">	<MvASSIGN NAME = "l.link_additions"	VALUE = "{ l.link_additions $ '&Per_Page=' $ encodeattribute( g.Per_Page ) }">	</MvIF>
	<MvIF EXPR = "{ NOT ISNULL g.Sort_By }">	<MvASSIGN NAME = "l.link_additions"	VALUE = "{ l.link_additions $ '&Sort_By=' $ encodeattribute( g.Sort_By ) }">	</MvIF>

	<MvASSIGN NAME = "l.page_count"						VALUE = "{ ceil( l.settings:total_count / l.settings:per_page ) }">
	<MvASSIGN NAME = "l.settings:page_links:last_page"	VALUE = "{ l.page_count }">

	<MvIF EXPR = "{ g.NextOffset }">	<MvASSIGN NAME = "l.settings:page_links:current_page" VALUE = "{ ceil( g.NextOffset / l.settings:per_page ) }">
	<MvELSE>							<MvASSIGN NAME = "l.settings:page_links:current_page" VALUE = "{ ceil( l.settings:total_count / l.settings:per_page ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL g.WishListSearch }">
		<MvASSIGN NAME = "l.link_additions" VALUE = "{ l.link_additions $ '&WishListSearch=' $ encodeattribute( g.WishListSearch ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings:page_links:next_link_params"		VALUE = "{ 'Offset=' $ encodeattribute( ( l.settings:page_links:current_page ) * l.settings:per_page ) $ l.link_additions }">
	<MvASSIGN NAME = "l.settings:page_links:prev_link_params"		VALUE = "{ 'Offset=' $ encodeattribute( ( l.settings:page_links:current_page - 2 ) * l.settings:per_page ) $ l.link_additions }">
	<MvASSIGN NAME = "l.settings:page_links:first_link_params"		VALUE = "{ 'Offset=0' $ l.link_additions }">

	<MvIF EXPR = "{ floor( l.settings:total_count / l.settings:per_page ) * l.settings:per_page EQ l.settings:total_count }">
		<MvASSIGN NAME = "l.last_page_offset"						VALUE = "{ floor( ( l.settings:total_count - 1 ) / l.settings:per_page ) * l.settings:per_page }">
		<MvASSIGN NAME = "l.settings:page_links:last_link_params"	VALUE = "{ 'Offset=' $ encodeattribute( floor( ( l.settings:total_count - 1 ) / l.settings:per_page ) * l.settings:per_page ) $ l.link_additions }">
	<MvELSE>
		<MvASSIGN NAME = "l.last_page_offset"						VALUE = "{ floor( ( l.settings:total_count ) / l.settings:per_page ) * l.settings:per_page }">
		<MvASSIGN NAME = "l.settings:page_links:last_link_params"	VALUE = "{ 'Offset=' $ encodeattribute( floor( l.settings:total_count / l.settings:per_page ) * l.settings:per_page ) $ l.link_additions }">
	</MvIF>

	<MvIF EXPR = "{ l.page_count LT l.settings:page_disp_count }">
		<MvASSIGN NAME = "l.left"	VALUE = "{ floor( ( l.page_count - 1 ) / 2 ) }">
		<MvASSIGN NAME = "l.right"	VALUE = "{ ceil( ( l.page_count - 1 ) / 2 ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.left"	VALUE = "{ floor( ( l.settings:page_disp_count - 1 ) / 2 ) }">
		<MvASSIGN NAME = "l.right"	VALUE = "{ ceil( ( l.settings:page_disp_count - 1 ) / 2 ) }">
	</MvIF>

	<MvASSIGN NAME = "l.counter"	VALUE = 0>
	<MvASSIGN NAME = "l.left_final" VALUE = 0>

	<MvWHILE EXPR = "{ l.counter LT l.left }">
		<MvIF EXPR = "{ ( ( l.settings:page_links:current_page - l.left + 1 ) + l.counter ) GT 1 }">
			<MvASSIGN NAME = "l.left_final"	VALUE = "{ l.left_final + 1 }">
		<MvELSE>
			<MvASSIGN NAME = "l.right" VALUE = "{ l.right + 1 }">
		</MvIF>

		<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
	</MvWHILE>

	<MvIF EXPR = "{ l.left_final EQ l.left }">
		<MvASSIGN NAME = "l.counter"	 VALUE = 0>
		<MvASSIGN NAME = "l.right_final" VALUE = 0>

		<MvWHILE EXPR = "{ l.counter LT l.right }">
			<MvIF EXPR = "{ ( ( l.settings:page_links:current_page + l.right - 1 ) - l.counter ) LT l.page_count }">
				<MvASSIGN NAME = "l.right_final" VALUE = "{ l.right_final + 1 }">
			<MvELSE>
				<MvASSIGN NAME = "l.left_final" VALUE = "{ l.left_final + 1 }">
			</MvIF>

			<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
		</MvWHILE>

		<MvASSIGN NAME = "l.pages" VALUE = "{ l.left_final + l.right_final + 1 }">
	<MvELSE>
		<MvASSIGN NAME = "l.pages" VALUE = "{ l.left_final + l.right + 1 }">
	</MvIF>

	<MvASSIGN NAME = "l.counter" VALUE = 0>

	<MvWHILE EXPR = "{ l.counter LT l.pages }">
		<MvASSIGN NAME = "l.settings:page_links:pages" INDEX = "{ l.counter + 1 }" MEMBER = "page_num" 		VALUE = "{ ( l.settings:page_links:current_page - l.left_final ) + l.counter }">
		<MvASSIGN NAME = "l.settings:page_links:pages" INDEX = "{ l.counter + 1 }" MEMBER = "link_params" 	VALUE = "{ 'Offset=' $ encodeattribute( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) $ l.link_additions }">

		<MvIF EXPR = "{ ( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) EQ 0 }">					<MvASSIGN NAME = "l.settings:page_links:contains_first"	VALUE = "YES">	</MvIF>
		<MvIF EXPR = "{ ( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) EQ l.last_page_offset }">	<MvASSIGN NAME = "l.settings:page_links:contains_last"	VALUE = "YES">	</MvIF>

		<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:items_per_page_filter }">
		<MvIF EXPR = "{ ISNULL g.Per_Page OR NOT [ g.Module_Admin ].Validate_WholeNumber_Required( g.Per_Page ) }">
			<MvIF EXPR = "{ l.settings:per_page EQ 0 }">
				<MvASSIGN NAME = "g.Per_Page"			VALUE = -1>
			<MvELSE>
				<MvASSIGN NAME = "g.Per_Page"			VALUE = "{ l.settings:per_page }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ g.Per_Page GE 0 }">
				<MvASSIGN NAME = "l.settings:per_page"	VALUE = "{ g.Per_Page }">
			<MvELSE>
				<MvASSIGN NAME = "g.Per_Page"			VALUE = -1>
				<MvASSIGN NAME = "l.settings:per_page"	VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.Sort_By }">
		<MvASSIGN NAME = "l.sort_by"					VALUE = "{ g.Sort_By }">
	<MvELSE>
		<MvASSIGN NAME = "l.sort_by"					VALUE = "{ l.settings:default_sort }">
		<MvASSIGN NAME = "g.Sort_By"					VALUE = "{ l.settings:default_sort }">
	</MvIF>

	<MvIF EXPR = "{ l.sort_by EQ 'id' AND
	 				l.settings:reverse_default_disp_order }">
		<MvASSIGN NAME = "l.sort_by" 					VALUE = "id_desc">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL g.WishListSearch }">
		<MvASSIGN NAME = "l.settings:item_count"		VALUE = "{ [ g.Module_Feature_WSH_DB ].WishListList_Load_Offset_Customer_Search( g.WishListSearch, g.Basket:cust_id, g.Offset, l.settings:per_page, l.sort_by, g.NextOffset, l.settings:items ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:item_count"		VALUE = "{ [ g.Module_Feature_WSH_DB ].WishListList_Load_Offset_Customer( g.Basket:cust_id, g.Offset, l.settings:per_page, l.sort_by, g.NextOffset, l.settings:items ) }">
	</MvIF>

	<MvASSIGN NAME = "l.null"							VALUE = "{ Create_Pagination( l.all_settings, l.settings ) }">

	<MvIF EXPR = "{ l.settings:item_count EQ 0 }">
		<MvASSIGN NAME = "l.settings:empty"				VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "g.PrevOffset"						VALUE = "{ g.Offset - l.settings:per_page }">

	<MvIF EXPR = "{ g.PrevOffset LT 0 }">
		<MvASSIGN NAME = "g.PrevOffset"					VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:template_filename, l.all_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.working_settings"	VALUE = "{ l.settings }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml, 'code', l.item ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O', l.provide_xml,	'WishListEntriesPerPage',		l.working_settings:per_page )						OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O', l.provide_xml,	'PageCountNumber',				l.working_settings:page_disp_count )				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'Paginate',						l.working_settings:paginate )						OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'ItemsPerPage',					l.working_settings:items_per_page_filter )			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'SortBySwitch',					l.working_settings:sort_by:switch )					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByTitle',					l.working_settings:sort_by:title )					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByFirstName',				l.working_settings:sort_by:fname )					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByLastName',				l.working_settings:sort_by:lname )					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml, 'ReverseDefaultDisplayOrder',	l.working_settings:reverse_default_disp_order )		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml,	'DefaultSort',					l.working_settings:default_sort,
																							'id,title_asc,title_desc,fname_asc,fname_desc,lname_asc,lname_desc',
																							'id,title_asc,title_desc,fname_asc,fname_desc,lname_asc,lname_desc' )				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'O', l.provide_xml,	'Notes',						l.notes ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Fields' ) }">
		<MvIF EXPR = "{ NOT ComponentModule_Provision_Fields( l.module, l.provide_xml:tags:Fields[ 1 ], l.working_settings ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
		<MvASSIGN NAME = "l.source"						VALUE = "{ trim( l.provide_xml:tags:Template[ 1 ]:value ) }">
		<MvASSIGN NAME = "l.working_settings:advanced"	VALUE = 1>
	<MvELSE>
		<MvASSIGN NAME = "l.working_settings:advanced"	VALUE = 0>

		<MvEVAL EXPR = "{ ComponentModule_Generate_Code( l.working_settings, l.source ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.source, l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings" VALUE = "{ l.working_settings }">

	<MvFUNCTIONRETURN>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision_Fields" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:field_title"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_name"	VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_notes"	VALUE = 0>

	<MvFOREACH ITERATOR = "l.child" ARRAY = "l.provide_xml:children" COUNT = "{ miva_array_elements( l.provide_xml:children ) }">
		<MvASSIGN NAME = "l.name"		VALUE = "{ tolower( l.child:name ) }">

		<MvIF EXPR = "{ l.name EQ 'title' }">			<MvASSIGN NAME = "l.settings:field_title"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'name' }">		<MvASSIGN NAME = "l.settings:field_name"	VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'notes' }">		<MvASSIGN NAME = "l.settings:field_notes"	VALUE = 1>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pages" VALUE = "{ [ g.Module_Feature_TUI_DB ].Item_Load_PageList( l.item ) }">

	<MvFUNCTIONRETURN VALUE = "{ 'Exports Wish Lists template from pages: ' $ l.pages }">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item_Runtime( l.item:id, l.pages ) }">
			<MvREFERENCEARRAY NAME = "l.settings" VARIABLE = "l.page:settings">
				<MvMEMBER NAME = "{ l.item:code }">
			</MvREFERENCEARRAY>

			<Page_Update code="{ l.page:code }">
				<Item code="{ l.item:code }">
					<WishListEntriesPerPage><MvEVAL EXPR = "{ int( l.settings:per_page ) }"></WishListEntriesPerPage>
					<PageCountNumber><MvEVAL EXPR = "{ int( l.settings:page_disp_count ) }"></PageCountNumber>
					<ItemsPerPage><MvIF EXPR = "{ l.settings:items_per_page_filter }">Yes<MvELSE>No</MvIF></ItemsPerPage>
					<SortBySwitch><MvIF EXPR = "{ l.settings:sort_by:switch }">Yes<MvELSE>No</MvIF></SortBySwitch>
					<SortByTitle><MvIF EXPR = "{ l.settings:sort_by:title }">Yes<MvELSE>No</MvIF></SortByTitle>
					<SortByFirstName><MvIF EXPR = "{ l.settings:sort_by:fname }">Yes<MvELSE>No</MvIF></SortByFirstName>
					<SortByLastName><MvIF EXPR = "{ l.settings:sort_by:lname }">Yes<MvELSE>No</MvIF></SortByLastName>
					<DefaultSort><MvIF EXPR = "{ ISNULL l.settings:default_sort }">id<MvELSE><MvEVAL EXPR = "{ l.settings:default_sort }"></MvIF></DefaultSort>
					<ReverseDefaultDisplayOrder><MvIF EXPR = "{ l.settings:reverse_default_disp_order }">Yes<MvELSE>No</MvIF></ReverseDefaultDisplayOrder>

					<Fields>
						<MvIF EXPR = "{ l.settings:field_title }">
							<Title />
						</MvIF>

						<MvIF EXPR = "{ l.settings:field_name }">
							<Name />
						</MvIF>

						<MvIF EXPR = "{ l.settings:field_notes }">
							<Notes />
						</MvIF>
					</Fields>

					<MvIF EXPR = "{ l.settings:advanced }">
						<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
							<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.templateversion:source ) }"></Template>
						</MvIF>
					</MvIF>

					<Notes>#Set_Current_Time#</Notes>
				</Item>
			</Page_Update>
		</MvFOREACH>
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Generate_Code" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code">
<MIVA STANDARDOUTPUTLEVEL = "text, html"><mvt:if expr="l.settings:wishlists:total_count EQ 0">
	<div class="wishlists_container wishlists_empty">No wish lists found</div>
	<mvt:exit />
</mvt:if>

<div class="line-item wishlists_container">
	<div id="filter-items-container"><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:sort_by:switch }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="l.settings:wishlists:total_count GT 1">
			<div class="sorting">
				<label for="Sort_By">Sort By:</label>
				<select name="Sort_By" id="Sort_By" onchange="window.location.href='&mvtj:urls:_self:auto_sep;Per_Page=' + encodeURIComponent( '&mvtj:global:Per_Page;' ) + '&amp;Sort_By=' + encodeURIComponent( this.options[ this.selectedIndex ].value ) + '&amp;WishListSearch=' + encodeURIComponent( '&mvtj:global:WishListSearch;' );"><MIVA STANDARDOUTPUTLEVEL = "">
				<MvIF EXPR = "{ l.settings:default_sort EQ 'id' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="ISNULL g.Sort_By">
						<option value="id" selected="selected">Default</option>
					<mvt:else>
						<option value="id">Default</option>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF>

				<MvIF EXPR = "{ l.settings:sort_by:title }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="g.Sort_By EQ 'title_asc'">
						<option value="title_asc" selected="selected">Title Ascending</option>
					<mvt:else>
						<option value="title_asc">Title Ascending</option>
					</mvt:if>

					<mvt:if expr="g.Sort_By EQ 'title_desc'">
						<option value="title_desc" selected="selected">Title Descending</option>
					<mvt:else>
						<option value="title_desc">Title Descending</option>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF>

				<MvIF EXPR = "{ l.settings:sort_by:fname }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="NOT ISNULL g.WishListSearch">
						<mvt:if expr="g.Sort_By EQ 'fname_asc'">
							<option value="fname_asc" selected="selected">First Name Ascending</option>
						<mvt:else>
							<option value="fname_asc">First Name Ascending</option>
						</mvt:if>

						<mvt:if expr="g.Sort_By EQ 'fname_desc'">
							<option value="fname_desc" selected="selected">First Descending</option>
						<mvt:else>
							<option value="fname_desc">First Name Descending</option>
						</mvt:if>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF>

				<MvIF EXPR = "{ l.settings:sort_by:lname }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="NOT ISNULL g.WishListSearch">
						<mvt:if expr="g.Sort_By EQ 'lname_asc'">
							<option value="lname_asc" selected="selected">Last Name Ascending</option>
						<mvt:else>
							<option value="lname_asc">Last Name Ascending</option>
						</mvt:if>

						<mvt:if expr="g.Sort_By EQ 'lname_desc'">
							<option value="lname_desc" selected="selected">Last Name Descending</option>
						<mvt:else>
							<option value="lname_desc">Last Name Descending</option>
						</mvt:if>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
				</select>
			</div>
		</mvt:if>
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
		
	<MvIF EXPR = "{ l.settings:items_per_page_filter }">
		<MvIF EXPR = "{ l.settings:per_page EQ 0 OR ISNULL l.settings:per_page }">
			<MvASSIGN NAME = "l.per_page" VALUE = 10>
		<MvELSE>
			<MvASSIGN NAME = "l.per_page" VALUE = "{ l.settings:per_page }">
		</MvIF>
		
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="l.settings:wishlists:total_count GT 1">
			<div class="per-page">
				<label for="Per_Page">View:</label>
				<select name="Per_Page" id="Per_Page" onchange="window.location.href='&mvtj:urls:_self:auto_sep;Per_Page=' + encodeURIComponent( this.options[ this.selectedIndex ].value ) + '&amp;Sort_By=' + encodeURIComponent( '&mvtj:global:Sort_By;' ) + '&amp;WishListSearch=' + encodeURIComponent( '&mvtj:global:WishListSearch;' );"><MIVA STANDARDOUTPUTLEVEL = "">
				<MvASSIGN NAME = "l.multiplier" VALUE = 1>
				<MvASSIGN NAME = "l.count" VALUE = 1>
					
				<MvWHILE EXPR = "{ l.count LE 3 }">
					<MvASSIGN NAME = "l.items_per" VALUE = "{ l.per_page * l.multiplier }">
					<MvASSIGN NAME = "l.multiplier" VALUE = "{ l.multiplier + l.count }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="{ 'g.Per_Page EQ ' $ encodeentities( l.items_per ) }">
						<option value="{ encodeentities( l.items_per ) }" selected="selected"><MvEVAL EXPR = "{ encodeentities( l.items_per ) }"></option>
					<mvt:else>
						<option value="{ encodeentities( l.items_per ) }"><MvEVAL EXPR = "{ encodeentities( l.items_per ) }"></option>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
					<MvASSIGN NAME = "l.count" VALUE = "{ l.count + 1 }">
				</MvWHILE>
					
					<MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="g.Per_Page EQ -1">
						<option value="-1" selected="selected">All</option>
					<mvt:else>
						<option value="-1">All</option>
					</mvt:if>
				</select>
			</div>
		</mvt:if>
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
		
	<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="l.settings:wishlists:page_links:last_page GT 1">
			<div class="page-links">
				<span class="page-links-title">Page(s):</span>
				<span class="page-links-container">
					<mvt:if expr="l.settings:wishlists:page_links:current_page NE 1">
						<a href="&mvte:urls:_self:auto_sep;&mvte:wishlists:page_links:prev_link_params;" class="page-links-previous">&lt;</a>
					<mvt:else>
						<span class="page-links-previous page-links-deactivated">&lt;</span>
					</mvt:if>
					<mvt:if expr="l.settings:wishlists:page_links:current_page NE l.settings:wishlists:page_links:last_page">
						<a href="&mvte:urls:_self:auto_sep;&mvte:wishlists:page_links:next_link_params;" class="page-links-next">&gt;</a>
					<mvt:else>
						<span class="page-links-next page-links-deactivated">&gt;</span>
					</mvt:if>
					<span class="page-disp">
						<mvt:if expr="NOT l.settings:wishlists:page_links:contains_first">
							<a href="&mvte:urls:_self:auto_sep;&mvte:wishlists:page_links:first_link_params;" class="page-links-inactive">1</a>...
						</mvt:if>
						<mvt:foreach iterator="pages" array="wishlists:page_links:pages">
							<mvt:if expr="l.settings:wishlists:page_links:current_page EQ l.settings:pages:page_num">
								<span class="page-links-active">&mvte:pages:page_num;</span>
							<mvt:else>
								<a href="&mvte:urls:_self:auto_sep;&mvte:pages:link_params;" class="page-links-inactive">&mvte:pages:page_num;</a>
							</mvt:if>
						</mvt:foreach>
						<mvt:if expr="NOT l.settings:wishlists:page_links:contains_last">
							...<a href="&mvte:urls:_self:auto_sep;&mvte:wishlists:page_links:last_link_params;" class="page-links-inactive">&mvte:wishlists:page_links:last_page;</a>
						</mvt:if>
					</span>
				</span>
			</div>
		</mvt:if>
	</div>
	
	<div class="clear"></div>

	<table>
		<tr class="heading"><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ l.settings:field_title }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="title">Title</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:field_name }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="NOT ISNULL g.WishListSearch">
				<td class="name">Name</td>
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:field_notes }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="notes">Notes</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

			<MIVA STANDARDOUTPUTLEVEL = "text, html">
		</tr>

		<mvt:foreach iterator="wishlist" array="wishlists:items">
			<mvt:if expr = "l.pos1 MOD 2">
				<tr class="alt_row">
			<mvt:else>
				<tr>
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">

			<MvIF EXPR = "{ l.settings:field_title }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<td class="title">
					<a href="&mvte:urls:WISH:rr_sep;WishList_ID=&mvta:wishlist:id;">&mvte:wishlist:title;</a>
				</td><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>

			<MvIF EXPR = "{ l.settings:field_name }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<mvt:if expr="NOT ISNULL g.WishListSearch">
					<td class="name">&mvte:wishlist:bill_fname; &mvte:wishlist:bill_lname;</td>
				</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>

			<MvIF EXPR = "{ l.settings:field_notes }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<td class="notes">&mvte:wishlist:notes;</td><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>

			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			</tr>
		</mvt:foreach>
	</table>

	<mvt:if expr="NOT l.settings:wishlists:page_disp_count GT 0">
		<mvt:if expr="( g.Offset OR g.NextOffset ) AND ( l.settings:wishlists:per_page NE 0 )">
			<table>
				<tr>
					<td class="previous-button">
						<mvt:if expr="g.Offset EQ 0">
							&nbsp;
						<mvt:else>
							<form method="post" action="&mvte:urls:_self:auto;" />
								<input type="hidden" name="Sort_By" value="&mvte:global:Sort_By;" />
								<input type="hidden" name="Offset" value="&mvte:global:PrevOffset;" />
								<input type="hidden" name="Per_Page" value="&mvte:global:Per_Page;" />
								<input type="hidden" name="WishListSearch" value="&mvte:global:WishListSearch;" />
								<mvt:item name="buttons" param="Previous" />
							</form>
						</mvt:if>
					</td>
					<td class="next-button">
						<mvt:if expr="g.NextOffset EQ 0">
							&nbsp;
						<mvt:else>
							<form method="post" action="&mvte:urls:_self:auto;">
								<input type="hidden" name="Sort_By" value="&mvte:global:Sort_By;" />
								<input type="hidden" name="Offset" value="&mvte:global:NextOffset;" />
								<input type="hidden" name="Per_Page" value="&mvte:global:Per_Page;" />
								<input type="hidden" name="WishListSearch" value="&mvte:global:WishListSearch;" />
								<mvt:item name="buttons" param="Next" />
							</form>
						</mvt:if>
					</td>
				</tr>
			</table>
		</mvt:if>
	</mvt:if>
</div>
	</MvCAPTURE>
</MvFUNCTION>

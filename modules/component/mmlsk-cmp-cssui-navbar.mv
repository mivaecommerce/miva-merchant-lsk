<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-NAV-
| Next Error Code: 10    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-navbar">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI Navigation Bar">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0800">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.60">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, data_store, vis_store, provision_store, skins">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'cssui-navbar.mvc', l.null_managedtemplate ) }">
		<MvASSIGN NAME = "l.cssui_navbar:nav_selstr" 	VALUE = "Select Store">
		<MvASSIGN NAME = "l.cssui_navbar:nav_store" 	VALUE = "Home">
		<MvASSIGN NAME = "l.cssui_navbar:nav_accnt" 	VALUE = "Account">
		<MvASSIGN NAME = "l.cssui_navbar:nav_search" 	VALUE = "Search">
		<MvASSIGN NAME = "l.cssui_navbar:nav_plst" 		VALUE = "All Products">
		<MvASSIGN NAME = "l.cssui_navbar:nav_basket" 	VALUE = "Basket">
		<MvASSIGN NAME = "l.cssui_navbar:nav_check" 	VALUE = "Checkout">
		<MvASSIGN NAME = "l.cssui_navbar:advanced"		VALUE = 0>

		<MvEVAL EXPR = "{ Generate_Code( l.cssui_navbar, l.code ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.code, l.cssui_navbar, 'cssui-navbar.mvc' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.module:version EQ l.version }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CSSUI-CMP-NAV-00009', 'Module \'' $ l.module:name $ '\' does not support manual upgrade.  New versions may only be obtained through the streaming update system.' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_GlobalDelete_ManagedTemplate_Filename( 'cssui-navbar.mvc' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Store_CSSUI_NavBar:nav_selstr"	VALUE = "{ trim( g.Store_CSSUI_NavBar:nav_selstr ) }">
	<MvASSIGN NAME = "g.Store_CSSUI_NavBar:nav_store"	VALUE = "{ trim( g.Store_CSSUI_NavBar:nav_store ) }">
	<MvASSIGN NAME = "g.Store_CSSUI_NavBar:nav_accnt"	VALUE = "{ trim( g.Store_CSSUI_NavBar:nav_accnt ) }">
	<MvASSIGN NAME = "g.Store_CSSUI_NavBar:nav_search"	VALUE = "{ trim( g.Store_CSSUI_NavBar:nav_search ) }">
	<MvASSIGN NAME = "g.Store_CSSUI_NavBar:nav_plst"	VALUE = "{ trim( g.Store_CSSUI_NavBar:nav_plst ) }">
	<MvASSIGN NAME = "g.Store_CSSUI_NavBar:nav_basket"	VALUE = "{ trim( g.Store_CSSUI_NavBar:nav_basket ) }">
	<MvASSIGN NAME = "g.Store_CSSUI_NavBar:nav_check"	VALUE = "{ trim( g.Store_CSSUI_NavBar:nav_check ) }">

	<MvIF EXPR = "{ g.Store_CSSUI_NavBar:advanced }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( g.Store_CSSUI_NavBar:template_code ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'CSSUINAG', 'Store_CSSUI_NavBar:template_code', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Filename( 'cssui-navbar.mvc', l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.settings"					VALUE = "{ l.managedtemplate:version:settings }">
	<MvASSIGN NAME = "l.settings:advanced"			VALUE = "{ g.Store_CSSUI_NavBar:advanced }">

	<MvIF EXPR = "{ NOT g.Store_CSSUI_NavBar:advanced }">
		<MvASSIGN NAME = "l.settings:nav_selstr"	VALUE = "{ g.Store_CSSUI_NavBar:nav_selstr }">
		<MvASSIGN NAME = "l.settings:nav_store"		VALUE = "{ g.Store_CSSUI_NavBar:nav_store }">
		<MvASSIGN NAME = "l.settings:nav_search"	VALUE = "{ g.Store_CSSUI_NavBar:nav_search }">
		
		<MvASSIGN NAME = "l.settings:nav_accnt"		VALUE = "{ g.Store_CSSUI_NavBar:nav_accnt }">
		<MvASSIGN NAME = "l.settings:nav_plst"		VALUE = "{ g.Store_CSSUI_NavBar:nav_plst }">
		<MvASSIGN NAME = "l.settings:nav_basket"	VALUE = "{ g.Store_CSSUI_NavBar:nav_basket }">
		<MvASSIGN NAME = "l.settings:nav_check"		VALUE = "{ g.Store_CSSUI_NavBar:nav_check }">

		<MvEVAL EXPR = "{ Generate_Code( l.settings, g.Store_CSSUI_NavBar:template_code ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, '', g.Store_CSSUI_NavBar:template_code, l.settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( 'CSSUINAG', 'Store_CSSUI_NavBar:template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Tabs" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "GT_UISETTINGS/CSSUINAG:Navigation Bar">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.load_fields }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( 'cssui-navbar.mvc', l.template ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "g.Store_CSSUI_NavBar"						VALUE = "{ l.template:settings }">
		<MvASSIGN NAME = "g.Store_CSSUI_NavBar:template_id"			VALUE = "{ l.template:templ_id }">
		<MvASSIGN NAME = "g.Store_CSSUI_NavBar:template_code"		VALUE = "{ l.template:source }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( g.Tab, 'CSSUINAG' ) }">
		<MvHIDE FIELDS = "g.Store_CSSUI_NavBar">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_UISETTINGS', 'CSSUINAG' ) }">
		<MvHIDE FIELDS = "g.Store_CSSUI_NavBar:template_id">
		
		<table border="0" cellpadding="2" cellspacing="0" width="100%">

		<MvIF EXPR = "{ g.Store_CSSUI_NavBar:advanced }">
			<tr><td colspan="2" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( 'Store_CSSUI_NavBar:template_code', g.Store_CSSUI_NavBar:template_code, 20, 100, g.Store_CSSUI_NavBar:template_id ) }">
			</td></tr>

			<MvHIDE FIELDS = "g.Store_CSSUI_NavBar:nav_selstr, g.Store_CSSUI_NavBar:nav_store, g.Store_CSSUI_NavBar:nav_accnt, 
							  g.Store_CSSUI_NavBar:nav_search, g.Store_CSSUI_NavBar:nav_plst, g.Store_CSSUI_NavBar:nav_basket,
							  g.Store_CSSUI_NavBar:nav_check">
		<MvELSE>
			<MvASSIGN NAME = "l.img_color" VALUE = "color.gif">

			<MvHIDE FIELDS = "g.Store_CSSUI_NavBar:template_code">

			<tr><td valign="middle" nowrap>
				Select Store:
			</td><td width="100%" nowrap>
				<input type="text" size="40" name="Store_CSSUI_NavBar:nav_selstr" value="{ encodeentities( g.Store_CSSUI_NavBar:nav_selstr ) }">
			</td></tr>

			<tr><td valign="middle" nowrap>
				Store Front:
			</td><td width="100%" nowrap>
				<input type="text" size="40" name="Store_CSSUI_NavBar:nav_store" value="{ encodeentities( g.Store_CSSUI_NavBar:nav_store ) }">
			</td></tr>
			
			<tr><td valign="middle" nowrap>
				Account:
			</td><td width="100%" nowrap>
				<input type="text" size="40" name="Store_CSSUI_NavBar:nav_accnt" value="{ encodeentities( g.Store_CSSUI_NavBar:nav_accnt ) }">
			</td></tr>

			<tr><td valign="middle" nowrap>
				Search:
			</td><td width="100%" nowrap>
				<input type="text" size="40" name="Store_CSSUI_NavBar:nav_search" value="{ encodeentities( g.Store_CSSUI_NavBar:nav_search ) }">
			</td></tr>
			
			<tr><td valign="middle" nowrap>
				Product List:
			</td><td width="100%" nowrap>
				<input type="text" size="40" name="Store_CSSUI_NavBar:nav_plst" value="{ encodeentities( g.Store_CSSUI_NavBar:nav_plst ) }">
			</td></tr>

			<tr><td valign="middle" nowrap>
				Basket Contents:
			</td><td width="100%" nowrap>
				<input type="text" size="40" name="Store_CSSUI_NavBar:nav_basket" value="{ encodeentities( g.Store_CSSUI_NavBar:nav_basket ) }">
			</td></tr>

			<tr><td valign="middle" nowrap>
				Checkout:
			</td><td width="100%" nowrap>
				<input type="text" size="40" name="Store_CSSUI_NavBar:nav_check" value="{ encodeentities( g.Store_CSSUI_NavBar:nav_check ) }">
			</td></tr>
		</MvIF>

		<tr><td colspan="2">&nbsp;</td></tr>

		<tr><td>
			&nbsp;
		</td><td>
			<input type="hidden" name="Store_CSSUI_NavBar:advanced" value="{ encodeentities( g.Store_CSSUI_NavBar:advanced ) }">
			<MvIF EXPR = "{ g.Store_CSSUI_NavBar:advanced }">
				<a href="#" onclick="Toggle( 'Store_CSSUI_NavBar:advanced', 0 ); return false;">Point + Click Mode</a>
			<MvELSE>
				<a href="#" onclick="Toggle( 'Store_CSSUI_NavBar:advanced', 1 ); return false;">Advanced Mode</a>
			</MvIF>
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>
	
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvDO FILE = "{ g.Store_Template_Path $ 'cssui-navbar.mvc' }" NAME = "l.null" VALUE = "{ Template_Render( l.null, l.all_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Code" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code">
	<MIVA STANDARDOUTPUTLEVEL = "text,html">
<ul><MIVA STANDARDOUTPUTLEVEL = "">

	<MvIF EXPR = "{ len( l.settings:nav_check ) }">
	<MIVA STANDARDOUTPUTLEVEL = "text,html">
	<mvt:if expr="l.settings:page:code EQ 'OUS1' OR 
					l.settings:page:code EQ 'OUSM' OR
					l.settings:page:code EQ 'OSEL' OR
					l.settings:page:code EQ 'OPAY' OR
					l.settings:page:code EQ 'OPRC' OR
					l.settings:page:code EQ 'OINF' OR
					l.settings:page:code EQ 'OCST' OR
					l.settings:page:code EQ 'ORDL' OR
					l.settings:page:code EQ 'INVC'">
	<li class="navigation-item-on first-child" id="nav-checkout">
	<mvt:else>
	<li class="navigation-item-off first-child" id="nav-checkout">
	</mvt:if>
		<div class="item-left-side"><div class="item-right-side"><mvt:item name="cssui_links" param="checkout"><MvEVAL EXPR = "{ l.settings:nav_check }"></mvt:item></div></div>
	</li><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>

	<MvIF EXPR = "{ len( l.settings:nav_search ) }">
	<MIVA STANDARDOUTPUTLEVEL = "text,html">
	<mvt:if expr="l.settings:page:code EQ 'SRCH'">
	<li class="navigation-item-on" id="nav-search">
	<mvt:else>
	<li class="navigation-item-off" id="nav-search">
	</mvt:if>
		<div class="item-left-side"><div class="item-right-side"><mvt:item name="cssui_links" param="search"><MvEVAL EXPR = "{ l.settings:nav_search }"></mvt:item></div></div>
	</li><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	
	<MvIF EXPR = "{ len( l.settings:nav_basket ) }">
	<MIVA STANDARDOUTPUTLEVEL = "text,html">
	<mvt:if expr="l.settings:page:code EQ 'BASK'">
	<li class="navigation-item-on" id="nav-basket">
	<mvt:else>
	<li class="navigation-item-off" id="nav-basket">
	</mvt:if>
		<div class="item-left-side"><div class="item-right-side"><mvt:item name="cssui_links" param="basket"><MvEVAL EXPR = "{ l.settings:nav_basket }"></mvt:item></div></div>
	</li><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>

	<MvIF EXPR = "{ len( l.settings:nav_accnt ) }">
	<MIVA STANDARDOUTPUTLEVEL = "text,html">
	<mvt:if expr="l.settings:page:code EQ 'LOGN' OR
			l.settings:page:code EQ 'AFCL' OR
			l.settings:page:code EQ 'ACED' OR
			l.settings:page:code EQ 'ACAD' OR
			((g.Error_Message_Count GT 0) AND (l.settings:page:code EQ 'SFNT'))">
	<li class="navigation-item-on" id="nav-account">
	<mvt:else>
	<li class="navigation-item-off" id="nav-account">
	</mvt:if>
		<div class="item-left-side"><div class="item-right-side">
			<mvt:if expr="g.Basket:cust_id EQ 0">
			<mvt:item name="cssui_links" param="customer_login"><MvEVAL EXPR = "{ l.settings:nav_accnt }"></mvt:item>
			<mvt:else>
			<mvt:item name="cssui_links" param="customer_account"><MvEVAL EXPR = "{ l.settings:nav_accnt }"></mvt:item>
			</mvt:if>
		</div></div>
	</li><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>

	<MvIF EXPR = "{ len( l.settings:nav_plst ) }">
	<MIVA STANDARDOUTPUTLEVEL = "text,html">
	<mvt:if expr="l.settings:page:code EQ 'PLST'">
	<li class="navigation-item-on" id="nav-all-products">
	<mvt:else>
	<li class="navigation-item-off" id="nav-all-products">
	</mvt:if>
		<div class="item-left-side"><div class="item-right-side"><mvt:item name="cssui_links" param="all_products"><MvEVAL EXPR = "{ l.settings:nav_plst }"></mvt:item></div></div>
	</li><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	
	<MvIF EXPR = "{ len( l.settings:nav_store ) }">
	<MIVA STANDARDOUTPUTLEVEL = "text,html">
	<mvt:if expr="l.settings:page:code EQ 'SFNT' AND g.Error_Message_Count EQ 0">
	<li class="navigation-item-on" id="nav-storefront">
	<mvt:else>
	<li class="navigation-item-off" id="nav-storefront">
	</mvt:if>
		<div class="item-left-side"><div class="item-right-side"><mvt:item name="cssui_links" param="storefront"><MvEVAL EXPR = "{ l.settings:nav_store }"></mvt:item></div></div>
	</li><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
	<MIVA STANDARDOUTPUTLEVEL = "text,html">
</ul><MIVA STANDARDOUTPUTLEVEL = "">
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Store" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Filename( 'cssui-navbar.mvc', l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings" VALUE = "{ l.managedtemplate:version:settings }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'o', l.provide_xml, 'SelectStore',	l.settings:nav_selstr )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'o', l.provide_xml, 'StoreFront',		l.settings:nav_store )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'o', l.provide_xml, 'Account',		l.settings:nav_accnt )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'o', l.provide_xml, 'Search',			l.settings:nav_search )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'o', l.provide_xml, 'ProductList',	l.settings:nav_plst )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'o', l.provide_xml, 'BasketContents',	l.settings:nav_basket )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'o', l.provide_xml, 'Checkout',		l.settings:nav_check )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'o', l.provide_xml, 'Template',		l.source )				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml, 'Notes',			l.notes ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
		<MvASSIGN NAME = "l.settings:advanced"	VALUE = 1>
	<MvELSE>
		<MvEVAL EXPR = "{ Generate_Code( l.settings, l.source ) }">
		<MvASSIGN NAME = "l.settings:advanced"	VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.source, l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "Exports Navigation Bar design/template (Global Code)">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( 'cssui-navbar.mvc', l.managedtemplateversion ) }">
		<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
			<Module code="{ encodeentities( l.module:code ) }" feature="component">
				<MvIF EXPR = "{ l.managedtemplateversion:settings:advanced }">
					<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.managedtemplateversion:source ) }"></Template>
					<Notes>#Set_Current_Time#</Notes>
				<MvELSE>
					<SelectStore><MvEVAL EXPR = "{ miva_cdata_encode( l.managedtemplateversion:settings:nav_selstr ) }"></SelectStore>
					<StoreFront><MvEVAL EXPR = "{ miva_cdata_encode( l.managedtemplateversion:settings:nav_store ) }"></StoreFront>
					<Account><MvEVAL EXPR = "{ miva_cdata_encode( l.managedtemplateversion:settings:nav_accnt ) }"></Account>
					<Search><MvEVAL EXPR = "{ miva_cdata_encode( l.managedtemplateversion:settings:nav_search ) }"></Search>
					<ProductList><MvEVAL EXPR = "{ miva_cdata_encode( l.managedtemplateversion:settings:nav_plst ) }"></ProductList>
					<BasketContents><MvEVAL EXPR = "{ miva_cdata_encode( l.managedtemplateversion:settings:nav_basket ) }"></BasketContents>
					<Checkout><MvEVAL EXPR = "{ miva_cdata_encode( l.managedtemplateversion:settings:nav_check ) }"></Checkout>
					<Notes>#Set_Current_Time#</Notes>
				</MvIF>
			</Module>
		</MvCAPTURE>
	</MvIF>
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-CCHL-
| Next Error Code: 6    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-customercredithistory">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI Customer Credit History">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0800">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "10.08">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, component_prov, skins">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ 'GT_PAGE/' $ l.item $ ':Customer Credit History List Layout' }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModuleValidate_SortMethod" PARAMETERS = "item, default_sort, value, sort_by, sorting_option, sorting_method" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( l.default_sort EQ l.value ) AND ( NOT l.sort_by ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, '', 'Sorting option \'' $ l.sorting_option $ '\' must be enabled to use the Default Sort Method of ' $ l.sorting_method ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.fields:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.fields:per_page"					VALUE = "{ trim( l.fields:per_page ) }">
	<MvASSIGN NAME = "l.fields:page_disp_count"				VALUE = "{ trim( l.fields:page_disp_count ) }">
	<MvASSIGN NAME = "l.fields:reverse_default_disp_order"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:reverse_default_disp_order ) }">

	<MvIF EXPR = "{ l.fields:paginate }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:per_page ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'per_page', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.fields:paginate_count }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:page_disp_count ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'page_disp_count', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.fields:advanced AND l.fields:sort_by:switch }">
		<MvASSIGN NAME = "l.ds" VALUE = "{ l.fields:default_sort }">
		<MvASSIGN NAME = "l.sb" VALUE = "{ l.fields:sort_by }">

		<MvIF EXPR = "{ NOT ComponentModuleValidate_SortMethod(		l.item, l.ds, 'order_id_asc', 	l.sb:orderid,	'OrderID',				'Order Ascending' ) }">						<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'order_id_desc', 	l.sb:orderid,	'OrderID', 				'Order Descending' ) }">					<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'txref_asc', 		l.sb:txref, 	'TransactionReference', 'Transaction Reference Ascending' ) }">		<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'txref_desc', 	l.sb:txref, 	'TransactionReference', 'Transaction Reference Descending' ) }">	<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'descrip_asc', 	l.sb:descrip,	'Description',			'Description Ascending' ) }">				<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'descrip_desc', 	l.sb:descrip,	'Description', 			'Description Descending' ) }">				<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'amount_asc', 	l.sb:amount,	'Amount', 				'Amount Ascending' ) }">					<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'amount_desc', 	l.sb:amount,	'Amount', 				'Amount Descending' ) }">					<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'dtstamp_asc', 	l.sb:dtstamp,	'Date', 				'Date Ascending' ) }">						<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT ComponentModuleValidate_SortMethod( l.item, l.ds, 'dtstamp_desc',	l.sb:dtstamp,	'Date', 				'Date Descending' ) }">						<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.fields:advanced }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.fields:template_code ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.settings:advanced"						VALUE = "{ l.fields:advanced }">
	<MvASSIGN NAME = "l.settings:reverse_default_disp_order" 	VALUE = "{ l.fields:reverse_default_disp_order }">

	<MvIF EXPR = "{ NOT l.fields:advanced }">
		<MvASSIGN NAME = "l.settings:field_orderid"		VALUE = "{ l.fields:field_orderid }">
		<MvASSIGN NAME = "l.settings:field_txref"		VALUE = "{ l.fields:field_txref }">
		<MvASSIGN NAME = "l.settings:field_descrip"		VALUE = "{ l.fields:field_descrip }">
		<MvASSIGN NAME = "l.settings:field_amount"		VALUE = "{ l.fields:field_amount }">
		<MvASSIGN NAME = "l.settings:field_dtstamp"		VALUE = "{ l.fields:field_dtstamp }">
	</MvIF>

	<MvCOMMENT>
	|
	| These settings are common between advanced and point + click mode
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.fields:paginate }">
		<MvASSIGN NAME = "l.settings:per_page"			VALUE = "{ l.fields:per_page }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:per_page"			VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.fields:paginate_count }">
		<MvASSIGN NAME = "l.settings:page_disp_count"	VALUE = "{ l.fields:page_disp_count }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:page_disp_count"	VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.settings:items_per_page_filter"	VALUE = "{ l.fields:items_per_page_filter }">

	<MvASSIGN NAME = "l.settings:sort_by:switch"		VALUE = "{ l.fields:sort_by:switch }">
	<MvASSIGN NAME = "l.settings:sort_by:orderid"		VALUE = "{ l.fields:sort_by:orderid }">
	<MvASSIGN NAME = "l.settings:sort_by:txref"			VALUE = "{ l.fields:sort_by:txref }">
	<MvASSIGN NAME = "l.settings:sort_by:descrip"		VALUE = "{ l.fields:sort_by:descrip }">
	<MvASSIGN NAME = "l.settings:sort_by:amount"		VALUE = "{ l.fields:sort_by:amount }">
	<MvASSIGN NAME = "l.settings:sort_by:dtstamp"		VALUE = "{ l.fields:sort_by:dtstamp }">

	<MvIF EXPR = "{ l.fields:default_sort EQ '' }">
		<MvASSIGN NAME = "l.settings:default_sort"		VALUE = "id">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:default_sort"		VALUE = "{ l.fields:default_sort }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-CCHL-00001', l.module:name $ ' \'' $ l.item $ '\' settings updated' ) }">

	<MvIF EXPR = "{ NOT l.fields:advanced }">
		<MvEVAL EXPR = "{ ComponentModule_Generate_Code( l.settings, l.fields:template_code ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, '', l.fields:template_code, l.settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.load_fields }">
		<MvASSIGN NAME = "l.fields:field_orderid"				VALUE = "{ l.settings:field_orderid }">
		<MvASSIGN NAME = "l.fields:field_txref"					VALUE = "{ l.settings:field_txref }">
		<MvASSIGN NAME = "l.fields:field_descrip"				VALUE = "{ l.settings:field_descrip }">
		<MvASSIGN NAME = "l.fields:field_amount"				VALUE = "{ l.settings:field_amount }">
		<MvASSIGN NAME = "l.fields:field_dtstamp"				VALUE = "{ l.settings:field_dtstamp }">

		<MvASSIGN NAME = "l.fields:advanced"					VALUE = "{ l.settings:advanced }">
		<MvASSIGN NAME = "l.fields:template_code"				VALUE = "{ l.templateversion:source }">
		<MvASSIGN NAME = "l.fields:template_filename"			VALUE = "{ l.settings:template_filename }">

		<MvIF EXPR = "{ l.settings:per_page EQ 0 }">
			<MvASSIGN NAME = "l.fields:paginate"				VALUE = 0>
			<MvASSIGN NAME = "l.fields:per_page"				VALUE = 10>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:paginate"				VALUE = 1>
			<MvASSIGN NAME = "l.fields:per_page"				VALUE = "{ l.settings:per_page }">
		</MvIF>

		<MvIF EXPR = "{ l.settings:page_disp_count EQ 0 }">
			<MvASSIGN NAME = "l.fields:paginate_count"			VALUE = 0>
			<MvASSIGN NAME = "l.fields:page_disp_count"			VALUE = 3>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:paginate_count"			VALUE = 1>
			<MvASSIGN NAME = "l.fields:page_disp_count"			VALUE = "{ l.settings:page_disp_count }">
		</MvIF>

		<MvIF EXPR = "{ l.settings:items_per_page_filter EQ 0 }">
			<MvASSIGN NAME = "l.fields:items_per_page_filter"	VALUE = 0>
		<MvELSE>
			<MvASSIGN NAME = "l.fields:items_per_page_filter"	VALUE = "{ l.settings:items_per_page_filter }">
		</MvIF>

		<MvASSIGN NAME = "l.fields:sort_by:switch"				VALUE = "{ l.settings:sort_by:switch }">
		<MvASSIGN NAME = "l.fields:sort_by:orderid"				VALUE = "{ l.settings:sort_by:orderid }">
		<MvASSIGN NAME = "l.fields:sort_by:txref"				VALUE = "{ l.settings:sort_by:txref }">
		<MvASSIGN NAME = "l.fields:sort_by:descrip"				VALUE = "{ l.settings:sort_by:descrip }">
		<MvASSIGN NAME = "l.fields:sort_by:amount"				VALUE = "{ l.settings:sort_by:amount }">
		<MvASSIGN NAME = "l.fields:sort_by:dtstamp"				VALUE = "{ l.settings:sort_by:dtstamp }">

		<MvIF EXPR = "{ l.settings:default_sort EQ '' }">
			<MvASSIGN NAME = "l.fields:default_sort"			VALUE = "id">
		<MvELSE>
			<MvASSIGN NAME = "l.fields:default_sort"			VALUE = "{ l.settings:default_sort }">
		</MvIF>

		<MvASSIGN NAME = "l.fields:reverse_default_disp_order" 	VALUE = "{ l.settings:reverse_default_disp_order }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'field_orderid,field_txref,field_descrip,field_amount,field_dtstamp,paginate,per_page,paginate_count,items_per_page_filter,page_disp_count,default_sort,sort_by:switch,sort_by:orderid,sort_by:txref,sort_by:descrip,sort_by:amount,sort_by:dtstamp,reverse_default_disp_order,template_filename,template_code,advanced' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_filename' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<MvIF EXPR = "{ NOT l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_code' ) }">
		<MvELSE>
			<tr><td colspan="2" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( l.field_prefix $ 'template_code', l.fields:template_code, 20, 100, l.templateversion:templ_id ) }">
			</td></tr>

			<MvIF EXPR = "{ l.customfield_count }">
				<tr><td colspan="2">&nbsp;</td></tr>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.fields:advanced }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'field_orderid,field_txref,field_descrip,field_amount,field_dtstamp' ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT l.fields:advanced }">
			<tr><td align="left" valign="top" nowrap>
				<b>Displayed Fields:</b>
			</td><td align="left" width="100%">
				<table border="0" cellpadding="0" cellspacing="0">
				<tr><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_orderid, l.field_prefix $ 'field_orderid', 1, 'Order ID' ) }">
				</td><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_txref, l.field_prefix $ 'field_txref', 1, 'Transaction Reference' ) }">
				</td></tr>

				<tr><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_descrip, l.field_prefix $ 'field_descrip', 1, 'Description' ) }">
				</td><td>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_amount, l.field_prefix $ 'field_amount', 1, 'Amount' ) }">
				</td></tr>

				<tr><td align="left" nowrap>
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:field_dtstamp, l.field_prefix $ 'field_dtstamp', 1, 'Date' ) }">
				</td><td>
					&nbsp;
				</td></tr>
				</table>
			</td></tr>
		</MvIF>

		<tr>
			<td align="left" nowrap>
				<b>Sorting Options:</b>
			</td>
			<td width="100%">
				<table width="100%">
					<tr>
						<td align="left" style="padding-right: 15px;" nowrap>
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'sort_by:switch', 0, l.fields:sort_by:switch, 'Off' ) }"><br />
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'sort_by:switch', 1, l.fields:sort_by:switch, 'On' ) }">
						</td>
						<td width="100%">
							<MvIF EXPR = "{ l.fields:advanced }">
								<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'sort_by:orderid,sort_by:txref,sort_by:descrip,sort_by:amount,sort_by:dtstamp' ) }">
							<MvELSE>
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:orderid, 	l.field_prefix $ 'sort_by:orderid', 	'1', 	'Order' ) }"><br />
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:txref,		l.field_prefix $ 'sort_by:txref',		'1', 	'Transaction Reference' ) }"><br />
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:descrip, 	l.field_prefix $ 'sort_by:descrip', 	'1', 	'Description' ) }"><br />
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:amount, 	l.field_prefix $ 'sort_by:amount',	 	'1', 	'Amount' ) }"><br />
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:sort_by:dtstamp, 	l.field_prefix $ 'sort_by:dtstamp', 	'1', 	'Date' ) }">
							</MvIF>
						</td>
					</tr>
				</table>
			</td>
		</tr>

		<tr>
			<td align="left" style="padding-right: 15px;" nowrap>
				<b>Default Sort Method:</b>
			</td>
			<td width="100%">
				<table width="100%">
					<tr>
						<td align="left" style="padding-right: 15px;" nowrap>
							<select name="{ encodeentities( l.field_prefix ) $ 'default_sort' }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'id',				l.fields:default_sort, 'Default' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'orderid_asc', 	l.fields:default_sort, 'Order Ascending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'orderid_desc', 	l.fields:default_sort, 'Order Descending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'txref_asc',		l.fields:default_sort, 'Transaction Reference Ascending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'txref_desc',		l.fields:default_sort, 'Transaction Reference Descending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'descrip_asc', 	l.fields:default_sort, 'Description Ascending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'descrip_desc', 	l.fields:default_sort, 'Description Descending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'amount_asc',		l.fields:default_sort, 'Amount Ascending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'amount_desc', 	l.fields:default_sort, 'Amount Descending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'dtstamp_asc', 	l.fields:default_sort, 'Date Ascending' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'dtstamp_desc', 	l.fields:default_sort, 'Date Descending' ) }">
							</select>
						</td>
						<td width="100%">
							<font color="red"><b>WARNING:</b></font>&nbsp;&nbsp;Changing the default sorting option to any value other than "default" will increase page load times and increase the load on your server. Be fully aware of the effect this will have on your store before changing this option!
						</td>
					</tr>
				</table>
			</td>
		</tr>

		<tr>
			<td align="left" nowrap>
				&nbsp;
			</td>
			<td width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:reverse_default_disp_order, l.field_prefix $ 'reverse_default_disp_order', '1', 'Reverse Default Display Order' ) }">
			</td>
		</tr>

		<tr><td align="left" nowrap valign="top">
			<b>Pagination:</b>
		</td><td align="left" nowrap width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate', 0, l.fields:paginate, 'Do not paginate customer credit history' ) }"><br>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate', 1, l.fields:paginate, 'Display' ) }"> <input type="text" name="{ encodeentities( l.field_prefix ) $ 'per_page' }" value="{ encodeentities( l.fields:per_page ) }" size="5"> customer credit history entries per page<br />
		</td></tr>

		<tr><td align="left" nowrap valign="top">
			<b>Page Numbers:</b>
		</td><td align="left" nowrap width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate_count', 0, l.fields:paginate_count, 'Do not show page count' ) }"><br>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate_count', 1, l.fields:paginate_count, 'Display' ) }"> <input type="text" name="{ encodeentities( l.field_prefix ) $ 'page_disp_count' }" value="{ encodeentities( l.fields:page_disp_count ) }" size="5"> pages at a time
		</td></tr>

		<tr><td align="left" nowrap valign="top">
			<b>Items Per Page Filter:</b>
		</td><td align="left" nowrap width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'items_per_page_filter', 0, l.fields:items_per_page_filter, 'Off' ) }"><br>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'items_per_page_filter', 1, l.fields:items_per_page_filter, 'On' ) }">
		</td></tr>

		<tr><td colspan="2">&nbsp;</td></tr>

		<tr><td>
			&nbsp;
		</td><td width="100%">
			<input type="hidden" name="{ encodeentities( l.field_prefix ) $ 'advanced' }" value="{ encodeentities( l.fields:advanced ) }">
			<MvIF EXPR = "{ l.fields:advanced }">
				<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 0 ); return false;' }">Point + Click Mode</a>
			<MvELSE>
				<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'advanced' $ '\', 1 ); return false;' }">Advanced Mode</a>
			</MvIF>
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:field_orderid"					VALUE = "1">
	<MvASSIGN NAME = "l.settings:field_txref"					VALUE = "0">
	<MvASSIGN NAME = "l.settings:field_descrip"					VALUE = "1">
	<MvASSIGN NAME = "l.settings:field_amount"					VALUE = "1">
	<MvASSIGN NAME = "l.settings:field_dtstamp"					VALUE = "1">
	<MvASSIGN NAME = "l.settings:per_page"						VALUE = "10">
	<MvASSIGN NAME = "l.settings:page_disp_count"				VALUE = "3">
	<MvASSIGN NAME = "l.settings:items_per_page_filter"			VALUE = "1">
	<MvASSIGN NAME = "l.settings:sort_by:switch"				VALUE = "1">
	<MvASSIGN NAME = "l.settings:sort_by:orderid"				VALUE = "1">
	<MvASSIGN NAME = "l.settings:sort_by:txref"					VALUE = "0">
	<MvASSIGN NAME = "l.settings:sort_by:descrip"				VALUE = "0">
	<MvASSIGN NAME = "l.settings:sort_by:amount"				VALUE = "1">
	<MvASSIGN NAME = "l.settings:sort_by:dtstamp"				VALUE = "1">
	<MvASSIGN NAME = "l.settings:default_sort"					VALUE = "id">
	<MvASSIGN NAME = "l.settings:reverse_default_disp_order" 	VALUE = "0">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ ComponentModule_Generate_Code( l.settings, l.template_source ) }">

	<MvASSIGN NAME = "l.settings:template_filename"	VALUE = "{ tolower( l.page:code ) $ '-' $ tolower( l.item ) $ '.mvc' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.template_source, l.settings, l.settings:template_filename ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'buttons' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:template_filename ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:template_filename, l.source_templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:template_filename, l.dest_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.dest_settings"						VALUE = "{ l.source_settings }">
	<MvASSIGN NAME = "l.dest_settings:template_filename"	VALUE = "{ l.dest_managedtemplate:version:settings:template_filename }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_managedtemplate, '', l.source_templateversion:source, l.dest_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.source_branch:id EQ l.dest_branch:id }">	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-CCHL-00004', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' on branch \'' $ l.dest_branch:name $ '\'' ) }">
	<MvELSE>													<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-CCHL-00005', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' from branch \'' $ l.source_branch:name $ '\' to branch \'' $ l.dest_branch:name $ '\'' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Create_Pagination" PARAMETERS = "all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_CUS_DB ].CustomerCreditHistoryList_Count_Customer( g.Basket:cust_id, l.settings:total_count ) }">
		<MvASSIGN NAME = "l.settings:page_disp_count" VALUE = 0>
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.settings:page_disp_count EQ 0 OR l.settings:per_page EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.flags:sep" 		VALUE = 1>
	<MvASSIGN NAME = "l.link_additions" VALUE = "">

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code_Cached( g.Screen, l.page ) }">
		<MvASSIGN NAME = "l.page_url" 	VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Page_URL( l.page, l.flags ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.page_url" 	VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Screen_URL( g.Screen, l.flags ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL g.Per_Page }">	<MvASSIGN NAME = "l.link_additions"	VALUE = "{ l.link_additions $ '&Per_Page=' $ encodeattribute( g.Per_Page ) }">	</MvIF>
	<MvIF EXPR = "{ NOT ISNULL g.Sort_By }">	<MvASSIGN NAME = "l.link_additions"	VALUE = "{ l.link_additions $ '&Sort_By=' $ encodeattribute( g.Sort_By ) }">	</MvIF>

	<MvASSIGN NAME = "l.page_count"						VALUE = "{ ceil( l.settings:total_count / l.settings:per_page ) }">
	<MvASSIGN NAME = "l.settings:page_links:last_page"	VALUE = "{ l.page_count }">

	<MvIF EXPR = "{ g.NextOffset }">	<MvASSIGN NAME = "l.settings:page_links:current_page" VALUE = "{ ceil( g.NextOffset / l.settings:per_page ) }">
	<MvELSE>							<MvASSIGN NAME = "l.settings:page_links:current_page" VALUE = "{ ceil( l.settings:total_count / l.settings:per_page ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| Keeping xxx_link values for backward compatibility
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.settings:page_links:next_link_params"		VALUE = "{ 'Offset=' $ encodeattribute( ( l.settings:page_links:current_page ) * l.settings:per_page ) $ l.link_additions }">
	<MvASSIGN NAME = "l.settings:page_links:next_link"				VALUE = "{ l.page_url $ l.settings:page_links:next_link_params }">
	<MvASSIGN NAME = "l.settings:page_links:prev_link_params"		VALUE = "{ 'Offset=' $ encodeattribute( ( l.settings:page_links:current_page - 2 ) * l.settings:per_page ) $ l.link_additions }">
	<MvASSIGN NAME = "l.settings:page_links:prev_link"				VALUE = "{ l.page_url $ l.settings:page_links:prev_link_params }">
	<MvASSIGN NAME = "l.settings:page_links:first_link_params"		VALUE = "{ 'Offset=0' $ l.link_additions }">
	<MvASSIGN NAME = "l.settings:page_links:first_link"				VALUE = "{ l.page_url $ l.settings:page_links:first_link_params }">

	<MvIF EXPR = "{ floor( l.settings:total_count / l.settings:per_page ) * l.settings:per_page EQ l.settings:total_count }">
		<MvASSIGN NAME = "l.last_page_offset"						VALUE = "{ floor( ( l.settings:total_count - 1 ) / l.settings:per_page ) * l.settings:per_page }">
		<MvASSIGN NAME = "l.settings:page_links:last_link_params"	VALUE = "{ 'Offset=' $ encodeattribute( floor( ( l.settings:total_count - 1 ) / l.settings:per_page ) * l.settings:per_page ) $ l.link_additions }">
		<MvASSIGN NAME = "l.settings:page_links:last_link"			VALUE = "{ l.page_url $ l.settings:page_links:last_link_params }">
	<MvELSE>
		<MvASSIGN NAME = "l.last_page_offset"						VALUE = "{ floor( ( l.settings:total_count ) / l.settings:per_page ) * l.settings:per_page }">
		<MvASSIGN NAME = "l.settings:page_links:last_link_params"	VALUE = "{ 'Offset=' $ encodeattribute( floor( l.settings:total_count / l.settings:per_page ) * l.settings:per_page ) $ l.link_additions }">
		<MvASSIGN NAME = "l.settings:page_links:last_link"			VALUE = "{ l.page_url $ l.settings:page_links:last_link_params }">
	</MvIF>

	<MvIF EXPR = "{ l.page_count LT l.settings:page_disp_count }">
		<MvASSIGN NAME = "l.left"	VALUE = "{ floor( ( l.page_count - 1 ) / 2 ) }">
		<MvASSIGN NAME = "l.right"	VALUE = "{ ceil( ( l.page_count - 1 ) / 2 ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.left"	VALUE = "{ floor( ( l.settings:page_disp_count - 1 ) / 2 ) }">
		<MvASSIGN NAME = "l.right"	VALUE = "{ ceil( ( l.settings:page_disp_count - 1 ) / 2 ) }">
	</MvIF>

	<MvASSIGN NAME = "l.counter"	VALUE = 0>
	<MvASSIGN NAME = "l.left_final" VALUE = 0>

	<MvWHILE EXPR = "{ l.counter LT l.left }">
		<MvIF EXPR = "{ ( ( l.settings:page_links:current_page - l.left + 1 ) + l.counter ) GT 1 }">
			<MvASSIGN NAME = "l.left_final"	VALUE = "{ l.left_final + 1 }">
		<MvELSE>
			<MvASSIGN NAME = "l.right" VALUE = "{ l.right + 1 }">
		</MvIF>

		<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
	</MvWHILE>

	<MvIF EXPR = "{ l.left_final EQ l.left }">
		<MvASSIGN NAME = "l.counter"	 VALUE = 0>
		<MvASSIGN NAME = "l.right_final" VALUE = 0>

		<MvWHILE EXPR = "{ l.counter LT l.right }">
			<MvIF EXPR = "{ ( ( l.settings:page_links:current_page + l.right - 1 ) - l.counter ) LT l.page_count }">
				<MvASSIGN NAME = "l.right_final" VALUE = "{ l.right_final + 1 }">
			<MvELSE>
				<MvASSIGN NAME = "l.left_final" VALUE = "{ l.left_final + 1 }">
			</MvIF>

			<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
		</MvWHILE>

		<MvASSIGN NAME = "l.pages" VALUE = "{ l.left_final + l.right_final + 1 }">
	<MvELSE>
		<MvASSIGN NAME = "l.pages" VALUE = "{ l.left_final + l.right + 1 }">
	</MvIF>

	<MvASSIGN NAME = "l.counter" VALUE = 0>

	<MvWHILE EXPR = "{ l.counter LT l.pages }">
		<MvASSIGN NAME = "l.settings:page_links:pages" INDEX = "{ l.counter + 1 }" MEMBER = "page_num" 		VALUE = "{ ( l.settings:page_links:current_page - l.left_final ) + l.counter }">
		<MvASSIGN NAME = "l.settings:page_links:pages" INDEX = "{ l.counter + 1 }" MEMBER = "link_params" 	VALUE = "{ 'Offset=' $ encodeattribute( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) $ l.link_additions }">
		<MvASSIGN NAME = "l.settings:page_links:pages" INDEX = "{ l.counter + 1 }" MEMBER = "link" 			VALUE = "{ l.page_url $ l.settings:page_links:pages[ l.counter + 1 ]:link_params }">

		<MvIF EXPR = "{ ( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) EQ 0 }">					<MvASSIGN NAME = "l.settings:page_links:contains_first"	VALUE = "YES">	</MvIF>
		<MvIF EXPR = "{ ( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) EQ l.last_page_offset }">	<MvASSIGN NAME = "l.settings:page_links:contains_last"	VALUE = "YES">	</MvIF>

		<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
    <MvIF EXPR = "{ l.settings:items_per_page_filter }">
		<MvIF EXPR = "{ ISNULL g.Per_Page OR NOT [ g.Module_Admin ].Validate_WholeNumber_Required( g.Per_Page ) }">
			<MvIF EXPR = "{ l.settings:per_page EQ 0 }">
				<MvASSIGN NAME = "g.Per_Page"			VALUE = -1>
			<MvELSE>
				<MvASSIGN NAME = "g.Per_Page"			VALUE = "{ l.settings:per_page }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ g.Per_Page GE 0 }">
				<MvASSIGN NAME = "l.settings:per_page"	VALUE = "{ g.Per_Page }">
			<MvELSE>
				<MvASSIGN NAME = "g.Per_Page"			VALUE = -1>
				<MvASSIGN NAME = "l.settings:per_page"	VALUE = 0>
			</MvIF>
		</MvIF>
    </MvIF>

	<MvIF EXPR = "{ g.Sort_By }">
		<MvASSIGN NAME = "l.sort_by"					VALUE = "{ g.Sort_By }">
	<MvELSE>
		<MvASSIGN NAME = "l.sort_by"					VALUE = "{ l.settings:default_sort }">
		<MvASSIGN NAME = "g.Sort_By"					VALUE = "{ l.settings:default_sort }">
	</MvIF>

	<MvIF EXPR = "{ l.sort_by EQ 'id' AND 
	 				l.settings:reverse_default_disp_order }">
		<MvASSIGN NAME = "l.sort_by" 					VALUE = "id_desc">
	</MvIF>

	<MvASSIGN NAME = "l.settings:item_count"			VALUE = "{ [ g.Module_Feature_CUS_DB ].CustomerCreditHistoryList_Load_Offset_Customer( g.Basket:cust_id, g.Offset, l.settings:per_page, l.sort_by, g.NextOffset, l.settings:items ) }">
	<MvASSIGN NAME = "l.null"							VALUE = "{ Create_Pagination( l.all_settings, l.settings ) }">

	<MvIF EXPR = "{ l.settings:item_count EQ 0 }">
		<MvASSIGN NAME = "l.settings:empty"				VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "g.PrevOffset"						VALUE = "{ g.Offset - l.settings:per_page }">

	<MvIF EXPR = "{ g.PrevOffset LT 0 }">
		<MvASSIGN NAME = "g.PrevOffset"					VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.credit" ARRAY = "l.settings:items" COUNT = "{ l.settings:item_count }">
		<MvASSIGN NAME = "l.credit:formatted_amount"	VALUE = "{ [ g.Module_Store_Module_Currency ].CurrencyModule_AddFormatting( g.Store_Module_Currency, l.credit:amount ) }">
		<MvASSIGN NAME = "l.credit:formatted_dtstamp"	VALUE = "{ [ g.Module_Library_Utilities ].Format_Date( l.credit:dtstamp, s.miva_language ) $ ' ' $ [ g.Module_Library_Utilities ].Format_Time_Short( l.credit:dtstamp, s.miva_language ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:template_filename, l.all_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.working_settings"	VALUE = "{ l.settings }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml, 'code', l.item ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O', l.provide_xml,	'CustomerCreditHistoryEntriesPerPage',	l.working_settings:per_page )															OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'O', l.provide_xml,	'PageCountNumber',						l.working_settings:page_disp_count )													OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'Paginate',								l.working_settings:paginate )															OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'ItemsPerPage',							l.working_settings:items_per_page_filter )												OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'SortBySwitch',							l.working_settings:sort_by:switch )														OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByOrderID',						l.working_settings:sort_by:orderid )													OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByTransactionReference',			l.working_settings:sort_by:txref )														OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByDescription',					l.working_settings:sort_by:descrip )													OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByAmount',							l.working_settings:sort_by:amount )														OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml,	'SortByDate',							l.working_settings:sort_by:dtstamp )													OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'o', l.provide_xml, 'ReverseDefaultDisplayOrder',			l.working_settings:reverse_default_disp_order )											OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'O', l.provide_xml,	'DefaultSort',							l.working_settings:default_sort,
																							'id,orderid_asc,orderid_desc,txref_asc,txref_desc,descrip_asc,descrip_desc,amount_asc,amount_desc,dtstamp_asc,dtstamp_desc',
																							'id,orderid_asc,orderid_desc,txref_asc,txref_desc,descrip_asc,descrip_desc,amount_asc,amount_desc,dtstamp_asc,dtstamp_desc' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'O', l.provide_xml,	'Notes',								l.notes ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Fields' ) }">
		<MvIF EXPR = "{ NOT ComponentModule_Provision_Fields( l.module, l.provide_xml:tags:Fields[ 1 ], l.working_settings ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
		<MvASSIGN NAME = "l.source"						VALUE = "{ trim( l.provide_xml:tags:Template[ 1 ]:value ) }">
		<MvASSIGN NAME = "l.working_settings:advanced"	VALUE = 1>
	<MvELSE>
		<MvASSIGN NAME = "l.working_settings:advanced"	VALUE = 0>

		<MvEVAL EXPR = "{ ComponentModule_Generate_Code( l.working_settings, l.source ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.source, l.working_settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings" VALUE = "{ l.working_settings }">

	<MvFUNCTIONRETURN>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision_Fields" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:field_orderid"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_txref"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_descrip"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_amount"		VALUE = 0>
	<MvASSIGN NAME = "l.settings:field_dtstamp"		VALUE = 0>

	<MvFOREACH ITERATOR = "l.child" ARRAY = "l.provide_xml:children" COUNT = "{ miva_array_elements( l.provide_xml:children ) }">
		<MvASSIGN NAME = "l.name"		VALUE = "{ tolower( l.child:name ) }">

		<MvIF EXPR = "{ l.name EQ 'orderid' }">							<MvASSIGN NAME = "l.settings:field_orderid"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'transactionreference' }">		<MvASSIGN NAME = "l.settings:field_txref"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'description' }">					<MvASSIGN NAME = "l.settings:field_descrip"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'amount' }">						<MvASSIGN NAME = "l.settings:field_amount"		VALUE = 1>
		<MvELSEIF EXPR = "{ l.name EQ 'date' }">						<MvASSIGN NAME = "l.settings:field_dtstamp"		VALUE = 1>
		</MvIF>

		<MvASSIGN NAME = "l.child_pos"	VALUE = "{ l.child_pos + 1 }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pages" VALUE = "{ [ g.Module_Feature_TUI_DB ].Item_Load_PageList( l.item ) }">

	<MvFUNCTIONRETURN VALUE = "{ 'Exports Customer Credit History List template from pages: ' $ l.pages }">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item_Runtime( l.item:id, l.pages ) }">
			<MvREFERENCEARRAY NAME = "l.settings" VARIABLE = "l.page:settings">
				<MvMEMBER NAME = "{ l.item:code }">
			</MvREFERENCEARRAY>

			<Page_Update code="{ l.page:code }">
				<Item code="{ l.item:code }">
					<CustomerCreditHistoryEntriesPerPage><MvEVAL EXPR = "{ int( l.settings:per_page ) }"></CustomerCreditHistoryEntriesPerPage>
					<PageCountNumber><MvEVAL EXPR = "{ int( l.settings:page_disp_count ) }"></PageCountNumber>
					<ItemsPerPage><MvIF EXPR = "{ l.settings:items_per_page_filter }">Yes<MvELSE>No</MvIF></ItemsPerPage>
					<SortBySwitch><MvIF EXPR = "{ l.settings:sort_by:switch }">Yes<MvELSE>No</MvIF></SortBySwitch>
					<SortByOrderID><MvIF EXPR = "{ l.settings:sort_by:orderid }">Yes<MvELSE>No</MvIF></SortByOrderID>
					<SortByTransactionReference><MvIF EXPR = "{ l.settings:sort_by:txref }">Yes<MvELSE>No</MvIF></SortByTransactionReference>
					<SortByDescription><MvIF EXPR = "{ l.settings:sort_by:descrip }">Yes<MvELSE>No</MvIF></SortByDescription>
					<SortByAmount><MvIF EXPR = "{ l.settings:sort_by:amount }">Yes<MvELSE>No</MvIF></SortByAmount>
					<SortByDate><MvIF EXPR = "{ l.settings:sort_by:dtstamp }">Yes<MvELSE>No</MvIF></SortByDate>
					<DefaultSort><MvIF EXPR = "{ ISNULL l.settings:default_sort }">id<MvELSE><MvEVAL EXPR = "{ l.settings:default_sort }"></MvIF></DefaultSort>
					<ReverseDefaultDisplayOrder><MvIF EXPR = "{ l.settings:reverse_default_disp_order }">Yes<MvELSE>No</MvIF></ReverseDefaultDisplayOrder>

					<Fields>
						<MvIF EXPR = "{ l.settings:field_orderid }">
							<OrderID />
						</MvIF>

						<MvIF EXPR = "{ l.settings:field_txref }">
							<TransactionReference />
						</MvIF>

						<MvIF EXPR = "{ l.settings:field_descrip }">
							<Description />
						</MvIF>

						<MvIF EXPR = "{ l.settings:field_amount }">
							<Amount />
						</MvIF>

						<MvIF EXPR = "{ l.settings:field_dtstamp }">
							<Date />
						</MvIF>
					</Fields>

					<MvIF EXPR = "{ l.settings:advanced }">
						<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
							<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.templateversion:source ) }"></Template>
						</MvIF>
					</MvIF>

					<Notes>#Set_Current_Time#</Notes>
				</Item>
			</Page_Update>
		</MvFOREACH>
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Generate_Code" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code">
<MIVA STANDARDOUTPUTLEVEL = "text, html"><mvt:if expr="l.settings:customercredithistory:total_count EQ 0">
	No account credit entries
	<mvt:exit />
</mvt:if>

<div class="line-item customercredithistory_container">
	<div id="filter-items-container"><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:sort_by:switch }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="l.settings:customercredithistory:total_count GT 1">
			<div class="sorting">
				<label for="Sort_By">Sort By:</label>
				<select name="Sort_By" id="Sort_By" onchange="window.location.href='&mvtj:urls:_self:auto_sep;Per_Page=' + encodeURIComponent( '&mvtj:global:Per_Page;' ) + '&amp;Sort_By=' + encodeURIComponent( this.options[ this.selectedIndex ].value );"><MIVA STANDARDOUTPUTLEVEL = "">
				<MvIF EXPR = "{ l.settings:default_sort EQ 'id' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="ISNULL g.Sort_By">
						<option value="id" selected="selected">Default</option>
					<mvt:else>
						<option value="id">Default</option>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
				</MvIF>

				<MvIF EXPR = "{ l.settings:sort_by:orderid }">
					<MvASSIGN NAME = "l.sort:disp_name_ascending"	VALUE = "Order Ascending">
					<MvASSIGN NAME = "l.sort:disp_code_ascending"	VALUE = "orderid_asc">
					<MvASSIGN NAME = "l.sort:disp_name_descending"	VALUE = "Order Descending">
					<MvASSIGN NAME = "l.sort:disp_code_descending"	VALUE = "orderid_desc">

					<MvASSIGN NAME = "l.sort_count"					VALUE = "{ miva_array_insert( l.sorts, l.sort, -1 ) }">
				</MvIF>

				<MvIF EXPR = "{ l.settings:sort_by:txref }">
					<MvASSIGN NAME = "l.sort:disp_name_ascending"	VALUE = "Transaction Reference Ascending">
					<MvASSIGN NAME = "l.sort:disp_code_ascending"	VALUE = "txref_asc">
					<MvASSIGN NAME = "l.sort:disp_name_descending"	VALUE = "Transaction Reference Descending">
					<MvASSIGN NAME = "l.sort:disp_code_descending"	VALUE = "txref_desc">

					<MvASSIGN NAME = "l.sort_count"					VALUE = "{ miva_array_insert( l.sorts, l.sort, -1 ) }">
				</MvIF>

				<MvIF EXPR = "{ l.settings:sort_by:descrip }">
					<MvASSIGN NAME = "l.sort:disp_name_ascending"	VALUE = "Description Ascending">
					<MvASSIGN NAME = "l.sort:disp_code_ascending"	VALUE = "descrip_asc">
					<MvASSIGN NAME = "l.sort:disp_name_descending"	VALUE = "Description Descending">
					<MvASSIGN NAME = "l.sort:disp_code_descending"	VALUE = "descrip_desc">

					<MvASSIGN NAME = "l.sort_count"					VALUE = "{ miva_array_insert( l.sorts, l.sort, -1 ) }">
				</MvIF>

				<MvIF EXPR = "{ l.settings:sort_by:amount }">
					<MvASSIGN NAME = "l.sort:disp_name_ascending"	VALUE = "Amount Ascending">
					<MvASSIGN NAME = "l.sort:disp_code_ascending"	VALUE = "amount_asc">
					<MvASSIGN NAME = "l.sort:disp_name_descending"	VALUE = "Amount Descending">
					<MvASSIGN NAME = "l.sort:disp_code_descending"	VALUE = "amount_desc">

					<MvASSIGN NAME = "l.sort_count"					VALUE = "{ miva_array_insert( l.sorts, l.sort, -1 ) }">
				</MvIF>

				<MvIF EXPR = "{ l.settings:sort_by:dtstamp }">
					<MvASSIGN NAME = "l.sort:disp_name_ascending"	VALUE = "Date Ascending">
					<MvASSIGN NAME = "l.sort:disp_code_ascending"	VALUE = "dtstamp_asc">
					<MvASSIGN NAME = "l.sort:disp_name_descending"	VALUE = "Date Descending">
					<MvASSIGN NAME = "l.sort:disp_code_descending"	VALUE = "dtstamp_desc">

					<MvASSIGN NAME = "l.sort_count"					VALUE = "{ miva_array_insert( l.sorts, l.sort, -1 ) }">
				</MvIF>

				<MvFOREACH ITERATOR = "l.sort" ARRAY = "l.sorts" COUNT = "{ l.sort_count }">
					<MvIF EXPR = "{ NOT ISNULL l.sort:disp_name_ascending }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="{ 'g.Sort_By EQ \'' $ encodeentities( l.sort:disp_code_ascending ) $ '\'' }">
						<option value="{ encodeentities( l.sort:disp_code_ascending ) }" selected="selected"><MvEVAL EXPR = "{ encodeentities( l.sort:disp_name_ascending ) }"></option>
					<mvt:else>
						<option value="{ encodeentities( l.sort:disp_code_ascending ) }"><MvEVAL EXPR = "{ encodeentities( l.sort:disp_name_ascending ) }"></option>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
					</MvIF>
					
					<MvIF EXPR = "{ NOT ISNULL l.sort:disp_name_descending }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="{ 'g.Sort_By EQ \'' $ encodeentities( l.sort:disp_code_descending ) $ '\'' }">
						<option value="{ encodeentities( l.sort:disp_code_descending ) }" selected="selected"><MvEVAL EXPR = "{ encodeentities( l.sort:disp_name_descending ) }"></option>
					<mvt:else>
						<option value="{ encodeentities( l.sort:disp_code_descending ) }"><MvEVAL EXPR = "{ encodeentities( l.sort:disp_name_descending ) }"></option>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
					</MvIF>
				</MvFOREACH>
				
				<MIVA STANDARDOUTPUTLEVEL = "text, html">
				</select>
			</div>
		</mvt:if>
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
		
	<MvIF EXPR = "{ l.settings:items_per_page_filter }">
		<MvIF EXPR = "{ l.settings:per_page EQ 0 OR ISNULL l.settings:per_page }">
			<MvASSIGN NAME = "l.per_page" VALUE = 10>
		<MvELSE>
			<MvASSIGN NAME = "l.per_page" VALUE = "{ l.settings:per_page }">
		</MvIF>
		
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="l.settings:customercredithistory:total_count GT 1">
			<div class="per-page">
				<label for="Per_Page">View:</label>
				<select name="Per_Page" id="Per_Page" onchange="window.location.href='&mvtj:urls:_self:auto_sep;Per_Page=' + encodeURIComponent( this.options[ this.selectedIndex ].value ) + '&amp;Sort_By=' + encodeURIComponent( '&mvtj:global:Sort_By;' );"><MIVA STANDARDOUTPUTLEVEL = "">
				<MvASSIGN NAME = "l.multiplier" VALUE = 1>
				<MvASSIGN NAME = "l.count" VALUE = 1>
					
				<MvWHILE EXPR = "{ l.count LE 3 }">
					<MvASSIGN NAME = "l.items_per" VALUE = "{ l.per_page * l.multiplier }">
					<MvASSIGN NAME = "l.multiplier" VALUE = "{ l.multiplier + l.count }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="{ 'g.Per_Page EQ ' $ encodeentities( l.items_per ) }">
						<option value="{ encodeentities( l.items_per ) }" selected="selected"><MvEVAL EXPR = "{ encodeentities( l.items_per ) }"></option>
					<mvt:else>
						<option value="{ encodeentities( l.items_per ) }"><MvEVAL EXPR = "{ encodeentities( l.items_per ) }"></option>
					</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
					<MvASSIGN NAME = "l.count" VALUE = "{ l.count + 1 }">
				</MvWHILE>
					
					<MIVA STANDARDOUTPUTLEVEL = "text, html">
					<mvt:if expr="g.Per_Page EQ -1">
						<option value="-1" selected="selected">All</option>
					<mvt:else>
						<option value="-1">All</option>
					</mvt:if>
				</select>
			</div>
		</mvt:if>
<MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF>
		
	<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="l.settings:customercredithistory:page_links:last_page GT 1">
			<div class="page-links">
				<span class="page-links-title">Page(s):</span>
				<span class="page-links-container">
					<mvt:if expr="l.settings:customercredithistory:page_links:current_page NE 1">
						<a href="&mvte:urls:_self:auto_sep;&mvte:customercredithistory:page_links:prev_link_params;" class="page-links-previous">&lt;</a>
					<mvt:else>
						<span class="page-links-previous page-links-deactivated">&lt;</span>
					</mvt:if>
					<mvt:if expr="l.settings:customercredithistory:page_links:current_page NE l.settings:customercredithistory:page_links:last_page">
						<a href="&mvte:urls:_self:auto_sep;&mvte:customercredithistory:page_links:next_link_params;" class="page-links-next">&gt;</a>
					<mvt:else>
						<span class="page-links-next page-links-deactivated">&gt;</span>
					</mvt:if>
					<span class="page-disp">
						<mvt:if expr="NOT l.settings:customercredithistory:page_links:contains_first">
							<a href="&mvte:urls:_self:auto_sep;&mvte:customercredithistory:page_links:first_link_params;" class="page-links-inactive">1</a>...
						</mvt:if>
						<mvt:foreach iterator="pages" array="customercredithistory:page_links:pages">
							<mvt:if expr="l.settings:customercredithistory:page_links:current_page EQ l.settings:pages:page_num">
								<span class="page-links-active">&mvte:pages:page_num;</span>
							<mvt:else>
								<a href="&mvte:urls:_self:auto_sep;&mvte:pages:link_params;" class="page-links-inactive">&mvte:pages:page_num;</a>
							</mvt:if>
						</mvt:foreach>
						<mvt:if expr="NOT l.settings:customercredithistory:page_links:contains_last">
							...<a href="&mvte:urls:_self:auto_sep;&mvte:customercredithistory:page_links:last_link_params;" class="page-links-inactive"><MvEVAL EXPR = "&mvte:customercredithistory:page_links:last_page;"></a>
						</mvt:if>
					</span>
				</span>
			</div>
		</mvt:if>
	</div>
	
	<div class="clear"></div>

	<table>
		<tr class="heading"><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ l.settings:field_orderid }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="orderid">Order #</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:field_txref }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="txref">Transaction Reference</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:field_descrip }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="descrip">Description</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:field_dtstamp }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="dtstamp">Date</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

		<MvIF EXPR = "{ l.settings:field_amount }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="amount">Amount</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

			<MIVA STANDARDOUTPUTLEVEL = "text, html">
		</tr>

		<mvt:foreach iterator="credit_entry" array="customercredithistory:items">
			<mvt:if expr = "l.pos1 MOD 2">
				<tr class="alt_row">
			<mvt:else>
				<tr>
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">

			<MvIF EXPR = "{ l.settings:field_orderid }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<td class="orderid">
					<mvt:if expr="l.settings:credit_entry:order_id EQ 0">
						N/A
					<mvt:else>
						&mvte:credit_entry:order_id;
					</mvt:if>
				</td><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>

			<MvIF EXPR = "{ l.settings:field_txref }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<td class="txref">&mvte:credit_entry:txref;</td><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>

			<MvIF EXPR = "{ l.settings:field_descrip }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<td class="descrip">&mvte:credit_entry:descrip;</td><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>

			<MvIF EXPR = "{ l.settings:field_dtstamp }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<td class="dtstamp">&mvt:credit_entry:formatted_dtstamp;</td><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>

			<MvIF EXPR = "{ l.settings:field_amount }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<td class="amount">&mvt:credit_entry:formatted_amount;</td><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>

			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			</tr>
		</mvt:foreach>
	</table>

	<mvt:if expr="NOT l.settings:customercredithistory:page_disp_count GT 0">
		<mvt:if expr="( g.Offset OR g.NextOffset ) AND ( l.settings:customercredithistory:per_page NE 0 )">
			<table>
				<tr>
					<td class="previous-button">
						<mvt:if expr="g.Offset EQ 0">
							&nbsp;
						<mvt:else>
							<form method="post" action="&mvte:urls:_self:auto;" />
								<input type="hidden" name="SortBy" value="&mvte:global:SortBy;" />
								<input type="hidden" name="Offset" value="&mvte:global:PrevOffset;" />
								<input type="hidden" name="Per_Page" value="&mvte:global:Per_Page;" />
								<mvt:item name="buttons" param="Previous" />
							</form>
						</mvt:if>
					</td>
					<td class="next-button">
						<mvt:if expr="g.NextOffset EQ 0">
							&nbsp;
						<mvt:else>
							<form method="post" action="&mvte:urls:_self:auto;">
								<input type="hidden" name="SortBy" value="&mvte:global:SortBy;" />
								<input type="hidden" name="Offset" value="&mvte:global:NextOffset;" />
								<input type="hidden" name="Per_Page" value="&mvte:global:Per_Page;" />
								<mvt:item name="buttons" param="Next" />
							</form>
						</mvt:if>
					</td>
				</tr>
			</table>
		</mvt:if>
	</mvt:if>
</div>
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update_Requires_Version" PARAMETERS = "module var, page var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_PostUpdate" PARAMETERS = "module var, page var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>


<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-BREAD-
| Next Error Code: 27   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-breadcrumbs">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI Smart Breadcrumbs">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0800">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.60">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, data_store, vis_store, provision_store, skins">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'cssui-breadcrumbs.mvc', l.null ) }">
		<MvEVAL EXPR = "{ Generate_Breadcrumbs_Code( l.code ) }">

		<MvASSIGN NAME = "l.settings:max_cat_name_length"	VALUE = 0>
		<MvASSIGN NAME = "l.settings:max_prod_name_length"	VALUE = 0>
		<MvASSIGN NAME = "l.settings:max_lev_to_disp"		VALUE = 0>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.code, l.settings, 'cssui-breadcrumbs.mvc' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.module:version EQ l.version }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CSSUI-CMP-BREAD-00026', 'Module \'' $ l.module:name $ '\' does not support manual upgrade.  New versions may only be obtained through the streaming update system.' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_GlobalDelete_ManagedTemplate_Filename( 'cssui-breadcrumbs.mvc' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'cssui-breadcrumbs.mvc', l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "g.CSSUI_BREAD:max_cat_name_length"	VALUE = "{ trim( g.CSSUI_BREAD:max_cat_name_length ) }">
	<MvASSIGN NAME = "g.CSSUI_BREAD:max_prod_name_length"	VALUE = "{ trim( g.CSSUI_BREAD:max_prod_name_length ) }">
	<MvASSIGN NAME = "g.CSSUI_BREAD:max_lev_to_disp"		VALUE = "{ trim( g.CSSUI_BREAD:max_lev_to_disp ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( g.CSSUI_BREAD:max_cat_name_length ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-CSSUI-BREADCRUMBS', 'CSSUI_BREAD:max_cat_name_length', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( g.CSSUI_BREAD:max_prod_name_length ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-CSSUI-BREADCRUMBS', 'CSSUI_BREAD:max_prod_name_length', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( g.CSSUI_BREAD:max_lev_to_disp ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-CSSUI-BREADCRUMBS', 'CSSUI_BREAD:max_lev_to_disp', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( g.CSSUI_BREAD:template_code ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-CSSUI-BREADCRUMBS', 'CSSUI_BREAD:template_code', g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( 'cssui-breadcrumbs.mvc', l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.settings:max_cat_name_length"	VALUE = "{ g.CSSUI_BREAD:max_cat_name_length }">
	<MvASSIGN NAME = "l.settings:max_prod_name_length"	VALUE = "{ g.CSSUI_BREAD:max_prod_name_length }">
	<MvASSIGN NAME = "l.settings:max_lev_to_disp"		VALUE = "{ g.CSSUI_BREAD:max_lev_to_disp }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, '', g.CSSUI_BREAD:template_code, l.settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-CSSUI-BREADCRUMBS', 'CSSUI_BREAD:template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Tabs" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "GT_UISETTINGS/CMP-CSSUI-BREADCRUMBS:Smart Breadcrumbs">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( 'cssui-breadcrumbs.mvc', l.templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.load_fields }">
		<MvASSIGN NAME = "g.CSSUI_BREAD:max_cat_name_length"	VALUE = "{ l.templateversion:settings:max_cat_name_length }">
		<MvASSIGN NAME = "g.CSSUI_BREAD:max_prod_name_length"	VALUE = "{ l.templateversion:settings:max_prod_name_length }">
		<MvASSIGN NAME = "g.CSSUI_BREAD:max_lev_to_disp"		VALUE = "{ l.templateversion:settings:max_lev_to_disp }">
		<MvASSIGN NAME = "g.CSSUI_BREAD:template_code"			VALUE = "{ l.templateversion:source }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( g.Tab, 'CMP-CSSUI-BREADCRUMBS' ) }">
		<MvHIDE FIELDS = "g.CSSUI_Bread">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_UISETTINGS', 'CMP-CSSUI-BREADCRUMBS' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<tr><td valign="top" nowrap>
			Smart Breadcrumbs:
		</td><td width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( 'CSSUI_BREAD:template_code', g.CSSUI_BREAD:template_code, 10, 58, l.templateversion:templ_id ) }">
		</td></tr>

		<tr><td valign="top" nowrap>
			Max Category Name Length:
		</td><td width="100%">
			<input type="text" size="3" name="CSSUI_BREAD:max_cat_name_length" value="{ encodeentities( g.CSSUI_BREAD:max_cat_name_length ) }"> (0 for unlimited)
		</td></tr>

		<tr><td valign="top" nowrap>
			Max Product Name Length:
		</td><td width="100%">
			<input type="text" size="3" name="CSSUI_BREAD:max_prod_name_length" value="{ encodeentities( g.CSSUI_BREAD:max_prod_name_length ) }"> (0 for unlimited)
		</td></tr>

		<tr><td valign="top" nowrap>
			Max Number of Link Levels to Display:
		</td><td width="100%">
			<input type="text" size="3" name="CSSUI_BREAD:max_lev_to_disp" value="{ encodeentities( g.CSSUI_BREAD:max_lev_to_disp ) }"> (0 for unlimited)
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Breadcrumbs_Code" PARAMETERS = "code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code">
	<MIVA STANDARDOUTPUTLEVEL = "text,html"><ul>
	<li><a href="&mvte:breadcrumbs:homelink;" class="breadcrumb-home">Home</a><span class="breadcrumb-border">&raquo;</span></li>
	<mvt:foreach iterator="cats" array="breadcrumbs:links">
			<mvt:if expr="l.settings:cats:name EQ '...'">
				<li><span class="cats-hidden">&mvt:cats:name;</span><span class="breadcrumb-border">&raquo;</span></li>
			<mvt:elseif expr="l.settings:cats:current">
				<li><span class="current-item">&mvt:cats:name;</span></li>
			<mvt:else>
				<li><a href="&mvte:cats:link;">&mvt:cats:name;</a><span class="breadcrumb-border">&raquo;</span></li>
			</mvt:if>
	</mvt:foreach>
</ul>
<div class="clear"></div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.all_settings:page:code EQ 'SFNT' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( 'cssui-breadcrumbs.mvc', l.managedtemplateversion ) }">
		<MvASSIGN NAME = "l.breadcrumbs"						VALUE = "{ l.managedtemplateversion:settings }">
	<MvELSE>
		<MvASSIGN NAME = "l.breadcrumbs:max_cat_name_length"	VALUE = 0>
		<MvASSIGN NAME = "l.breadcrumbs:max_prod_name_length"	VALUE = 0>
		<MvASSIGN NAME = "l.breadcrumbs:max_lev_to_disp"		VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code_Cached( 'SFNT', l.page ) }">
		<MvASSIGN NAME = "l.settings:homelink"					VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Page_URL( l.page, l.null ) }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Initialize_Item( l.all_settings, 'category' ) }">
		<MvIF EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Initialize_Item( l.all_settings, 'category_tree' ) }">
			<MvASSIGN NAME = "l.ok" VALUE = "{ Breadcrumbs_Generate_From_Cattree( l.all_settings, l.settings, l.breadcrumbs, l.array ) }">
		<MvELSE>
			<MvASSIGN NAME = "l.ok" VALUE = "{ Breadcrumbs_Initialize( l.all_settings, l.settings, l.breadcrumbs, l.array ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ( l.all_settings:page:code EQ 'PROD' ) OR ( NOT ISNULL l.all_settings:product:code ) }">
		<MvASSIGN NAME = "l.ok"		VALUE = "{ Product_Initialize( l.all_settings, l.settings, l.breadcrumbs, l.array ) }">
	</MvIF>

	<MvIF EXPR = "{ ( l.all_settings:page:code EQ 'ACED' ) OR
					( l.all_settings:page:code EQ 'ABAL' ) OR
					( l.all_settings:page:code EQ 'RGFT' ) OR
					( l.all_settings:page:code EQ 'CADA' ) OR
					( l.all_settings:page:code EQ 'CADE' ) OR
					( l.all_settings:page:code EQ 'CABK' ) OR
					( l.all_settings:page:code EQ 'CPCD' ) OR
					( l.all_settings:page:code EQ 'CPCA' ) OR
					( l.all_settings:page:code EQ 'CPCE' ) OR
					( l.all_settings:page:code EQ 'CEML' ) OR
					( l.all_settings:page:code EQ 'CPWD' ) OR
					( l.all_settings:page:code EQ 'CSBE' ) OR
					( l.all_settings:page:code EQ 'CSUB' ) OR
				    ( g.Basket:cust_id AND
					  (
						( l.all_settings:page:code EQ 'ORDH' ) OR
						( l.all_settings:page:code EQ 'ORDS' )
					  )
					) }">
		<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code_Cached( 'ACLN', l.page ) }">
			<MvASSIGN NAME = "l.index"											VALUE = "{ miva_array_elements( l.array ) + 1 }">
			<MvASSIGN NAME = "l.array" MEMBER = "code" INDEX = "{ l.index }"	VALUE = "{ l.page:code }">
			<MvASSIGN NAME = "l.array" MEMBER = "name" INDEX = "{ l.index }"	VALUE = "{ l.page:name }">
			<MvASSIGN NAME = "l.array" MEMBER = "link" INDEX = "{ l.index }"	VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Page_URL( l.page, l.null ) }">
			<MvASSIGN NAME = "l.index"											VALUE = "{ l.index + 1 }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ( l.all_settings:page:code EQ 'CADA' ) OR
					( l.all_settings:page:code EQ 'CADE' ) }">
		<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code_Cached( 'CABK', l.page ) }">
			<MvASSIGN NAME = "l.index"											VALUE = "{ miva_array_elements( l.array ) + 1 }">
			<MvASSIGN NAME = "l.array" MEMBER = "code" INDEX = "{ l.index }"	VALUE = "{ l.page:code }">
			<MvASSIGN NAME = "l.array" MEMBER = "name" INDEX = "{ l.index }"	VALUE = "{ l.page:name }">
			<MvASSIGN NAME = "l.array" MEMBER = "link" INDEX = "{ l.index }"	VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Page_URL( l.page, l.null ) }">
			<MvASSIGN NAME = "l.index"											VALUE = "{ l.index + 1 }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ( l.all_settings:page:code EQ 'CPCA' ) OR
					( l.all_settings:page:code EQ 'CPCE' ) }">
		<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code_Cached( 'CPCD', l.page ) }">
			<MvASSIGN NAME = "l.index"											VALUE = "{ miva_array_elements( l.array ) + 1 }">
			<MvASSIGN NAME = "l.array" MEMBER = "code" INDEX = "{ l.index }"	VALUE = "{ l.page:code }">
			<MvASSIGN NAME = "l.array" MEMBER = "name" INDEX = "{ l.index }"	VALUE = "{ l.page:name }">
			<MvASSIGN NAME = "l.array" MEMBER = "link" INDEX = "{ l.index }"	VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Page_URL( l.page, l.null ) }">
			<MvASSIGN NAME = "l.index"											VALUE = "{ l.index + 1 }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ( l.all_settings:page:code EQ 'CSBE' ) }">
		<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code_Cached( 'CSUB', l.page ) }">
			<MvASSIGN NAME = "l.index"											VALUE = "{ miva_array_elements( l.array ) + 1 }">
			<MvASSIGN NAME = "l.array" MEMBER = "code" INDEX = "{ l.index }"	VALUE = "{ l.page:code }">
			<MvASSIGN NAME = "l.array" MEMBER = "name" INDEX = "{ l.index }"	VALUE = "{ l.page:name }">
			<MvASSIGN NAME = "l.array" MEMBER = "link" INDEX = "{ l.index }"	VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Page_URL( l.page, l.null ) }">
			<MvASSIGN NAME = "l.index"											VALUE = "{ l.index + 1 }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ( ISNULL l.settings:current_item:code ) OR ( l.all_settings:page:code EQ 'BASK' ) }">
		<MvASSIGN NAME = "l.settings:current_item:code"	VALUE = "{ l.all_settings:page:code }">
		<MvASSIGN NAME = "l.settings:current_item:name"	VALUE = "{ l.all_settings:page:name }">
		<MvASSIGN NAME = "l.settings:current_item:link"	VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Page_URL( l.all_settings:page, l.null ) }">
		<MvASSIGN NAME = "l.settings:current_item:type"	VALUE = "page">

		<MvASSIGN NAME = "l.index"												VALUE = "{ miva_array_elements( l.array ) + 1 }">
		<MvASSIGN NAME = "l.array" MEMBER = "code" INDEX = "{ l.index }"		VALUE = "{ l.all_settings:page:code }">
		<MvASSIGN NAME = "l.array" MEMBER = "name" INDEX = "{ l.index }"		VALUE = "{ l.all_settings:page:name }">
		<MvASSIGN NAME = "l.array" MEMBER = "link" INDEX = "{ l.index }"		VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Page_URL( l.all_settings:page, l.null ) }">
	</MvIF>

	<MvASSIGN NAME = "l.array_count" VALUE = "{ miva_array_collapse( l.array ) }">

	<MvIF EXPR = "{ ( l.array_count GT l.breadcrumbs:max_lev_to_disp ) AND ( NOT l.breadcrumbs:max_lev_to_disp LE 0 ) }">
		<MvASSIGN NAME = "l.settings:links"	MEMBER = "code" INDEX = 2 VALUE = "...">
		<MvASSIGN NAME = "l.settings:links"	MEMBER = "name" INDEX = 2 VALUE = "...">
		<MvASSIGN NAME = "l.settings:links"	MEMBER = "link" INDEX = 2 VALUE = "">

		<MvASSIGN NAME = "l.size"	VALUE = "{ l.array_count + 1 }">
		<MvASSIGN NAME = "l.first"	VALUE = "{ l.size - l.breadcrumbs:max_lev_to_disp }">
		<MvASSIGN NAME = "l.last"	VALUE = "{ l.size - 1 }">
	<MvELSE>
		<MvASSIGN NAME = "l.size"	VALUE = "{ l.array_count + 1 }">
		<MvASSIGN NAME = "l.first"	VALUE = "{ 1 }">
		<MvASSIGN NAME = "l.last"	VALUE = "{ l.size - 1 }">
	</MvIF>

	<MvFOREACH 	ITERATOR = "l.item"
				ARRAY 	 = "l.array"
				INDEX 	 = "l.pos"
				FIRST 	 = "{ l.first }"
       			NEXT 	 = "{ l.pos + 1 }"
       			LAST 	 = "{ l.last }">
		<MvIF EXPR = "{ NOT ISNULL l.item:code }">
			<MvASSIGN NAME = "l.settings:links" MEMBER = "code" 	INDEX = "{ l.pos + 1 }" VALUE = "{ l.item:code }">
			<MvASSIGN NAME = "l.settings:links" MEMBER = "name" 	INDEX = "{ l.pos + 1 }" VALUE = "{ l.item:name }">
			<MvASSIGN NAME = "l.settings:links" MEMBER = "link" 	INDEX = "{ l.pos + 1 }" VALUE = "{ l.item:link }">
			<MvASSIGN NAME = "l.settings:links" MEMBER = "current" 	INDEX = "{ l.pos + 1 }" VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvASSIGN NAME = "l.last_pos"															VALUE = "{ miva_array_collapse( l.settings:links ) }">
	<MvASSIGN NAME = "l.settings:links" MEMBER = "current" INDEX = "{ l.last_pos }"			VALUE = 1>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( 'cssui-breadcrumbs.mvc', l.all_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Store" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Filename( 'cssui-breadcrumbs.mvc', l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings" VALUE = "{ l.managedtemplate:version:settings }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'o', l.provide_xml, 'MaxCatNameLength',		l.settings:max_cat_name_length ) 	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'o', l.provide_xml, 'MaxProdNameLength',	l.settings:max_prod_name_length ) 	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'o', l.provide_xml, 'MaxCatDisplay',		l.settings:max_lev_to_disp ) 		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml, 'Template',				l.source ) 							OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'O', l.provide_xml, 'Notes',				l.notes ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( l.settings:max_cat_name_length  ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Value set for MaxCatNameLength was invalid.' ) }">
		<MvASSIGN NAME = "l.settings:max_cat_name_length" VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( l.settings:max_prod_name_length ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Value set for MaxProdNameLength was invalid.' ) }">
		<MvASSIGN NAME = "l.settings:max_prod_name_length" VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( l.settings:max_lev_to_disp ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Value set for MaxCatDisplay was invalid.' ) }">
		<MvASSIGN NAME = "l.settings:max_lev_to_disp" VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) }">
		<MvASSIGN NAME = "l.source" VALUE = "{ l.managedtemplate:version:source }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion_LowLevel( l.managedtemplate, l.managedtemplate:version, l.notes, l.source, l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "Exports Smart Breadcrumbs (Global Code)">
</MvFUNCTION>

<MvFUNCTION NAME ="SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( 'cssui-breadcrumbs.mvc', l.managedtemplateversion ) }">
		<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
			<Module code="{ encodeentities( l.module:code ) }" feature="component">
				<MaxCatNameLength><MvEVAL EXPR = "{ int( l.managedtemplateversion:settings:max_cat_name_length ) }"></MaxCatNameLength>
				<MaxProdNameLength><MvEVAL EXPR = "{ int( l.managedtemplateversion:settings:max_prod_name_length ) }"></MaxProdNameLength>
				<MaxCatDisplay><MvEVAL EXPR = "{ int( l.managedtemplateversion:settings:max_lev_to_disp ) }"></MaxCatDisplay>
				<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.managedtemplateversion:source ) }"></Template>
				<Notes>#Set_Current_Time#</Notes>
			</Module>
		</MvCAPTURE>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryList_Load_Recursive" PARAMETERS = "parent_category var, category_list var, category_count" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.output_count" VALUE = 0>

	<MvFOREACH ITERATOR = "l.sibling" ARRAY = "l.parent_siblings" COUNT = "{ [ g.Module_Library_DB ].Runtime_CategoryList_Load_Parent( l.parent_category:parent_id, l.parent_siblings ) }">
		<MvASSIGN NAME = "l.output_count" VALUE = "{ l.output_count + 1 }">
		<MvASSIGN NAME = "l.output_array" INDEX = "{ l.output_count }" VALUE = "{ l.sibling }">
		<MvASSIGN NAME = "l.output_array" INDEX = "{ l.output_count }" MEMBER = "level" VALUE = 1>

		<MvIF EXPR = "{ l.sibling:id EQ l.parent_category:id }">
			<MvFOREACH ITERATOR = "l.category" ARRAY = "l.category_list" COUNT = "{ l.category_count }">
				<MvASSIGN NAME = "l.output_count" VALUE = "{ l.output_count + 1 }">
				<MvASSIGN NAME = "l.output_array" INDEX = "{ l.output_count }" VALUE = "{ l.category }">
				<MvASSIGN NAME = "l.output_array" INDEX = "{ l.output_count }" MEMBER = "level" VALUE = "{ l.category:level + 1 }">
			</MvFOREACH>
		</MvIF>
	</MvFOREACH>

	<MvIF EXPR = "{ l.parent_category:parent_id EQ 0 }">
		<MvASSIGN NAME = "l.parent_category:id" VALUE = 0>
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_Category_Load_ID_Cached( l.parent_category:parent_id, l.parent_category ) }">
		<MvASSIGN NAME = "l.parent_category:id" VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.category_list" VALUE = "{ l.output_array }">
	<MvFUNCTIONRETURN VALUE = "{ l.output_count }">
</MvFUNCTION>

<MvFUNCTION NAME = "Breadcrumb_Array_Generator" PARAMETERS = "counter_cat var, category var, all_settings var, breadcrumbs var, cat_array var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.breadcrumbs:max_cat_name_length GT 0 AND len_var( l.category:name ) GT l.breadcrumbs:max_cat_name_length }">
		<MvASSIGN NAME = "l.cat_array"	MEMBER = "name" INDEX = "{ l.counter_cat }" VALUE = "{ substring( l.category:name, 1, l.breadcrumbs:max_cat_name_length ) $ '...' }">
	<MvELSE>
		<MvASSIGN NAME = "l.cat_array"	MEMBER = "name" INDEX = "{ l.counter_cat }" VALUE = "{ l.category:name }">
	</MvIF>

	<MvASSIGN NAME = "l.cat_array"		MEMBER = "code" INDEX = "{ l.counter_cat }" VALUE = "{ l.category:code }">
	<MvASSIGN NAME = "l.cat_array" 		MEMBER = "link" INDEX = "{ l.counter_cat }" VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Category_URL( l.category, l.null ) }">
	<MvASSIGN NAME = "l.counter_cat" VALUE = "{ l.counter_cat + 1 }">
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryList_Load_PathToRoot" PARAMETERS = "category var, category_list var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.category_count"									VALUE = 1>
    <MvASSIGN NAME = "l.category_list" INDEX = "{ l.category_count }"	VALUE = "{ l.category }">

	<MvWHILE EXPR = "{ l.category:parent_id GT 0 }">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].Runtime_Category_Load_ID_Cached( l.category:parent_id, l.category ) }">
			<MvASSIGN NAME = "l.category_count"									VALUE = "{ l.category_count + 1 }">
			<MvASSIGN NAME = "l.category_list" INDEX = "{ l.category_count }"	VALUE = "{ l.category }">
		<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Category_Load_ID( l.category:parent_id, l.category ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = "{ l.category_count }">
</MvFUNCTION>

<MvFUNCTION NAME = "Breadcrumbs_Initialize" PARAMETERS = "all_settings var, settings var, breadcrumbs, array var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.breadcrumbs:max_lev_to_disp EQ 0 }">
		<MvASSIGN NAME = "l.breadcrumbs:max_lev_to_disp" VALUE = -1>
	</MvIF>

	<MvASSIGN NAME = "l.category" VALUE = "{ l.all_settings:category }">

	<MvIF EXPR = "{ NOT ISNULL l.category:id }">
		<MvASSIGN NAME = "l.category_pathtoroot_count"		VALUE = "{ CategoryList_Load_PathToRoot( l.category, l.category_pathtoroot_list ) }">

		<MvIF EXPR = "{ l.breadcrumbs:max_cat_name_length GT 0 AND len_var( l.category:name ) GT l.breadcrumbs:max_cat_name_length }">
			<MvASSIGN NAME = "l.settings:current_item:name" VALUE = "{ substring( l.category:name, 1, l.breadcrumbs:max_cat_name_length ) $ '...' }">
		<MvELSE>
			<MvASSIGN NAME = "l.settings:current_item:name" VALUE = "{ l.category:name }">
		</MvIF>

		<MvASSIGN NAME = "l.settings:current_item:code"		VALUE = "{ l.category:code }">
		<MvASSIGN NAME = "l.settings:current_item:link"		VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Category_URL( l.category, l.null ) }">
		<MvASSIGN NAME = "l.settings:current_item:type"		VALUE = "cat">
		<MvASSIGN NAME = "l.counter_cat"					VALUE = 1>

        <MvFOREACH ITERATOR = "l.category_element" ARRAY = "l.category_pathtoroot_list">
			<MvIF EXPR = "{ l.breadcrumbs:max_lev_to_disp LT 0 OR l.breadcrumbs:max_lev_to_disp GT 0 }">
				<MvEVAL EXPR = "{ Breadcrumb_Array_Generator( l.counter_cat, l.category_element, l.all_settings, l.breadcrumbs, l.cat_array ) }">
			</MvIF>
		</MvFOREACH>

		<MvASSIGN NAME = "l.null"		VALUE = "{ miva_array_collapse( l.cat_array ) }">
		<MvASSIGN NAME = "l.cat_count"  VALUE = "{ miva_array_elements( l.cat_array ) + 1 }">

		<MvIF EXPR = "{ NOT ISNULL l.cat_array[1]:code }">
			<MvFOREACH 	ITERATOR 	 = "l.cat_item"
						ARRAY 		 = "l.cat_array"
						INDEX 		 = "l.pos">
				<MvASSIGN NAME = "l.array" 	MEMBER = "code" 	INDEX = "{ l.cat_count - l.pos + 1 }"  VALUE = "{ l.cat_item:code }">
				<MvASSIGN NAME = "l.array" 	MEMBER = "name" 	INDEX = "{ l.cat_count - l.pos + 1 }"  VALUE = "{ l.cat_item:name }">
				<MvASSIGN NAME = "l.array" 	MEMBER = "link" 	INDEX = "{ l.cat_count - l.pos + 1 }"  VALUE = "{ l.cat_item:link }">
			</MvFOREACH>

			<MvASSIGN NAME = "l.ok" VALUE = "{ miva_array_collapse( l.array ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Breadcrumbs_Generate_From_Cattree" PARAMETERS = "all_settings var, settings var, breadcrumbs, array var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.category" VALUE = "{ l.all_settings:category }">

	<MvIF EXPR = "{ NOT ISNULL l.category:code }">
		<MvIF EXPR = "{ l.breadcrumbs:max_lev_to_disp EQ 0 }">
			<MvASSIGN NAME = "l.breadcrumbs:max_lev_to_disp" VALUE = -1>
		</MvIF>

		<MvASSIGN NAME = "l.settings:current_item:code"  VALUE = "{ l.category:code }">

		<MvIF EXPR = "{ l.breadcrumbs:max_cat_name_length GT 0 AND len( l.category:name ) GT l.breadcrumbs:max_cat_name_length }">
			<MvASSIGN NAME = "l.settings:current_item:name"  VALUE = "{ substring( l.category:name, 1, l.breadcrumbs:max_cat_name_length ) $ '...' }">
		<MvELSE>
			<MvASSIGN NAME = "l.settings:current_item:name"  VALUE = "{ l.category:name }">
		</MvIF>

		<MvASSIGN NAME = "l.settings:current_item:link"		VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Category_URL( l.category, l.null ) }">
		<MvASSIGN NAME = "l.settings:current_item:type"		VALUE = "cat">

		<MvASSIGN NAME = "l.counter_cat"	VALUE = 1>

		<MvFOREACH ITERATOR = "l.cat" ARRAY = "l.all_settings:cattree_categories">
			<MvIF EXPR = "{ NOT ISNULL l.cat:cattree_parent_id AND l.cat:id NE l.category:id }">
				<MvEVAL EXPR = "{ Breadcrumb_Array_Generator( l.counter_cat, l.cat, l.all_settings, l.breadcrumbs, l.array ) }">
			</MvIF>
		</MvFOREACH>

		<MvASSIGN NAME = "l.index"											VALUE = "{ miva_array_elements( l.array ) + 1 }">
		<MvASSIGN NAME = "l.array" MEMBER = "code" INDEX = "{ l.index }"	VALUE = "{ l.settings:current_item:code }">
		<MvASSIGN NAME = "l.array" MEMBER = "link" INDEX = "{ l.index }"	VALUE = "{ l.settings:current_item:link }">

		<MvIF EXPR = "{ l.breadcrumbs:max_cat_name_length GT 0 AND len( l.category:name ) GT l.breadcrumbs:max_cat_name_length }">
			<MvASSIGN NAME = "l.array" MEMBER = "name" INDEX = "{ l.index }" VALUE = "{ substring( l.settings:current_item:name, 1, l.breadcrumbs:max_cat_name_length ) $ '...' }">
		<MvELSE>
			<MvASSIGN NAME = "l.array" MEMBER = "name" INDEX = "{ l.index }" VALUE = "{ l.settings:current_item:name }">
		</MvIF>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Initialize" PARAMETERS = "all_settings var, settings var, breadcrumbs, array var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Initialize_Item( l.all_settings, 'product' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.pos"  VALUE = "{ miva_array_elements( l.array ) }">
	<MvASSIGN NAME = "l.array" MEMBER = "code" INDEX = "{ l.pos + 1 }" VALUE = "{ l.all_settings:product:code }">
	<MvASSIGN NAME = "l.array" MEMBER = "link" INDEX = "{ l.pos + 1 }" VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Product_URL( l.all_settings:product, l.null ) }">

	<MvIF EXPR = "{ l.breadcrumbs:max_prod_name_length GT 0 AND len( l.all_settings:product:name ) GT l.breadcrumbs:max_prod_name_length }">
		<MvASSIGN NAME = "l.array" MEMBER = "name" INDEX = "{ l.pos + 1 }" VALUE = "{ substring( l.all_settings:product:name, 1, l.breadcrumbs:max_prod_name_length ) $ '...' }">
	<MvELSE>
		<MvASSIGN NAME = "l.array" MEMBER = "name" INDEX = "{ l.pos + 1 }" VALUE = "{ l.all_settings:product:name }">
	</MvIF>

	<MvIF EXPR = "{ l.breadcrumbs:max_prod_name_length GT 0 AND len( l.all_settings:product:name ) GT l.breadcrumbs:max_prod_name_length }">
		<MvASSIGN NAME = "l.settings:current_item:name" VALUE = "{ substring( l.all_settings:product:name, 1, l.breadcrumbs:max_prod_name_length ) $ '...' }">
	<MvELSE>
		<MvASSIGN NAME = "l.settings:current_item:name" VALUE = "{ l.all_settings:product:name }">
	</MvIF>

	<MvASSIGN NAME = "l.settings:current_item:code"  VALUE = "{ l.all_settings:product:code }">
	<MvASSIGN NAME = "l.settings:current_item:link"  VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Product_URL( l.all_settings:product, l.null ) }">
	<MvASSIGN NAME = "l.settings:current_item:type"  VALUE = "prod">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvINCLUDE FILE = "modules/component/seo_include.mv">

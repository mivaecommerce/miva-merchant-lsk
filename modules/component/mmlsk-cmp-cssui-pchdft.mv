<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-PCHDFT-
| Next Error Code: 72   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-pchdft">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI Product/Category Header & Footer">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0800">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.72">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, vis_product, vis_category, provision_store, skins, fields_prod, fields_cat, copy_prod">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: vis_category
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Category_Head" PARAMETERS = "module var, tab, category var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Category_Tabs" PARAMETERS = "module var, category var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "GT_CTGY/CMP-CSSUI-CATHDFT:Header & Footer">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Category_Content" PARAMETERS = "module var, tab, load_fields, category var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.category:id }">
		<MvIF EXPR = "{ l.load_fields }">
			<MvASSIGN NAME = "g.CSSUI_Category_HDFT"						VALUE = "">

			<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current_Category( 'header', l.category:id, l.header_template ) }">
				<MvASSIGN NAME = "g.CSSUI_Category_HDFT:header_template_id"	VALUE = "{ l.header_template:templ_id }">
				<MvASSIGN NAME = "g.CSSUI_Category_HDFT:header"				VALUE = "{ l.header_template:source }">
			</MvIF>

			<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current_Category( 'footer', l.category:id, l.footer_template ) }">
				<MvASSIGN NAME = "g.CSSUI_Category_HDFT:footer_template_id"	VALUE = "{ l.footer_template:templ_id }">
				<MvASSIGN NAME = "g.CSSUI_Category_HDFT:footer"				VALUE = "{ l.footer_template:source }">
			</MvIF>
		</MvIF>
	<MvELSE>
		<MvIF EXPR = "{ l.load_fields }">
			<MvASSIGN NAME = "g.CSSUI_Category_HDFT:header"					VALUE = "">
			<MvASSIGN NAME = "g.CSSUI_Category_HDFT:footer"					VALUE = "">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, 'CMP-CSSUI-CATHDFT' ) }">
		<MvHIDE FIELDS = "g.CSSUI_Category_HDFT">
	<MvELSE>
		<MvHIDE FIELDS = "g.CSSUI_Category_HDFT:header_template_id, g.CSSUI_Category_HDFT:footer_template_id">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_CTGY', 'CMP-CSSUI-CATHDFT' ) }">
		<table border=0 cellpadding=2 cellspacing = 0 width = "100%">
		<tr><td valign="top" nowrap>
			Header:
		</td><td width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( 'CSSUI_Category_HDFT:header', g.CSSUI_Category_HDFT:header, 10, 58, g.CSSUI_Category_HDFT:header_template_id ) }">
		</td></tr>

		<tr><td valign="top" nowrap>
			Footer:
		</td><td width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( 'CSSUI_Category_HDFT:footer', g.CSSUI_Category_HDFT:footer, 10, 58, g.CSSUI_Category_HDFT:footer_template_id ) }">
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Category_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( g.CSSUI_Category_HDFT:header ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-CSSUI-CATHDFT', 'CSSUI_Category_HDFT:header', g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( g.CSSUI_Category_HDFT:footer ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-CSSUI-CATHDFT', 'CSSUI_Category_HDFT:footer', g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Category_Insert" PARAMETERS = "module var, category var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ Category_HDFT_InsertOrUpdate( 'header', l.category:id, g.CSSUI_Category_HDFT:header ) AND
								 Category_HDFT_InsertOrUpdate( 'footer', l.category:id, g.CSSUI_Category_HDFT:footer ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Category_Update" PARAMETERS = "module var, category var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ Category_HDFT_InsertOrUpdate( 'header', l.category:id, g.CSSUI_Category_HDFT:header ) AND
								 Category_HDFT_InsertOrUpdate( 'footer', l.category:id, g.CSSUI_Category_HDFT:footer ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Category_Delete" PARAMETERS = "module var, category var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: vis_product
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Product_Head" PARAMETERS = "module var, tab, product var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Tabs" PARAMETERS = "module var, product var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "GT_PROD/CMP-CSSUI-PRDHDFT:Header & Footer">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Content" PARAMETERS = "module var, tab, load_fields, product var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.product:id }">
		<MvIF EXPR = "{ l.load_fields }">
			<MvASSIGN NAME = "g.CSSUI_Product_HDFT"							VALUE = "">

			<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current_Product( 'header', l.product:id, l.header_template ) }">
				<MvASSIGN NAME = "g.CSSUI_Product_HDFT:header_template_id"	VALUE = "{ l.header_template:templ_id }">
				<MvASSIGN NAME = "g.CSSUI_Product_HDFT:header"				VALUE = "{ l.header_template:source }">
			</MvIF>

			<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current_Product( 'footer', l.product:id, l.footer_template ) }">
				<MvASSIGN NAME = "g.CSSUI_Product_HDFT:footer_template_id"	VALUE = "{ l.footer_template:templ_id }">
				<MvASSIGN NAME = "g.CSSUI_Product_HDFT:footer"				VALUE = "{ l.footer_template:source }">
			</MvIF>
		</MvIF>
	<MvELSE>
		<MvIF EXPR = "{ l.load_fields }">
			<MvASSIGN NAME = "g.CSSUI_Product_HDFT:header"					VALUE = "">
			<MvASSIGN NAME = "g.CSSUI_Product_HDFT:footer"					VALUE = "">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, 'CMP-CSSUI-PRDHDFT' ) }">
		<MvHIDE FIELDS = "g.CSSUI_Product_HDFT">
	<MvELSE>
		<MvHIDE FIELDS = "g.CSSUI_Product_HDFT:header_template_id, g.CSSUI_Product_HDFT:footer_template_id">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PROD', 'CMP-CSSUI-PRDHDFT' ) }">
		<table border=0 cellpadding=2 cellspacing = 0 width = "100%">
		<tr><td valign="top" nowrap>
			Header:
		</td><td width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( 'CSSUI_Product_HDFT:header', g.CSSUI_Product_HDFT:header, 10, 58, g.CSSUI_Product_HDFT:header_template_id ) }">
		</td></tr>
		
		<tr><td valign="top" nowrap>
			Footer:
		</td><td width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( 'CSSUI_Product_HDFT:footer', g.CSSUI_Product_HDFT:footer, 10, 58, g.CSSUI_Product_HDFT:footer_template_id ) }">
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( g.CSSUI_Product_HDFT:header ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-CSSUI-PRDHDFT', 'CSSUI_Product_HDFT:header', g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( g.CSSUI_Product_HDFT:footer ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'CMP-CSSUI-PRDHDFT', 'CSSUI_Product_HDFT:footer', g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Insert" PARAMETERS = "module var, product var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ Product_HDFT_InsertOrUpdate( 'header', l.product:id, g.CSSUI_Product_HDFT:header ) AND
								 Product_HDFT_InsertOrUpdate( 'footer', l.product:id, g.CSSUI_Product_HDFT:footer ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Update" PARAMETERS = "module var, product var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ Product_HDFT_InsertOrUpdate( 'header', l.product:id, g.CSSUI_Product_HDFT:header ) AND
								 Product_HDFT_InsertOrUpdate( 'footer', l.product:id, g.CSSUI_Product_HDFT:footer ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Delete" PARAMETERS = "module var, product var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: fields_prod
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Product_Fields" PARAMETERS = "module var, fields var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.fields" INDEX = 1 MEMBER = "code"	VALUE = "header">
	<MvASSIGN NAME = "l.fields" INDEX = 1 MEMBER = "name"	VALUE = "Product Header">

	<MvASSIGN NAME = "l.fields" INDEX = 2 MEMBER = "code"	VALUE = "footer">
	<MvASSIGN NAME = "l.fields" INDEX = 2 MEMBER = "name"	VALUE = "Product Footer">

	<MvFUNCTIONRETURN VALUE = 2>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Field_Name" PARAMETERS = "module var, code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.code EQ 'header' }">		<MvFUNCTIONRETURN VALUE = "Product Header">
	<MvELSEIF EXPR = "{ l.code EQ 'footer' }">	<MvFUNCTIONRETURN VALUE = "Product Footer">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Field_Value" PARAMETERS = "module var, product_id, code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session:cache:cmp_cssui_pchdft:product:product_id NE l.product_id }">
		<MvASSIGN NAME = "g.Session:cache:cmp_cssui_pchdft:product"								VALUE = "">
		<MvASSIGN NAME = "g.Session:cache:cmp_cssui_pchdft:product:product_id"					VALUE = "{ l.product_id }">
	</MvIF>

	<MvIF EXPR = "{ ( l.code EQ 'header' ) AND
					NOT g.Session:cache:cmp_cssui_pchdft:product:header_loaded }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current_Product( 'header', l.product_id, l.header_template ) }">
			<MvFUNCTIONRETURN VALUE = "">
		</MvIF>

		<MvASSIGN NAME = "g.Session:cache:cmp_cssui_pchdft:product:header_loaded"				VALUE = 1>
		<MvASSIGN NAME = "g.Session:cache:cmp_cssui_pchdft:product:fields" MEMBER = "header"	VALUE = "{ l.header_template:source }">
	<MvELSEIF EXPR = "{ ( l.code EQ 'footer' ) AND
						NOT g.Session:cache:cmp_cssui_pchdft:product:footer_loaded }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current_Product( 'footer', l.product_id, l.footer_template ) }">
			<MvFUNCTIONRETURN VALUE = "">
		</MvIF>

		<MvASSIGN NAME = "g.Session:cache:cmp_cssui_pchdft:product:footer_loaded"				VALUE = 1>
		<MvASSIGN NAME = "g.Session:cache:cmp_cssui_pchdft:product:fields" MEMBER = "footer"	VALUE = "{ l.footer_template:source }">
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.field" VARIABLE = "g.Session:cache:cmp_cssui_pchdft:product:fields">
		<MvMEMBER NAME = "{ l.code }">
	</MvREFERENCEARRAY>

	<MvFUNCTIONRETURN VALUE = "{ l.field }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Set_Field" PARAMETERS = "module var, product_id, code, value" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.code EQ 'header' }">
		<MvIF EXPR = "{ NOT Product_HDFT_InsertOrUpdate( 'header', l.product_id, l.value ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	<MvELSEIF EXPR = "{ l.code EQ 'footer' }">
		<MvIF EXPR = "{ NOT Product_HDFT_InsertOrUpdate( 'footer', l.product_id, l.value ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvCOMMENT>
	|
	| Update in-memory cache, if neccessary
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ g.Session:cache:cmp_cssui_pchdft:product:product_id EQ l.product_id }">
		<MvASSIGNARRAY NAME = "g.Session:cache:cmp_cssui_pchdft:product:fields" VALUE = "{ l.value }">
			<MvMEMBER NAME = "{ l.code }">
		</MvASSIGNARRAY>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: fields_cat
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Category_Fields" PARAMETERS = "module var, fields var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.fields" INDEX = 1 MEMBER = "code"	VALUE = "header">
	<MvASSIGN NAME = "l.fields" INDEX = 1 MEMBER = "name"	VALUE = "Category Header">

	<MvASSIGN NAME = "l.fields" INDEX = 2 MEMBER = "code"	VALUE = "footer">
	<MvASSIGN NAME = "l.fields" INDEX = 2 MEMBER = "name"	VALUE = "Category Footer">

	<MvFUNCTIONRETURN VALUE = 2>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Category_Field_Name" PARAMETERS = "module var, code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.code EQ 'header' }">		<MvFUNCTIONRETURN VALUE = "Category Header">
	<MvELSEIF EXPR = "{ l.code EQ 'footer' }">	<MvFUNCTIONRETURN VALUE = "Category Footer">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Category_Field_Value" PARAMETERS = "module var, cat_id, code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session:cache:cmp_cssui_pchdft:category:cat_id NE l.cat_id }">
		<MvASSIGN NAME = "g.Session:cache:cmp_cssui_pchdft:category"							VALUE = "">
		<MvASSIGN NAME = "g.Session:cache:cmp_cssui_pchdft:category:cat_id"						VALUE = "{ l.cat_id }">
	</MvIF>
	
	<MvIF EXPR = "{ ( l.code EQ 'header' ) AND 
					( NOT g.Session:cache:cmp_cssui_pchdft:category:header_loaded ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current_Category( 'header', l.cat_id, l.header_template ) }">
			<MvFUNCTIONRETURN VALUE = "">
		</MvIF>

		<MvASSIGN NAME = "g.Session:cache:cmp_cssui_pchdft:category:header_loaded"				VALUE = 1>
		<MvASSIGN NAME = "g.Session:cache:cmp_cssui_pchdft:category:fields"	MEMBER = "header"	VALUE = "{ l.header_template:source }">
	<MvELSEIF EXPR = "{ ( l.code EQ 'footer' ) AND 
						( NOT g.Session:cache:cmp_cssui_pchdft:category:footer_loaded ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current_Category( 'footer', l.cat_id, l.footer_template ) }">
			<MvFUNCTIONRETURN VALUE = "">
		</MvIF>
		
		<MvASSIGN NAME = "g.Session:cache:cmp_cssui_pchdft:category:footer_loaded"				VALUE = 1>
		<MvASSIGN NAME = "g.Session:cache:cmp_cssui_pchdft:category:fields" MEMBER = "footer"	VALUE = "{ l.footer_template:source }">
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.field" VARIABLE = "g.Session:cache:cmp_cssui_pchdft:category:fields">
		<MvMEMBER NAME = "{ l.code }">
	</MvREFERENCEARRAY>

	<MvFUNCTIONRETURN VALUE = "{ l.field }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Category_Set_Field" PARAMETERS = "module var, cat_id, code, value" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.code EQ 'header' }">
		<MvIF EXPR = "{ NOT Category_HDFT_InsertOrUpdate( 'header', l.cat_id, l.value ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	<MvELSEIF EXPR = "{ l.code EQ 'footer' }">
		<MvIF EXPR = "{ NOT Category_HDFT_InsertOrUpdate( 'footer', l.cat_id, l.value ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvCOMMENT>
	|
	| Update in-memory cache, if neccessary
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ g.Session:cache:cmp_cssui_pchdft:category:cat_id EQ l.cat_id }">
		<MvASSIGNARRAY NAME = "g.Session:cache:cmp_cssui_pchdft:category:fields" VALUE = "{ l.value }">
			<MvMEMBER NAME = "{ l.code }">
		</MvASSIGNARRAY>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: component
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ toupper( l.param ) EQ 'CTGY_HEADER' }">		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_DB ].Runtime_ManagedProperty_Load_Category( 'header', l.all_settings:category:id, l.managedproperty ) }">
	<MvELSEIF EXPR = "{ toupper( l.param ) EQ 'CTGY_FOOTER' }">	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_DB ].Runtime_ManagedProperty_Load_Category( 'footer', l.all_settings:category:id, l.managedproperty ) }">
	<MvELSEIF EXPR = "{ toupper( l.param ) EQ 'PROD_HEADER' }">	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_DB ].Runtime_ManagedProperty_Load_Product( 'header', l.all_settings:product:id, l.managedproperty ) }">
	<MvELSEIF EXPR = "{ toupper( l.param ) EQ 'PROD_FOOTER' }">	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_DB ].Runtime_ManagedProperty_Load_Product( 'footer', l.all_settings:product:id, l.managedproperty ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.managedproperty:template_filename }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Store_Template_Path $ l.managedproperty:template_filename ].Template_Render( l.null, l.all_settings ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: provision_store
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Provision_Store" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.child_xml" ARRAY = "l.provide_xml:children">
		<MvASSIGN NAME = "l.name"		VALUE = "{ tolower( l.child_xml:name ) }">

		<MvIF EXPR = "{ l.name EQ 'category_header' }">		<MvEVAL EXPR = "{ Module_Provision_Store_Category( l.module, 'header', l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'category_footer' }">	<MvEVAL EXPR = "{ Module_Provision_Store_Category( l.module, 'footer', l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'product_header' }">	<MvEVAL EXPR = "{ Module_Provision_Store_Product( l.module, 'header', l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'product_footer' }">	<MvEVAL EXPR = "{ Module_Provision_Store_Product( l.module, 'footer', l.child_xml ) }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Store_Category" PARAMETERS = "module var, type, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml, 'category_code', l.category_code ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Category_Load_Code( l.category_code, l.category ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load category \'' $ l.category_code $ '\'' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'r', l.provide_xml, 'Template', l.source ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT Category_HDFT_InsertOrUpdate( l.type, l.category:id, l.source ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Store_Product" PARAMETERS = "module var, type, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml, 'product_code', l.product_code ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_Code( l.product_code, l.product ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load product \'' $ l.product_code $ '\'' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'r', l.provide_xml, 'Template', l.source ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT Product_HDFT_InsertOrUpdate( l.type, l.product:id, l.source ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: skins
|
</MvCOMMENT>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "Exports all available Product/Category Headers & Footers">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<Module code="{ encodeentities( l.module:code ) }" feature="component">
			<MvFOREACH ITERATOR = "l.managedproperty" ARRAY = "l.managedproperties" COUNT = "{ [ g.Module_Feature_TUI_DB ].ManagedPropertyList_Load_Type( 'header', l.managedproperties ) }">
				<MvIF EXPR = "{ l.managedproperty:cat_id }">
					<MvIF EXPR = "{ [ g.Module_Library_DB ].Category_Load_ID( l.managedproperty:cat_id, l.category ) AND
									[ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current( l.managedproperty:id, l.managedtemplateversion ) }">
						<Category_Header category_code="{ encodeentities( l.category:code ) }">
							<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.managedtemplateversion:source ) }"></Template>
						</Category_Header>
					</MvIF>
				<MvELSEIF EXPR = "{ l.managedproperty:product_id }">
					<MvIF EXPR = "{ [ g.Module_Library_DB ].Product_Load_ID( l.managedproperty:product_id, l.product ) AND
									[ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current( l.managedproperty:id, l.managedtemplateversion ) }">
						<Product_Header product_code="{ encodeentities( l.product:code ) }">
							<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.managedtemplateversion:source ) }"></Template>
						</Product_Header>
					</MvIF>
				</MvIF>
			</MvFOREACH>

			<MvFOREACH ITERATOR = "l.managedproperty" ARRAY = "l.managedproperties" COUNT = "{ [ g.Module_Feature_TUI_DB ].ManagedPropertyList_Load_Type( 'footer', l.managedproperties ) }">
				<MvIF EXPR = "{ l.managedproperty:cat_id }">
					<MvIF EXPR = "{ [ g.Module_Library_DB ].Category_Load_ID( l.managedproperty:cat_id, l.category ) AND
									[ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current( l.managedproperty:id, l.managedtemplateversion ) }">
						<Category_Footer category_code="{ encodeentities( l.category:code ) }">
							<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.managedtemplateversion:source ) }"></Template>
						</Category_Footer>
					</MvIF>
				<MvELSEIF EXPR = "{ l.managedproperty:product_id }">
					<MvIF EXPR = "{ [ g.Module_Library_DB ].Product_Load_ID( l.managedproperty:product_id, l.product ) AND
									[ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current( l.managedproperty:id, l.managedtemplateversion ) }">
						<Product_Footer product_code="{ encodeentities( l.product:code ) }">
							<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.managedtemplateversion:source ) }"></Template>
						</Product_Footer>
					</MvIF>
				</MvIF>
			</MvFOREACH>
		</Module>
	</MvCAPTURE>
</MvFUNCTION>

<MvCOMMENT>
|
| Copy Product Feature (copy_prod)
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Copy_Product" PARAMETERS = "module var, source_product var, dest_product var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedProperty_Load_Product( 'header', l.dest_product:id, l.header_managedproperty ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedProperty( l.header_managedproperty ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedProperty_Load_Product( 'footer', l.dest_product:id, l.footer_managedproperty ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedProperty( l.footer_managedproperty ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current_Product( 'header', l.source_product:id, l.header_template ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	<MvELSE>
		<MvIF EXPR = "{ NOT Product_HDFT_InsertOrUpdate( 'header', l.dest_product:id, l.header_template:source ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current_Product( 'footer', l.source_product:id, l.footer_template ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	<MvELSE>
		<MvIF EXPR = "{ NOT Product_HDFT_InsertOrUpdate( 'footer', l.dest_product:id, l.footer_template:source ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Helper functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "Category_HDFT_InsertOrUpdate" PARAMETERS = "type, cat_id, source var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedProperty_Load_Category( l.type, l.cat_id, l.managedproperty ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvCOMMENT>
		|
		| Do not create a property if the source is empty
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ ISNULL l.source }">
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Category_ManagedProperty( l.type, l.cat_id, 'nosync', l.source, l.null, l.managedproperty ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-PCHDFT-00068', 'CSSUI ' $ l.type $ ' for category ' $ l.cat_id $ ' created' ) }">

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current( l.managedproperty:id, l.managedtemplateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	<MvELSE>
		<MvCOMMENT>
		|
		| Do not create a new version if the template source has not changed
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ crypto_md5( l.managedtemplateversion:source ) EQ crypto_md5( l.source ) }">
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Property_ManagedTemplateVersion( l.managedproperty, l.source ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-PCHDFT-00069', 'CSSUI ' $ l.type $ ' for category ' $ l.cat_id $ ' updated' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_HDFT_InsertOrUpdate" PARAMETERS = "type, product_id, source var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedProperty_Load_Product( l.type, l.product_id, l.managedproperty ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvCOMMENT>
		|
		| Do not create a property if the source is empty
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ ISNULL l.source }">
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Product_ManagedProperty( l.type, l.product_id, 'nosync', l.source, l.null, l.managedproperty ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-PCHDFT-00070', 'CSSUI ' $ l.type $ ' for product ' $ l.product_id $ ' created' ) }">

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Property_Current( l.managedproperty:id, l.managedtemplateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	<MvELSE>
		<MvCOMMENT>
		|
		| Do not create a new version if the template source has not changed
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ crypto_md5( l.managedtemplateversion:source ) EQ crypto_md5( l.source ) }">
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Property_ManagedTemplateVersion( l.managedproperty, l.source ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-PCHDFT-00071', 'CSSUI ' $ l.type $ ' for product ' $ l.product_id $ ' updated' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| These functions are emulated for backwards compatibility
|
</MvCOMMENT>

<MvFUNCTION NAME = "Category_HDFT_Load" PARAMETERS = "category_id, category_hdft var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.category_hdft:cat_id"			VALUE = "{ l.category_id }">

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedProperty_Load_Category( 'header', l.category_id, l.header_property ) }">
		<MvASSIGN NAME = "l.category_hdft:header_id"	VALUE = "{ l.header_property:templ_id }">
	<MvELSEIF EXPR = "{ [ g.Module_Library_DB ].Error_Is_EOF() }">
		<MvASSIGN NAME = "l.category_hdft:header_id"	VALUE = 0>
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedProperty_Load_Category( 'footer', l.category_id, l.footer_property ) }">
		<MvASSIGN NAME = "l.category_hdft:footer_id"	VALUE = "{ l.footer_property:templ_id }">
	<MvELSEIF EXPR = "{ [ g.Module_Library_DB ].Error_Is_EOF() }">
		<MvASSIGN NAME = "l.category_hdft:footer_id"	VALUE = 0>
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_HDFT_Load" PARAMETERS = "product_id, product_hdft var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.product_hdft:product_id"	VALUE = "{ l.product_id }">

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedProperty_Load_Product( 'header', l.product_id, l.header_property ) }">
		<MvASSIGN NAME = "l.product_hdft:header_id"	VALUE = "{ l.header_property:templ_id }">
	<MvELSEIF EXPR = "{ [ g.Module_Library_DB ].Error_Is_EOF() }">
		<MvASSIGN NAME = "l.product_hdft:header_id"	VALUE = 0>
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedProperty_Load_Product( 'footer', l.product_id, l.footer_property ) }">
		<MvASSIGN NAME = "l.product_hdft:footer_id"	VALUE = "{ l.footer_property:templ_id }">
	<MvELSEIF EXPR = "{ [ g.Module_Library_DB ].Error_Is_EOF() }">
		<MvASSIGN NAME = "l.product_hdft:footer_id"	VALUE = 0>
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

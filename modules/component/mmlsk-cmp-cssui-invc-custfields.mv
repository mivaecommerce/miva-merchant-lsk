<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-ICUS-
| Next Error Code: 6    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-invc-custfields">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI Invoice Customer Information">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0800">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "10.08">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, not_fields, component_prov, skins">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ 'GT_PAGE/' $ l.item $ ':Customer Information' }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.fields:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.fields:template_code ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.fields:prevent_template_auto_update" VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:prevent_template_auto_update ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.settings:prevent_template_auto_update" VALUE = "{ l.fields:prevent_template_auto_update }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, '', l.fields:template_code, l.settings ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "text,html,compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.load_fields }">
		<MvASSIGN NAME = "l.fields:template_code"					VALUE = "{ l.templateversion:source }">
		<MvASSIGN NAME = "l.fields:template_filename"				VALUE = "{ l.settings:template_filename }">
		<MvASSIGN NAME = "l.fields:prevent_template_auto_update"	VALUE = "{ l.settings:prevent_template_auto_update }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Admin ].Tab_Visible( l.tab, l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_filename' ) }">
		<table border=0 cellpadding=2 cellspacing=0 width="100%">
		<tr><td colspan=2 width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea_WithRecall( l.field_prefix $ 'template_code', l.fields:template_code, 20, 100, l.templateversion:templ_id ) }">
		</td></tr>

		<tr><td colspan=2>&nbsp;</td></tr>

		<tr><td>
			&nbsp;
		</td><td>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox_With_Label( l.fields:prevent_template_auto_update, l.field_prefix $ 'prevent_template_auto_update', 'Yes', 'Prevent Automatic Updates to This Template' ) }">
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, '' ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StandardFields_Load( l.fields ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvEVAL EXPR = "{ Generate_Code( l.fields, l.template_source ) }">

	<MvASSIGN NAME = "l.settings:template_filename"	VALUE = "{ tolower( l.page:code ) $ '-' $ tolower( l.item ) $ '.mvc' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.template_source, l.settings, l.settings:template_filename ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'states' ) OR
					NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'countries' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate_Filename( l.settings:template_filename ) }">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Copy" PARAMETERS = "module var, item, source_branch var, source_page var, source_settings var, dest_branch var, dest_page var, dest_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Branch_Head( l.source_branch:id, l.source_settings:template_filename, l.source_templateversion ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateAndVersion_Load_Branch_Filename( l.dest_branch:id, l.dest_settings:template_filename, l.dest_managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.dest_settings"						VALUE = "{ l.source_settings }">
	<MvASSIGN NAME = "l.dest_settings:template_filename"	VALUE = "{ l.dest_managedtemplate:version:settings:template_filename }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.dest_managedtemplate, '', l.source_templateversion:source, l.dest_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.source_branch:id EQ l.dest_branch:id }">	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-ICUS-00004', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' on branch \'' $ l.dest_branch:name $ '\'' ) }">
	<MvELSE>													<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-ICUS-00005', l.module:name $ ' settings copied from page \'' $ l.source_page:code $ '\' to page \'' $ l.dest_page:code $ '\' for item \'' $ l.item $ '\' from branch \'' $ l.source_branch:name $ '\' to branch \'' $ l.dest_branch:name $ '\'' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Template( l.settings:template_filename, l.all_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update_Requires_Version" PARAMETERS = "module var, page var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_PostUpdate" PARAMETERS = "module var, page var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Notify_StandardFields" PARAMETERS = "module var, standardfields var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.item" ARRAY = "l.items" COUNT = "{ [ g.Module_Feature_TUI_DB ].ItemList_Load_Module( l.module:id, l.items ) }">
		<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item( l.item:id, l.pages ) }">
			<MvREFERENCEARRAY NAME = "l.settings" VARIABLE = "l.page:settings">
				<MvMEMBER NAME = "{ l.item:code }">
			</MvREFERENCEARRAY>

			<MvIF EXPR = "{ l.settings:prevent_template_auto_update }">
				<MvFOREACHCONTINUE>
			</MvIF>

			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvEVAL EXPR = "{ Generate_Code( l.standardfields, l.template_code ) }">

			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate,
																													  'Field Settings Update: ' $ [ g.Module_Library_Utilities ].Format_Date( s.dyn_time_t, s.miva_language ) $ ' ' $ [ g.Module_Library_Utilities ].Format_Time( s.dyn_time_t, s.miva_language ),
																													  l.template_code,
																													  l.settings ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-CSSUI-CMP-ICUS-00003', 'Created managed template version \'' $ l.managedtemplate:id $ '\'' ) }">
		</MvFOREACH>
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Code" PARAMETERS = "fields var, template_source var" STANDARDOUTPUTLEVEL = "text,html,compresswhitespace">
	<MvCAPTURE VARIABLE = "l.template_source">
		<MvIF		EXPR = "{ l.fields:primaddr EQ 'shipping' }">	<MvASSIGN NAME = "l.order" VALUE = "shipping,billing">
		<MvELSEIF	EXPR = "{ l.fields:primaddr EQ 'billing' }">	<MvASSIGN NAME = "l.order" VALUE = "billing,shipping">
		</MvIF>

		<MvASSIGN NAME = "l.pos"		VALUE = 1>
		<div id="customer-fields" class="non-editable">
		<MvWHILE EXPR = "{ l.pos LE 2 }">

			<MvASSIGN NAME = "l.type"	VALUE = "{ gettoken( l.order, ',', l.pos ) }">
			<MvIF EXPR = "{ ( l.type EQ 'shipping' ) AND ( l.fields:shipping NE 'H' ) }">
				<MvEVAL EXPR = "{ Generate_Code_Shipping( l.settings, l.fields ) }">
			<MvELSEIF EXPR = "{ ( l.type EQ 'billing' ) AND ( l.fields:billing NE 'H' ) }">	
				<MvEVAL EXPR = "{ Generate_Code_Billing( l.settings, l.fields ) }">
			</MvIF>
			
			<MvASSIGN NAME = "l.pos"	VALUE = "{ l.pos + 1 }">
		</MvWHILE>
		</div>
		<div class="clear"></div>
	</MvCAPTURE>
	
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Code_Shipping" PARAMETERS = "settings var, fields var" STANDARDOUTPUTLEVEL = "text,html,compresswhitespace">
	<div id="ship-to">
		<h2 class="fields-heading">
			Ship To:
		</h2>
		<div id="ship-to-fields">
		<MvIF EXPR = "{ l.fields:mode_fname NE 'H' }">
			<div class="form_row">
				<label>Name:</label>
				<div>
					<mvt:if expr="l.settings:order:ship_fname OR l.settings:ship_lname">
					&mvte:order:ship_fname; &mvte:order:ship_lname;
					<mvt:else>
					&nbsp;
					</mvt:if>
				</div>
			</div>
		</MvIF>

		<MvIF EXPR = "{ l.fields:mode_email NE 'H' }">
			<div class="form_row">
				<label>Email Address:</label>
				<div>
					<mvt:if expr="l.settings:order:ship_email">
					&mvte:order:ship_email;
					<mvt:else>
					&nbsp;
					</mvt:if>
				</div>
			</div>
		</MvIF>

		<MvIF EXPR = "{ l.fields:mode_phone NE 'H' }">
			<div class="form_row">
				<label>Phone Number:</label>
				<div>
					<mvt:if expr="l.settings:order:ship_phone">
					&mvte:order:ship_phone;
					<mvt:else>
					&nbsp;
					</mvt:if>
				</div>
			</div>
		</MvIF>

		<MvIF EXPR = "{ l.fields:mode_fax NE 'H' }">
			<div class="form_row">
				<label>Fax Number:</label>
				<div>
					<mvt:if expr="l.settings:order:ship_fax">
					&mvte:order:ship_fax;
					<mvt:else>
					&nbsp;
					</mvt:if>
				</div>
			</div>
		</MvIF>

		<MvIF EXPR = "{ l.fields:mode_comp NE 'H' }">
			<div class="form_row">
				<label>Company:</label>
				<div>
					<mvt:if expr="l.settings:order:ship_comp">
					&mvte:order:ship_comp;
					<mvt:else>
					&nbsp;
					</mvt:if>
				</div>
			</div>
		</MvIF>

		<MvIF EXPR = "{ l.fields:mode_addr NE 'H' }">
			<MvASSIGN NAME = "l.address_ship_content" VALUE = "{ '&mvte:order:ship_addr1;<br />' $ asciichar( 10 ) }">
			
			<MvIF EXPR = "{ l.fields:mode_addr2 NE 'H' }">
				<MvASSIGN NAME = "l.address_ship_content" VALUE = "{ l.address_ship_content $ '&mvte:order:ship_addr2;<br />' $ asciichar( 10 ) }">
			</MvIF>

			<MvIF EXPR = "{ ( l.fields:mode_city NE 'H' ) OR ( l.fields:mode_state NE 'H' ) OR ( l.fields:mode_zip NE 'H' ) }">
				<MvIF EXPR = "{ l.fields:mode_city NE 'H' }">
					<MvIF EXPR = "{ l.fields:mode_state NE 'H' }">
						<MvASSIGN NAME = "l.address_ship_content" VALUE = "{ l.address_ship_content $ 
																			   '<mvt:if expr="( NOT ISNULL l.settings:order:ship_city ) AND ( NOT ISNULL l.settings:order:ship_state )">'	$ asciichar( 10 ) $
																			   '	&mvte:order:ship_city;,'																	$ asciichar( 10 ) $ 
																			   '<mvt:else>'																						$ asciichar( 10 ) $
																			   '	&mvte:order:ship_city;'																		$ asciichar( 10 ) $
																			   '</mvt:if>'																						$ asciichar( 10 ) }">
					<MvELSE>
						<MvASSIGN NAME = "l.address_ship_content" VALUE = "{ l.address_ship_content $ '&mvte:order:ship_city;' $ asciichar( 10 ) }">
					</MvIF>
				</MvIF>

				<MvIF EXPR = "{ l.fields:mode_state NE 'H' }">
					<MvASSIGN NAME = "l.address_ship_content"	VALUE = "{ l.address_ship_content $ '&mvte:order:ship_state;' $ asciichar( 10 ) }">
				</MvIF>

				<MvIF EXPR = "{ l.fields:mode_zip NE 'H' }">
					<MvASSIGN NAME = "l.address_ship_content"	VALUE = "{ l.address_ship_content $ '&mvte:order:ship_zip;' $ asciichar( 10 ) }">
				</MvIF>
			</MvIF>

			<MvIF EXPR = "{ l.fields:mode_cntry NE 'H' }">
				<MvASSIGN NAME = "l.address_ship_content"	VALUE = "{ l.address_ship_content $ '<br /> &mvte:order:ship_cntry;' }">
			</MvIF>
			<div class="form_row">
				<label>Address:</label>
				<div>
					<mvt:if expr="l.settings:order:ship_addr1 OR l.settings:order:ship_addr2 OR l.settings:order:ship_city OR l.settings:order:ship_state OR l.settings:order:ship_zip OR l.settings:order:ship_cntry OR l.settings:order:ship_res">
					<MvEVAL EXPR = "{ l.address_ship_content }">

						<mvt:if expr="l.settings:order:ship_res">
							<br />
							Residential Address
						</mvt:if>
					<mvt:else>
					&nbsp;
					</mvt:if>
				</div>
			</div>
		</MvIF>
		</div>
	</div>
</MvFUNCTION>

<MvFUNCTION NAME = "Generate_Code_Billing" PARAMETERS = "settings var, fields var" STANDARDOUTPUTLEVEL = "text,html,compresswhitespace">
	<div id="bill-to">
		<h2 class="fields-heading">
			Bill To:
		</h2>
		<div id="bill-to-fields">
		<MvIF EXPR = "{ l.fields:mode_fname NE 'H' }">
			<div class="form_row">
				<label>Name:</label>
				<div>
					<mvt:if expr="l.settings:order:bill_fname OR l.settings:order:bill_lname">
					&mvte:order:bill_fname; &mvte:order:bill_lname;
					<mvt:else>
					&nbsp;
					</mvt:if>
				</div>
			</div>
		</MvIF>

		<MvIF EXPR = "{ l.fields:mode_email NE 'H' }">
			<div class="form_row">
				<label>Email Address:</label>
				<div>
					<mvt:if expr="l.settings:order:bill_email">
					&mvte:order:bill_email;
					<mvt:else>
					&nbsp;
					</mvt:if>
				</div>
			</div>
		</MvIF>

		<MvIF EXPR = "{ l.fields:mode_phone NE 'H' }">
			<div class="form_row">
				<label>Phone Number:</label>
				<div>
					<mvt:if expr="l.settings:order:bill_phone">
					&mvte:order:bill_phone;
					<mvt:else>
					&nbsp;
					</mvt:if>
				</div>
			</div>
		</MvIF>

		<MvIF EXPR = "{ l.fields:mode_fax NE 'H' }">
			<div class="form_row">
				<label>Fax Number:</label>
				<div>
					<mvt:if expr="l.settings:order:bill_fax">
					&mvte:order:bill_fax;
					<mvt:else>
					&nbsp;
					</mvt:if>
				</div>
			</div>
		</MvIF>

		<MvIF EXPR = "{ l.fields:mode_comp NE 'H' }">
			<div class="form_row">
				<label>Company:</label>
				<div>
					<mvt:if expr="l.settings:order:bill_comp">
					&mvte:order:bill_comp;
					<mvt:else>
					&nbsp;
					</mvt:if>
				</div>
			</div>
		</MvIF>

		<MvIF EXPR = "{ l.fields:mode_addr NE 'H' }">
			<MvASSIGN NAME = "l.address_bill_content"			VALUE = "{ '&mvte:order:bill_addr1;<br />' $ asciichar( 10 ) }">
			
			<MvIF EXPR = "{ l.fields:mode_addr2 NE 'H' }">
				<MvASSIGN NAME = "l.address_bill_content"			VALUE = "{ l.address_bill_content $ '&mvte:order:bill_addr2; <br />' $ asciichar( 10 ) }">
			</MvIF>

			<MvIF EXPR = "{ ( l.fields:mode_city NE 'H' ) OR ( l.fields:mode_state NE 'H' ) OR ( l.fields:mode_zip NE 'H' ) }">
				<MvIF EXPR = "{ l.fields:mode_city NE 'H' }">
					<MvIF EXPR = "{ l.fields:mode_state NE 'H' }">
						<MvASSIGN NAME = "l.address_bill_content"	VALUE = "{ l.address_bill_content $ 
																			   '<mvt:if expr="( NOT ISNULL l.settings:order:bill_city ) AND ( NOT ISNULL l.settings:order:bill_state )">'	$ asciichar( 10 ) $
																			   '	&mvte:order:bill_city;,'																	$ asciichar( 10 ) $ 
																			   '<mvt:else>'																						$ asciichar( 10 ) $
																			   '	&mvte:order:bill_city;'																		$ asciichar( 10 ) $
																			   '</mvt:if>'																						$ asciichar( 10 ) }">
					<MvELSE>
						<MvASSIGN NAME = "l.address_bill_content"	VALUE = "{ l.address_bill_content $ '&mvte:order:bill_city;' $ asciichar( 10 ) }">
					</MvIF>
				</MvIF>

				<MvIF EXPR = "{ l.fields:mode_state NE 'H' }">
					<MvASSIGN NAME = "l.address_bill_content"		VALUE = "{ l.address_bill_content $ '&mvte:order:bill_state;' $ asciichar( 10 ) }">
				</MvIF>

				<MvIF EXPR = "{ l.fields:mode_zip NE 'H' }">
					<MvASSIGN NAME = "l.address_bill_content"		VALUE = "{ l.address_bill_content $ '&mvte:order:bill_zip;' }">
				</MvIF>
			</MvIF>

			<MvIF EXPR = "{ l.fields:mode_cntry NE 'H' }">
				<MvASSIGN NAME = "l.address_bill_content"			VALUE = "{ l.address_bill_content $ '<br /> &mvte:order:bill_cntry;' }">
			</MvIF>
			<div class="form_row">
				<label>Address:</label>
				<div>
					<mvt:if expr="l.settings:order:bill_addr1 OR l.settings:order:bill_addr2 OR l.settings:order:bill_city OR l.settings:order:bill_state OR l.settings:order:bill_zip OR l.settings:order:bill_cntry">
					<MvEVAL EXPR = "{ l.address_bill_content }">
					<mvt:else>
					&nbsp;
					</mvt:if>
				</div>
			</div>
		</MvIF>
		</div>
	</div>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Template' ) AND
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'PreventAutomaticTemplateUpdates' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Either Template or PreventAutomaticTemplateUpdates must be specified' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'o', l.provide_xml,	'Template',							l.template )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'O', l.provide_xml,	'Notes',							l.notes ) 		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml,	'PreventAutomaticTemplateUpdates',	l.settings:prevent_template_auto_update ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_Filename( l.settings:template_filename, l.managedtemplate ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Unable to load template' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_CreateIfModified_ManagedTemplateVersion( l.managedtemplate, l.notes, l.template, l.settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Description" PARAMETERS = "module var, item var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pages" VALUE = "{ [ g.Module_Feature_TUI_DB ].Item_Load_PageList( l.item ) }">
	
	<MvFUNCTIONRETURN VALUE = "{ 'Exports Invoice Customer Fields template from pages: ' $ l.pages }">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item_Runtime( l.item:id, l.pages ) }">
			<MvREFERENCEARRAY NAME = "l.settings" VARIABLE = "l.page:settings">
				<MvMEMBER NAME = "{ l.item:code }">
			</MvREFERENCEARRAY>

			<Page_Update code="{ l.page:code }">
				<Item code="{ l.item:code }">
					<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Filename_Current( l.settings:template_filename, l.templateversion ) }">
						<Template><MvEVAL EXPR = "{ miva_cdata_encode( l.templateversion:source ) }"></Template>
					</MvIF>

					<PreventAutomaticTemplateUpdates><MvIF EXPR = "{ l.settings:prevent_template_auto_update }">Yes<MvELSE>No</MvIF></PreventAutomaticTemplateUpdates>
					<Notes>#Set_Current_Time#</Notes>
				</Item>
			</Page_Update>
		</MvFOREACH>
	</MvCAPTURE>
</MvFUNCTION>

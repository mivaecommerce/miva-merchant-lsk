<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2025 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CSSUI-CMP-BASK-
| Next Error Code: 7    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cmp-cssui-basket">
	<MvASSIGN NAME = "l.module:name"		VALUE = "CSSUI Basket Contents">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.1300">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "10.08">
	<MvASSIGN NAME = "l.module:features"	VALUE = "component, component_prov, skins">
</MvFUNCTION>

<MvFUNCTION NAME = "BasketComponentModule_Generate_Code" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:groups }">	<MvFUNCTIONRETURN VALUE = "{ BasketComponentModule_Generate_Code_Groups( l.settings, l.code ) }">
	<MvELSE>								<MvFUNCTIONRETURN VALUE = "{ BasketComponentModule_Generate_Code_Items( l.settings, l.code ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketComponentModule_Generate_Code_Groups" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code">
	<MIVA STANDARDOUTPUTLEVEL = "text, html">
<mvt:if expr="l.settings:basket:empty">
Your shopping basket is currently empty.
<p><mvt:item name="cssui_links" param="storefront">Home</mvt:item></p>
<mvt:exit />
</mvt:if>
<table>
	<tr class="basket-heading"><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ ( l.settings:editable AND NOT l.settings:button_wish ) OR ( NOT ISNULL l.settings:imagetypes[ 1 ] ) }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="item-remove">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="item-name">Item</td>
		<td class="item-quantity">Qty.</td>
		<td class="item-price">Item Price</td>
		<td class="item-total">Total Price</td>
	</tr>
	<mvt:foreach iterator="group" array="basket:groups">
	<tr><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ l.settings:editable AND l.settings:button_wish }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="l.settings:group:option_count">
			<mvt:assign name="l.settings:rowspan" value="l.settings:group:option_count + 2" />
		<mvt:else>
			<mvt:assign name="l.settings:rowspan" value="2" />
		</mvt:if>
<MIVA STANDARDOUTPUTLEVEL = "">
		<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="l.settings:group:option_count">
			<mvt:assign name="l.settings:rowspan" value="l.settings:group:option_count + 1" />
		<mvt:else>
			<mvt:assign name="l.settings:rowspan" value="1" />
		</mvt:if>
<MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="l.settings:group:subterm_id">
			<mvt:assign name="l.settings:rowspan" value="l.settings:rowspan + 1" />
		</mvt:if>
<MIVA STANDARDOUTPUTLEVEL = "">

		<MvIF EXPR = "{ ( l.settings:editable AND NOT l.settings:button_wish ) OR ( NOT ISNULL l.settings:imagetypes[ 1 ] ) }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="item-remove" rowspan="&mvt:rowspan;"><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ NOT ISNULL l.settings:imagetypes[ 1 ] }">
				<MvASSIGN NAME = "l.imagetype"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.settings:imagetypes[ 1 ] ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="{ 'ISNULL l.settings:group:imagetypes:' $ l.imagetype }">
				&nbsp;
			<mvt:else>
				<div class="item-image"><img src="{ '&mvte:group:imagetypes:' $ l.imagetype $ ';' }" alt="&mvte:group:name;" /></div>
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<form method="post" action="&mvte:urls:BASK:auto;">
			<input type="hidden" name="Action" value="RGRP" />
			<input type="hidden" name="Basket_Group" value="&mvte:group:group_id;" />
			<input type="hidden" name="Offset" value="&mvte:global:Offset;" />
			<input type="hidden" name="AllOffset" value="&mvte:global:AllOffset;" />
			<input type="hidden" name="CatListingOffset" value="&mvte:global:CatListingOffset;" />
			<input type="hidden" name="RelatedOffset" value="&mvte:global:RelatedOffset;" />
			<input type="hidden" name="SearchOffset" value="&mvte:global:SearchOffset;" />
			<mvt:item name="buttons" param="Remove" />
			</form><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
		</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="item-name">
			<div class="special-offer"><mvt:if expr="l.settings:group:upsold">(Special Offer)</mvt:if></div>
			<div class="item-name">&mvt:group:name; - 
			<span class="item-code"><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ l.settings:editable }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<mvt:if expr="ISNULL l.settings:group:link">
					&mvt:group:code;
				<mvt:else>
					<a href="&mvte:group:link;">&mvt:group:code;</a>
				</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
				&mvt:group:code;<MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>
			<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:fields_custom">
				<MvASSIGN NAME = "l.safe_module_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.customfield:module ) }">
				<MvASSIGN NAME = "l.safe_field_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.customfield:code ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<mvt:if expr="{ 'NOT ISNULL l.settings:group:customfield_values:' $ l.safe_module_code $ ':' $ l.safe_field_code }">
					<div class="custom-field" id="{ l.safe_module_code $ '-' $ l.safe_field_code }">&mvt:customfield_names:<MvEVAL EXPR = "{ l.safe_module_code }">:<MvEVAL EXPR = "{ l.safe_field_code }">;: &mvt:group:customfield_values:<MvEVAL EXPR = "{ l.safe_module_code }">:<MvEVAL EXPR = "{ l.safe_field_code }">;</div>
				</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			</MvFOREACH>
			<MvIF EXPR = "{ l.settings:lineitemdiscounts }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<mvt:foreach iterator="discount" array="group:discounts">
					<mvt:if expr="l.settings:discount:display">
						<div class="item-discount">&mvt:discount:descrip;: &mvt:discount:formatted_discount;</div>
					</mvt:if>
				</mvt:foreach><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
			</span>
			</div>
		</td>
		<td class="item-quantity"><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ NOT l.settings:editable }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			&mvt:group:quantity;<MIVA STANDARDOUTPUTLEVEL = "">
		<MvELSE>
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:group:upsold">
			&mvt:group:quantity;
			<mvt:else>
			<form method="post" action="&mvte:urls:BASK:auto;">
			<input type="hidden" name="Action" value="QTYG" />
			<input type="hidden" name="Basket_Group" value="&mvte:group:group_id;" />
			<input type="hidden" name="Old_Screen" value="BASK" />
			<input type="hidden" name="Offset" value="&mvte:global:Offset;" />
			<input type="hidden" name="AllOffset" value="&mvte:global:AllOffset;" />
			<input type="hidden" name="CatListingOffset" value="&mvte:global:CatListingOffset;" />
			<input type="hidden" name="RelatedOffset" value="&mvte:global:RelatedOffset;" />
			<input type="hidden" name="SearchOffset" value="&mvte:global:SearchOffset;" />
			<input type="text" name="Quantity" class="textfield basket-qty-input small" value="&mvt:group:quantity;" />
			<mvt:item name="buttons" param="Update" />
			</form>
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ ( NOT ISNULL l.settings:imagetypes[ 1 ] ) AND ( NOT l.settings:button_wish ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<div class="item-remove-link"><a href="&mvte:urls:BASK:auto_sep;Action=RGRP&amp;Basket_Group=&mvta:group:group_id;&amp;Offset=&mvta:global:Offset;&amp;AllOffset=&mvta:global:AllOffset;&amp;CatListingOffset=&mvta:global:CatListingOffset;&amp;RelatedOffset=&mvta:global:RelatedOffset;&amp;SearchOffset=&mvta:global:SearchOffset;">Remove</a></div><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>
		</MvIF>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		</td>
		<td class="item-price"><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ l.settings:lineitemprice EQ 'retail' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:group:retail">
				&mvt:group:formatted_retail;
			<mvt:else>
				&mvt:group:formatted_base_price;
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSEIF EXPR = "{ l.settings:lineitemprice EQ 'base' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			&mvt:group:formatted_base_price;<MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
			&mvt:group:formatted_price;<MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
		</td>
		<td class="item-total"><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ l.settings:lineitemprice EQ 'retail' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:group:subtotal_retail_comprehensive AND l.settings:group:subtotal_retail_comprehensive GT l.settings:group:subtotal_comprehensive">
				<span style="text-decoration: line-through;">&mvt:group:formatted_subtotal_retail_comprehensive;</span>
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSEIF EXPR = "{ l.settings:lineitemprice EQ 'base' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:group:subtotal_base_price_comprehensive GT l.settings:group:subtotal_comprehensive">
				<span style="text-decoration: line-through;">&mvt:group:formatted_subtotal_base_price_comprehensive;</span>
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
			&mvt:group:formatted_total;
		</td>
	</tr>
	<mvt:foreach iterator="option" array="group:options">
	<tr class="item-options">
		<td class="item-name">
			<mvt:if expr="l.settings:option:option_id">
				&mvt:option:attr_code;: &mvt:option:opt_code;
			<mvt:elseif expr="NOT ISNULL l.settings:option:data">
				&mvt:option:attr_code;: &mvt:option:data;
			<mvt:elseif expr="NOT ISNULL l.settings:option:data_long">
				&mvt:option:attr_code;: &mvt:option:data_long;
			<mvt:else>
				&mvt:option:attr_code;
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ l.settings:lineitemdiscounts }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:foreach iterator="discount" array="option:discounts">
				<mvt:if expr="l.settings:discount:display">
					<div class="item-discount">&mvt:discount:descrip;: &mvt:discount:formatted_discount;</div>
				</mvt:if>
			</mvt:foreach><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
		</td>
		<td class="item-quantity">&nbsp;</td>
		<td class="item-price"><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ l.settings:lineitemprice EQ 'retail' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:option:retail">
				&mvt:option:formatted_retail;
			<mvt:elseif expr="l.settings:option:base_price">
				&mvt:option:formatted_base_price;<MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSEIF EXPR = "{ l.settings:lineitemprice EQ 'base' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:option:base_price">
				&mvt:option:formatted_base_price;<MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:option:price">
				&mvt:option:formatted_price;<MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:else>
				&nbsp;
			</mvt:if>
		</td>
		<td class="item-total">
			&nbsp;
		</td>
	</tr>
	</mvt:foreach>

	<mvt:if expr="l.settings:group:subterm_id">
		<tr class="item-options">
			<td class="item-name">
				Subscription: &mvte:group:productsubscriptionterm:descrip;
			</td>
			<td class="item-quantity">&nbsp;</td>
			<td class="item-price">&nbsp;</td>
			<td class="item-total">&nbsp;</td>
		</tr>
	</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">

	<MvIF EXPR = "{ l.settings:button_wish }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<tr class="item-options">
		<td class="item-name">
			<a href="&mvte:urls:BASK:auto_sep;Action=RGRP&amp;Basket_Group=&mvta:group:group_id;&amp;Offset=&mvta:global:Offset;&amp;AllOffset=&mvta:global:AllOffset;&amp;CatListingOffset=&mvta:global:CatListingOffset;&amp;RelatedOffset=&mvta:global:RelatedOffset;&amp;SearchOffset=&mvta:global:SearchOffset;">Remove</a>
			<mvt:if expr="l.settings:group:product:id AND ( NOT l.settings:group:product:productsubscriptionsettings:enabled OR NOT l.settings:group:product:productsubscriptionsettings:mandatory )">
				<span class="grey">|</span>
				<a href="&mvte:urls:WISH:secure_sep;Action=MPWL&amp;Group_ID=&mvta:group:group_id;">Move To Wish List</a>
			</mvt:if>
		</td>
		<td class="item-quantity">&nbsp;</td>
		<td class="item-price">&nbsp;</td>
		<td class="item-total">&nbsp;</td>
	</tr><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
	</mvt:foreach><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:lineitemcoupons }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<mvt:foreach iterator="coupon" array="basket:coupons">
	<tr><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ l.settings:editable AND NOT ISNULL l.settings:imagetypes[ 1 ] }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="item-remove">
			<form method="post" action="&mvte:urls:BASK:auto;">
			<input type="hidden" name="Action" value="RCPN" />
			<input type="hidden" name="Coupon_Code" value="&mvte:coupon:code;" />
			<mvt:item name="buttons" param="Remove" />
			</form>
		</td><MIVA STANDARDOUTPUTLEVEL = "">
		<MvELSEIF EXPR = "{ NOT ISNULL l.settings:imagetypes[ 1 ] }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="item-remove">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="item-name">
			<div class="item-name">Coupon -
			<span class="item-code">
				&mvt:coupon:code;
			</span>
			<mvt:if expr="NOT ISNULL l.settings:coupon:descrip">
			<div class="item-discount">&mvt:coupon:descrip;</div>
			</mvt:if>
			</div>
		</td>
		<td class="item-quantity">&nbsp;</td>
		<td class="item-price">&nbsp;</td>
		<td class="item-total">&nbsp;</td>
	</tr><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:editable AND ISNULL l.settings:imagetypes[ 1 ] }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<tr class="item-options">
		<td class="item-name">
			<a href="&mvte:urls:BASK:auto_sep;Action=RCPN&amp;Coupon_Code=&mvte:coupon:code;&amp;Offset=&mvta:global:Offset;&amp;AllOffset=&mvta:global:AllOffset;&amp;CatListingOffset=&mvta:global:CatListingOffset;&amp;RelatedOffset=&mvta:global:RelatedOffset;&amp;SearchOffset=&mvta:global:SearchOffset;">Remove</a>
		</td>
		<td class="item-quantity">&nbsp;</td>
		<td class="item-price">&nbsp;</td>
		<td class="item-total">&nbsp;</td>
	</tr><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
	</mvt:foreach><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<mvt:foreach iterator="charge" array="basket:charges">
	<tr class="basket-charges"><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ ( l.settings:editable AND NOT l.settings:button_wish ) OR ( NOT ISNULL l.settings:imagetypes[ 1 ] ) }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td colspan="3">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
		<MvELSE>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td colspan="2">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="charge-description">&mvt:charge:descrip;:</td>
		<td class="formatted-charge">&mvt:charge:formatted_disp_amt;</td>
	</tr>
	</mvt:foreach>
	<tr id="basket-totals"><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ ( l.settings:editable AND NOT l.settings:button_wish ) OR ( NOT ISNULL l.settings:imagetypes[ 1 ] ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td colspan="3"><MIVA STANDARDOUTPUTLEVEL = "">
		<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td colspan="2"><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF><MvIF EXPR = "{ ( NOT l.settings:applycoupons ) AND ( NOT l.settings:redeemgiftcerts ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			&nbsp;<MIVA STANDARDOUTPUTLEVEL = "">
		<MvELSE>
			<MvIF EXPR = "{ l.settings:applycoupons AND l.settings:redeemgiftcerts }">
				<MvASSIGN NAME = "l.input_class" 	VALUE = "input_width_small">
				<MvASSIGN NAME = "l.class"			VALUE = "floatright">
			<MvELSE>
				<MvASSIGN NAME = "l.input_class" 	VALUE = "input_width_medium">
				<MvASSIGN NAME = "l.class"			VALUE = "floatleft">
			</MvIF>

			<MvIF EXPR = "{ l.settings:applycoupons }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<form class="floatleft" method="post" action="&mvte:urls:_self:auto;">
			<input type="hidden" name="Action" value="ACPN" />
			Coupon: <input type="text" name="Coupon_Code" class="{ l.input_class }"> <mvt:item name="buttons" param="ApplyCoupon" />
			</form><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>

			<MvIF EXPR = "{ l.settings:redeemgiftcerts }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="g.Basket:cust_id EQ 0">
				<a class="{ l.class }" href="&mvte:urls:GFTL:auto;">Redeem Gift Certificate</a>
			<mvt:else>
				<form class="{ l.class }" method="post" action="&mvte:urls:_self:auto;">
				<input type="hidden" name="Action" value="RDGC" />
				Gift Certificate:
				<input type="text" name="GiftCertificate_Code" class="{ l.input_class }"> <mvt:item name="buttons" param="ApplyGiftCert" />
				</form>
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>
		</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
		</td>
		<td class="total-prompt">Total:</td>
		<td class="formatted-total">&mvt:basket:formatted_total;</td>
	</tr><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:splitpayments }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<mvt:foreach iterator="split" array="splitpayment:splits">
	<tr class="basket-payments"><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ ( l.settings:editable AND NOT l.settings:button_wish ) OR ( NOT ISNULL l.settings:imagetypes[ 1 ] ) }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td colspan="3">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
		<MvELSE>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td colspan="2">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="charge-description">Pending Payment: &mvt:split:desc;:</td>
		<td class="formatted-charge">&mvt:split:formatted_amount_negative;</td>
	</tr>
	</mvt:foreach>
	<mvt:if expr="( NOT ISNULL l.settings:splitpayment:remaining ) AND ( l.settings:splitpayment:remaining NE l.settings:basket:total )">
		<tr id="basket-remaining"><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ ( l.settings:editable AND NOT l.settings:button_wish ) OR ( NOT ISNULL l.settings:imagetypes[ 1 ] ) }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td colspan="3">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSE>
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td colspan="2">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="total-prompt">Total Remaining:</td>
			<td class="formatted-total">&mvt:splitpayment:formatted_remaining;</td>
		</tr>
	</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
</table>
<div class="clear"></div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "BasketComponentModule_Generate_Code_Items" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code">
	<MIVA STANDARDOUTPUTLEVEL = "text, html">
<mvt:if expr="l.settings:basket:empty">
Your shopping basket is currently empty.
<p><mvt:item name="cssui_links" param="storefront">Home</mvt:item></p>
<mvt:exit />
</mvt:if>
<table>
	<tr class="basket-heading"><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ ( l.settings:editable AND NOT l.settings:button_wish ) OR ( NOT ISNULL l.settings:imagetypes[ 1 ] ) }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="item-remove">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="item-name">Item</td>
		<td class="item-quantity">Qty.</td>
		<td class="item-price">Item Price</td>
		<td class="item-total">Total Price</td>
	</tr>
	<mvt:foreach iterator="item" array="basket:items">
	<tr><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ l.settings:editable AND l.settings:button_wish }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="l.settings:item:option_count">
			<mvt:assign name="l.settings:rowspan" value="l.settings:item:option_count + 2" />
		<mvt:else>
			<mvt:assign name="l.settings:rowspan" value="2" />
		</mvt:if>
<MIVA STANDARDOUTPUTLEVEL = "">
		<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="l.settings:item:option_count">
			<mvt:assign name="l.settings:rowspan" value="l.settings:item:option_count + 1" />
		<mvt:else>
			<mvt:assign name="l.settings:rowspan" value="1" />
		</mvt:if>
<MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>

<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<mvt:if expr="l.settings:item:subterm_id">
			<mvt:assign name="l.settings:rowspan" value="l.settings:rowspan + 1" />
		</mvt:if>
<MIVA STANDARDOUTPUTLEVEL = "">

		<MvIF EXPR = "{ ( l.settings:editable AND NOT l.settings:button_wish ) OR ( NOT ISNULL l.settings:imagetypes[ 1 ] ) }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="item-remove" rowspan="&mvt:rowspan;"><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ NOT ISNULL l.settings:imagetypes[ 1 ] }">
				<MvASSIGN NAME = "l.imagetype"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.settings:imagetypes[ 1 ] ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="{ 'ISNULL l.settings:item:imagetypes:' $ l.imagetype }">
				&nbsp;
			<mvt:else>
				<div class="item-image"><img src="{ '&mvte:item:imagetypes:' $ l.imagetype $ ';' }" alt="&mvte:item:name;" /></div>
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<form method="post" action="&mvte:urls:BASK:auto;">
			<input type="hidden" name="Action" value="RPRD" />
			<input type="hidden" name="Basket_Line" value="&mvte:item:line_id;" />
			<input type="hidden" name="Offset" value="&mvte:global:Offset;" />
			<input type="hidden" name="AllOffset" value="&mvte:global:AllOffset;" />
			<input type="hidden" name="CatListingOffset" value="&mvte:global:CatListingOffset;" />
			<input type="hidden" name="RelatedOffset" value="&mvte:global:RelatedOffset;" />
			<input type="hidden" name="SearchOffset" value="&mvte:global:SearchOffset;" />
			<mvt:item name="buttons" param="Remove" />
			</form><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
		</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="item-name">
			<div class="special-offer"><mvt:if expr="l.settings:item:upsold">(Special Offer)</mvt:if></div>
			<div class="item-name">&mvt:item:name; - 
			<span class="item-code"><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ l.settings:editable }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<mvt:if expr="ISNULL l.settings:item:link">
					&mvt:item:code;
				<mvt:else>
					<a href="&mvte:item:link;">&mvt:item:code;</a>
				</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
				&mvt:item:code;<MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>
			<MvFOREACH ITERATOR = "l.customfield" ARRAY = "l.settings:fields_custom">
				<MvASSIGN NAME = "l.safe_module_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.customfield:module ) }">
				<MvASSIGN NAME = "l.safe_field_code"	VALUE = "{ [ g.Module_Feature_TUI_UT ].StripSpecialCharacters_TemplateCode( l.customfield:code ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<mvt:if expr="{ 'NOT ISNULL l.settings:item:customfield_values:' $ l.safe_module_code $ ':' $ l.safe_field_code }">
					<div class="custom-field" id="{ l.safe_module_code $ '-' $ l.safe_field_code }">&mvt:customfield_names:<MvEVAL EXPR = "{ l.safe_module_code }">:<MvEVAL EXPR = "{ l.safe_field_code }">;: &mvt:item:customfield_values:<MvEVAL EXPR = "{ l.safe_module_code }">:<MvEVAL EXPR = "{ l.safe_field_code }">;</div>
				</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			</MvFOREACH>
			<MvIF EXPR = "{ l.settings:lineitemdiscounts }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
				<mvt:foreach iterator="discount" array="item:discounts">
					<mvt:if expr="l.settings:discount:display">
						<div class="item-discount">&mvt:discount:descrip;: &mvt:discount:formatted_discount;</div>
					</mvt:if>
				</mvt:foreach><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
			</span>
			</div>
		</td>
		<td class="item-quantity"><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ NOT l.settings:editable }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			&mvt:item:quantity;<MIVA STANDARDOUTPUTLEVEL = "">
		<MvELSE>
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:item:upsold">
			&mvt:item:quantity;
			<mvt:else>
			<form method="post" action="&mvte:urls:BASK:auto;">
			<input type="hidden" name="Action" value="QNTY" />
			<input type="hidden" name="Basket_Line" value="&mvte:item:line_id;" />
			<input type="hidden" name="Old_Screen" value="BASK" />
			<input type="hidden" name="Offset" value="&mvte:global:Offset;" />
			<input type="hidden" name="AllOffset" value="&mvte:global:AllOffset;" />
			<input type="hidden" name="CatListingOffset" value="&mvte:global:CatListingOffset;" />
			<input type="hidden" name="RelatedOffset" value="&mvte:global:RelatedOffset;" />
			<input type="hidden" name="SearchOffset" value="&mvte:global:SearchOffset;" />
			<input type="text" name="Quantity" class="textfield basket-qty-input small" value="&mvt:item:quantity;" />
			<mvt:item name="buttons" param="Update" />
			</form>
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ ( NOT ISNULL l.settings:imagetypes[ 1 ] ) AND ( NOT l.settings:button_wish ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<div class="item-remove-link"><a href="&mvte:urls:BASK:auto_sep;Action=RPRD&amp;Basket_Line=&mvta:item:line_id;&amp;Offset=&mvta:global:Offset;&amp;AllOffset=&mvta:global:AllOffset;&amp;CatListingOffset=&mvta:global:CatListingOffset;&amp;RelatedOffset=&mvta:global:RelatedOffset;&amp;SearchOffset=&mvta:global:SearchOffset;">Remove</a></div><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>
		</MvIF>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		</td>
		<td class="item-price"><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ l.settings:lineitemprice EQ 'retail' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:item:retail AND l.settings:item:retail NE l.settings:item:price">
				<span style="text-decoration: line-through;">&mvt:item:formatted_retail;</span>
			<mvt:elseif expr="l.settings:item:base_price NE l.settings:item:price">
				<span style="text-decoration: line-through;">&mvt:item:formatted_base_price;</span>
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSEIF EXPR = "{ l.settings:lineitemprice EQ 'base' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:item:base_price NE l.settings:item:price">
				<span style="text-decoration: line-through;">&mvt:item:formatted_base_price;</span>
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
			&mvt:item:formatted_price;
		</td>
		<td class="item-total">
			&mvt:item:formatted_total;
		</td>
	</tr>
	<mvt:foreach iterator="option" array="item:options">
	<tr class="item-options">
		<td class="item-name">
			<mvt:if expr="l.settings:option:option_id">
				&mvt:option:attr_code;: &mvt:option:opt_code;
			<mvt:elseif expr="NOT ISNULL l.settings:option:data">
				&mvt:option:attr_code;: &mvt:option:data;
			<mvt:elseif expr="NOT ISNULL l.settings:option:data_long">
				&mvt:option:attr_code;: &mvt:option:data_long;
			<mvt:else>
				&mvt:option:attr_code;
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ l.settings:lineitemdiscounts }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:foreach iterator="discount" array="option:discounts">
				<mvt:if expr="l.settings:discount:display">
					<div class="item-discount">&mvt:discount:descrip;: &mvt:discount:formatted_discount;</div>
				</mvt:if>
			</mvt:foreach><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
		</td>
		<td class="item-quantity">&nbsp;</td>
		<td class="item-price"><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ l.settings:lineitemprice EQ 'retail' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:option:retail OR l.settings:option:base_price OR l.settings:option:price">
				<mvt:if expr="l.settings:option:retail AND l.settings:option:retail NE l.settings:option:price">
					<span style="text-decoration: line-through;">&mvt:option:formatted_retail;</span>
				<mvt:elseif expr="l.settings:option:base_price NE l.settings:option:price">
					<span style="text-decoration: line-through;">&mvt:option:formatted_base_price;</span>
				</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSEIF EXPR = "{ l.settings:lineitemprice EQ 'base' }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:option:base_price OR l.settings:option:price">
				<mvt:if expr="l.settings:option:base_price NE l.settings:option:price">
					<span style="text-decoration: line-through;">&mvt:option:formatted_base_price;</span>
				</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="l.settings:option:price"><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
				&mvt:option:formatted_price;
			<mvt:else>
				&nbsp;
			</mvt:if>
		</td>
		<td class="item-total">
			&nbsp;
		</td>
	</tr>
	</mvt:foreach>

	<mvt:if expr="l.settings:item:subterm_id">
		<tr class="item-options">
			<td class="item-name">
				Subscription: &mvte:item:productsubscriptionterm:descrip;
			</td>
			<td class="item-quantity">&nbsp;</td>
			<td class="item-price">&nbsp;</td>
			<td class="item-total">&nbsp;</td>
		</tr>
	</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:button_wish }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<tr class="item-options">
		<td class="item-name">
			<a href="&mvte:urls:BASK:auto_sep;Action=RPRD&amp;Basket_Line=&mvta:item:line_id;&amp;Offset=&mvta:global:Offset;&amp;AllOffset=&mvta:global:AllOffset;&amp;CatListingOffset=&mvta:global:CatListingOffset;&amp;RelatedOffset=&mvta:global:RelatedOffset;&amp;SearchOffset=&mvta:global:SearchOffset;">Remove</a>
			<mvt:if expr="l.settings:item:product:id AND ( NOT l.settings:item:product:productsubscriptionsettings:enabled OR NOT l.settings:item:product:productsubscriptionsettings:mandatory )">
				<span class="grey">|</span>
				<a href="&mvte:urls:WISH:secure_sep;Action=MPWL&amp;Line_ID=&mvta:item:line_id;">Move To Wish List</a>
			</mvt:if>
		</td>
		<td class="item-quantity">&nbsp;</td>
		<td class="item-price">&nbsp;</td>
		<td class="item-total">&nbsp;</td>
	</tr><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
	</mvt:foreach><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:lineitemcoupons }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<mvt:foreach iterator="coupon" array="basket:coupons">
	<tr><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ l.settings:editable AND NOT ISNULL l.settings:imagetypes[ 1 ] }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="item-remove">
			<form method="post" action="&mvte:urls:BASK:auto;">
			<input type="hidden" name="Action" value="RCPN" />
			<input type="hidden" name="Coupon_Code" value="&mvte:coupon:code;" />
			<mvt:item name="buttons" param="Remove" />
			</form>
		</td><MIVA STANDARDOUTPUTLEVEL = "">
		<MvELSEIF EXPR = "{ NOT ISNULL l.settings:imagetypes[ 1 ] }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="item-remove">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="item-name">
			<div class="item-name">Coupon -
			<span class="item-code">
				&mvt:coupon:code;
			</span>
			<mvt:if expr="NOT ISNULL l.settings:coupon:descrip">
			<div class="item-discount">&mvt:coupon:descrip;</div>
			</mvt:if>
			</div>
		</td>
		<td class="item-quantity">&nbsp;</td>
		<td class="item-price">&nbsp;</td>
		<td class="item-total">&nbsp;</td>
	</tr><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:editable AND ISNULL l.settings:imagetypes[ 1 ] }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<tr class="item-options">
		<td class="item-name">
			<a href="&mvte:urls:BASK:auto_sep;Action=RCPN&amp;Coupon_Code=&mvte:coupon:code;&amp;Offset=&mvta:global:Offset;&amp;AllOffset=&mvta:global:AllOffset;&amp;CatListingOffset=&mvta:global:CatListingOffset;&amp;RelatedOffset=&mvta:global:RelatedOffset;&amp;SearchOffset=&mvta:global:SearchOffset;">Remove</a>
		</td>
		<td class="item-quantity">&nbsp;</td>
		<td class="item-price">&nbsp;</td>
		<td class="item-total">&nbsp;</td>
	</tr><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
	</mvt:foreach><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<mvt:foreach iterator="charge" array="basket:charges">
	<tr class="basket-charges"><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ ( l.settings:editable AND NOT l.settings:button_wish ) OR ( NOT ISNULL l.settings:imagetypes[ 1 ] ) }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td colspan="3">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
		<MvELSE>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td colspan="2">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="charge-description">&mvt:charge:descrip;:</td>
		<td class="formatted-charge">&mvt:charge:formatted_disp_amt;</td>
	</tr>
	</mvt:foreach>
	<tr id="basket-totals"><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ ( l.settings:editable AND NOT l.settings:button_wish ) OR ( NOT ISNULL l.settings:imagetypes[ 1 ] ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td colspan="3"><MIVA STANDARDOUTPUTLEVEL = "">
		<MvELSE><MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td colspan="2"><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF><MvIF EXPR = "{ ( NOT l.settings:applycoupons ) AND ( NOT l.settings:redeemgiftcerts ) }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			&nbsp;<MIVA STANDARDOUTPUTLEVEL = "">
		<MvELSE>
			<MvIF EXPR = "{ l.settings:applycoupons AND l.settings:redeemgiftcerts }">
				<MvASSIGN NAME = "l.input_class" 	VALUE = "input_width_small">
				<MvASSIGN NAME = "l.class"			VALUE = "floatright">
			<MvELSE>
				<MvASSIGN NAME = "l.input_class" 	VALUE = "input_width_medium">
				<MvASSIGN NAME = "l.class"			VALUE = "floatleft">
			</MvIF>
			
			<MvIF EXPR = "{ l.settings:applycoupons }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<form class="floatleft" method="post" action="&mvte:urls:_self:auto;">
			<input type="hidden" name="Action" value="ACPN" />
			Coupon: <input type="text" name="Coupon_Code" class="{ l.input_class }"> <mvt:item name="buttons" param="ApplyCoupon" />
			</form><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>

			<MvIF EXPR = "{ l.settings:redeemgiftcerts }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
			<mvt:if expr="g.Basket:cust_id EQ 0">
				<a class="{ l.class }" href="&mvte:urls:GFTL:auto;">Redeem Gift Certificate</a>
			<mvt:else>
				<form class="{ l.class }" method="post" action="&mvte:urls:_self:auto;">
				<input type="hidden" name="Action" value="RDGC" />
				Gift Certificate:
				<input type="text" name="GiftCertificate_Code" class="{ l.input_class }"> <mvt:item name="buttons" param="ApplyGiftCert" />
				</form>
			</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>
		</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
		</td>
		<td class="total-prompt">Total:</td>
		<td class="formatted-total">&mvt:basket:formatted_total;</td>
	</tr><MIVA STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:splitpayments }"><MIVA STANDARDOUTPUTLEVEL = "text, html">
	<mvt:foreach iterator="split" array="splitpayment:splits">
	<tr class="basket-payments"><MIVA STANDARDOUTPUTLEVEL = "">
		<MvIF EXPR = "{ ( l.settings:editable AND NOT l.settings:button_wish ) OR ( NOT ISNULL l.settings:imagetypes[ 1 ] ) }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td colspan="3">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
		<MvELSE>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td colspan="2">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
		</MvIF>
		<MIVA STANDARDOUTPUTLEVEL = "text, html">
		<td class="charge-description">&mvt:split:desc;:</td>
		<td class="formatted-charge">&mvt:split:formatted_amount_negative;</td>
	</tr>
	</mvt:foreach>
	<mvt:if expr="( NOT ISNULL l.settings:splitpayment:remaining ) AND ( l.settings:splitpayment:remaining NE l.settings:basket:total )">
		<tr id="basket-remaining"><MIVA STANDARDOUTPUTLEVEL = "">
			<MvIF EXPR = "{ ( l.settings:editable AND NOT l.settings:button_wish ) OR ( NOT ISNULL l.settings:imagetypes[ 1 ] ) }">
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td colspan="3">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
			<MvELSE>
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td colspan="2">&nbsp;</td><MIVA STANDARDOUTPUTLEVEL = "">
			</MvIF>
			<MIVA STANDARDOUTPUTLEVEL = "text, html">
			<td class="total-prompt">Total Remaining:</td>
			<td class="formatted-total">&mvt:splitpayment:formatted_remaining;</td>
		</tr>
	</mvt:if><MIVA STANDARDOUTPUTLEVEL = "">
	</MvIF><MIVA STANDARDOUTPUTLEVEL = "text, html">
</table>
<div class="clear"></div><MIVA STANDARDOUTPUTLEVEL = "">
	</MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Provision" PARAMETERS = "module var, provide_xml var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean( 'O', l.provide_xml, 'Editable', l.settings:editable ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ BasketComponentModule_Provision( l.module, l.provide_xml, l.settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "SkinsComponentModule_Export_Item" PARAMETERS = "module var, item, output var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.output" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
		<MvFOREACH ITERATOR = "l.page" ARRAY = "l.pages" COUNT = "{ [ g.Module_Feature_TUI_DB ].PageList_Load_Item_Runtime( l.item:id, l.pages ) }">
			<MvREFERENCEARRAY NAME = "l.settings" VARIABLE = "l.page:settings">
				<MvMEMBER NAME = "{ l.item:code }">
			</MvREFERENCEARRAY>

			<Page_Update code="{ l.page:code }">
				<Item code="{ l.item:code }">
					<Editable><MvIF EXPR = "{ l.settings:editable }">Yes<MvELSE>No</MvIF></Editable>
			
					<MvEVAL EXPR = "{ BasketSkinsComponentModule_Export_ItemPageSettings( l.module, l.item, l.page, l.settings, l.page_output ) }">
					<MvEVAL EXPR = "{ l.page_output }">
				</Item>
			</Page_Update>
		</MvFOREACH>
	</MvCAPTURE>
</MvFUNCTION>

<MvINCLUDE FILE = "modules/component/cmp-include-basket.mv">

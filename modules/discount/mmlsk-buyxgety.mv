<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2020 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DSC-BXY-
| Next Error Code: 1    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-discount_buyxgety">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Buy X Get Y">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "9.01">
	<MvASSIGN NAME = "l.module:features"	VALUE = "discount">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature discount
|
</MvCOMMENT>

<MvFUNCTION NAME = "DiscountModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:items"					VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:basket"				VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:shipping"				VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:tax"					VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:qualifying"			VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:provision_settings"	VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Fields" PARAMETERS = "module var, pricegroup var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.pricegroup:id }">
		<MvASSIGN NAME = "g.BuyXGetY_ApplyTo"				VALUE = "{ l.pricegroup:config:applyto }">
		<MvASSIGN NAME = "g.BuyXGetY_X"						VALUE = "{ l.pricegroup:config:x }">
		<MvASSIGN NAME = "g.BuyXGetY_Y"						VALUE = "{ l.pricegroup:config:y }">
		<MvASSIGN NAME = "g.BuyXGetY_Discount_Type"			VALUE = "{ l.pricegroup:config:type }">

		<MvIF EXPR = "{ g.BuyXGetY_Discount_Type EQ 'percent' }">
			<MvASSIGN NAME = "g.BuyXGetY_Discount_Amount"	VALUE = "{ l.pricegroup:config:amount }">
		<MvELSE>
			<MvASSIGN NAME = "g.BuyXGetY_Discount_Amount"	VALUE = "{ l.pricegroup:config:amount ROUND 2 }">
		</MvIF>

		<MvASSIGN NAME = "g.BuyXGetY_Selection"				VALUE = "{ l.pricegroup:config:selection }">
		<MvASSIGN NAME = "g.BuyXGetY_Max_Quantity"			VALUE = "{ l.pricegroup:config:max_quantity }">
	<MvELSE>
		<MvASSIGN NAME = "g.BuyXGetY_ApplyTo"				VALUE = "all">
		<MvASSIGN NAME = "g.BuyXGetY_X"						VALUE = 1>
		<MvASSIGN NAME = "g.BuyXGetY_Y"						VALUE = 1>
		<MvASSIGN NAME = "g.BuyXGetY_Discount_Type"			VALUE = "percent">
		<MvASSIGN NAME = "g.BuyXGetY_Discount_Amount"		VALUE = 100>
		<MvASSIGN NAME = "g.BuyXGetY_Selection"				VALUE = "lowest">
		<MvASSIGN NAME = "g.BuyXGetY_Max_Quantity"			VALUE = "0">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "applyto,x,y,discount,selection,max_quantity">
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.field_id EQ 'applyto' }">				<MvFUNCTIONRETURN VALUE = "Apply To:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'x' }">				<MvFUNCTIONRETURN VALUE = "Buy:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'y' }">				<MvFUNCTIONRETURN VALUE = "Get:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'discount' }">		<MvFUNCTIONRETURN VALUE = "Discount:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'selection' }">		<MvFUNCTIONRETURN VALUE = "Apply Discount To:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'max_quantity' }">	<MvFUNCTIONRETURN VALUE = "Maximum Discount Quantity:">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.field_id EQ 'applyto' }">
		<select name="BuyXGetY_ApplyTo">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'all',				g.BuyXGetY_ApplyTo, 'All Eligible Products' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'same_product',	g.BuyXGetY_ApplyTo, 'Same Product' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'same_variant',	g.BuyXGetY_ApplyTo, 'Same Product Variant' ) }">
		</select>
	<MvELSEIF EXPR = "{ l.field_id EQ 'x' }">
		<input type="text" size="5" name="BuyXGetY_X" value="{ encodeentities( g.BuyXGetY_X ) }"> or More Qualifying Products
	<MvELSEIF EXPR = "{ l.field_id EQ 'y' }">
		<input type="text" size="5" name="BuyXGetY_Y" value="{ encodeentities( g.BuyXGetY_Y ) }"> Discounted Product(s) at Discounted Price
	<MvELSEIF EXPR = "{ l.field_id EQ 'discount' }">
		<select name="BuyXGetY_Discount_Type">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'fixed',	g.BuyXGetY_Discount_Type, 'Fixed' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'percent',	g.BuyXGetY_Discount_Type, 'Percent' ) }">
		</select>

		<input type="text" size="5" name="BuyXGetY_Discount_Amount" value="{ encodeentities( g.BuyXGetY_Discount_Amount ) }">
	<MvELSEIF EXPR = "{ l.field_id EQ 'selection' }">
		<select name="BuyXGetY_Selection">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'lowest',	g.BuyXGetY_Selection, 'Lowest' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'highest',	g.BuyXGetY_Selection, 'Highest' ) }">
		</select>

		Priced Line Item
	<MvELSEIF EXPR = "{ l.field_id EQ 'max_quantity' }">
		<input type="text" size="5" name="BuyXGetY_Max_Quantity" value="{ encodeentities( g.BuyXGetY_Max_Quantity ) }"> (0 = no limit)
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Validate" PARAMETERS = "module var, pricegroup var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.BuyXGetY_ApplyTo"				VALUE = "{ trim( g.BuyXGetY_ApplyTo ) }">
	<MvASSIGN NAME = "g.BuyXGetY_X"						VALUE = "{ trim( g.BuyXGetY_X ) }">
	<MvASSIGN NAME = "g.BuyXGetY_Y"						VALUE = "{ trim( g.BuyXGetY_Y ) }">
	<MvASSIGN NAME = "g.BuyXGetY_Discount_Type"			VALUE = "{ trim( g.BuyXGetY_Discount_Type ) }">
	<MvASSIGN NAME = "g.BuyXGetY_Discount_Amount"		VALUE = "{ trim( g.BuyXGetY_Discount_Amount ) }">
	<MvASSIGN NAME = "g.BuyXGetY_Selection"				VALUE = "{ trim( g.BuyXGetY_Selection ) }">
	<MvASSIGN NAME = "g.BuyXGetY_Max_Quantity"			VALUE = "{ trim( g.BuyXGetY_Max_Quantity ) }">

	<MvIF EXPR = "{ ( g.BuyXGetY_ApplyTo NE 'all' )				AND
					( g.BuyXGetY_ApplyTo NE 'same_product' )	AND
					( g.BuyXGetY_ApplyTo NE 'same_variant' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select the products to which the discount applies' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( g.BuyXGetY_X ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'BuyXGetY_X', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( g.BuyXGetY_Y ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'BuyXGetY_Y', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ ( g.BuyXGetY_Discount_Type NE 'fixed' )		AND
					( g.BuyXGetY_Discount_Type NE 'percent' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select a discount type' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Currency_NonNegative_Optional( g.BuyXGetY_Discount_Amount ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'BuyXGetY_Discount_Amount', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ ( g.BuyXGetY_Selection NE 'lowest' )		AND
					( g.BuyXGetY_Selection NE 'highest' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select the line item to which the discount should be applied' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( g.BuyXGetY_Max_Quantity ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'BuyXGetY_Max_Quantity', g.Validation_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Update" PARAMETERS = "module var, pricegroup var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pricegroup:config:applyto"		VALUE = "{ g.BuyXGetY_ApplyTo }">
	<MvASSIGN NAME = "l.pricegroup:config:x"			VALUE = "{ g.BuyXGetY_X }">
	<MvASSIGN NAME = "l.pricegroup:config:y"			VALUE = "{ g.BuyXGetY_Y }">
	<MvASSIGN NAME = "l.pricegroup:config:type"			VALUE = "{ g.BuyXGetY_Discount_Type }">
	<MvASSIGN NAME = "l.pricegroup:config:amount"		VALUE = "{ g.BuyXGetY_Discount_Amount }">
	<MvASSIGN NAME = "l.pricegroup:config:selection"	VALUE = "{ g.BuyXGetY_Selection }">
	<MvASSIGN NAME = "l.pricegroup:config:max_quantity"	VALUE = "{ g.BuyXGetY_Max_Quantity }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Discount_Items" PARAMETERS = "module var, pricegroup var, discount_state var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.pricegroup:config:applyto EQ 'all' }">					<MvEVAL EXPR = "{ BuyXGetY_Discounts_Build_All( l.pricegroup, l.discount_state, l.discounts ) }">
	<MvELSEIF EXPR = "{ l.pricegroup:config:applyto EQ 'same_product' }">		<MvEVAL EXPR = "{ BuyXGetY_Discounts_Build_Same_Product( l.pricegroup, l.discount_state, l.discounts ) }">
	<MvELSEIF EXPR = "{ l.pricegroup:config:applyto EQ 'same_variant' }">		<MvEVAL EXPR = "{ BuyXGetY_Discounts_Build_Same_Variant( l.pricegroup, l.discount_state, l.discounts ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| Apply the discount(s) using the configured selection direction until we hit the limit
	| of discounted products or run out of items to discount.
	|
	| We must apply our own sorting logic because of the way the module "consumes" items to be discounted.
	| For discount predictions to work properly, we must break ties by line_id ascending (rather than the default descending),
	| so that higher line_id items receive discounts, as is the case with all the other discounting modules.
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.eligible_item_count"		VALUE = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Eligible_Items_Sorted_TotalPriceDescending( l.discount_state, l.eligible_items ) }">
	<MvASSIGN NAME = "l.qualifying_item_count"		VALUE = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Qualifying_Items_Sorted_TotalPriceAscending( l.discount_state, l.qualifying_items ) }">

	<MvIF EXPR = "{ l.pricegroup:config:selection EQ 'highest' }">
		<MvASSIGN NAME = "l.eligible_item_count"	VALUE = "{ miva_array_sort( l.eligible_items, 'BuyXGetY_Item_Sort_TotalPriceDescending', l.null ) }">
		<MvASSIGN NAME = "l.qualifying_item_count"	VALUE = "{ miva_array_sort( l.qualifying_items, 'BuyXGetY_Item_Sort_TotalPriceAscending', l.null ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.eligible_item_count"	VALUE = "{ miva_array_sort( l.eligible_items, 'BuyXGetY_Item_Sort_TotalPriceAscending', l.null ) }">
		<MvASSIGN NAME = "l.qualifying_item_count"	VALUE = "{ miva_array_sort( l.qualifying_items, 'BuyXGetY_Item_Sort_TotalPriceDescending', l.null ) }">
	</MvIF>

	<MvEVAL EXPR = "{ BuyXGetY_QuantityRemaining_Build( l.eligible_items, l.eligible_item_count, l.quantity_remaining ) }">
	<MvEVAL EXPR = "{ BuyXGetY_QuantityRemaining_Build( l.qualifying_items, l.qualifying_item_count, l.quantity_remaining ) }">

	<MvCOMMENT>
	|
	| Calculate discounts
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.total_discounted"						VALUE = 0>

	<MvFOREACH ITERATOR = "l.eligible_item" ARRAY = "l.eligible_items" COUNT = "{ l.eligible_item_count }">
		<MvIF EXPR = "{ ( l.eligible_item:line_id LE 0 ) OR ( l.eligible_item:product_id LE 0 ) OR ( l.eligible_item:variant_id LT 0 ) }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvREFERENCEARRAY NAME = "l.discount" VARIABLE = "l.discounts">
			<MvDIMENSION INDEX = "{ l.eligible_item:product_id }">
			<MvMEMBER NAME = "variants">
			<MvDIMENSION INDEX = "{ l.eligible_item:variant_id + 1 }">
		</MvREFERENCEARRAY>

		<MvWHILE EXPR = "{ NOT l.discount:exhausted }">
			<MvIF EXPR = "{ l.discount:discount_quantity EQ 0 }">
				<MvIF EXPR = "{ ( l.discount:eligible_quantity LE 0 ) OR ( l.discount:qualifying_quantity LT l.pricegroup:config:x ) }">
					<MvASSIGN NAME = "l.discount:exhausted"			VALUE = 1>
					<MvWHILESTOP>
				</MvIF>

				<MvASSIGN NAME = "l.discount:qualifying_quantity"	VALUE = "{ l.discount:qualifying_quantity - l.pricegroup:config:x }">

				<MvEVAL EXPR = "{ BuyXGetY_QuantityRemaining_Decrement( l.pricegroup, l.eligible_item:product_id, l.eligible_item:variant_id, l.qualifying_items, l.qualifying_item_count, l.quantity_remaining, l.pricegroup:config:x ) }">

				<MvIF EXPR = "{ l.discount:eligible_quantity LT l.pricegroup:config:y }">	<MvASSIGN NAME = "l.discount:discount_quantity"	VALUE = "{ l.discount:eligible_quantity }">
				<MvELSE>																	<MvASSIGN NAME = "l.discount:discount_quantity"	VALUE = "{ l.pricegroup:config:y }">
				</MvIF>

				<MvASSIGN NAME = "l.discount:eligible_quantity"		VALUE = "{ l.discount:eligible_quantity - l.discount:discount_quantity }">
			</MvIF>

			<MvREFERENCEARRAY NAME = "l.eligible_item_quantity_remaining" VARIABLE = "l.quantity_remaining">
				<MvDIMENSION INDEX = "{ l.eligible_item:line_id }">
			</MvREFERENCEARRAY>

			<MvIF EXPR = "{ l.eligible_item_quantity_remaining EQ 0 }">
				<MvFOREACHCONTINUE>
			</MvIF>

			<MvIF EXPR = "{ l.discount:discount_quantity GT l.eligible_item_quantity_remaining }">	<MvASSIGN NAME = "l.eligible_item_discount_quantity"	VALUE = "{ l.eligible_item_quantity_remaining }">
			<MvELSE>																				<MvASSIGN NAME = "l.eligible_item_discount_quantity"	VALUE = "{ l.discount:discount_quantity }">
			</MvIF>

			<MvIF EXPR = "{ l.pricegroup:config:max_quantity AND ( ( l.total_discounted + l.eligible_item_discount_quantity ) GT l.pricegroup:config:max_quantity ) }">
				<MvASSIGN NAME = "l.eligible_item_discount_quantity"		VALUE = "{ l.pricegroup:config:max_quantity - l.total_discounted }">
			</MvIF>

			<MvIF EXPR = "{ l.pricegroup:config:type EQ 'fixed' }">			<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Discount_Item_Fixed( l.discount_state, l.eligible_item, l.eligible_item_discount_quantity, l.pricegroup:config:amount ) }">
			<MvELSEIF EXPR = "{ l.pricegroup:config:type EQ 'percent' }">	<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Discount_Item_Percent( l.discount_state, l.eligible_item, l.eligible_item_discount_quantity, l.pricegroup:config:amount ) }">
			</MvIF>

			<MvASSIGN NAME = "l.eligible_item_quantity_remaining"			VALUE = "{ l.eligible_item_quantity_remaining - l.eligible_item_discount_quantity }">
			<MvASSIGN NAME = "l.discount:discount_quantity"					VALUE = "{ l.discount:discount_quantity - l.eligible_item_discount_quantity }">
			<MvASSIGN NAME = "l.total_discounted"							VALUE = "{ l.total_discounted + l.eligible_item_discount_quantity }">

			<MvIF EXPR = "{ l.eligible_item:qualifying }">
				<MvASSIGN NAME = "l.discount:qualifying_quantity"			VALUE = "{ l.discount:qualifying_quantity - l.eligible_item_discount_quantity }">
			</MvIF>

			<MvIF EXPR = "{ l.pricegroup:config:max_quantity AND ( l.total_discounted GE l.pricegroup:config:max_quantity ) }">
				<MvFOREACHSTOP>
			</MvIF>
		</MvWHILE>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Provision_Settings" PARAMETERS = "module var, pricegroup var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.pricegroup:module_id NE l.module:id }">
		<MvASSIGN NAME = "l.pricegroup:config:applyto"		VALUE = "all">
		<MvASSIGN NAME = "l.pricegroup:config:x"			VALUE = 1>
		<MvASSIGN NAME = "l.pricegroup:config:y"			VALUE = 1>
		<MvASSIGN NAME = "l.pricegroup:config:type"			VALUE = "percent">
		<MvASSIGN NAME = "l.pricegroup:config:amount"		VALUE = 100>
		<MvASSIGN NAME = "l.pricegroup:config:selection"	VALUE = "lowest">
		<MvASSIGN NAME = "l.pricegroup:config:max_quantity"	VALUE = "0">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'o', l.provide_xml, 'ApplyTo',		l.pricegroup:config:applyto,	'All,SameProduct,SameVariant',	'all,same_product,same_variant' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'o', l.provide_xml, 'X',			l.pricegroup:config:x )																				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'o', l.provide_xml, 'Y',			l.pricegroup:config:y )																				OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'o', l.provide_xml, 'Type',			l.pricegroup:config:type,		'Fixed,Percent',				'fixed,percent' )					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Number(		'o', l.provide_xml, 'Amount',		l.pricegroup:config:amount,		10, 2 )																OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'o', l.provide_xml, 'Selection',	l.pricegroup:config:selection,	'Lowest,Highest',				'lowest,highest' )					OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'o', l.provide_xml, 'MaxQuantity',	l.pricegroup:config:max_quantity ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.pricegroup:config:x LE 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'X must be a whole number greater than 0' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.pricegroup:config:y LE 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Y must be a whole number greater than 0' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.pricegroup:config:amount LT 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Amount must be a non-negative number' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.pricegroup:config:max_quantity LT 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'MaxQuantity must be a non-negative whole number' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_PriceGroup_Delete" PARAMETERS = "module var, pricegroup var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Helper functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "BuyXGetY_Discounts_Build_All" PARAMETERS = "pricegroup var, discount_state var, discounts var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.exhausted"											VALUE = 0>
	<MvASSIGN NAME = "l.qualifying_quantity"								VALUE = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Qualifying_Quantity( l.discount_state ) }">
	<MvASSIGN NAME = "l.eligible_quantity"									VALUE = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Eligible_Quantity( l.discount_state ) }">
	<MvASSIGN NAME = "l.discount_quantity"									VALUE = 0>

	<MvFOREACH ITERATOR = "l.eligible_product" ARRAY = "l.eligible_products" COUNT = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Eligible_Products( l.discount_state, l.eligible_products ) }">
		<MvIF EXPR = "{ l.eligible_product:product_id LE 0 }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvREFERENCEARRAY NAME = "l.discount_product" VARIABLE = "l.discounts">
			<MvDIMENSION INDEX = "{ l.eligible_product:product_id }">
		</MvREFERENCEARRAY>

		<MvFOREACH ITERATOR = "l.variant" ARRAY = "l.eligible_product:variants">
			<MvIF EXPR = "{ l.variant:variant_id LT 0 }">
				<MvFOREACHCONTINUE>
			</MvIF>

			<MvREFERENCEARRAY NAME = "l.discount_variant" VARIABLE = "l.discount_product:variants">
				<MvDIMENSION INDEX = "{ l.variant:variant_id + 1 }">
			</MvREFERENCEARRAY>
			
			<MvREFERENCE NAME = "l.discount_variant:exhausted"				VARIABLE = "l.exhausted">
			<MvREFERENCE NAME = "l.discount_variant:qualifying_quantity"	VARIABLE = "l.qualifying_quantity">
			<MvREFERENCE NAME = "l.discount_variant:eligible_quantity"		VARIABLE = "l.eligible_quantity">
			<MvREFERENCE NAME = "l.discount_variant:discount_quantity"		VARIABLE = "l.discount_quantity">
		</MvFOREACH>
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "BuyXGetY_Discounts_Build_Same_Product" PARAMETERS = "pricegroup var, discount_state var, discounts var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.eligible_product" ARRAY = "l.eligible_products" COUNT = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Eligible_Products( l.discount_state, l.eligible_products ) }">
		<MvIF EXPR = "{ l.eligible_product:product_id LE 0 }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvREFERENCEARRAY NAME = "l.discount_product" VARIABLE = "l.discounts">
			<MvDIMENSION INDEX = "{ l.eligible_product:product_id }">
		</MvREFERENCEARRAY>

		<MvASSIGN NAME = "l.discount_product:exhausted"						VALUE = 0>
		<MvASSIGN NAME = "l.discount_product:qualifying_quantity"			VALUE = "{ l.eligible_product:qualifying_quantity }">
		<MvASSIGN NAME = "l.discount_product:eligible_quantity"				VALUE = "{ l.eligible_product:eligible_quantity }">
		<MvASSIGN NAME = "l.discount_product:discount_quantity"				VALUE = 0>

		<MvFOREACH ITERATOR = "l.variant" ARRAY = "l.eligible_product:variants">
			<MvIF EXPR = "{ l.variant:variant_id LT 0 }">
				<MvFOREACHCONTINUE>
			</MvIF>

			<MvREFERENCEARRAY NAME = "l.discount_variant" VARIABLE = "l.discount_product:variants">
				<MvDIMENSION INDEX = "{ l.variant:variant_id + 1 }">
			</MvREFERENCEARRAY>

			<MvIF EXPR = "{ NOT l.eligible_product:qualifying }">
				<MvASSIGN NAME = "l.discount_product:qualifying_quantity"	VALUE = "{ l.discount_product:qualifying_quantity + l.variant:qualifying_quantity }">
			</MvIF>
			
			<MvREFERENCE NAME = "l.discount_variant:exhausted"				VARIABLE = "l.discount_product:exhausted">
			<MvREFERENCE NAME = "l.discount_variant:qualifying_quantity"	VARIABLE = "l.discount_product:qualifying_quantity">
			<MvREFERENCE NAME = "l.discount_variant:eligible_quantity"		VARIABLE = "l.discount_product:eligible_quantity">
			<MvREFERENCE NAME = "l.discount_variant:discount_quantity"		VARIABLE = "l.discount_product:discount_quantity">
		</MvFOREACH>
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "BuyXGetY_Discounts_Build_Same_Variant" PARAMETERS = "pricegroup var, discount_state var, discounts var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.eligible_product" ARRAY = "l.eligible_products" COUNT = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Eligible_Products( l.discount_state, l.eligible_products ) }">
		<MvIF EXPR = "{ l.eligible_product:product_id LE 0 }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvFOREACH ITERATOR = "l.eligible_variant" ARRAY = "l.eligible_product:variants">
			<MvIF EXPR = "{ ( l.eligible_variant:variant_id LT 0 ) OR ( NOT l.eligible_variant:qualifying ) }">
				<MvFOREACHCONTINUE>
			</MvIF>

			<MvREFERENCEARRAY NAME = "l.discount_variant" VARIABLE = "l.discounts">
				<MvDIMENSION INDEX = "{ l.eligible_product:product_id }">
				<MvMEMBER NAME = "variants">
				<MvDIMENSION INDEX = "{ l.eligible_variant:variant_id + 1 }">
			</MvREFERENCEARRAY>

			<MvASSIGN NAME = "l.discount_variant:exhausted"					VALUE = 0>
			<MvASSIGN NAME = "l.discount_variant:qualifying_quantity"		VALUE = "{ l.eligible_variant:qualifying_quantity }">
			<MvASSIGN NAME = "l.discount_variant:eligible_quantity"			VALUE = "{ l.eligible_variant:eligible_quantity }">
			<MvASSIGN NAME = "l.discount_variant:discount_quantity"			VALUE = 0>
		</MvFOREACH>
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "BuyXGetY_QuantityRemaining_Build" PARAMETERS = "items var, item_count, quantity_remaining var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.item" ARRAY = "l.items" COUNT = "{ l.item_count }">
		<MvIF EXPR = "{ l.item:line_id LE 0 }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.quantity_remaining" INDEX = "{ l.item:line_id }"	VALUE = "{ l.item:quantity }">
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "BuyXGetY_QuantityRemaining_Decrement" PARAMETERS = "pricegroup var, product_id, variant_id, items var, item_count, quantity_remaining var, amount" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| Two passes are required.  In the first pass, we skip eligible items.  If needed, a second pass is made that
	| covers items which are both qualifying and eligible.
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.filter"			VALUE = "{ '( l.item:line_id GT 0 ) AND l.item:qualifying AND ( NOT l.item:eligible )' }">

	<MvIF EXPR = "{ l.pricegroup:config:applyto EQ 'same_product' }">		<MvASSIGN NAME = "l.filter" VALUE = "{ l.filter $ ' AND ( l.item:product_id EQ l.product_id )' }">
	<MvELSEIF EXPR = "{ l.pricegroup:config:applyto EQ 'same_variant' }">	<MvASSIGN NAME = "l.filter" VALUE = "{ l.filter $ ' AND ( l.item:product_id EQ l.product_id ) AND ( l.item:variant_id EQ l.variant_id )' }">
	</MvIF>

	<MvASSIGN NAME = "l.item_pos"		VALUE = "{ miva_array_search( l.items, 1, l.item, l.filter ) }">
	<MvWHILE EXPR = "{ l.item_pos }">
		<MvASSIGN NAME = "l.amount"		VALUE = "{ BuyXGetY_QuantityRemaining_Decrement_Amount( l.quantity_remaining[ l.item:line_id ], l.amount ) }">

		<MvIF EXPR = "{ l.amount EQ 0 }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvASSIGN NAME = "l.item_pos"	VALUE = "{ miva_array_search( l.items, l.item_pos + 1, l.item, l.filter ) }">
	</MvWHILE>

	<MvCOMMENT>
	|
	| Second pass, permitting eligible items.
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.filter"			VALUE = "{ '( l.item:line_id GT 0 ) AND l.item:qualifying' }">

	<MvIF EXPR = "{ l.pricegroup:config:applyto EQ 'same_product' }">		<MvASSIGN NAME = "l.filter" VALUE = "{ l.filter $ ' AND ( l.item:product_id EQ l.product_id )' }">
	<MvELSEIF EXPR = "{ l.pricegroup:config:applyto EQ 'same_variant' }">	<MvASSIGN NAME = "l.filter" VALUE = "{ l.filter $ ' AND ( l.item:product_id EQ l.product_id ) AND ( l.item:variant_id EQ l.variant_id )' }">
	</MvIF>

	<MvASSIGN NAME = "l.item_pos"		VALUE = "{ miva_array_search( l.items, 1, l.item, l.filter ) }">
	<MvWHILE EXPR = "{ l.item_pos }">
		<MvASSIGN NAME = "l.amount"		VALUE = "{ BuyXGetY_QuantityRemaining_Decrement_Amount( l.quantity_remaining[ l.item:line_id ], l.amount ) }">

		<MvIF EXPR = "{ l.amount EQ 0 }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvASSIGN NAME = "l.item_pos"	VALUE = "{ miva_array_search( l.items, l.item_pos + 1, l.item, l.filter ) }">
	</MvWHILE>
</MvFUNCTION>

<MvFUNCTION NAME = "BuyXGetY_QuantityRemaining_Decrement_Amount" PARAMETERS = "item_quantity_remaining var, amount" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.amount GT l.item_quantity_remaining }">	<MvASSIGN NAME = "l.decrement_amount"	VALUE = "{ l.item_quantity_remaining }">
	<MvELSE>													<MvASSIGN NAME = "l.decrement_amount"	VALUE = "{ l.amount }">
	</MvIF>

	<MvASSIGN NAME = "l.item_quantity_remaining"	VALUE = "{ l.item_quantity_remaining - l.decrement_amount }">

	<MvFUNCTIONRETURN VALUE = "{ l.amount - l.decrement_amount }">
</MvFUNCTION>

<MvFUNCTION NAME = "BuyXGetY_Item_Sort_TotalPriceDescending" PARAMETERS = "left var, right var, data var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.left:total_price GT l.right:total_price }">		<MvFUNCTIONRETURN VALUE = -1>
	<MvELSEIF EXPR = "{ l.left:total_price LT l.right:total_price }">	<MvFUNCTIONRETURN VALUE = 1>

	<MvELSEIF EXPR = "{ l.left:line_id LT l.right:line_id }">			<MvFUNCTIONRETURN VALUE = -1>
	<MvELSEIF EXPR = "{ l.left:line_id GT l.right:line_id }">			<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "BuyXGetY_Item_Sort_TotalPriceAscending" PARAMETERS = "left var, right var, data var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.left:total_price LT l.right:total_price }">		<MvFUNCTIONRETURN VALUE = -1>
	<MvELSEIF EXPR = "{ l.left:total_price GT l.right:total_price }">	<MvFUNCTIONRETURN VALUE = 1>

	<MvELSEIF EXPR = "{ l.left:line_id LT l.right:line_id }">			<MvFUNCTIONRETURN VALUE = -1>
	<MvELSEIF EXPR = "{ l.left:line_id GT l.right:line_id }">			<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2020 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DSC-PRD-
| Next Error Code: 1    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-discount_product">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Product Discount">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "9.01">
	<MvASSIGN NAME = "l.module:features"	VALUE = "discount">
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:items"					VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:basket"				VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:shipping"				VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:tax"					VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:provision_settings"	VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Fields" PARAMETERS = "module var, pricegroup var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.pricegroup:id }">
		<MvASSIGN NAME = "g.Product_Discount"		VALUE = "{ l.pricegroup:config:discount ROUND 2 }">
		<MvASSIGN NAME = "g.Product_Type"			VALUE = "{ l.pricegroup:config:type }">
		<MvASSIGN NAME = "g.Product_Selection"		VALUE = "{ l.pricegroup:config:selection }">
		<MvASSIGN NAME = "g.Product_MaxQuantity"	VALUE = "{ l.pricegroup:config:maxquantity }">
		<MvASSIGN NAME = "g.Product_MaxDiscount"	VALUE = "{ l.pricegroup:config:maxdiscount ROUND 2 }">
	<MvELSE>
		<MvASSIGN NAME = "g.Product_Discount"		VALUE = 0.00>
		<MvASSIGN NAME = "g.Product_Type"			VALUE = "percent">
		<MvASSIGN NAME = "g.Product_Selection"		VALUE = "lowest">
		<MvASSIGN NAME = "g.Product_MaxQuantity"	VALUE = 0>
		<MvASSIGN NAME = "g.Product_MaxDiscount"	VALUE = 0.00>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "discount,selection,maxquantity,maxdiscount">
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.field_id EQ 'discount' }">		<MvFUNCTIONRETURN VALUE = "Discount:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'selection' }">	<MvFUNCTIONRETURN VALUE = "Apply Discount To:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'maxquantity' }">	<MvFUNCTIONRETURN VALUE = "Maximum Item Discount Quantity:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'maxdiscount' }">	<MvFUNCTIONRETURN VALUE = "Maximum Cumulative Discount:">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.field_id EQ 'discount' }">
		<input type="text" size="5" name="Product_Discount" value="{ encodeentities( g.Product_Discount ) }">

		<select name="Product_Type">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'fixed',	g.Product_Type, 'Fixed' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'percent',	g.Product_Type, 'Percent' ) }">
		</select>
	<MvELSEIF EXPR = "{ l.field_id EQ 'selection' }">
		<select name="Product_Selection">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'lowest',	g.Product_Selection, 'Lowest' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'highest',	g.Product_Selection, 'Highest' ) }">
		</select>

		Priced Line Item
	<MvELSEIF EXPR = "{ l.field_id EQ 'maxquantity' }">
		<input type="text" size="5" name="Product_MaxQuantity" value="{ encodeentities( g.Product_MaxQuantity ) }"> (0 = unlimited)
	<MvELSEIF EXPR = "{ l.field_id EQ 'maxdiscount' }">
		<input type="text" size="5" name="Product_MaxDiscount" value="{ encodeentities( g.Product_MaxDiscount ) }"> (0 = unlimited)
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Validate" PARAMETERS = "module var, pricegroup var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Product_Discount"		VALUE = "{ trim( g.Product_Discount ) }">
	<MvASSIGN NAME = "g.Product_Type"			VALUE = "{ trim( g.Product_Type ) }">
	<MvASSIGN NAME = "g.Product_Selection"		VALUE = "{ trim( g.Product_Selection ) }">
	<MvASSIGN NAME = "g.Product_MaxQuantity"	VALUE = "{ trim( g.Product_MaxQuantity ) }">
	<MvASSIGN NAME = "g.Product_MaxDiscount"	VALUE = "{ trim( g.Product_MaxDiscount ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Currency_NonNegative_Required( g.Product_Discount ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Product_Discount', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ ( g.Product_Type NE 'fixed' )	AND
					( g.Product_Type NE 'percent' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select the type of discount calculation' ) }">
	</MvIF>

	<MvIF EXPR = "{ ( g.Product_Selection NE 'lowest' )	AND
					( g.Product_Selection NE 'highest' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select the line item to which the discount should be applied' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( g.Product_MaxQuantity ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Product_MaxQuantity', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Currency_NonNegative_Required( g.Product_MaxDiscount ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Product_MaxDiscount', g.Validation_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Update" PARAMETERS = "module var, pricegroup var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pricegroup:config:discount"		VALUE = "{ g.Product_Discount }">
	<MvASSIGN NAME = "l.pricegroup:config:type"			VALUE = "{ g.Product_Type }">
	<MvASSIGN NAME = "l.pricegroup:config:selection"	VALUE = "{ g.Product_Selection }">
	<MvASSIGN NAME = "l.pricegroup:config:maxquantity"	VALUE = "{ g.Product_MaxQuantity }">
	<MvASSIGN NAME = "l.pricegroup:config:maxdiscount"	VALUE = "{ g.Product_MaxDiscount }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Discount_Items" PARAMETERS = "module var, pricegroup var, discount_state var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( l.pricegroup:config:type NE 'fixed' ) AND
					( l.pricegroup:config:type NE 'percent' ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.pricegroup:config:selection EQ 'highest' }">		<MvASSIGN NAME = "l.item_count" VALUE = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Eligible_Items_Sorted_TotalPriceDescending( l.discount_state, l.items ) }">
	<MvELSE>															<MvASSIGN NAME = "l.item_count" VALUE = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Eligible_Items_Sorted_TotalPriceAscending( l.discount_state, l.items ) }">
	</MvIF>

	<MvASSIGN NAME = "l.discount_remaining"					VALUE = "{ l.pricegroup:config:maxdiscount }">
	<MvASSIGN NAME = "l.discount_quantity"					VALUE = "{ l.pricegroup:config:maxquantity }">

	<MvFOREACH ITERATOR = "l.item" ARRAY = "l.items" COUNT = "{ l.item_count }">
		<MvIF EXPR = "{ ( l.pricegroup:config:maxquantity EQ 0 ) OR
						( l.item:quantity LT l.discount_quantity ) }">	<MvASSIGN NAME = "l.item_discount_quantity"	VALUE = "{ l.item:quantity }">
		<MvELSE>														<MvASSIGN NAME = "l.item_discount_quantity" VALUE = "{ l.discount_quantity }">
		</MvIF>

		<MvIF EXPR = "{ l.pricegroup:config:maxdiscount EQ 0 }">
			<MvIF EXPR = "{ l.pricegroup:config:type EQ 'fixed' }">			<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Discount_Item_Fixed( l.discount_state, l.item, l.item_discount_quantity, l.pricegroup:config:discount ) }">
			<MvELSEIF EXPR = "{ l.pricegroup:config:type EQ 'percent' }">	<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Discount_Item_Percent( l.discount_state, l.item, l.item_discount_quantity, l.pricegroup:config:discount ) }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ l.pricegroup:config:type EQ 'fixed' }">
				<MvIF EXPR = "{ l.item:eligible_price GT l.pricegroup:config:discount }">	<MvASSIGN NAME = "l.item_discount"	VALUE = "{ l.pricegroup:config:discount }">
				<MvELSE>																	<MvASSIGN NAME = "l.item_discount"	VALUE = "{ l.item:eligible_price }">
				</MvIF>
			<MvELSEIF EXPR = "{ l.pricegroup:config:type EQ 'percent' }">
				<MvASSIGN NAME = "l.item_discount"			VALUE = "{ l.item:eligible_price * ( l.pricegroup:config:discount / 100 ) }">
			</MvIF>

			<MvFOR COUNT = "{ l.item_discount_quantity }">
				<MvIF EXPR = "{ l.item_discount GT l.discount_remaining }">
					<MvASSIGN NAME = "l.item_discount"		VALUE = "{ l.discount_remaining }">
				</MvIF>

				<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Discount_Item_Fixed( l.discount_state, l.item, 1, l.item_discount ) }">

				<MvASSIGN NAME = "l.discount_remaining"		VALUE = "{ l.discount_remaining - l.item_discount }">
				<MvIF EXPR = "{ l.discount_remaining EQ 0 }">
					<MvFOREACHSTOP>
				</MvIF>
			</MvFOR>
		</MvIF>
			
		<MvIF EXPR = "{ l.pricegroup:config:maxquantity }">
			<MvASSIGN NAME = "l.discount_quantity"			VALUE = "{ l.discount_quantity - l.item_discount_quantity }">
			<MvIF EXPR = "{ l.discount_quantity EQ 0 }">
				<MvFOREACHSTOP>
			</MvIF>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Provision_Settings" PARAMETERS = "module var, pricegroup var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.pricegroup:module_id NE l.module:id }">
		<MvASSIGN NAME = "l.pricegroup:config:discount"		VALUE = 0.00>
		<MvASSIGN NAME = "l.pricegroup:config:type"			VALUE = "percent">
		<MvASSIGN NAME = "l.pricegroup:config:selection"	VALUE = "lowest">
		<MvASSIGN NAME = "l.pricegroup:config:maxquantity"	VALUE = 0>
		<MvASSIGN NAME = "l.pricegroup:config:maxdiscount"	VALUE = 0.00>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Number(		'o', l.provide_xml, 'Discount',		l.pricegroup:config:discount,		10, 2 )									OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'o', l.provide_xml, 'Type',			l.pricegroup:config:type,			'Fixed,Percent',	'fixed,percent' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(		'o', l.provide_xml, 'Selection',	l.pricegroup:config:selection,		'Lowest,Highest',	'lowest,highest' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Integer(	'o', l.provide_xml, 'MaxQuantity',	l.pricegroup:config:maxquantity )											OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Number(		'o', l.provide_xml, 'MaxDiscount',	l.pricegroup:config:maxdiscount,	10, 2 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Currency_NonNegative_Required( l.pricegroup:config:discount ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Discount must be a non-negative number' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_NonNegative_Required( l.pricegroup:config:maxquantity ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'MaxQuantity must be a non-negative whole number' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Currency_NonNegative_Required( l.pricegroup:config:maxdiscount ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'MaxDiscount must be a non-negative number' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_PriceGroup_Delete" PARAMETERS = "module var, pricegroup var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

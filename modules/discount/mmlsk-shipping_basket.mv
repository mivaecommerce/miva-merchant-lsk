<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2020 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DSC-SHB-
| Next Error Code: 1    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-discount_shipping_basket">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Shipping Discount (Entire Order)">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "9.01">
	<MvASSIGN NAME = "l.module:features"	VALUE = "discount">
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:items"					VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:basket"				VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:shipping"				VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:tax"					VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:provision_settings"	VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Fields" PARAMETERS = "module var, pricegroup var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.pricegroup:id }">
		<MvASSIGN NAME = "g.Shipping_Basket"							VALUE = "{ l.pricegroup:config }">
	<MvELSE>
		<MvASSIGN NAME = "g.Shipping_Basket:list_adjust"				VALUE = 1>
		<MvASSIGN NAME = "g.Shipping_Basket:list_descrip"				VALUE = "none">
		<MvASSIGN NAME = "g.Shipping_Basket:list_append"				VALUE = "">
		<MvASSIGN NAME = "g.Shipping_Basket:list_replace"				VALUE = "">

		<MvASSIGN NAME = "g.Shipping_Basket:charge"						VALUE = "modify">
		<MvASSIGN NAME = "g.Shipping_Basket:charge_modify_descrip"		VALUE = "none">
		<MvASSIGN NAME = "g.Shipping_Basket:charge_modify_append"		VALUE = " (discounted)">
		<MvASSIGN NAME = "g.Shipping_Basket:charge_modify_replace"		VALUE = "Discounted Shipping">
		<MvASSIGN NAME = "g.Shipping_Basket:charge_add_descrip"			VALUE = "Shipping Discount">

		<MvASSIGN NAME = "g.Shipping_Basket:states"						VALUE = "">
		<MvASSIGN NAME = "g.Shipping_Basket:countries"					VALUE = "">
		<MvASSIGN NAME = "g.Shipping_Basket:zips"						VALUE = "">

		<MvASSIGN NAME = "g.Shipping_Basket:methods"					VALUE = "">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "list,charge,methods,geo_restrictions,zip_restrictions">
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.field_id EQ 'list' }">					<MvFUNCTIONRETURN VALUE = "Method Selection:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'charge' }">				<MvFUNCTIONRETURN VALUE = "Display in Basket:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'methods' }">				<MvFUNCTIONRETURN VALUE = "Method Discounts:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'geo_restrictions' }">	<MvFUNCTIONRETURN VALUE = "Geographic Restrictions:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'zip_restrictions' }">	<MvFUNCTIONRETURN VALUE = "Restrict to Zip Codes:">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.field_id EQ 'list' }">
		<div style="margin-top: 4px;">
			<div style="display: flex; align-items: center;"><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.Shipping_Basket:list_adjust, 'Shipping_Basket:list_adjust', 'Yes', 'Reflect Discount in Shipping Method List' ) }"></div>
			<div style="padding: 3px 0;"><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'Shipping_Basket:list_descrip', 'none',	g.Shipping_Basket:list_descrip, 'Do Not Modify Shipping Method Name' ) }"></div>
			<div style="padding: 3px 0;"><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'Shipping_Basket:list_descrip', 'append',	g.Shipping_Basket:list_descrip, 'Append' ) }"> <input type="text" size="20" name="Shipping_Basket:list_append" value="{ encodeentities( g.Shipping_Basket:list_append ) }"> to Shipping Method Name</div>
			<div style="padding: 3px 0;"><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'Shipping_Basket:list_descrip', 'replace',	g.Shipping_Basket:list_descrip, 'Replace Shipping Method Name with' ) }"> <input type="text" size="20" name="Shipping_Basket:list_replace" value="{ encodeentities( g.Shipping_Basket:list_replace ) }"></div>
		</div>
	<MvELSEIF EXPR = "{ l.field_id EQ 'charge' }">
		<div style="margin-top: 4px;">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'Shipping_Basket:charge', 'modify',	g.Shipping_Basket:charge, 'Modify Applied Charge to Reflect Discount' ) }">
			<div style="margin-left: 20px;">
				<div style="padding: 3px 0;"><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'Shipping_Basket:charge_modify_descrip', 'none',		g.Shipping_Basket:charge_modify_descrip, 'Do Not Modify Description' ) }"></div>
				<div style="padding: 3px 0;"><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'Shipping_Basket:charge_modify_descrip', 'append',		g.Shipping_Basket:charge_modify_descrip, 'Append' ) }"> <input type="text" size="20" name="Shipping_Basket:charge_modify_append" value="{ encodeentities( g.Shipping_Basket:charge_modify_append ) }"> to Description</div>
				<div style="padding: 3px 0;"><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'Shipping_Basket:charge_modify_descrip', 'replace',	g.Shipping_Basket:charge_modify_descrip, 'Replace Description with' ) }"> <input type="text" size="20" name="Shipping_Basket:charge_modify_replace" value="{ encodeentities( g.Shipping_Basket:charge_modify_replace ) }"></div>
			</div>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'Shipping_Basket:charge', 'add',		g.Shipping_Basket:charge, 'Add a Charge Showing the Discount Amount' ) }">
			<div style="margin-left: 20px; padding: 3px 0;">
				Description: <input type="text" size="20" name="Shipping_Basket:charge_add_descrip" value="{ encodeentities( g.Shipping_Basket:charge_add_descrip ) }">
			</div>
		</div>
	<MvELSEIF EXPR = "{ l.field_id EQ 'methods' }">
		<MvASSIGN NAME = "l.method_count" VALUE = "{ [ g.Module_Feature_SHP_UT ].ShippingMethodList_Load_Enabled( l.methods ) }">

		<MvIF EXPR = "{ l.method_count LT 1 }">
			<div style="margin-top: 7px;">
				No shipping methods are configured
			</div>
		<MvELSE>
			<table border="0" cellspacing="0" cellpadding="2" style="margin-top: 4px;">
			<MvFOREACH INDEX = "l.pos" ITERATOR = "l.method" ARRAY = "l.methods" COUNT = "{ l.method_count }">
				<MvREFERENCEARRAY NAME = "l.method_settings" VARIABLE = "g.Shipping_Basket">
					<MvMEMBER NAME = "methods">
					<MvMEMBER NAME = "{ l.method:module:code }">
					<MvMEMBER NAME = "{ l.method:code }">
				</MvREFERENCEARRAY>

				<MvASSIGN NAME = "l.method_prefix"				VALUE = "{ 'Shipping_Basket:methods[' $ l.pos $ ']:' }">
				<MvASSIGN NAME = "l.method_settings:discount"	VALUE = "{ l.method_settings:discount ROUND 2 }">

				<input type="hidden" name="{ l.method_prefix $ 'module_code' }" value="{ encodeentities( l.method:module:code ) }">
				<input type="hidden" name="{ l.method_prefix $ 'method_code' }" value="{ encodeentities( l.method:code ) }">

				<tr>
					<td valign="top"><input type="text" size="5" name="{ l.method_prefix $ 'discount' }" value="{ encodeentities( l.method_settings:discount ) }"></td>
					<td valign="top">
						<select name="{ l.method_prefix $ 'discount_type' }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'fixed',	l.method_settings:discount_type, 'Fixed' ) }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'percent',	l.method_settings:discount_type, 'Percent' ) }">
						</select>
					</td>
					<td><MvEVAL EXPR = "{ [ g.Module_Feature_SHP_UT ].ShippingMethod_EncodeEntities( l.method:name ) }"></td>
				</tr>
			</MvFOREACH>
			</table>
		</MvIF>
	<MvELSEIF EXPR = "{ l.field_id EQ 'geo_restrictions' }">
		<MvASSIGN NAME = "l.restrict_states"	VALUE = "{ NOT ISNULL g.Shipping_Basket:states }">
		<MvASSIGN NAME = "l.restrict_countries"	VALUE = "{ NOT ISNULL g.Shipping_Basket:countries }">

		<table border="0" cellspacing="0" cellpadding="2" style="margin-top: 4px;">
			<tr>
				<td>
					<label>
						<MvIF EXPR = "{ l.restrict_states }">	<input type="checkbox" id="shipping_basket_restrict_states" name="Shipping_Basket_Restrict_States" value="Yes" onchange="document.getElementById( 'shipping_basket_states' ).disabled = !this.checked;" checked /> Restrict to These States:
						<MvELSE>								<input type="checkbox" id="shipping_basket_restrict_states" name="Shipping_Basket_Restrict_States" value="Yes" onchange="document.getElementById( 'shipping_basket_states' ).disabled = !this.checked;" /> Restrict to These States:
						</MvIF>
					</label>
				</td>

				<td>
					<label>
						<MvIF EXPR = "{ l.restrict_countries }">	<input type="checkbox" id="shipping_basket_restrict_countries" name="Shipping_Basket_Restrict_Countries" value="Yes" onchange="document.getElementById( 'shipping_basket_countries' ).disabled = !this.checked;" checked /> Restrict to These Countries:
						<MvELSE>									<input type="checkbox" id="shipping_basket_restrict_countries" name="Shipping_Basket_Restrict_Countries" value="Yes" onchange="document.getElementById( 'shipping_basket_countries' ).disabled = !this.checked;" /> Restrict to These Countries:
						</MvIF>
					</label>
				</td>
			</tr>
	
			<tr>
				<td>
					<MvIF EXPR = "{ l.restrict_states }">	<select id="shipping_basket_states" name="Shipping_Basket:states" multiple style="margin-left: 25px; margin-right: 50px; height: 120px;">
					<MvELSE>								<select id="shipping_basket_states" name="Shipping_Basket:states" multiple style="margin-left: 25px; margin-right: 50px; height: 120px;" disabled>
					</MvIF>

					<MvFOREACH ITERATOR = "l.state" ARRAY = "l.states" COUNT = "{ [ g.Module_Library_DB ].StateList_Load_All( l.states ) }">
						<MvIF EXPR = "{ NOT ISNULL l.state:code }">
							<MvIF EXPR = "{ miva_member_exists( g.Shipping_Basket:states, l.state:code ) }">	<option value="{ encodeentities( l.state:code ) }" selected><MvEVAL EXPR = "{ encodeentities( l.state:name ) }"></option>
							<MvELSE>																			<option value="{ encodeentities( l.state:code ) }"><MvEVAL EXPR = "{ encodeentities( l.state:name ) }"></option>
							</MvIF>
						</MvIF>
					</MvFOREACH>
					</select>
				</td>

				<td>
					<MvIF EXPR = "{ l.restrict_countries }">	<select id="shipping_basket_countries" name="Shipping_Basket:countries" multiple style="margin-left: 25px; height: 120px;">
					<MvELSE>									<select id="shipping_basket_countries" name="Shipping_Basket:countries" multiple style="margin-left: 25px; height: 120px;" disabled>
					</MvIF>

					<MvFOREACH ITERATOR = "l.country" ARRAY = "l.countries" COUNT = "{ [ g.Module_Library_DB ].CountryList_Load_All( l.countries ) }">
						<MvIF EXPR = "{ NOT ISNULL l.country:alpha }">
							<MvIF EXPR = "{ miva_member_exists( g.Shipping_Basket:countries, l.country:alpha ) }">	<option value="{ encodeentities( l.country:alpha ) }" selected><MvEVAL EXPR = "{ encodeentities( l.country:name ) }"></option>
							<MvELSE>																				<option value="{ encodeentities( l.country:alpha ) }"><MvEVAL EXPR = "{ encodeentities( l.country:name ) }"></option>
							</MvIF>
						</MvIF>
					</MvFOREACH>
					</select>
				</td>
			</tr>
		</table>
	<MvELSEIF EXPR = "{ l.field_id EQ 'zip_restrictions' }">
		<input type="text" name="Shipping_Basket:zips" size="35" value="{ encodeentities( g.Shipping_Basket:zips ) }" /> (comma separated, empty = no restriction)
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Validate" PARAMETERS = "module var, pricegroup var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Shipping_Basket:list_adjust"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Shipping_Basket:list_adjust ) }">
	<MvASSIGN NAME = "g.Shipping_Basket:list_descrip"				VALUE = "{ trim( g.Shipping_Basket:list_descrip ) }">
	<MvASSIGN NAME = "g.Shipping_Basket:list_append"				VALUE = "{ rtrim( g.Shipping_Basket:list_append ) }">
	<MvASSIGN NAME = "g.Shipping_Basket:list_replace"				VALUE = "{ trim( g.Shipping_Basket:list_replace ) }">
	<MvASSIGN NAME = "g.Shipping_Basket:charge"						VALUE = "{ trim( g.Shipping_Basket:charge ) }">
	<MvASSIGN NAME = "g.Shipping_Basket:charge_modify_descrip"		VALUE = "{ trim( g.Shipping_Basket:charge_modify_descrip ) }">
	<MvASSIGN NAME = "g.Shipping_Basket:charge_modify_append"		VALUE = "{ rtrim( g.Shipping_Basket:charge_modify_append ) }">
	<MvASSIGN NAME = "g.Shipping_Basket:charge_modify_replace"		VALUE = "{ trim( g.Shipping_Basket:charge_modify_replace ) }">
	<MvASSIGN NAME = "g.Shipping_Basket:charge_add_descrip"			VALUE = "{ trim( g.Shipping_Basket:charge_add_descrip ) }">
	<MvASSIGN NAME = "g.Shipping_Basket:zips"						VALUE = "{ trim( g.Shipping_Basket:zips ) }">
	<MvASSIGN NAME = "g.Shipping_Basket_Restrict_States"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Shipping_Basket_Restrict_States ) }">
	<MvASSIGN NAME = "g.Shipping_Basket_Restrict_Countries"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.Shipping_Basket_Restrict_Countries ) }">

	<MvIF EXPR = "{ miva_variable_type( g.Shipping_Basket:states ) NE 'ARRAY' }">
		<MvASSIGN NAME = "l.state"									VALUE = "{ g.Shipping_Basket:states }">
		<MvASSIGN NAME = "g.Shipping_Basket:states" INDEX = 1		VALUE = "{ l.state }">
	</MvIF>

	<MvFOREACH ITERATOR = "l.state" ARRAY = "g.Shipping_Basket:states">
		<MvASSIGN NAME = "l.state"									VALUE = "{ trim( l.state ) }">
	</MvFOREACH>

	<MvIF EXPR = "{ miva_variable_type( g.Shipping_Basket:countries ) NE 'ARRAY' }">
		<MvASSIGN NAME = "l.country"								VALUE = "{ g.Shipping_Basket:countries }">
		<MvASSIGN NAME = "g.Shipping_Basket:countries" INDEX = 1	VALUE = "{ l.country }">
	</MvIF>

	<MvFOREACH ITERATOR = "l.country" ARRAY = "g.Shipping_Basket:countries">
		<MvASSIGN NAME = "l.country"								VALUE = "{ trim( l.country ) }">
	</MvFOREACH>

	<MvIF EXPR = "{ g.Shipping_Basket:list_descrip EQ 'append' }">
		<MvIF EXPR = "{ ISNULL g.Shipping_Basket:list_append }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Shipping_Basket:list_append', 'Please enter a value to be appended to the shipping method name' ) }">
		</MvIF>
	<MvELSEIF EXPR = "{ g.Shipping_Basket:list_descrip EQ 'replace' }">
		<MvIF EXPR = "{ ISNULL g.Shipping_Basket:list_replace }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Shipping_Basket:list_replace', 'Please enter a replacement value for the shipping method name' ) }">
		</MvIF>
	<MvELSEIF EXPR = "{ g.Shipping_Basket:list_descrip NE 'none' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select a method selection behavior' ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Shipping_Basket:charge EQ 'modify' }">
		<MvIF EXPR = "{ g.Shipping_Basket:charge_modify_descrip EQ 'append' }">
			<MvIF EXPR = "{ ISNULL g.Shipping_Basket:charge_modify_append }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Shipping_Basket:charge_modify_append', 'Please enter a value to be appended to the shipping charge description' ) }">
			</MvIF>
		<MvELSEIF EXPR = "{ g.Shipping_Basket:charge_modify_descrip EQ 'replace' }">
			<MvIF EXPR = "{ ISNULL g.Shipping_Basket:charge_modify_replace }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Shipping_Basket:charge_modify_replace', 'Please enter a replacement description for the shipping charge' ) }">
			</MvIF>
		<MvELSEIF EXPR = "{ g.Shipping_Basket:charge_modify_descrip NE 'none' }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select a charge modification behavior' ) }">
		</MvIF>
	<MvELSEIF EXPR = "{ g.Shipping_Basket:charge EQ 'add' }">
		<MvIF EXPR = "{ ISNULL g.Shipping_Basket:charge_add_descrip }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Shipping_Basket:charge_add_descrip', 'Please enter a description for the discount charge' ) }">
		</MvIF>
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select a basket display behavior' ) }">
	</MvIF>

	<MvFOREACH INDEX = "l.pos" ITERATOR = "l.method" ARRAY = "g.Shipping_Basket:methods">
		<MvASSIGN NAME = "l.method:discount"					VALUE = "{ trim( l.method:discount ) }">
		<MvASSIGN NAME = "l.method:discount_type"				VALUE = "{ trim( l.method:discount_type ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Currency_NonNegative_Optional( l.method:discount ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Shipping_Basket:methods[' $ l.pos $ ']:discount', g.Validation_Message ) }">
		</MvIF>

		<MvIF EXPR = "{ ( l.method:discount_type NE 'fixed' ) AND
						( l.method:discount_type NE 'percent' ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select a discount type' ) }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Update" PARAMETERS = "module var, pricegroup var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pricegroup:config:list_adjust"				VALUE = "{ g.Shipping_Basket:list_adjust }">
	<MvASSIGN NAME = "l.pricegroup:config:list_descrip"				VALUE = "{ g.Shipping_Basket:list_descrip }">
	<MvASSIGN NAME = "l.pricegroup:config:list_append"				VALUE = "{ g.Shipping_Basket:list_append }">
	<MvASSIGN NAME = "l.pricegroup:config:list_replace"				VALUE = "{ g.Shipping_Basket:list_replace }">
	<MvASSIGN NAME = "l.pricegroup:config:charge"					VALUE = "{ g.Shipping_Basket:charge }">
	<MvASSIGN NAME = "l.pricegroup:config:charge_modify_descrip"	VALUE = "{ g.Shipping_Basket:charge_modify_descrip }">
	<MvASSIGN NAME = "l.pricegroup:config:charge_modify_append"		VALUE = "{ g.Shipping_Basket:charge_modify_append }">
	<MvASSIGN NAME = "l.pricegroup:config:charge_modify_replace"	VALUE = "{ g.Shipping_Basket:charge_modify_replace }">
	<MvASSIGN NAME = "l.pricegroup:config:charge_add_descrip"		VALUE = "{ g.Shipping_Basket:charge_add_descrip }">
	<MvASSIGN NAME = "l.pricegroup:config:zips"						VALUE = "{ g.Shipping_Basket:zips }">
	<MvASSIGN NAME = "l.pricegroup:config:states"					VALUE = "">
	<MvASSIGN NAME = "l.pricegroup:config:countries"				VALUE = "">

	<MvIF EXPR = "{ g.Shipping_Basket_Restrict_States AND NOT ISNULL g.Shipping_Basket:states }">
		<MvFOREACH ITERATOR = "l.state" ARRAY = "g.Shipping_Basket:states">
			<MvASSIGN NAME = "l.pricegroup:config:states" MEMBER = "{ l.state }" VALUE = 1>
		</MvFOREACH>
	</MvIF>

	<MvIF EXPR = "{ g.Shipping_Basket_Restrict_Countries AND NOT ISNULL g.Shipping_Basket:countries }">
		<MvFOREACH ITERATOR = "l.country" ARRAY = "g.Shipping_Basket:countries">
			<MvASSIGN NAME = "l.pricegroup:config:countries" MEMBER = "{ l.country }" VALUE = 1>
		</MvFOREACH>
	</MvIF>

	<MvCOMMENT>
	|
	| Only copy over methods that have non-0 discounts
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.pricegroup:config:methods"					VALUE = "">

	<MvFOREACH ITERATOR = "l.method" ARRAY = "g.Shipping_Basket:methods">
		<MvIF EXPR = "{ l.method:discount }">
			<MvASSIGNARRAY NAME = "l.pricegroup" VALUE = "{ l.method:discount }">
				<MvMEMBER NAME = "config">
				<MvMEMBER NAME = "methods">
				<MvMEMBER NAME = "{ l.method:module_code }">
				<MvMEMBER NAME = "{ l.method:method_code }">
				<MvMEMBER NAME = "discount">
			</MvASSIGNARRAY>

			<MvASSIGNARRAY NAME = "l.pricegroup" VALUE = "{ l.method:discount_type }">
				<MvMEMBER NAME = "config">
				<MvMEMBER NAME = "methods">
				<MvMEMBER NAME = "{ l.method:module_code }">
				<MvMEMBER NAME = "{ l.method:method_code }">
				<MvMEMBER NAME = "discount_type">
			</MvASSIGNARRAY>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Discount_ShippingMethodList" PARAMETERS = "module var, pricegroup var, discount_state var, methods var, method_count" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ Shipping_Basket_ConfigurationExcludes( l.pricegroup, l.discount_state ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFOREACH ITERATOR = "l.method" ARRAY = "l.methods" COUNT = "{ l.method_count }">
		<MvREFERENCEARRAY NAME = "l.method_settings" VARIABLE = "l.pricegroup">
			<MvMEMBER NAME = "config">
			<MvMEMBER NAME = "methods">
			<MvMEMBER NAME = "{ l.method:module:code }">
			<MvMEMBER NAME = "{ l.method:code }">
		</MvREFERENCEARRAY>

		<MvIF EXPR = "{ ( l.method_settings:discount LE 0 ) OR ( l.method:price LE 0 ) }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvIF EXPR = "{ l.pricegroup:config:list_adjust }">						<MvASSIGN NAME = "l.method:price"	VALUE = "{ ( l.method:price - ShippingBasket_Calculate_Discount( l.method_settings, l.method:price ) ) ROUND 2 }">
		</MvIF>

		<MvIF EXPR = "{ l.pricegroup:config:list_descrip EQ 'append' }">		<MvASSIGN NAME = "l.method:name"	VALUE = "{ l.method:name $ l.pricegroup:config:list_append }">
		<MvELSEIF EXPR = "{ l.pricegroup:config:list_descrip EQ 'replace' }">	<MvASSIGN NAME = "l.method:name"	VALUE = "{ l.pricegroup:config:list_replace }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Discount_Shipping" PARAMETERS = "module var, pricegroup var, discount_state var, shipping_module var, shipping_method_code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ Shipping_Basket_ConfigurationExcludes( l.pricegroup, l.discount_state ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.method_settings" VARIABLE = "l.pricegroup">
		<MvMEMBER NAME = "config">
		<MvMEMBER NAME = "methods">
		<MvMEMBER NAME = "{ l.shipping_module:code }">
		<MvMEMBER NAME = "{ l.shipping_method_code }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ l.method_settings:discount LE 0 }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.charge_count"					VALUE = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Charges( l.discount_state, 'SHIPPING', l.charges ) }">
	<MvIF EXPR = "{ l.charge_count EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.shipping_total"					VALUE = 0.00>
	<MvASSIGN NAME = "l.shipping_disp_total"			VALUE = 0.00>

	<MvFOREACH ITERATOR = "l.charge" ARRAY = "l.charges" COUNT = "{ l.charge_count }">
		<MvASSIGN NAME = "l.shipping_total"				VALUE = "{ l.shipping_total + l.charge:amount }">
		<MvASSIGN NAME = "l.shipping_disp_total"		VALUE = "{ l.shipping_disp_total + l.charge:disp_amt }">
	</MvFOREACH>

	<MvIF EXPR = "{ l.shipping_total LE 0 }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.pricegroup:config:charge EQ 'add' }">
		<MvASSIGN NAME = "l.append_charge:module_id"	VALUE = "{ l.module:id }">
		<MvASSIGN NAME = "l.append_charge:type"			VALUE = "SHIPPING">
		<MvASSIGN NAME = "l.append_charge:descrip"		VALUE = "{ l.pricegroup:config:charge_add_descrip }">
		<MvASSIGN NAME = "l.append_charge:tax_exempt"	VALUE = "{ l.charges[ 1 ]:tax_exempt }">
		<MvASSIGN NAME = "l.append_charge:amount"		VALUE = "{ ( 0 - ShippingBasket_Calculate_Discount( l.method_settings, l.shipping_total ) ) ROUND 2 }">
		<MvASSIGN NAME = "l.append_charge:disp_amt"		VALUE = "{ ( 0 - ShippingBasket_Calculate_Discount( l.method_settings, l.shipping_disp_total ) ) ROUND 2 }">

		<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Discount_Charge_Add( l.discount_state, l.append_charge ) }">
	<MvELSEIF EXPR = "{ l.pricegroup:config:charge EQ 'modify' }">
		<MvIF EXPR = "{ l.method_settings:discount_type EQ 'percent' }">
			<MvFOREACH ITERATOR = "l.charge" ARRAY = "l.charges" COUNT = "{ l.charge_count }">
				<MvASSIGN NAME = "l.discount"			VALUE = "{ ShippingBasket_Calculate_Discount( l.method_settings, l.charge:amount ) }">
				<MvASSIGN NAME = "l.disp_discount"		VALUE = "{ ShippingBasket_Calculate_Discount( l.method_settings, l.charge:disp_amt ) }">

				<MvIF EXPR = "{ l.pricegroup:config:charge_modify_descrip EQ 'append' }">
					<MvASSIGN NAME = "l.charge:descrip"	VALUE = "{ l.charge:descrip $ l.pricegroup:config:charge_modify_append }">
				<MvELSEIF EXPR = "{ l.pricegroup:config:charge_modify_descrip EQ 'replace' }">
					<MvASSIGN NAME = "l.charge:descrip"	VALUE = "{ l.pricegroup:config:charge_modify_replace }">
				</MvIF>

				<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Discount_Charge_Fixed( l.discount_state, l.charge, l.discount, l.disp_discount ) }">
			</MvFOREACH>
		<MvELSEIF EXPR = "{ l.method_settings:discount_type EQ 'fixed' }">
			<MvASSIGN NAME = "l.discount"				VALUE = "{ ShippingBasket_Calculate_Discount( l.method_settings, l.shipping_total ) }">
			<MvASSIGN NAME = "l.disp_discount"			VALUE = "{ ShippingBasket_Calculate_Discount( l.method_settings, l.shipping_disp_total ) }">

			<MvIF EXPR = "{ l.discount OR l.disp_discount }">
				<MvREFERENCEARRAY NAME = "l.charge" VARIABLE = "l.charges">
					<MvDIMENSION INDEX = 1>
				</MvREFERENCEARRAY>

				<MvIF EXPR = "{ l.pricegroup:config:charge_modify_descrip EQ 'append' }">
					<MvASSIGN NAME = "l.charge:descrip"	VALUE = "{ l.charge:descrip $ l.pricegroup:config:charge_modify_append }">
				<MvELSEIF EXPR = "{ l.pricegroup:config:charge_modify_descrip EQ 'replace' }">
					<MvASSIGN NAME = "l.charge:descrip"	VALUE = "{ l.pricegroup:config:charge_modify_replace }">
				</MvIF>

				<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Discount_Charge_Fixed( l.discount_state, l.charge, l.discount, l.disp_discount ) }">
			</MvIF>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Provision_Settings" PARAMETERS = "module var, pricegroup var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.pricegroup:module_id NE l.module:id }">
		<MvASSIGN NAME = "l.pricegroup:config:list_adjust"				VALUE = 1>
		<MvASSIGN NAME = "l.pricegroup:config:list_descrip"				VALUE = "none">
		<MvASSIGN NAME = "l.pricegroup:config:list_append"				VALUE = "">
		<MvASSIGN NAME = "l.pricegroup:config:list_replace"				VALUE = "">
		
		<MvASSIGN NAME = "l.pricegroup:config:charge"					VALUE = "modify">
		<MvASSIGN NAME = "l.pricegroup:config:charge_modify_descrip"	VALUE = "none">
		<MvASSIGN NAME = "l.pricegroup:config:charge_modify_append"		VALUE = " (discounted)">
		<MvASSIGN NAME = "l.pricegroup:config:charge_modify_replace"	VALUE = "Discounted Shipping">
		<MvASSIGN NAME = "l.pricegroup:config:charge_add_descrip"		VALUE = "Shipping Discount">

		<MvASSIGN NAME = "l.pricegroup:config:states"					VALUE = "">
		<MvASSIGN NAME = "l.pricegroup:config:countries"				VALUE = "">
		<MvASSIGN NAME = "l.pricegroup:config:zips"						VALUE = "">

		<MvASSIGN NAME = "l.pricegroup:config:methods"					VALUE = "">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean( 'o', l.provide_xml, 'ShowDiscountInMethodList', l.pricegroup:config:list_adjust ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'MethodListName' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_List( l.provide_xml:tags:MethodListName[ 1 ], 'modification', l.pricegroup:config:list_descrip, 'none,append,replace', 'none,append,replace' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ l.pricegroup:config:list_descrip EQ 'append' }">
			<MvASSIGN NAME = "l.pricegroup:config:list_append"			VALUE = "{ rtrim( l.provide_xml:tags:MethodListName[ 1 ]:value ) }">

			<MvIF EXPR = "{ ISNULL l.pricegroup:config:list_append }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'MethodListName may not be empty when the modification type is \'append\'' ) }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.pricegroup:config:list_descrip EQ 'replace' }">
			<MvASSIGN NAME = "l.pricegroup:config:list_replace"			VALUE = "{ trim( l.provide_xml:tags:MethodListName[ 1 ]:value ) }">

			<MvIF EXPR = "{ ISNULL l.pricegroup:config:list_replace }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'MethodListName may not be empty when the modification type is \'replace\'' ) }">
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'DisplayInBasket' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_List( l.provide_xml:tags:DisplayInBasket[ 1 ], 'method', l.pricegroup:config:charge, 'modify,add', 'modify,add' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ l.pricegroup:config:charge EQ 'add' }">
			<MvASSIGN NAME = "l.pricegroup:config:charge_add_descrip"	VALUE = "{ trim( l.provide_xml:tags:DisplayInBasket[ 1 ]:value ) }">
			
			<MvIF EXPR = "{ ISNULL l.pricegroup:config:charge_add_descrip }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'DisplayInBasket may not be empty when the display method type is \'add\'' ) }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.pricegroup:config:charge EQ 'modify' }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_List( l.provide_xml:tags:DisplayInBasket[ 1 ], 'modification', l.pricegroup:config:charge_modify_descrip, 'none,append,replace', 'none,append,replace' ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvIF EXPR = "{ l.pricegroup:config:charge_modify_descrip EQ 'append' }">
				<MvASSIGN NAME = "l.pricegroup:config:charge_modify_append"		VALUE = "{ rtrim( l.provide_xml:tags:DisplayInBasket[ 1 ]:value ) }">

				<MvIF EXPR = "{ ISNULL l.pricegroup:config:charge_modify_append }">
					<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'DisplayInBasket may not be empty when the modification type is \'append\'' ) }">
				</MvIF>
			<MvELSEIF EXPR = "{ l.pricegroup:config:charge_modify_descrip EQ 'replace' }">
				<MvASSIGN NAME = "l.pricegroup:config:charge_modify_replace"	VALUE = "{ trim( l.provide_xml:tags:DisplayInBasket[ 1 ]:value ) }">

				<MvIF EXPR = "{ ISNULL l.pricegroup:config:charge_modify_replace }">
					<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'DisplayInBasket may not be empty when the modification type is \'replace\'' ) }">
				</MvIF>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'MethodDiscounts' ) }">
		<MvFOREACH ITERATOR = "l.child_xml" ARRAY = "l.provide_xml:tags:MethodDiscounts[ 1 ]:children">
			<MvASSIGN NAME = "l.name"		VALUE = "{ tolower( l.child_xml:name ) }">

			<MvIF EXPR = "{ l.name EQ 'methoddiscount' }">
				<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.child_xml, 'module_code',	l.module_code ) OR
								NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.child_xml, 'method_code',	l.method_code ) }">
					<MvFUNCTIONRETURN VALUE = 0>
				</MvIF>

				<MvREFERENCEARRAY NAME = "l.method" VARIABLE = "l.pricegroup">
					<MvMEMBER NAME = "config">
					<MvMEMBER NAME = "methods">
					<MvMEMBER NAME = "{ l.module_code }">
					<MvMEMBER NAME = "{ l.method_code }">
				</MvREFERENCEARRAY>

				<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_List(		l.child_xml, 'type',			l.method:discount_type,	'fixed,percent', 'fixed,percent' )	OR
								NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Number(	l.child_xml, 'discount',		l.method:discount,		10, 2 ) }">
					<MvFUNCTIONRETURN VALUE = 0>
				</MvIF>
			<MvELSE>
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.child_xml, 'Unknown tag' ) }">
			</MvIF>
		</MvFOREACH>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Restrictions' ) }">
		<MvIF EXPR = "{ NOT DiscountModule_Provision_Settings_GeographicRestrictions( l.provide_xml:tags:Restrictions[ 1 ], l.pricegroup ) }">
			<MvFUNCTIONRETURN>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Provision_Settings_GeographicRestrictions" PARAMETERS = "provide_xml var, pricegroup var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'o', l.provide_xml, 'ZipCodes', l.pricegroup:config:zips ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'States' ) }">
		<MvEVAL EXPR = "{ DiscountModule_Provision_Settings_GeographicRestrictions_States( l.provide_xml:tags:States[ 1 ], l.pricegroup:config:states ) }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Countries' ) }">
		<MvEVAL EXPR = "{ DiscountModule_Provision_Settings_GeographicRestrictions_Countries( l.provide_xml:tags:Countries[ 1 ], l.pricegroup:config:countries ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Provision_Settings_GeographicRestrictions_States" PARAMETERS = "provide_xml var, states var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.states"										VALUE = "">

	<MvFOREACH ITERATOR = "l.child_xml" ARRAY = "l.provide_xml:children">
		<MvASSIGN NAME = "l.name"									VALUE = "{ tolower( l.child_xml:name ) }">

		<MvIF EXPR = "{ l.name EQ 'state' }">
			<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.child_xml, 'code', l.state ) }">
				<MvASSIGN NAME = "l.states" MEMBER = "{ l.state }"	VALUE = 1>
			</MvIF>
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.child_xml, 'Unknown tag' ) }">
		</MvIF>
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Provision_Settings_GeographicRestrictions_Countries" PARAMETERS = "provide_xml var, countries var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.countries"										VALUE = "">

	<MvFOREACH ITERATOR = "l.child_xml" ARRAY = "l.provide_xml:children">
		<MvASSIGN NAME = "l.name"										VALUE = "{ tolower( l.child_xml:name ) }">

		<MvIF EXPR = "{ l.name EQ 'country' }">
			<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.child_xml, 'code', l.country ) }">
				<MvASSIGN NAME = "l.countries" MEMBER = "{ l.country }"	VALUE = 1>
			</MvIF>
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.child_xml, 'Unknown tag' ) }">
		</MvIF>
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_PriceGroup_Delete" PARAMETERS = "module var, pricegroup var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingBasket_Calculate_Discount" PARAMETERS = "method_settings var, method_price" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.method_settings:discount_type EQ 'percent' }">
		<MvFUNCTIONRETURN VALUE = "{ ( l.method_price * ( l.method_settings:discount / 100 ) ) ROUND 2 }">
	<MvELSEIF EXPR = "{ l.method_settings:discount_type EQ 'fixed' }">
		<MvIF EXPR = "{ l.method_settings:discount GT l.method_price }">	<MvFUNCTIONRETURN VALUE = "{ l.method_price ROUND 2 }">
		<MvELSE>															<MvFUNCTIONRETURN VALUE = "{ l.method_settings:discount ROUND 2 }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0.00>
</MvFUNCTION>

<MvCOMMENT>
|
| Helper Functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "Shipping_Basket_ConfigurationExcludes" PARAMETERS = "pricegroup var, discount_state var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.custinfo" VALUE = "{ [ g.Module_Feature_PGR_UT ].DiscountState_CustomerInformation( l.discount_state ) }">

	<MvIF EXPR = "{ NOT ISNULL l.pricegroup:config:zips }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].ZipInList( l.pricegroup:config:zips, l.custinfo:ship_zip ) }">
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.pricegroup:config:states }">
		<MvIF EXPR = "{ NOT miva_member_exists( l.pricegroup:config:states, l.custinfo:ship_state ) }">
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.pricegroup:config:countries }">
		<MvIF EXPR = "{ NOT miva_member_exists( l.pricegroup:config:countries, l.custinfo:ship_cntry ) }">
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

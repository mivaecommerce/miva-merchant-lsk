<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2019 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-DSC-BSKPROD-
| Next Error Code: 1
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-discount_basket_product">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Basket Discount (Specific Products)">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "9.00">
	<MvASSIGN NAME = "l.module:features"	VALUE = "discount">
</MvFUNCTION>

<MvCOMMENT>
|
| Discounting Subsystem Feature (discount)
|
</MvCOMMENT>

<MvFUNCTION NAME = "DiscountModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities"						VALUE = "">
	<MvASSIGN NAME = "l.capabilities:items"					VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:eligibility"			VALUE = "builtin">
	<MvASSIGN NAME = "l.capabilities:basket"				VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:shipping"				VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:tax"					VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:provision_settings"	VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Fields" PARAMETERS = "module var, pricegroup var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.pricegroup:id }">
		<MvASSIGN NAME = "g.Basket_Discount"	VALUE = "{ l.pricegroup:config:discount ROUND 2 }">
		<MvASSIGN NAME = "g.Basket_Type"		VALUE = "{ l.pricegroup:config:type }">
		<MvASSIGN NAME = "g.Basket_MaxDiscount"	VALUE = "{ l.pricegroup:config:maxdiscount ROUND 2 }">
	<MvELSE>
		<MvASSIGN NAME = "g.Basket_Discount"	VALUE = 0.00>
		<MvASSIGN NAME = "g.Basket_Type"		VALUE = "fixed">
		<MvASSIGN NAME = "g.Basket_MaxDiscount"	VALUE = 0.00>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "discount,maxdiscount">
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.field_id EQ 'discount' }">		<MvFUNCTIONRETURN VALUE = "Discount:">
	<MvELSEIF EXPR = "{ l.field_id EQ 'maxdiscount' }">	<MvFUNCTIONRETURN VALUE = "Maximum Discount Amount:">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.field_id EQ 'discount' }">
		<input type="text" size="5" name="Basket_Discount" value="{ encodeentities( g.Basket_Discount ) }">

		<select name="Basket_Type">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'fixed',	g.Basket_Type, 'Fixed' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'percent',	g.Basket_Type, 'Percent' ) }">
		</select>
	<MvELSEIF EXPR = "{ l.field_id EQ 'maxdiscount' }">
		<input type="text" size="5" name="Basket_MaxDiscount" value="{ encodeentities( g.Basket_MaxDiscount ) }"> (0 = unlimited)
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Validate" PARAMETERS = "module var, pricegroup var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Basket_Discount"	VALUE = "{ trim( g.Basket_Discount ) }">
	<MvASSIGN NAME = "g.Basket_Type"		VALUE = "{ trim( g.Basket_Type ) }">
	<MvASSIGN NAME = "g.Basket_MaxDiscount"	VALUE = "{ trim( g.Basket_MaxDiscount ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Currency_NonNegative_Required( g.Basket_Discount ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Basket_Discount', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].Value_In_List( g.Basket_Type, 'fixed,percent' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select the type of discount calculation' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Currency_NonNegative_Required( g.Basket_MaxDiscount ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Basket_MaxDiscount', g.Validation_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Update" PARAMETERS = "module var, pricegroup var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pricegroup:config"				VALUE = "">
	<MvASSIGN NAME = "l.pricegroup:config:discount"		VALUE = "{ g.Basket_Discount }">
	<MvASSIGN NAME = "l.pricegroup:config:type"			VALUE = "{ g.Basket_Type }">
	<MvASSIGN NAME = "l.pricegroup:config:maxdiscount"	VALUE = "{ g.Basket_MaxDiscount }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Discount_Basket" PARAMETERS = "module var, pricegroup var, discount_state var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.pricegroup:config:type EQ 'percent' }">	<MvASSIGN NAME = "l.discount" VALUE = "{ ( [ g.Module_Feature_PGR_UT ].DiscountState_Eligible_Total( l.discount_state ) * ( l.pricegroup:config:discount / 100.0 ) ) ROUND 2 }">
	<MvELSEIF EXPR = "{ l.pricegroup:config:type EQ 'fixed' }">	<MvASSIGN NAME = "l.discount" VALUE = "{ l.pricegroup:config:discount }">
	<MvELSE>													<MvASSIGN NAME = "l.discount" VALUE = 0.00>
	</MvIF>

	<MvIF EXPR = "{ l.pricegroup:config:maxdiscount AND ( l.discount GT l.pricegroup:config:maxdiscount ) }">
		<MvASSIGN NAME = "l.discount" VALUE = "{ l.pricegroup:config:maxdiscount }">
	</MvIF>

	<MvIF EXPR = "{ l.discount }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_PGR_UT ].DiscountState_Discount_Eligible_Fixed( l.discount_state, l.discount ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "DiscountModule_Provision_Settings" PARAMETERS = "module var, pricegroup var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.pricegroup:module_id NE l.module:id }">
		<MvASSIGN NAME = "l.pricegroup:config"				VALUE = "">
		<MvASSIGN NAME = "l.pricegroup:config:discount"		VALUE = 0.00>
		<MvASSIGN NAME = "l.pricegroup:config:type"			VALUE = "fixed">
		<MvASSIGN NAME = "l.pricegroup:config:maxdiscount"	VALUE = 0.00>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Number(	'o', l.provide_xml, 'Discount',		l.pricegroup:config:discount,		10, 2 )								OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List(	'o', l.provide_xml, 'Type',			l.pricegroup:config:type,			'Fixed,Percent', 'fixed,percent' )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Number( 'o', l.provide_xml, 'MaxDiscount',	l.pricegroup:config:maxdiscount,	10, 2 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Currency_NonNegative_Required( l.pricegroup:config:discount ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Discount must be a non-negative number' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Currency_NonNegative_Required( l.pricegroup:config:maxdiscount ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'MaxDiscount must be a non-negative number' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

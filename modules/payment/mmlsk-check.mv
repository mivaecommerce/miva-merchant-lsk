<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2020 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-PAY-CHK-
| Next Error Code: 2    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-check">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Check Payment">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.00">
	<MvASSIGN NAME = "l.module:description"	VALUE = "Use this module to enable payment by check.  The Check Payment module has no settings and enabling it does not create a tab.  If you enable this module, \"Check\" appears in the Pay With drop-down list.  It is an offline module; payment is received after the order is created.  For this reason it is useful for test purposes.  When payment is received, manually edit the order to mark the funds as captured.">
	<MvASSIGN NAME = "l.module:features"	VALUE = "payment">
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_LeftNavigation" PARAMETERS = "module var, indent" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Payment_Methods" PARAMETERS = "module var, methods var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.methods" INDEX = 1 MEMBER = "code"	VALUE = "check">
	<MvASSIGN NAME = "l.methods" INDEX = 1 MEMBER = "name"	VALUE = "Check">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Payment_URL" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Payment_Description" PARAMETERS = "module var, data" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "Check">
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Payment_Message" PARAMETERS = "module var, data" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Payment_Hide_Additional_Fields" PARAMETERS = "module var, data" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Payment_Fields" PARAMETERS = "module var, data" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "check">
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Payment_Prompt" PARAMETERS = "module var, data, field_id" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.prompt" VALUE = "">

	<MvIF EXPR = "{ l.field_id EQ 'check' }">
		<MvASSIGN NAME = "l.prompt" VALUE = "Check #:">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.prompt }">
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Payment_Invalid" PARAMETERS = "module var, data, field_id" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.invalid" VALUE = 0>

	<MvIF EXPR = "{ ( l.field_id EQ 'check' ) AND g.Check_Number_Invalid }">
		<MvASSIGN NAME = "l.invalid" VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.invalid }">
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Payment_Field" PARAMETERS = "module var, data, field_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.field_id EQ 'check' }">
		<input type="text" name="Check_Number" size=40 value="{ encodeentities( g.Check_Number ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Payment_Validate" PARAMETERS = "module var, pay_data" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Check_Number" VALUE = "{ trim( g.Check_Number ) }">
	<MvIF EXPR = "{ ( len( g.Check_Number ) EQ 0 ) OR ( NOT isdigit( g.Check_Number ) ) }">
		<MvASSIGN NAME = "g.Check_Number_Invalid" VALUE = 1>
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Report_Description" PARAMETERS = "module var, data" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "Check">
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Report_Fields" PARAMETERS = "module var, data, secure_data" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "check">
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Report_Label" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.label" VALUE = "">

	<MvIF EXPR = "{ l.field_id EQ 'check' }">
		<MvASSIGN NAME = "l.label" VALUE = "Check #:">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.label }">
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Report_Value" PARAMETERS = "module var, field_id, data, secure_data" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.value" VALUE = "">

	<MvIF EXPR = "{ l.field_id EQ 'check' }">
		<MvASSIGN NAME = "l.value" VALUE = "{ encodeentities( l.data:check_number ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.value }">
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Manipulate_Shipping" PARAMETERS = "module var, data" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Authorize" PARAMETERS = "module var, module_data, total, data var, secure_data var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pay_data" 		VALUE = "">
	<MvASSIGN NAME = "l.secure_data" 	VALUE = "">
	<MvASSIGN NAME = "l.data" 			VALUE = "">

	<MvIF EXPR = "{ len( g.Check_Number ) AND isdigit( g.Check_Number ) }">
		<MvASSIGN NAME = "l.pay_data" 			VALUE = "{ g.Check_Number }">
		<MvASSIGN NAME = "l.data:check_number" 	VALUE = "{ g.Check_Number }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.pay_data }">
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Process" PARAMETERS = "module var, data var, secure_data var, order var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Order_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Order_Check_Number" VALUE = "{ trim( g.Order_Check_Number ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Order_Update" PARAMETERS = "module var, data var, secure_data var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.data:check_number" VALUE = "{ g.Order_Check_Number }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-PAY-CHK-00001', 'Check Payment Module Order updated' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Order_Delete" PARAMETERS = "module var, data, secure_data" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Order_Tabs" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "CHECK:Check Payment">
</MvFUNCTION>

<MvFUNCTION NAME = "PaymentModule_Order_Content" PARAMETERS = "module var, tab, load_fields, data, secure_data" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.load_fields }">
		<MvASSIGN NAME = "g.Order_Check_Number" VALUE = "{ l.data:check_number }">
	</MvIF>

	<MvIF EXPR = "{ l.tab EQ 'CHECK' }">
		<table border=0 cellpadding=2 cellspacing=0 width = "100%">
		<tr><td nowrap>
				<b>Check #:</b>
		</td><td width="100%">
				<input type="text" size=40 name="Order_Check_Number" value="{ encodeentities( g.Order_Check_Number ) }">
		</td></tr>
		</table>
	<MvELSE>
		<input type="hidden" name="Order_Check_Number" value="{ encodeentities( g.Order_Check_Number ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

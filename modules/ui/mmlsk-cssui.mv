<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-SUI-CSSUI-
| Next Error Code: 13   
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-cssui">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Miva Merchant CSSUI">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0900">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "10.09">
	<MvASSIGN NAME = "l.module:features"	VALUE = "storeui, vis_store, data_store, not_seo">
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUIModule_Thumbnail" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ 'graphics/' $ s.miva_language $ '/cssui/store_th.gif' }">
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUIModule_Create_Items" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUIModule_Create_Pages" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUIModule_Default_Framework" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "shadows">
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUIModule_Create_Frameworks" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.skin_code" ARRAY = "l.frameworks" COUNT = "{ miva_splitstring( 'cssui_default_fw,shadows', ',', l.frameworks, 'trim' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].SKIN_Directory_Verify( l.skin_code, l.skin_dir, l.skin_file ) OR 
						NOT [ g.Module_Feature_TUI_UT ].SKIN_Sample_Retrieve( l.skin_code ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ]._SKIN_Install( g.Store:code, 'shadows', 1, 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUIModule_Create_URIs" PARAMETERS = "module var, pg_regen" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.screen" ARRAY = "l.screens" COUNT = "{ StoreUIModule_Screens( l.module, l.screens ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_URI_UT ].URI_Generate_Screen_Update_WithPageRegenerate( l.pg_regen, l.screen:code, l.screen:name ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUIModule_Screens" PARAMETERS = "module var, screens var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.screens"		VALUE = "">
	<MvASSIGN NAME = "l.screen_count"	VALUE = 0>

	<MvASSIGN NAME = "l.screen:code"	VALUE = "OINF">
	<MvASSIGN NAME = "l.screen:name"	VALUE = "Checkout">
	<MvASSIGN NAME = "l.screen_count"	VALUE = "{ miva_array_insert_var( l.screens, l.screen, -1 ) }">

	<MvASSIGN NAME = "l.screen:code"	VALUE = "ACNT">
	<MvASSIGN NAME = "l.screen:name"	VALUE = "Customer">
	<MvASSIGN NAME = "l.screen_count"	VALUE = "{ miva_array_insert_var( l.screens, l.screen, -1 ) }">

	<MvASSIGN NAME = "l.screen:code"	VALUE = "AFAE">
	<MvASSIGN NAME = "l.screen:name"	VALUE = "Affiliate">
	<MvASSIGN NAME = "l.screen_count"	VALUE = "{ miva_array_insert_var( l.screens, l.screen, -1 ) }">

	<MvASSIGN NAME = "l.screen:code"	VALUE = "OUSL">
	<MvASSIGN NAME = "l.screen:name"	VALUE = "Checkout: Special Offer(s)">
	<MvASSIGN NAME = "l.screen_count"	VALUE = "{ miva_array_insert_var( l.screens, l.screen, -1 ) }">

	<MvFUNCTIONRETURN VALUE = "{ l.screen_count }">
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUIModule_Screen_Secure" PARAMETERS = "module var, screen" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.screen EQ 'OINF' }">		<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ l.screen EQ 'ACNT' }">	<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ l.screen EQ 'AFAE' }">	<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ l.screen EQ 'OUSL' }">	<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUIModule_Dispatch" PARAMETERS = "module var, page var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.page" VALUE = "{ toupper( l.page ) }">

	<MvIF EXPR = "{ ( l.page EQ 'OINF' ) OR
					( l.page EQ 'OUSL' ) OR
					( l.page EQ 'OUS1' ) OR
					( l.page EQ 'OUSM' ) OR
					( l.page EQ 'UATM' ) OR
					( l.page EQ 'UATR' ) OR
					( l.page EQ 'OSEL' ) OR
					( l.page EQ 'OPAY' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].OrderMinimumsMet() }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( g.Store:omin_msg ) }">		
			<MvASSIGN NAME = "l.page" VALUE = "OMIN">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_INV_RT ].Inventory_Check_Available_AtCheckout( g.Basket, l.available ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT l.available }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Merchant ].UIException( 'inventory_unavailable_atcheckout' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.page EQ 'OINF' }">
		<MvIF EXPR = "{ g.Basket:cust_id AND g.Customer_Session_Verified }">
			<MvASSIGN NAME = "l.page" VALUE = "OCST">
		<MvELSE>
			<MvIF EXPR = "{ NOT CSSUI_Settings_Load( l.settings ) }">
				<MvFUNCTIONRETURN VALUE = -1>
			</MvIF>

			<MvIF EXPR = "{ l.settings:chk_logn }">
				<MvASSIGN NAME = "l.page" VALUE = "ORDL">
			<MvELSE>
				<MvASSIGN NAME = "l.page" VALUE = "OCST">
			</MvIF>
		</MvIF>
	<MvELSEIF EXPR = "{ l.page EQ 'CTGY' }">
		<MvFUNCTIONRETURN VALUE = "{ CSSUI_Dispatch_Category( g.Session:uri:cat_id ) }">
	<MvELSEIF EXPR = "{ l.page EQ 'FEED' }">
		<MvFUNCTIONRETURN VALUE = "{ CSSUI_Dispatch_Feed( g.Session:uri:feed_id ) }">
	<MvELSEIF EXPR = "{ l.page EQ 'PROD' }">
		<MvIF EXPR = "{ NOT ISNULL g.Current_Product_Code }">
			<MvASSIGN NAME = "g.Product_Code" VALUE = "{ g.Current_Product_Code }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ CSSUI_Dispatch_Product( g.Session:uri:product_id ) }">
	<MvELSEIF EXPR = "{ l.page EQ 'ACNT' }">
		<MvIF EXPR = "{ g.Basket:cust_id }">
			<MvASSIGN NAME = "l.page" VALUE = "ACED">
		<MvELSE>
			<MvASSIGN NAME = "l.page" VALUE = "ACAD">
		</MvIF>
	<MvELSEIF EXPR = "{ l.page EQ 'AFAE' }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_AFF_DB ].Affiliate_Load_Session( g.Basket:session_id, l.null ) }">
			<MvASSIGN NAME = "l.page" VALUE = "AFAD">
		<MvELSE>
			<MvASSIGN NAME = "l.page" VALUE = "AFED">
		</MvIF>
	<MvELSEIF EXPR = "{ l.page EQ 'OUSL' }">
		<MvASSIGN NAME = "l.upsold_product_count" VALUE = "{ [ g.Module_Feature_USL_RT ].Runtime_UpsoldProductList_Load_Basket_Eligible( g.Basket, l.upsold_products ) }">
		<MvIF EXPR = "{ l.upsold_product_count EQ 1 }">
			<MvASSIGN NAME = "l.page" VALUE = "OUS1">
		<MvELSEIF EXPR = "{ l.upsold_product_count GT 1 }">
			<MvASSIGN NAME = "l.page" VALUE = "OUSM">
		<MvELSE>
			<MvASSIGN NAME = "l.page" VALUE = "OSEL">
		</MvIF>
	</MvIF>

	<MvCOMMENT>
	|
	| Verify Customer Session before displaying customer pages
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ( l.page EQ 'CPCD' ) OR
					( l.page EQ 'CPCA' ) OR
					( l.page EQ 'CPCE' ) }">
		<MvIF EXPR = "{ [ g.Module_Feature_PAY_DB ].MivaPaySettings_Load_Cached( l.mivapay_settings ) AND l.mivapay_settings:enabled }">
			<MvASSIGN NAME = "l.mivapay_settings:enabled" VALUE = "{ [ g.Module_Feature_PAY_UT ].MivaPay_Verify_Enabled_Cached( l.mivapay_settings ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT l.mivapay_settings:enabled }">
			<MvASSIGN NAME = "l.page" VALUE = "ACLN">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ( l.page EQ 'ACED' ) OR
					( l.page EQ 'ACLN' ) OR
					( l.page EQ 'ABAL' ) OR
					( l.page EQ 'RGFT' ) OR
					( l.page EQ 'CABK' ) OR
					( l.page EQ 'CADA' ) OR
					( l.page EQ 'CADE' ) OR
					( l.page EQ 'CPCD' ) OR
					( l.page EQ 'CPCA' ) OR
					( l.page EQ 'CPCE' ) OR
					( l.page EQ 'CEML' ) OR
					( l.page EQ 'CPWD' ) }">
		<MvIF EXPR = "{ NOT g.Customer_Session_Verified }">
			<MvIF EXPR = "{ l.page EQ 'RGFT' }">	<MvASSIGN NAME = "l.page" VALUE = "GFTL">
			<MvELSE>								<MvASSIGN NAME = "l.page" VALUE = "LOGN">
			</MvIF>
		</MvIF>
	</MvIF>

	<MvCOMMENT>
	|
	| In order to view order history or view a specific order the following criteria must
	| be met, otherwise the ORHL should be rendered
	|
	|	1. The customer is logged-in and verified
	|	2. The customer is not logged-in and one of the the two supported order lookup methods is populated
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ( l.page EQ 'ORDH' ) OR 
					( l.page EQ 'ORDS' ) }">
		<MvIF EXPR = "{ NOT g.Customer_Session_Verified AND
						(
							 ( g.Basket:cust_id NE 0 ) 	OR
							 (
							 	( ISNULL g.Order_BillEmail AND ISNULL g.Order_BillZip ) AND
							 	( ISNULL g.Order_ID AND ISNULL g.Order_BillEmail )
							 )
						) }">
			<MvASSIGN NAME = "l.page" VALUE = "ORHL">
		</MvIF>
	</MvIF>

	<MvCOMMENT>
	|
	| Verify Affiliate Session before displaying affiliate pages
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.page EQ 'AFED' }">
		<MvIF EXPR = "{ NOT g.Affiliate_Session_Verified }">
			<MvASSIGN NAME = "l.page" VALUE = "AFCL">
		</MvIF>
	</MvIF>

	<MvCOMMENT>
	|
	| Verify Checkout Session before displaying checkout pages
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ( l.page EQ 'OCST' ) OR
					( l.page EQ 'OUSL' ) OR
					( l.page EQ 'OUS1' ) OR
					( l.page EQ 'OUSM' ) OR
					( l.page EQ 'UATM' ) OR
					( l.page EQ 'UATR' ) OR
					( l.page EQ 'OSEL' ) OR
					( l.page EQ 'OPAY' ) }">
		<MvIF EXPR = "{ NOT g.Checkout_Session_Verified }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Clear_Customer_Information( g.Basket ) }">
				<MvFUNCTIONRETURN VALUE = -1>
			</MvIF>

			<MvASSIGN NAME = "l.page" VALUE = "OCST">
		</MvIF>
	</MvIF>

	<MvCOMMENT>
	|
	| If a customer is logged in, the OCST page will display their information even without a checkout session, so it
	| must be cleared.  If the user is being redirected to the ORDL page, we must also clear the customer information,
	| otherwise the user will be unable to proceed past the ORDL page without logging in.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ( l.page EQ 'OCST' ) OR
					( l.page EQ 'ORDL' ) }">
		<MvIF EXPR = "{ g.Basket:cust_id AND ( NOT g.Customer_Session_Verified ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Clear_Customer_ID( g.Basket ) }">
				<MvFUNCTIONRETURN VALUE = -1>
			</MvIF>

			<MvASSIGN NAME = "l.page" VALUE = "ORDL">
		</MvIF>
	</MvIF>

	<MvCOMMENT>
	|
	| These MvIFs are separate because the block above may change the page code
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ( l.page EQ 'OCST' ) OR
					( l.page EQ 'OSEL' ) OR
					( l.page EQ 'OPAY' ) }">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].Basket_Quantity_All( g.Basket:basket_id ) EQ 0 }">
			<MvASSIGN NAME = "l.page" VALUE = "BSKE">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvCOMMENT>
	|
	| Verify g.Customer_PasswordResetToken is not blank when viewing the CSTR screen
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.page EQ 'CSTR' }">
		<MvIF EXPR = "{ ISNULL g.Customer_PasswordResetToken }">
			<MvASSIGN NAME = "l.page" VALUE = "LOGN">
		</MvIF>
	</MvIF>

	<MvCOMMENT>
	|
	| Verify g.Affiliate_PasswordResetToken is not blank when viewing the AFFR screen
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.page EQ 'AFFR' }">
		<MvIF EXPR = "{ ISNULL g.Affiliate_PasswordResetToken }">
			<MvASSIGN NAME = "l.page" VALUE = "AFCL">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUIModule_Exception" PARAMETERS = "module var, code" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.checkout_page" VALUE = "OPAY">	
	<MvASSIGN NAME = "l.customer_page" VALUE = "OCST">	
	
	<MvIF EXPR = "{ l.code EQ 'inventory_limited' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'PLMT' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'inventory_out' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'POUT' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'inventory_unavailable_atcheckout' }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_INV_DB ].InventorySettings_Load_Cached( l.inventorysettings ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( l.inventorysettings:ex_page ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'basket_changed' }">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].Basket_Quantity_All( g.Basket:basket_id ) EQ 0 }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'BASK' ) }">
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The contents of your basket changed during the checkout process.  As a security precaution, you have been returned to the beginning of the checkout process.' ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'OCST' ) }">
		</MvIF>
	<MvELSEIF EXPR = "{ l.code EQ 'customerorder_invalid_order' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Order not found.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'ORDH' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'order_invalid_info' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more required fields were not filled out correctly.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'OCST' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'order_invalid_payment' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more required fields were not filled out correctly.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( l.checkout_page ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'additional_payment' }">
		<MvIF EXPR = "{ ISNULL g.AdditionalPaymentMethod }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'OSEL' ) }">
		<MvELSE>
			<MvASSIGN NAME = "g.PaymentMethod" VALUE = "{ g.AdditionalPaymentMethod }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( l.checkout_page ) }">
		</MvIF>
	<MvELSEIF EXPR = "{ l.code EQ 'order_split_authorizationfailure' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'OSEL' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'order_authorizationfailure' }">
		<MvIF EXPR = "{ ISNULL g.PaymentMethod }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( l.customer_page ) }">
		<MvELSE>									<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( l.checkout_page ) }">
		</MvIF>
	<MvELSEIF EXPR = "{ l.code EQ 'order_authorizationfailure_storedcard' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'OSEL' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'order_alreadyprocessed' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'OPRC' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'order_basketchanged' }">
	<MvELSEIF EXPR = "{ l.code EQ 'order_invoice' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'INVC' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'order_customer_required' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Subscriptions require a customer account. Please sign in or create a new customer account.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'ORDL' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'page_not_found' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Page \'' $ encodeentities( g.Screen ) $ '\' no longer exists.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'NTFD' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'product_not_found' }">
		<MvIF EXPR = "{ ISNULL g.Product_Code }">	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The specified product is not available at this time.' ) }">
		<MvELSE>									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Product \'' $ encodeentities( g.Product_Code ) $ '\' is not available at this time.' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'NTFD' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'category_not_found' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Category \'' $ encodeentities( g.Category_Code ) $ '\' is not available at this time.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'NTFD' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'feed_not_found' OR l.code EQ 'feed_not_enabled' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Feed \'' $ encodeentities( g.Feed_Code ) $ '\' is not available at this time.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'NTFD' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'product_attributes' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Attributes in <span class="bold">bold</span> are required.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'PATR' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'invalid_variant' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Please select a valid combination of attributes.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'PATR' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'upsell_attributes' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Attributes in <span class="bold">bold</span> are required.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'UATR' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'upsell_invalid_variant' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Please select a valid combination of attributes.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'UATR' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'upsell_attributes_multiple' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Attributes in <span class="bold">bold</span> are required.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'UATM' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'upsell_invalid_variant_multiple' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more combinations of attributes is invalid.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'UATM' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'upsell_toomanyselected' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'OUSM' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'customer_invalid_csrf_token' }">
		<MvCOMMENT>
		|
		| Certain actions are not assigned a screen as those actions are not directly associated with a screen.
		| Those actions should just use the original g.Screen value.
		|
		</MvCOMMENT>

		<MvASSIGN NAME = "l.lookup" VALUE = "{ miva_array_deserialize( '[01]:action=CPWD,[01]:screen=CPWD,
																		[02]:action=CEML,[02]:screen=CEML,
																		[03]:action=UCST,[03]:screen=ACED,
																		[04]:action=ICST,[04]:screen=ACAD,
																		[05]:action=ICSA,[05]:screen=CADA,
																		[06]:action=UCSA,[06]:screen=CADE,
																		[07]:action=DCSA,[07]:screen=,
																		[08]:action=CCSA,[08]:screen=,
																		[09]:action=ICPC,[09]:screen=CPCA,
																		[10]:action=UCPC,[10]:screen=CPCE,
																		[11]:action=DCPC,[11]:screen=,
																		[12]:action=UCSB,[12]:screen=CSBE,
																		[13]:action=DCSB,[13]:screen=' ) }">

		<MvFOREACH ITERATOR = "l.action" ARRAY = "l.actions" COUNT = "{ miva_splitstring( g.Action, ',', l.actions, 'trim,upper' ) }">
			<MvIF EXPR = "{ miva_array_search( l.lookup, 1, l.element, 'l.element:action EQ l.action' ) }">
				<MvIF EXPR = "{ NOT ISNULL l.element:screen }">
					<MvASSIGN NAME = "g.Screen" VALUE = "{ l.element:screen }">
				</MvIF>

				<MvFOREACHSTOP>
			</MvIF>
		</MvFOREACH>

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Invalid customer token.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( g.Screen ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'customer_invalid_card' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Invalid card.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'CPCD' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'customer_invalid_login' }">
		<MvIF EXPR = "{ g.Customer_Login_Invalid OR g.Customer_Password_Invalid }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The username or password you entered is incorrect.' ) }">
		</MvIF>

		<MvIF EXPR = "{ g.Screen EQ l.customer_page }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'ORDL' ) }">
		<MvELSE>
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'LOGN' ) }">
		</MvIF>
	<MvELSEIF EXPR = "{ l.code EQ 'customer_invalid_login_email_password' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more required fields were not filled out correctly.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'FPWD' ) }">
	<MvELSEIF EXPR = "{ ( l.code EQ 'customer_email_error' ) OR
						( l.code EQ 'customer_email_sent' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Information( 'Instructions to reset your password have been sent to the email address on file with your account.  Please contact us if you require further assistance.' ) }">

		<MvIF EXPR = "{ g.Screen EQ l.customer_page }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'ORDL' ) }">
		<MvELSE>
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'LOGN' ) }">
		</MvIF>
	<MvELSEIF EXPR = "{ l.code EQ 'customer_invalid_reset' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The password reset link you followed is not valid or has expired.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'LOGN' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'customer_invalid_reset_password' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more required fields were not filled out correctly.' ) }">

		<MvIF EXPR = "{ g.Customer_Password_Invalid }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( encodeentities( g.Validation_Message ) ) }">
		<MvELSEIF EXPR = "{ g.Customer_VerifyPassword_Invalid }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The passwords do not match.' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'CSTR' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'customer_password_reset' }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_CUS_DB ].CustomerSettings_Load_Cached( l.customersettings ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ l.customersettings:resettype EQ 'temporary' }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'ACRT' ) }">
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Information( 'Account password updated successfully.' ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'LOGN' ) }">
		</MvIF>
	<MvELSEIF EXPR = "{ l.code EQ 'customer_invalid_addinfo' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more required fields were not filled out correctly.' ) }">

		<MvIF EXPR = "{ g.Customer_Login_InUse }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The login you selected is already in use.' ) }">
		<MvELSEIF EXPR = "{ g.Customer_LoginEmail_InUse }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The email address you entered is already in use.' ) }">
		<MvELSEIF EXPR = "{ g.Customer_Password_Invalid }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( encodeentities( g.Validation_Message ) ) }">
		<MvELSEIF EXPR = "{ g.Customer_VerifyPassword_Invalid }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The passwords do not match.' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'ACAD' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'customer_invalid_editinfo' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more required fields were not filled out correctly.' ) }">

		<MvIF EXPR = "{ g.Customer_Login_InUse }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The login you selected is already in use.' ) }">
		<MvELSEIF EXPR = "{ g.Customer_LoginEmail_InUse }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The email address you entered is already in use.' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'ACED' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'customer_invalid_changepassword' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more required fields were not filled out correctly.' ) }">
		
		<MvIF EXPR = "{ g.Customer_Password_Invalid }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( encodeentities( g.Validation_Message ) ) }">
		<MvELSEIF EXPR = "{ g.Customer_VerifyPassword_Invalid }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The passwords do not match.' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'CPWD' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'customer_invalid_changeemailaddress' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more required fields were not filled out correctly.' ) }">

		<MvIF EXPR = "{ g.Customer_LoginEmail_InUse }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The email address you entered is already in use.' ) }">
		<MvELSEIF EXPR = "{ g.Customer_LoginEmailVerifyLoginEmail_Mismatch }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The email addresses do not match.' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'CEML' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'customer_invalid_address' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Invalid address.' ) }">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'CABK' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'customeraddress_invalid_addinfo' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more required fields were not filled out correctly.' ) }">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'CADA' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'customeraddress_invalid_editinfo' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more required fields were not filled out correctly.' ) }">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'CADE' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'customeraddress_in_use' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Customer address is associated with a subscription and cannot be removed.' ) }">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( g.Screen ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'customerpaymentcard_alreadyinserted' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The specified payment card has already been added to your account.' ) }">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( g.Screen ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'customerpaymentcard_invalid_response' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'An error has occurred.' ) }">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( g.Screen ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'paymentcard_in_use' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Payment card is associated with a subscription and cannot be removed.' ) }">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( g.Screen ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'giftcertificate_customer_login' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'GFTL' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'giftcertificate_invalid' }">
		<MvASSIGN NAME = "l.result"		VALUE = "{ StoreUIModule_Dispatch( l.module, g.Screen ) }">
		<MvIF EXPR = "{ l.result LT 0 }">
			<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ l.result NE 0 }">
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>

		<MvIF EXPR = "{ ISNULL g.Screen }">
			<MvASSIGN NAME = "g.Screen"	VALUE = "RGFT">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( g.Screen ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'wishlist_invalid' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Invalid wish list' ) }">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'WLST' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'wishlist_customer_login' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'WLGN' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'wishlist_pick' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'WPCK' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'wishlist_basketitem_notfound' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Unable to load requested Basket Item(s). Some or all of the Basket Contents may no longer be available.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'BASK' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'wishlist_invalid_addinfo' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more required fields were not filled out correctly.' ) }">

		<MvIF EXPR = "{ g.WishList_Title_Invalid }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Please specify a wish list title.' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'WLAD' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'wishlist_invalid_editinfo' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more required fields were not filled out correctly.' ) }">

		<MvIF EXPR = "{ g.WishList_Title_Invalid }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Please specify a wish list title.' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'WLED' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'affiliate_invalid_login' }">
		<MvIF EXPR = "{ g.Affiliate_Code_Invalid }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The login or password you entered is incorrect.' ) }">
		<MvELSEIF EXPR = "{ g.Affiliate_Password_Invalid }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The login or password you entered is incorrect.' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'AFCL' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'affiliate_invalid_code_email_password' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more required fields were not filled out correctly.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'AFPW' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'affiliate_email_sent' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Information( 'Instructions to reset your password have been sent to the email address on file with your account.  Please contact us if you require further assistance.' ) }">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'AFCL' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'affiliate_invalid_reset' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The password reset link you followed is not valid or has expired.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'AFCL' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'affiliate_invalid_reset_password' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more required fields were not filled out correctly.' ) }">

		<MvIF EXPR = "{ g.Affiliate_Password_Invalid }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( encodeentities( g.Validation_Message ) ) }">
		<MvELSEIF EXPR = "{ g.Affiliate_VerifyPassword_Invalid }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The passwords do not match.' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'AFFR' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'affiliate_password_reset' }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_AFF_DB ].AffiliateOptions_Load( l.affiliateoptions ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ l.affiliateoptions:resettype EQ 'temporary' }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'AFRT' ) }">
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Information( 'Account password updated successfully.' ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'AFCL' ) }">
		</MvIF>
	<MvELSEIF EXPR = "{ l.code EQ 'affiliate_invalid_addinfo' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more required fields were not filled out correctly.' ) }">

		<MvIF EXPR = "{ g.Affiliate_Login_InUse }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The login you selected is already in use.' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'AFAD' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'affiliate_invalid_editinfo' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more required fields were not filled out correctly.' ) }">

		<MvIF EXPR = "{ g.Affiliate_Login_InUse }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'The login you selected is already in use.' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'AFED' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'store_offline' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'MNTN' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'no_payment_methods' }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PAY_DB ].PaymentRules_Load( l.paymentrules ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'No payment methods are available for your basket.  This may be due to a network or configuration error, or the contents of your basket may not be eligible for any of the configured payment methods.' ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( l.paymentrules:req_page ) }">
			<MvIF EXPR = "{ ( NOT g.TemplateManager_Exception ) OR
							( g.TemplateManager_Exception_Code EQ 'no_payment_methods' ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Merchant ].UIException( g.TemplateManager_Exception_Code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ l.code EQ 'no_shipping_methods' }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_SHP_DB ].ShippingRules_Load( l.shippingrules ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Shipping could not be calculated for your basket.  This may be due to a network or configuration error, or the contents of your basket may not be able to be shipped in a single shipment.' ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( l.shippingrules:req_page ) }">
			<MvIF EXPR = "{ ( NOT g.TemplateManager_Exception ) OR
							( g.TemplateManager_Exception_Code EQ 'no_shipping_methods' ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Merchant ].UIException( g.TemplateManager_Exception_Code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ l.code EQ 'customer_invalid_session' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Information( 'Your session has expired.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'LOGN' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'affiliate_invalid_session' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Information( 'Your session has expired.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'AFCL' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'checkout_invalid_session' }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Clear_Customer_Information( g.Basket ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Information( 'Your session has timed out.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'OCST' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'invoice_invalid_session' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'OPRC' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'invalid_shipping_method' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Invalid shipping method.' ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'OSEL' ) }">
			<MvIF EXPR = "{ ( NOT g.TemplateManager_Exception ) OR
							( g.TemplateManager_Exception_Code EQ 'invalid_shipping_method' ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Merchant ].UIException( g.TemplateManager_Exception_Code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ l.code EQ 'invalid_payment_method' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Invalid payment method.' ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'OSEL' ) }">
			<MvIF EXPR = "{ ( NOT g.TemplateManager_Exception ) OR
							( g.TemplateManager_Exception_Code EQ 'invalid_payment_method' ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Merchant ].UIException( g.TemplateManager_Exception_Code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ l.code EQ 'invalid_subscription' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Invalid subscription.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'PATR' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'subscription_not_found' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Unable to load subscription.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'CSUB' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'invalid_subscription_edit' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more required fields were not filled out correctly.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'CSBE' ) }">
	<MvELSEIF EXPR = "{ l.code EQ 'subscription_required' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Subscription is required for this product.' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'PATR' ) }">
	<MvELSEIF EXPR = "{ ( l.code EQ 'coupon_invalid' )		OR
						( l.code EQ 'coupon_ineligible' )	OR
						( l.code EQ 'coupon_removed' ) }">
		<MvIF EXPR = "{ l.code EQ 'coupon_invalid' }">			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Invalid coupon code.' ) }">
		<MvELSEIF EXPR = "{ l.code EQ 'coupon_ineligible' }">	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Your basket is not eligible for the specified coupon.' ) }">
		<MvELSEIF EXPR = "{ l.code EQ 'coupon_removed' }">		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'One or more coupons have been removed from your basket.' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.result"		VALUE = "{ StoreUIModule_Dispatch( l.module, g.Screen ) }">
		<MvIF EXPR = "{ l.result LT 0 }">
			<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ l.result NE 0 }">
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>

		<MvIF EXPR = "{ ISNULL g.Screen }">
			<MvASSIGN NAME = "g.Screen"	VALUE = "BASK">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( g.Screen ) }">
	<MvELSEIF EXPR = "{ ( l.code EQ 'customer_validate_editinfo' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Please verify your address' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'ACED' ) }">
	<MvELSEIF EXPR = "{ ( l.code EQ 'customer_validate_addinfo' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Please verify your address' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'ACAD' ) }">
	<MvELSEIF EXPR = "{ ( l.code EQ 'customeraddress_validate_addinfo' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Please verify your address' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'CADA' ) }">
	<MvELSEIF EXPR = "{ ( l.code EQ 'customeraddress_validate_editinfo' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Please verify your address' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'CADE' ) }">
	<MvELSEIF EXPR = "{ ( l.code EQ 'order_validate_info' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Message_Error( 'Please verify your address' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache( 'OCST' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SUI-CSSUI-00003', 'Unhandled UI exception \'' $ l.code $ '\'' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'CSSUI_Settings
						  (
							chk_logn	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()	$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SUI-CSSUI-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.cssui_settings:chk_logn"	VALUE = 1>

	<MvFUNCTIONRETURN VALUE = "{ CSSUI_Settings_Insert( l.cssui_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.version LT 5.0001 }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_Code( 'PROD', l.page ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_UPG_AD ].Upgrade_Install_Error( 'MER-SUI-CSSUI-00005', g.Error_Message ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'category' ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_UPG_AD ].Upgrade_Install_Error( 'MER-SUI-CSSUI-00006', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'CSSUI_Settings' }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Head" PARAMETERS = "module var, tab" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.CSSUI_Settings:chk_logn"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.CSSUI_Settings:chk_logn ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ CSSUI_Settings_Update( g.CSSUI_Settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Tabs" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "GT_UISETTINGS/CMP-CSSUI-SETTINGS:Shopping Interface Settings">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Store_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.load_fields }">
		<MvIF EXPR = "{ NOT CSSUI_Settings_Load( g.CSSUI_Settings ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Admin ].Tab_Visible( g.Tab, 'CMP-CSSUI-SETTINGS' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_UISETTINGS', 'CMP-CSSUI-SETTINGS' ) }">
		<table border=0 cellpadding=2 cellspacing=0 width="100%">
		<tr><td valign="middle" nowrap>
			&nbsp;
		</td><td width="100%" nowrap>
			<MvDO FILE = "{ g.Module_Library_Utilities }" NAME = "l.null" VALUE = "{ DrawCheckbox( g.CSSUI_Settings:chk_logn, 'CSSUI_Settings:chk_logn', 'Yes', 'Display Customer Login Before Checkout' ) }">
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	<MvELSE>
		<MvHIDE FIELDS = "g.CSSUI_Settings">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUI_Dispatch_Exception" PARAMETERS = "code" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null"	VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Throw_Exception( l.code ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Merchant ].UIException( g.TemplateManager_Exception_Code ) }">
		<MvFUNCTIONRETURN VALUE = -1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUI_Dispatch_Page_Load" PARAMETERS = "page_code, fallback_code, page var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT ISNULL l.page_code }">
		<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code_Cached( l.page_code, l.page ) }">
			<MvIF EXPR = "{ NOT l.page:admin }">													<MvFUNCTIONRETURN VALUE = 1>
			</MvIF>
		<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">							<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code_Cached( l.fallback_code, l.page ) AND ( NOT l.page:admin ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUI_Dispatch_Category" PARAMETERS = "cat_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.cat_id }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_Category_Load_ID( l.cat_id, l.category ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = -1>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ CSSUI_Dispatch_Exception( 'category_not_found' ) }">
		</MvIF>

		<MvASSIGN NAME = "g.Category_Code"	VALUE = "{ l.category:code }">
	<MvELSEIF EXPR = "{ ISNULL g.Category_Code }">
		<MvFUNCTIONRETURN VALUE = 0>
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_Category_Load_Code( g.Category_Code, l.category ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = -1>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ CSSUI_Dispatch_Exception( 'category_not_found' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT CSSUI_Dispatch_Page_Load( l.category:page_code, 'CTGY', l.page ) }">
		<MvFUNCTIONRETURN VALUE = "{ CSSUI_Dispatch_Exception( 'page_not_found' ) }">
	</MvIF>

	<MvREFERENCE NAME = "l.page:settings:category" VARIABLE = "l.category">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache_LowLevel( g.Screen, l.page ) }">
		<MvIF EXPR = "{ NOT g.TemplateManager_Exception }">
			<MvFUNCTIONRETURN VALUE = -1>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Merchant ].UIException( g.TemplateManager_Exception_Code ) }">
			<MvFUNCTIONRETURN VALUE = -1>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUI_Dispatch_Product" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.product_id }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_Product_Load_ID( l.product_id, l.product ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = -1>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ CSSUI_Dispatch_Exception( 'product_not_found' ) }">
		</MvIF>

		<MvASSIGN NAME = "g.Product_Code"	VALUE = "{ l.product:code }">
	<MvELSEIF EXPR = "{ ISNULL g.Product_Code }">
		<MvFUNCTIONRETURN VALUE = 0>
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_Product_Load_Code( g.Product_Code, l.product ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = -1>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ CSSUI_Dispatch_Exception( 'product_not_found' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT CSSUI_Dispatch_Page_Load( l.product:page_code, 'PROD', l.page ) }">
		<MvFUNCTIONRETURN VALUE = "{ CSSUI_Dispatch_Exception( 'page_not_found' ) }">
	</MvIF>

	<MvREFERENCE NAME = "l.page:settings:product" VARIABLE = "l.product">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_Cache_LowLevel( g.Screen, l.page ) }">
		<MvIF EXPR = "{ NOT g.TemplateManager_Exception }">
			<MvFUNCTIONRETURN VALUE = -1>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Merchant ].UIException( g.TemplateManager_Exception_Code ) }">
			<MvFUNCTIONRETURN VALUE = -1>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUI_Dispatch_Feed" PARAMETERS = "feed_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.feed_id }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_FDS_DB ].Feed_Load_ID( l.feed_id, l.feed ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = -1>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ CSSUI_Dispatch_Exception( 'feed_not_found' ) }">
		</MvIF>

		<MvASSIGN NAME = "g.Feed_Code" VALUE = "{ l.feed:code }">
	<MvELSEIF EXPR = "{ ISNULL g.Feed_Code }">
		<MvFUNCTIONRETURN VALUE = 0>
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Feature_FDS_DB ].Feed_Load_Code( g.Feed_Code, l.feed ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = -1>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ CSSUI_Dispatch_Exception( 'feed_not_found' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.feed:uri_enbld }">
		<MvFUNCTIONRETURN VALUE = "{ CSSUI_Dispatch_Exception( 'feed_not_enabled' ) }">
	</MvIF>

	<MvASSIGN NAME = "g.Feed_AccessKey" VALUE = "{ trim( g.Feed_AccessKey ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_FDS_UT ].Feed_Process_URI( l.feed, g.Feed_AccessKey ) }">
		<MvFUNCTIONRETURN VALUE = -1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUI_Settings_Insert" PARAMETERS = "cssui_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'CSSUI_Settings
						  ( chk_logn )
						  VALUES
						  ( ? )' }"
			 FIELDS	= "l.cssui_settings:chk_logn">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SUI-CSSUI-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUI_Settings_Update" PARAMETERS = "cssui_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'CSSUI_Settings
						  SET
							chk_logn	= ?' }"
			 FIELDS	= "l.cssui_settings:chk_logn">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SUI-CSSUI-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CSSUI_Settings_Load" PARAMETERS = "cssui_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CSSUI_Settings"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CSSUI_Settings' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SUI-CSSUI-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.cssui_settings:chk_logn"	VALUE = "{ CSSUI_Settings.d.chk_logn }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CSSUI_Settings">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Notify_SEOSettings" PARAMETERS = "module var, seo_settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.page_exists"	VALUE = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code( 'SMAP', l.page ) }">

	<MvIF EXPR = "{ l.seo_settings:sm_active }">
		<MvIF EXPR = "{ NOT l.page_exists }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Page( 'SMAP', 'Sitemap', l.module:id, 'cssui-smap.mvt', l.settings, l.page ) }">	<MvFUNCTIONRETURN VALUE = 0> </MvIF>
			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'customerlink' ) }">												<MvFUNCTIONRETURN VALUE = 0> </MvIF>
			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'affiliatelink' ) }">											<MvFUNCTIONRETURN VALUE = 0> </MvIF>
			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'cssui_links' ) }">		<MvFUNCTIONRETURN VALUE = 0> </MvIF>
			
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-SUI-CSSUI-00010', 'Page \'' $ l.page:code $ '\' created' ) }">
		</MvIF>
	<MvELSE>
		<MvIF EXPR = "{ l.page_exists AND ( l.page:ui_id EQ l.module:id ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_Page( l.page ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-SUI-CSSUI-00011', 'Page \'' $ l.page:code $ '\' deleted' ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_Action" PARAMETERS = "module var, action" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.action EQ 'SETO' }">
		<MvASSIGN NAME = "g.ShipFirstName" 	VALUE = "{ g.Customer_ShipFirstName }">
		<MvASSIGN NAME = "g.ShipLastName" 	VALUE = "{ g.Customer_ShipLastName }">
		<MvASSIGN NAME = "g.ShipEmail" 		VALUE = "{ g.Customer_ShipEmail }">
		<MvASSIGN NAME = "g.ShipCompany" 	VALUE = "{ g.Customer_ShipCompany }">
		<MvASSIGN NAME = "g.ShipPhone" 		VALUE = "{ g.Customer_ShipPhone }">
		<MvASSIGN NAME = "g.ShipFax" 		VALUE = "{ g.Customer_ShipFax }">
		<MvASSIGN NAME = "g.ShipAddress1" 	VALUE = "{ g.Customer_ShipAddress1 }">
		<MvASSIGN NAME = "g.ShipAddress2" 	VALUE = "{ g.Customer_ShipAddress2 }">
		<MvASSIGN NAME = "g.ShipCity" 		VALUE = "{ g.Customer_ShipCity }">
		<MvASSIGN NAME = "g.ShipState" 		VALUE = "{ g.Customer_ShipState }">

		<MvIF EXPR = "{ len( g.Customer_ShipStateSelect ) }">
			<MvASSIGN NAME = "g.ShipState"		VALUE = "{ g.Customer_ShipStateSelect }">
		</MvIF>

		<MvASSIGN NAME = "g.ShipZip" 		VALUE = "{ g.Customer_ShipZip }">
		<MvASSIGN NAME = "g.ShipCountry" 	VALUE = "{ g.Customer_ShipCountry }">
		<MvASSIGN NAME = "g.BillFirstName"	VALUE = "{ g.Customer_BillFirstName }">
		<MvASSIGN NAME = "g.BillLastName" 	VALUE = "{ g.Customer_BillLastName }">
		<MvASSIGN NAME = "g.BillEmail" 		VALUE = "{ g.Customer_BillEmail }">
		<MvASSIGN NAME = "g.BillCompany" 	VALUE = "{ g.Customer_BillCompany }">
		<MvASSIGN NAME = "g.BillPhone" 		VALUE = "{ g.Customer_BillPhone }">
		<MvASSIGN NAME = "g.BillFax" 		VALUE = "{ g.Customer_BillFax }">
		<MvASSIGN NAME = "g.BillAddress1" 	VALUE = "{ g.Customer_BillAddress1 }">
		<MvASSIGN NAME = "g.BillAddress2" 	VALUE = "{ g.Customer_BillAddress2 }">
		<MvASSIGN NAME = "g.BillCity" 		VALUE = "{ g.Customer_BillCity }">
		<MvASSIGN NAME = "g.BillState" 		VALUE = "{ g.Customer_BillState }">

		<MvIF EXPR = "{ len( g.Customer_BillStateSelect ) }">
			<MvASSIGN NAME = "g.BillState"	VALUE = "{ g.Customer_BillStateSelect }">
		</MvIF>

		<MvASSIGN NAME = "g.BillZip" 		VALUE = "{ g.Customer_BillZip }">
		<MvASSIGN NAME = "g.BillCountry" 	VALUE = "{ g.Customer_BillCountry }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "1">
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_Screen" PARAMETERS = "module var, screen" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "1">
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_UIException" PARAMETERS = "module var, code" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "1">
</MvFUNCTION>

<MvFUNCTION NAME = "SKIN_Export_CSS" PARAMETERS = "skin_code, skin_dir, control var, external_files var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SUI-CSSUI-00012', 'SKIN_Export_CSS is deprecated and should not be called' ) }">
</MvFUNCTION>

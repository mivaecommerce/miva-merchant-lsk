<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2020 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-SUI-MMS-
| Next Error Code: 5    
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-mmui_stsl">
	<MvASSIGN NAME = "l.module:name"		VALUE = "MMUI Store Selection">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.00">
	<MvASSIGN NAME = "l.module:features"	VALUE = "storeselui, data_domain">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Install" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE MMUI_StoreSelection
						  (
							col_num		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							header		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()		$ ',
							body_bg		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )	$ ',
							body_img	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )	$ ',
							body_text	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )	$ ',
							body_link	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )	$ ',
							body_alink	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )	$ ',
							body_vlink	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )	$ ',
							body_font	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )	$ ',
							body_fsize	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )	$ ',
							nav_bg		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )	$ ',
							nav_logo	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )	$ ',
							nav_selstr	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )	$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SUI-MMS-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.mmui_stsl:col_num" 		VALUE = 2>
	<MvASSIGN NAME = "l.mmui_stsl:header" 		VALUE = "">
	<MvASSIGN NAME = "l.mmui_stsl:body_bg" 		VALUE = "#ffffff">
	<MvASSIGN NAME = "l.mmui_stsl:body_img" 	VALUE = "">
	<MvASSIGN NAME = "l.mmui_stsl:body_text" 	VALUE = "">
	<MvASSIGN NAME = "l.mmui_stsl:body_link" 	VALUE = "">
	<MvASSIGN NAME = "l.mmui_stsl:body_alink" 	VALUE = "">
	<MvASSIGN NAME = "l.mmui_stsl:body_vlink" 	VALUE = "">
	<MvASSIGN NAME = "l.mmui_stsl:body_font" 	VALUE = "Arial, Helvetica">
	<MvASSIGN NAME = "l.mmui_stsl:body_fsize" 	VALUE = "-1">
	<MvASSIGN NAME = "l.mmui_stsl:nav_bg" 		VALUE = "#000000">
	<MvASSIGN NAME = "l.mmui_stsl:nav_logo" 	VALUE = "">
	<MvASSIGN NAME = "l.mmui_stsl:nav_selstr" 	VALUE = "{ 'graphics/' $ s.miva_language $ '/mmui/selstr.gif' }">

	<MvFUNCTIONRETURN VALUE = "{ MMUI_StoreSelection_Insert( l.mmui_stsl ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "DROP TABLE MMUI_StoreSelection">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "MMUI_StoreSelection_Insert" PARAMETERS = "mmui_stsl var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO MMUI_StoreSelection
						  ( col_num, header, body_bg, body_img,
						  	body_text, body_link, body_alink,
						  	body_vlink, body_font, body_fsize,
						  	nav_bg, nav_logo, nav_selstr )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.mmui_stsl:col_num, l.mmui_stsl:header, l.mmui_stsl:body_bg, l.mmui_stsl:body_img,
			 		   l.mmui_stsl:body_text, l.mmui_stsl:body_link, l.mmui_stsl:body_alink, l.mmui_stsl:body_vlink,
			 		   l.mmui_stsl:body_font, l.mmui_stsl:body_fsize, l.mmui_stsl:nav_bg, l.mmui_stsl:nav_logo, l.mmui_stsl:nav_selstr">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SUI-MMS-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "MMUI_StoreSelection_Update" PARAMETERS = "mmui_stsl var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE MMUI_StoreSelection
						  SET
							col_num		= ?,
							header		= ?,
							body_bg		= ?,
							body_img	= ?,
							body_text	= ?,
							body_link	= ?,
							body_alink	= ?,
							body_vlink	= ?,
							body_font	= ?,
							body_fsize	= ?,
							nav_bg		= ?,
							nav_logo	= ?,
							nav_selstr	= ?' }"
			 FIELDS	= "l.mmui_stsl:col_num, l.mmui_stsl:header, l.mmui_stsl:body_bg, l.mmui_stsl:body_img,
			 		   l.mmui_stsl:body_text, l.mmui_stsl:body_link, l.mmui_stsl:body_alink, l.mmui_stsl:body_vlink,
			 		   l.mmui_stsl:body_font, l.mmui_stsl:body_fsize, l.mmui_stsl:nav_bg, l.mmui_stsl:nav_logo, l.mmui_stsl:nav_selstr">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SUI-MMS-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "MMUI_StoreSelection_Load" PARAMETERS = "mmui_stsl var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
			    VIEW	= "MMUI_StoreSelection"
				QUERY	= "SELECT * FROM MMUI_StoreSelection">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SUI-MMS-00004', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.mmui_stsl:col_num"		VALUE = "{ MMUI_StoreSelection.d.col_num }">
	<MvASSIGN NAME = "l.mmui_stsl:header"		VALUE = "{ MMUI_StoreSelection.d.header }">
	<MvASSIGN NAME = "l.mmui_stsl:body_bg"		VALUE = "{ MMUI_StoreSelection.d.body_bg }">
	<MvASSIGN NAME = "l.mmui_stsl:body_img"		VALUE = "{ MMUI_StoreSelection.d.body_img }">
	<MvASSIGN NAME = "l.mmui_stsl:body_text"	VALUE = "{ MMUI_StoreSelection.d.body_text }">
	<MvASSIGN NAME = "l.mmui_stsl:body_link"	VALUE = "{ MMUI_StoreSelection.d.body_link }">
	<MvASSIGN NAME = "l.mmui_stsl:body_alink"	VALUE = "{ MMUI_StoreSelection.d.body_alink }">
	<MvASSIGN NAME = "l.mmui_stsl:body_vlink"	VALUE = "{ MMUI_StoreSelection.d.body_vlink }">
	<MvASSIGN NAME = "l.mmui_stsl:body_font"	VALUE = "{ MMUI_StoreSelection.d.body_font }">
	<MvASSIGN NAME = "l.mmui_stsl:body_fsize"	VALUE = "{ MMUI_StoreSelection.d.body_fsize }">
	<MvASSIGN NAME = "l.mmui_stsl:nav_bg"		VALUE = "{ MMUI_StoreSelection.d.nav_bg }">
	<MvASSIGN NAME = "l.mmui_stsl:nav_logo"		VALUE = "{ MMUI_StoreSelection.d.nav_logo }">
	<MvASSIGN NAME = "l.mmui_stsl:nav_selstr"	VALUE = "{ MMUI_StoreSelection.d.nav_selstr }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "MMUI_StoreSelection">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "UIModule_StoreSelection_Validate" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Domain_MMUI_StoreSelection:col_num"		VALUE = "{ trim( g.Domain_MMUI_StoreSelection:col_num ) }">
	<MvASSIGN NAME = "g.Domain_MMUI_StoreSelection:header"		VALUE = "{ trim( g.Domain_MMUI_StoreSelection:header ) }">
	<MvASSIGN NAME = "g.Domain_MMUI_StoreSelection:body_bg"		VALUE = "{ trim( g.Domain_MMUI_StoreSelection:body_bg ) }">
	<MvASSIGN NAME = "g.Domain_MMUI_StoreSelection:body_img"	VALUE = "{ trim( g.Domain_MMUI_StoreSelection:body_img ) }">
	<MvASSIGN NAME = "g.Domain_MMUI_StoreSelection:body_text"	VALUE = "{ trim( g.Domain_MMUI_StoreSelection:body_text ) }">
	<MvASSIGN NAME = "g.Domain_MMUI_StoreSelection:body_link"	VALUE = "{ trim( g.Domain_MMUI_StoreSelection:body_link ) }">
	<MvASSIGN NAME = "g.Domain_MMUI_StoreSelection:body_alink"	VALUE = "{ trim( g.Domain_MMUI_StoreSelection:body_alink ) }">
	<MvASSIGN NAME = "g.Domain_MMUI_StoreSelection:body_vlink"	VALUE = "{ trim( g.Domain_MMUI_StoreSelection:body_vlink ) }">
	<MvASSIGN NAME = "g.Domain_MMUI_StoreSelection:body_font"	VALUE = "{ trim( g.Domain_MMUI_StoreSelection:body_font ) }">
	<MvASSIGN NAME = "g.Domain_MMUI_StoreSelection:body_fsize"	VALUE = "{ trim( g.Domain_MMUI_StoreSelection:body_fsize ) }">
	<MvASSIGN NAME = "g.Domain_MMUI_StoreSelection:nav_bg"		VALUE = "{ trim( g.Domain_MMUI_StoreSelection:nav_bg ) }">
	<MvASSIGN NAME = "g.Domain_MMUI_StoreSelection:nav_logo"	VALUE = "{ trim( g.Domain_MMUI_StoreSelection:nav_logo ) }">
	<MvASSIGN NAME = "g.Domain_MMUI_StoreSelection:nav_selstr"	VALUE = "{ trim( g.Domain_MMUI_StoreSelection:nav_selstr ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Range_Required( g.Domain_MMUI_StoreSelection:col_num, 1, 10 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'MMUIDPY', 'Domain_MMUI_StoreSelection:col_num', g.Validation_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "UIModule_StoreSelection_Tabs" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "GT_MMUISTSL:Store Selection Layout,GT_MMUISTSL/MMUIDPY:Store Selection Display,GT_MMUISTSL/MMUICLR:Store Selection Colors,GT_MMUISTSL/MMUIFNT:Store Selection Fonts,GT_MMUISTSL/MMUIIMG:Store Selection Images">
</MvFUNCTION>

<MvFUNCTION NAME = "UIModule_StoreSelection_Content" PARAMETERS = "tab, load_fields" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.load_fields }">
		<MvIF EXPR = "{ NOT MMUI_StoreSelection_Load( g.Domain_MMUI_StoreSelection ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, 'MMUIDPY' ) }">
		<MvHIDE FIELDS = "g.Domain_MMUI_StoreSelection:col_num, g.Domain_MMUI_StoreSelection:header">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_MMUISTSL', 'MMUIDPY' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<tr><td valign="middle" nowrap>
			<b>Stores/Line:</b>
		</td><td width="100%">
			<input type="text" size="10" name="Domain_MMUI_StoreSelection:col_num" value="{ encodeentities( g.Domain_MMUI_StoreSelection:col_num ) }">
		</td></tr>

		<tr><td valign="top" nowrap>
			Header:
		</td><td width="100%">
			<textarea NAME="Domain_MMUI_StoreSelection:header" rows="10" cols="58" wrap="off"><MvEVAL EXPR="{ encodeentities( g.Domain_MMUI_StoreSelection:header ) }"></textarea>
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, 'MMUICLR' ) }">
		<MvHIDE FIELDS = "g.Domain_MMUI_StoreSelection:body_bg, g.Domain_MMUI_StoreSelection:body_text, g.Domain_MMUI_StoreSelection:body_link,
						  g.Domain_MMUI_StoreSelection:body_alink, g.Domain_MMUI_StoreSelection:body_vlink, g.Domain_MMUI_StoreSelection:nav_bg">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_MMUISTSL', 'MMUICLR' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<tr><td valign="middle" nowrap>
			Body Background:
		</td><td width="100%" nowrap>
			<input type="text" size="10" name="Domain_MMUI_StoreSelection:body_bg" value="{ encodeentities( g.Domain_MMUI_StoreSelection:body_bg ) }"> <a href="JavaScript:PopupColorSelector( 'Domain_MMUI_StoreSelection:body_bg' );"><img alt="Select Color" border=0 src="{ g.AdminGraphics_Path $ 'color.gif' }"></a>
		</td></tr>

		<tr><td valign="middle" nowrap>
			Body Text:
		</td><td width="100%" nowrap>
			<input type="text" size="10" name="Domain_MMUI_StoreSelection:body_text" value="{ encodeentities( g.Domain_MMUI_StoreSelection:body_text ) }"> <a href="JavaScript:PopupColorSelector( 'Domain_MMUI_StoreSelection:body_text' );"><img alt="Select Color" border=0 src="{ g.AdminGraphics_Path $ 'color.gif' }"></a>
		</td></tr>

		<tr><td valign="middle" nowrap>
			Body Link:
		</td><td width="100%" nowrap>
			<input type="text" size="10" name="Domain_MMUI_StoreSelection:body_link" value="{ encodeentities( g.Domain_MMUI_StoreSelection:body_link ) }"> <a href="JavaScript:PopupColorSelector( 'Domain_MMUI_StoreSelection:body_link' );"><img alt="Select Color" border=0 src="{ g.AdminGraphics_Path $ 'color.gif' }"></a>
		</td></tr>

		<tr><td valign="middle" nowrap>
			Body Active Link:
		</td><td width="100%" nowrap>
			<input type="text" size="10" name="Domain_MMUI_StoreSelection:body_alink" value="{ encodeentities( g.Domain_MMUI_StoreSelection:body_alink ) }"> <a href="JavaScript:PopupColorSelector( 'Domain_MMUI_StoreSelection:body_alink' );"><img alt="Select Color" border=0 src="{ g.AdminGraphics_Path $ 'color.gif' }"></a>
		</td></tr>

		<tr><td valign="middle" nowrap>
			Body Viewed Link:
		</td><td width="100%" nowrap>
			<input type="text" size="10" name="Domain_MMUI_StoreSelection:body_vlink" value="{ encodeentities( g.Domain_MMUI_StoreSelection:body_vlink ) }"> <a href="JavaScript:PopupColorSelector( 'Domain_MMUI_StoreSelection:body_vlink' );"><img alt="Select Color" border=0 src="{ g.AdminGraphics_Path $ 'color.gif' }"></a>
		</td></tr>

		<tr><td valign="middle" nowrap>
			Navigation Bar Background:
		</td><td width="100%" nowrap>
			<input type="text" size="10" name="Domain_MMUI_StoreSelection:nav_bg" value="{ encodeentities( g.Domain_MMUI_StoreSelection:nav_bg ) }"> <a href="JavaScript:PopupColorSelector( 'Domain_MMUI_StoreSelection:nav_bg' );"><img alt="Select Color" border=0 src="{ g.AdminGraphics_Path $ 'color.gif' }"></a>
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, 'MMUIFNT' ) }">
		<MvHIDE FIELDS = "g.Domain_MMUI_StoreSelection:body_font, g.Domain_MMUI_StoreSelection:body_fsize">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_MMUISTSL', 'MMUIFNT' ) }">
		<table border="0" cellpadding=2 cellspacing=0 width="100%">
		<tr><td valign="top" nowrap>
			<b>Body Text:</b>
		</td><td width="100%">
			<table border="0" cellpadding="2" cellspacing="0">
			<tr><td valign="middle">
				Face:
			</td><td valign="middle">
				<input type="text" size="40" name="Domain_MMUI_StoreSelection:body_font" value="{ encodeentities( g.Domain_MMUI_StoreSelection:body_font ) }">
			</td></tr>

			<tr><td valign="middle">
				Size:
			</td><td valign="middle">
				<input type="text" size="10" name="Domain_MMUI_StoreSelection:body_fsize" value="{ encodeentities( g.Domain_MMUI_StoreSelection:body_fsize ) }">
			</td></tr>
			</table>
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, 'MMUIIMG' ) }">
		<MvHIDE FIELDS = "g.Domain_MMUI_StoreSelection:body_img, g.Domain_MMUI_StoreSelection:nav_logo, g.Domain_MMUI_StoreSelection:nav_logoLink,
						  g.Domain_MMUI_StoreSelection:nav_selstr">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_MMUISTSL', 'MMUIIMG' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
		<tr><td valign="middle" nowrap>
			Body Background:
		</td><td width="100%" nowrap>
			<input type="text" size="40" name="Domain_MMUI_StoreSelection:body_img" value="{ encodeentities( g.Domain_MMUI_StoreSelection:body_img ) }"> <a href="JavaScript:PopupFileUpload( 'Image', '', 'Domain_MMUI_StoreSelection:body_img' );"><img alt="Upload File" border=0 src="{ g.AdminGraphics_Path $ 'upload.gif' }"></a>
		</td></tr>

		<tr><td valign="middle" nowrap>
			Navigation Bar Logo:
		</td><td width="100%" nowrap>
			<input type="text" size="40" name="Domain_MMUI_StoreSelection:nav_logo" value="{ encodeentities( g.Domain_MMUI_StoreSelection:nav_logo ) }"> <a href="JavaScript:PopupFileUpload( 'Image', '', 'Domain_MMUI_StoreSelection:nav_logo' );"><img alt="Upload File" border=0 src="{ g.AdminGraphics_Path $ 'upload.gif' }"></a>
		</td></tr>

		<tr><td valign="middle" nowrap>
			Navigation Bar "Select Store":
		</td><td width="100%" nowrap>
			<input type="text" size="40" name="Domain_MMUI_StoreSelection:nav_selstr" value="{ encodeentities( g.Domain_MMUI_StoreSelection:nav_selstr ) }"> <a href="JavaScript:PopupFileUpload( 'Image', '', 'Domain_MMUI_StoreSelection:nav_selstr' );"><img alt="Upload File" border=0 src="{ g.AdminGraphics_Path $ 'upload.gif' }"></a>
		</td></tr>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "UIModule_StoreSelection_Update" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT MMUI_StoreSelection_Update( g.Domain_MMUI_StoreSelection ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "UIModule_StoreSelection_Thumbnail" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ 'graphics/' $ s.miva_language $ '/mmui/store_th.gif' }">
</MvFUNCTION>

<MvFUNCTION NAME = "UIModule_StoreSelection_Render" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT MMUI_StoreSelection_Load( l.mmui_store_select ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>
	<html>
	<head>
	<title><MvEVAL EXPR = "{ g.Domain:name }"></title>
	<MvIF EXPR = "{ g.Secure }">
		<base href = "{ encodeentities( g.secure_baseurl ) }">
	<MvELSE>
		<base href = "{ encodeentities( g.baseurl ) }">
	</MvIF>
	</head>

	<MvASSIGN NAME = "l.tag_attributes" VALUE = "">

	<MvIF EXPR = "{ len( l.mmui_store_select:body_bg ) }">
		<MvASSIGN NAME = "l.tag_attributes" VALUE = "{ l.tag_attributes $ ' bgcolor="' $ encodeentities( l.mmui_store_select:body_bg ) $ '"' }">
	</MvIF>
	<MvIF EXPR = "{ len( l.mmui_store_select:body_text ) }">
		<MvASSIGN NAME = "l.tag_attributes" VALUE = "{ l.tag_attributes $ ' TEXT = "' $ encodeentities( l.mmui_store_select:body_text ) $ '"' }">
	</MvIF>
	<MvIF EXPR = "{ len( l.mmui_store_select:body_link ) }">
		<MvASSIGN NAME = "l.tag_attributes" VALUE = "{ l.tag_attributes $ ' LINK = "' $ encodeentities( l.mmui_store_select:body_link ) $ '"' }">
	</MvIF>
	<MvIF EXPR = "{ len( l.mmui_store_select:body_alink ) }">
		<MvASSIGN NAME = "l.tag_attributes" VALUE = "{ l.tag_attributes $ ' ALINK = "' $ encodeentities( l.mmui_store_select:body_alink ) $ '"' }">
	</MvIF>
	<MvIF EXPR = "{ len( l.mmui_store_select:body_vlink ) }">
		<MvASSIGN NAME = "l.tag_attributes" VALUE = "{ l.tag_attributes $ ' VLINK = "' $ encodeentities( l.mmui_store_select:body_vlink ) $ '"' }">
	</MvIF>
	<MvIF EXPR = "{ len( l.mmui_store_select:body_img ) }">
		<MvASSIGN NAME = "l.tag_attributes" VALUE = "{ l.tag_attributes $ ' BACKGROUND = "' $ encodeentities( l.mmui_store_select:body_img ) $ '"' }">
	</MvIF>

	<MvEVAL EXPR = "{ '<BODY' $ l.tag_attributes $ '>' }">

	<table border=0 cellpadding=0 cellspacing=0 width="100%">
	<tr><td align="left" valign="middle" colspan="4" bgcolor="{ encodeentities( l.mmui_store_select:nav_bg ) }">
		<table border="0" cellpadding=4 cellspacing=0 width="100%">
		<tr><td align="left" valign="bottom" bgcolor="{ encodeentities( l.mmui_store_select:nav_bg ) }" nowrap>
			<table border="0" cellpadding=0 cellspacing=0 width="100%">
			<tr><td align="left" valign = "bottom">
				<img src="{ encodeentities( l.mmui_store_select:nav_logo ) }">&nbsp;
				<a href="{ g.sessionurl $ 'Screen=STSL' }"><IMG SRC="{ encodeentities( l.mmui_store_select:nav_selstr ) }" ALT="Select Store" BORDER=0></a>
			</td></tr>
			</table>
		</td></tr>
		</table>
	</td></tr>

	<tr><td align="left" valign="top" width="1" bgcolor="{ encodeentities( l.mmui_store_select:nav_bg ) }">
		<img src="{ 'graphics/' $ s.miva_language $ '/mmui/dot.gif' }">
	</td><td align="left" valign="top">
		<img src="{ 'graphics/' $ s.miva_language $ '/mmui/dot.gif' }" width=350 height=1><br>
		<blockquote>
			<MIVA STANDARDOUTPUTLEVEL = "text, html"><font face="{ l.mmui_store_select:body_font }" size="{ l.mmui_store_select:body_fsize }"><MvEVAL EXPR = "{ l.mmui_store_select:header }"></font><MIVA STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">

			<MvASSIGN NAME = "l.column" VALUE = 1>
			<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.store_count" VALUE = "{ StoreList_Load_All( l.store ) }">

			<table border="0">
			<tr>
			<MvASSIGN NAME = "l.pos" VALUE = 1>
			<MvWHILE EXPR = "{ l.pos LE l.store_count }">
				<td align="left" valign="middle">
					<font face="{ l.mmui_store_select:body_font }" size="{ l.mmui_store_select:body_fsize }">
					<a href = "{ g.sessionurl $ 'Screen=SFNT&Store_Code=' $ encodeattribute( l.store[ l.pos ]:code ) }"><MvEVAL EXPR = "{ l.store[ l.pos ]:name }"></a>
					</font>
				</td>

				<MvASSIGN NAME = "l.column" VALUE = "{ l.column + 1 }">
				<MvIF EXPR = "{ l.column GT l.mmui_store_select:col_num }">
					</tr><tr>

					<MvASSIGN NAME = "l.column" VALUE = 1>
				</MvIF>

				<MvASSIGN NAME = "l.pos" VALUE = "{ l.pos + 1 }">
			</MvWHILE>

			<MvWHILE EXPR = "{ l.column LE l.mmui_store_select:col_num }">
				<td>
					&nbsp;
				</td>

				<MvASSIGN NAME = "l.column" VALUE = "{ l.column + 1 }">
			</MvWHILE>
			</tr>
			</table>
		</blockquote>
	</td></tr>
	</table>
	</body>
<html>
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-EXP-UST-
| Next Error Code: 10
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-userdataexp">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Export User Data to XML File">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0803">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.00">
	<MvASSIGN NAME = "l.module:features"	VALUE = "export">
</MvFUNCTION>

<MvFUNCTION NAME = "ExportModule_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.UserDataExport_File"					VALUE = "{ trim( g.UserDataExport_File ) }">
	<MvASSIGN NAME = "g.UserDataExport_Email"					VALUE = "{ trim( g.UserDataExport_Email ) }">
	<MvASSIGN NAME = "g.UserDataExport_SendEmail"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.UserDataExport_SendEmail ) }">
	<MvASSIGN NAME = "g.UserDataExport_Bookmarks"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.UserDataExport_Bookmarks ) }">
	<MvASSIGN NAME = "g.UserDataExport_DeleteBookmarks"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.UserDataExport_DeleteBookmarks ) }">
	<MvASSIGN NAME = "g.UserDataExport_UserSettings"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.UserDataExport_UserSettings ) }">
	<MvASSIGN NAME = "g.UserDataExport_UserPreferences"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.UserDataExport_UserPreferences ) }">
	<MvASSIGN NAME = "g.UserDataExport_DeleteUserPreferences"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.UserDataExport_DeleteUserPreferences ) }">

	<MvIF EXPR = "{ ISNULL g.UserDataExport_File }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'UserDataExport_File', 'Please specify a file' ) }">
	<MvELSE>
		<MvIF EXPR = "{ '/' IN g.UserDataExport_File }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'UserDataExport_File', 'Please do not include a path in the file name' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.UserDataExport_SendEmail AND len( g.UserDataExport_Email ) EQ 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'UserDataExport_Email', 'Please specify a valid email address' ) }">
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.UserDataExport_Email ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'UserDataExport_Email', 'Please specify a valid email address' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT g.UserDataExport_Bookmarks 		AND
					NOT g.UserDataExport_UserSettings	AND
					NOT g.UserDataExport_UserPreferences }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', 'Please select at least one field' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ExportModule_Export" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.directory"							VALUE = "{ g.MerchantPath $ 's' $ padl( g.Store:id, 2, '0' ) $ '/export/' }">
	<MvASSIGN NAME = "l.output_file"						VALUE = "{ l.directory $ g.UserDataExport_File }">
	<MvASSIGN NAME = "g.UserDataExport_Export_Time_Start"	VALUE = "{ s.time_t }">

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Start( 'UserSetting Export Progress', 'EXPT' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Show() }">

	<MvIF EXPR = "{ NOT UserDataExport_Initialize_Export( l.directory, l.output_file ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	<MvELSEIF EXPR = "{ UserDataExport_Finalize_Export( l.directory, l.output_file ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ g.UserDataExport_Bookmarks }">
		<MvIF EXPR = "{ g.UserDataExport_DeleteBookmarks AND NOT g.UserDataExport_Export_DeleteBookmarksSet }">
			<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '	<Bookmarks_DeleteAll />' ) }">

			<MvASSIGN NAME = "g.UserDataExport_Export_DeleteBookmarksSet" VALUE = 1>
		</MvIF>

		<MvIF EXPR = "{ NOT UserDataExport_Export_Bookmarks( l.output_file, g.UserDataExport_Export_LastBookmark, g.UserDataExport_Export_ExportedBookmarkCount, g.UserDataExport_Export_TotalBookmarkCount ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.UserDataExport_UserSettings AND NOT g.UserDataExport_Export_NeedRefresh }">
		<MvIF EXPR = "{ NOT UserDataExport_Export_UserSettings( l.output_file, g.UserDataExport_Export_ExportedUserSettingCount, g.UserDataExport_Export_TotalUserSettingCount ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.UserDataExport_UserPreferences AND NOT g.UserDataExport_Export_NeedRefresh }">
		<MvIF EXPR = "{ g.UserDataExport_DeleteUserPreferences AND NOT g.UserDataExport_Export_DeleteUserPreferencesSet }">
			<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '	<UserPreferences_DeleteAll />' ) }">

			<MvASSIGN NAME = "g.UserDataExport_Export_DeleteUserPreferencesSet" VALUE = 1>
		</MvIF>

		<MvIF EXPR = "{ NOT UserDataExport_Export_UserPreferences( l.output_file, g.UserDataExport_Export_LastUserpreference, g.UserDataExport_Export_ExportedUserpreferenceCount, g.UserDataExport_Export_TotalUserpreferenceCount ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvEVAL EXPR = "{ UserDataExport_Calculate_Counts( l.exported_count, l.total_count ) }">
	
	<MvIF EXPR = "{ l.exported_count GE l.total_count }">
		<MvASSIGN NAME = "g.UserDataExport_Export_NeedRefresh" VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "UserDataExport_Initialize_Export" PARAMETERS = "directory, output_file" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.UserDataExport_Export_Initialized }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "g.UserDataExport_Export_LastBookmark"					VALUE = 0>
	<MvASSIGN NAME = "g.UserDataExport_Export_ExportedBookmarkCount" 		VALUE = 0>
	<MvASSIGN NAME = "g.UserDataExport_Export_DeleteBookmarksSet" 			VALUE = 0>

	<MvASSIGN NAME = "g.UserDataExport_Export_ExportedUserSettingCount" 	VALUE = 0>

	<MvASSIGN NAME = "g.UserDataExport_Export_LastUserPreference"			VALUE = 0>
	<MvASSIGN NAME = "g.UserDataExport_Export_ExportedUserPreferenceCount" 	VALUE = 0>
	<MvASSIGN NAME = "g.UserDataExport_Export_DeleteUserPreferencesSet" 	VALUE = 0>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].EnsurePathExists( 'data', l.directory ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ fexists( l.output_file ) }">
		<MvIF EXPR = "{ NOT g.UserDataExport_Append }">
			<MvASSIGN NAME = "l.null" VALUE = "{ fdelete( l.output_file ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.UserDataExport_Bookmarks }">
		<MvIF EXPR = "{ NOT UserDataExport_Export_BookmarkCount( g.UserDataExport_Export_TotalBookmarkCount ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.UserDataExport_UserSettings }">
		<MvASSIGN NAME = "g.UserDataExport_Export_TotalUserSettingCount" VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ g.UserDataExport_UserPreferences }">
		<MvIF EXPR = "{ NOT UserDataExport_Export_UserPreferenceCount( g.UserDataExport_Export_TotalUserPreferenceCount ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '<User>' ) }">
	
	<MvASSIGN NAME = "g.UserDataExport_Export_Initialized" VALUE = 1>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "UserDataExport_Finalize_Export" PARAMETERS = "directory, output_file" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ UserDataExport_Calculate_Counts( l.exported_count, l.total_count ) }">

	<MvIF EXPR = "{ l.exported_count GE l.total_count }">
		<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '</User>' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_ProgressComplete( l.exported_count $ ' user data ' $ [ g.Module_Library_Utilities ].Plural( l.exported_count, 'record', 'records' ) $ ' exported to \'' $ l.output_file $ '\'' ) }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-EXP-UST-00009', 'User data exported' ) }">

		<MvIF EXPR = "{ g.UserDataExport_SendEmail }">
			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_Utilities ].Send_Email_Attachment( g.Store:email, g.UserDataExport_Email, '', 'Miva Merchant User Data Export', g.UserDataExport_File, l.directory, 'data' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-EXP-UST-00001', 'UserDataExport Email Sent' ) }">
		</MvIF>		

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "UserDataExport_Calculate_Counts" PARAMETERS = "exported_count var, total_count var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.total_count" 		VALUE = 0>
	<MvASSIGN NAME = "l.exported_count" 	VALUE = 0>

	<MvIF EXPR = "{ g.UserDataExport_Bookmarks }">
		<MvASSIGN NAME = "l.total_count" 	VALUE = "{ g.UserDataExport_Export_TotalBookmarkCount + l.total_count }">
		<MvASSIGN NAME = "l.exported_count"	VALUE = "{ g.UserDataExport_Export_ExportedBookmarkCount + l.exported_count }">
	</MvIF>

	<MvIF EXPR = "{ g.UserDataExport_UserSettings }">
		<MvASSIGN NAME = "l.total_count" 	VALUE = "{ g.UserDataExport_Export_TotalUserSettingCount + l.total_count }">
		<MvASSIGN NAME = "l.exported_count"	VALUE = "{ g.UserDataExport_Export_ExportedUserSettingCount + l.exported_count }">
	</MvIF>

	<MvIF EXPR = "{ g.UserDataExport_UserPreferences }">
		<MvASSIGN NAME = "l.total_count"	VALUE = "{ g.UserDataExport_Export_TotalUserPreferenceCount + l.total_count }">
		<MvASSIGN NAME = "l.exported_count"	VALUE = "{ g.UserDataExport_Export_ExportedUserPreferenceCount + l.exported_count }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "UserDataExport_Export_BookmarkCount" PARAMETERS = "total_count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Bookmarks"
				QUERY	= "{ 'SELECT COUNT( * ) AS total_count FROM Bookmarks WHERE user_id = ?' }"
				FIELDS	= "g.User:id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-EXP-UST-00002', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.total_count" VALUE = "{ Bookmarks.d.total_count }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Bookmarks">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "UserDataExport_Export_Bookmarks" PARAMETERS = "output_file, last_id var, export_count var, total_count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.last_id" VALUE = "{ int( l.last_id ) }">

	<MvIF EXPR = "{ l.export_count GE l.total_count }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Bookmarks"
				QUERY	= "{ 'SELECT
								b.id,
								b.title,
								b.action,
								b.store_id,
								b.disp_order,
								s.code AS store_code
							  FROM 
							  	Bookmarks b
								LEFT OUTER JOIN Stores s ON b.store_id = s.id
							  WHERE
							  	b.user_id 		= ? AND
							  	b.disp_order 	> ?
							  ORDER BY b.disp_order' }"
				FIELDS	= "g.User:id, l.last_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-EXP-UST-00003', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT Bookmarks.d.EOF AND NOT g.UserDataExport_Export_NeedRefresh }">
		<MvASSIGN NAME = "l.export_count"	VALUE = "{ l.export_count + 1 }">

		<MvEVAL EXPR = "{ UserDataExport_Calculate_Counts( l.display_exported_count, l.display_total_count ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Update( l.display_exported_count, l.display_total_count ) }">

		<MvIF EXPR = "{ Bookmarks.d.store_id AND NOT ISNULL Bookmarks.d.store_code }">
			<MvASSIGN NAME = "l.store_code" VALUE = "{ Bookmarks.d.store_code }">
		<MvELSE>
			<MvASSIGN NAME = "l.store_code" VALUE = "">
		</MvIF>

		<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '	<Bookmark_Add store_code="' $ encodeentities( l.store_code ) $ '">' ) }">
		<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '		<Title>'		$ [ g.Module_Library_Utilities ].CDATA_Encode( Bookmarks.d.title ) 	$ '</Title>' ) }">
		<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '		<Action>'		$ [ g.Module_Library_Utilities ].CDATA_Encode( Bookmarks.d.action ) 	$ '</Action>' ) }">
		<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '	</Bookmark_Add>' ) }">

		<MvIF EXPR = "{ ( s.globaltimeout EQ 0 ) OR ( s.globaltimeout GT 20 ) }">
			<MvASSIGN NAME = "l.end_time" VALUE = "20">
		<MvELSE>
			<MvASSIGN NAME = "l.end_time" VALUE = "{ s.globaltimeout / 3 }">		
		</MvIF>

		<MvIF EXPR = "{ ( s.dyn_time_t - g.UserDataExport_Export_Time_Start ) GT l.end_time }">
			<MvASSIGN NAME = "l.last_id" 							VALUE = "{ Bookmarks.d.disp_order }">
			<MvASSIGN NAME = "g.UserDataExport_Export_NeedRefresh" 	VALUE = 1>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "Bookmarks" ROWS = 1>
	</MvWHILE>

	<MvIF EXPR = "{ g.UserDataExport_Export_NeedRefresh AND Bookmarks.d.EOF }">
		<MvASSIGN NAME = "g.UserDataExport_Export_NeedRefresh" 		VALUE = 0>
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Bookmarks">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "UserDataExport_Export_UserSettings" PARAMETERS = "output_file, export_count var, total_count var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.export_count GE l.total_count }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].User_Load_ID( g.User:id, l.user ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '	<Settings_Update>' ) }">
	<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '		<BatchListSaveSortWhenSearching>'		$ Boolean_Encode( l.user:mmbl_sort )	$ '</BatchListSaveSortWhenSearching>' ) }">
	<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '		<HoverOn>'								$ Boolean_Encode( l.user:hover_on )		$ '</HoverOn>' ) }">
	<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '		<DefaultPagination>'					$ int( l.user:page_len )				$ '</DefaultPagination>' ) }">
	<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '		<WordWrapProductDescription>'			$ Boolean_Encode( l.user:wwdescrip )	$ '</WordWrapProductDescription>' ) }">

	<MvIF EXPR = "{ l.user:mmbl_count EQ 0 }">
		<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '		<DefaultBatchListLoadCount type="auto" />' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '		<DefaultBatchListLoadCount type="custom">' $ int( l.user:mmbl_count ) 		$ '</DefaultBatchListLoadCount>' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '	</Settings_Update>' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-EXP-UST-00005', 'UserData User Settings exported' ) }">

	<MvASSIGN NAME = "l.export_count" VALUE = "{ l.export_count + 1 }">

	<MvIF EXPR = "{ ( s.globaltimeout EQ 0 ) OR ( s.globaltimeout GT 20 ) }">
		<MvASSIGN NAME = "l.end_time" VALUE = "20">
	<MvELSE>
		<MvASSIGN NAME = "l.end_time" VALUE = "{ s.globaltimeout / 3 }">		
	</MvIF>

	<MvIF EXPR = "{ ( s.dyn_time_t - g.UserDataExport_Export_Time_Start ) GT l.end_time }">
		<MvASSIGN NAME = "g.UserDataExport_Export_NeedRefresh" 	VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "UserDataExport_Export_UserPreferenceCount" PARAMETERS = "total_count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "UserPreferences"
				QUERY	= "{ 'SELECT COUNT( * ) AS total_count FROM UserPreferences WHERE user_id = ?' }"
				FIELDS	= "g.User:id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-EXP-UST-00006', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.total_count" VALUE = "{ UserPreferences.d.total_count }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "UserPreferences">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "UserDataExport_Export_UserPreferences" PARAMETERS = "output_file, last_id var, export_count var, total_count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.store_id"	VALUE = "{ int( gettoken( l.last_id, ':', 1 ) ) }">
	<MvASSIGN NAME = "l.keymd5"		VALUE = "{ gettoken( l.last_id, ':', 2 ) }">

	<MvIF EXPR = "{ l.export_count GE l.total_count }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "UserPreferences"
				QUERY	= "{ 'SELECT
								up.mm_key,
								up.mm_value,
								up.mm_keymd5,
								up.store_id,
								s.code AS store_code
							  FROM 
							  	UserPreferences up
								LEFT OUTER JOIN Stores s ON up.store_id = s.id
							  WHERE
							  	up.user_id 				= ? 	AND
							  	(
							  		up.mm_keymd5		> ? 	OR
							  		(
							  			up.mm_keymd5 	= ? 	AND
							  			up.store_id 	> ?
							  		)
							  	)
							  ORDER BY up.mm_keymd5' }"
				FIELDS	= "g.User:id, l.keymd5, l.keymd5, l.store_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-EXP-UST-00007', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT UserPreferences.d.EOF AND NOT g.UserDataExport_Export_NeedRefresh }">
		<MvASSIGN NAME = "l.export_count"	VALUE = "{ l.export_count + 1 }">

		<MvEVAL EXPR = "{ UserDataExport_Calculate_Counts( l.display_exported_count, l.display_total_count ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Update( l.display_exported_count, l.display_total_count ) }">

		<MvIF EXPR = "{ UserPreferences.d.store_id AND NOT ISNULL UserPreferences.d.store_code }">
			<MvASSIGN NAME = "l.store_code" VALUE = "{ UserPreferences.d.store_code }">
		<MvELSE>
			<MvASSIGN NAME = "l.store_code" VALUE = "">
		</MvIF>

		<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '	<UserPreference_Add store_code="' $ encodeentities( l.store_code ) $ '">' ) }">
		<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '		<Key>'			$ [ g.Module_Library_Utilities ].CDATA_Encode( UserPreferences.d.mm_key ) 		$ '</Key>' ) }">
		<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '		<Value>'		$ [ g.Module_Library_Utilities ].CDATA_Encode( UserPreferences.d.mm_value )	$ '</Value>' ) }">
		<MvEVAL EXPR = "{ UserDataExport_Export_Line( l.output_file, '	</UserPreference_Add>' ) }">

		<MvIF EXPR = "{ ( s.globaltimeout EQ 0 ) OR ( s.globaltimeout GT 20 ) }">
			<MvASSIGN NAME = "l.end_time" VALUE = "20">
		<MvELSE>
			<MvASSIGN NAME = "l.end_time" VALUE = "{ s.globaltimeout / 3 }">		
		</MvIF>

		<MvIF EXPR = "{ ( s.dyn_time_t - g.UserDataExport_Export_Time_Start ) GT l.end_time }">
			<MvASSIGN NAME = "l.last_id" 							VALUE = "{ UserPreferences.d.store_id $ ':' $ UserPreferences.d.mm_keymd5 }">
			<MvASSIGN NAME = "g.UserDataExport_Export_NeedRefresh" 	VALUE = 1>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "UserPreferences" ROWS = 1>
	</MvWHILE>

	<MvIF EXPR = "{ g.UserDataExport_Export_NeedRefresh AND UserPreferences.d.EOF }">
		<MvASSIGN NAME = "g.UserDataExport_Export_NeedRefresh" 		VALUE = 0>
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "UserPreferences">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ExportModule_Screen" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.UserDataExport_Export_NeedRefresh }">
		<MvASSIGN NAME = "g.UserDataExport_Working_Append" VALUE = 1>
	
		<MvHIDE FIELDS = "g.Module_Code, UserDataExport_Export_Initialized, g.Reset, g.UserDataExport_Working_Append,
						  g.UserDataExport_File, g.UserDataExport_Append, g.UserDataExport_Email, g.UserDataExport_SendEmail,
						  g.UserDataExport_UserSettings, g.UserDataExport_Export_ExportedUserSettingCount, g.UserDataExport_Export_TotalUserSettingCount,
						  g.UserDataExport_Bookmarks, g.UserDataExport_DeleteBookmarks, g.UserDataExport_Export_LastBookmark, g.UserDataExport_Export_ExportedBookmarkCount, g.UserDataExport_Export_TotalBookmarkCount, g.UserDataExport_Export_DeleteBookmarksSet,
						  g.UserDataExport_UserPreferences, g.UserDataExport_DeleteUserPreferences, g.UserDataExport_Export_LastUserPreference, g.UserDataExport_Export_ExportedUserPreferenceCount, g.UserDataExport_Export_TotalUserPreferenceCount, UserDataExport_Export_DeleteUserPreferencesSet">

		<script type="text/javascript">
			function exp_continue()
			{
				document.forms[ Screen ].submit();
			}
			window.onload = exp_continue;
		</script>

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_End() }">

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ g.Load_Fields }">
		<MvIF EXPR = "{ g.Reset NE 'No' }">
			<MvASSIGN NAME = "g.UserDataExport_File"					VALUE = "">
			<MvASSIGN NAME = "g.UserDataExport_Append"					VALUE = "">
			<MvASSIGN NAME = "g.UserDataExport_SendEmail"				VALUE = 0>

			<MvASSIGN NAME = "g.UserDataExport_Bookmarks"				VALUE = 1>
			<MvASSIGN NAME = "g.UserDataExport_DeleteBookmarks"			VALUE = 0>
			<MvASSIGN NAME = "g.UserDataExport_UserSettings"			VALUE = 1>
			<MvASSIGN NAME = "g.UserDataExport_UserPreferences"			VALUE = 1>
			<MvASSIGN NAME = "g.UserDataExport_DeleteUserPreferences"	VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ ISNULL g.UserDataExport_File }">		<MvASSIGN NAME = "g.UserDataExport_File"	VALUE = "userdata.xml">		</MvIF>
		<MvIF EXPR = "{ ISNULL g.UserDataExport_Append }">		<MvASSIGN NAME = "g.UserDataExport_Append"	VALUE = 0>					</MvIF>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_Start( 'Export User Data to XML File', 'EXPT', '' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_JavaScript( 'Exporting User Data to XML File', 'EXPT', 'EXPT' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_CSS() }">

	<script language="JavaScript">
		HaveCustomReset = 1;

		function CustomReset()
		{
			document.EXPT.Reset.value = 'Yes';
		}
	</script>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_End( 'Export User Data to XML File', 'EXPT', '' ) }">

	<input type="hidden" name="Reset" value="No">
	<MvHIDE FIELDS = "g.Module_Code">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginContent() }">
	<table width="100%">
		<tr><td nowrap>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Open( 'Destination', 110 ) }">
				<tr><td nowrap>
					<font face="Arial, Helvetica" size=-1><b>
						Export User Data to File:
					</font></b>
				</td><td width="100%">
					<input type="text" name="UserDataExport_File" size="40" value="{ encodeentities( g.UserDataExport_File ) }">
				</td></tr>

				<tr><td valign="top" nowrap>
					<b>If File Exists:</b>
				</td><td width="100%">
					<MvIF EXPR = "{ g.UserDataExport_Append }">
						<label><input type="radio" name="UserDataExport_Append" value="Yes" checked>Append To File<br></label>
						<label><input type="radio" name="UserDataExport_Append" value="">Replace File</label>
					<MvELSE>
						<label><input type="radio" name="UserDataExport_Append" value="Yes">Append To File</label>

						<br>

						<label><input type="radio" name="UserDataExport_Append" value="" checked>Replace File</label>
					</MvIF>
				</td></tr>

				<tr><td valign="top" nowrap>
					Email File To:
				</td><td width="100%" nowrap>
					<MvIF EXPR = "{ NOT g.UserDataExport_SendEmail }">
						<label><input type="radio" name="UserDataExport_SendEmail" value="0" checked>Do Not Email<br></label>
						<input type="radio" name="UserDataExport_SendEmail" value=1>
					<MvELSE>
						<label><input type="radio" name="UserDataExport_SendEmail" value="0">Do Not Email<br></label>
						<input type="radio" name="UserDataExport_SendEmail" value="1" checked>
					</MvIF>

					<input type="text" size="40" name="UserDataExport_Email" value="{ encodeentities( g.Store:email ) }">
				</td></tr>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Close() }">
		</td><td width = "100%">
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Open( 'Data to Export', 110 ) }">
				<tr><td valign="top" nowrap>
					<b>User Data:</b>
				</td><td align="left" valign="middle" colspan="3">
					<table>
						<tr><td align="left" valign="top" nowrap>
							<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.UserDataExport_Bookmarks, 'UserDataExport_Bookmarks', 'Yes', 'Bookmarks' ) }"></label>
						</td><td width="100%" valign="top">
							<table cellpadding="0" cellspacing="0">
								<tr><td>
									<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'UserDataExport_DeleteBookmarks', '', g.UserDataExport_DeleteBookmarks, 'Append mode' ) }"></label>
								</td></tr>

								<tr><td>
									<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'UserDataExport_DeleteBookmarks', 'Yes', g.UserDataExport_DeleteBookmarks, 'Replace all mode' ) }"></label>
								</td></tr>
							</table>
						</td></tr>

						<tr><td align="left" colspan="2" nowrap>
							<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.UserDataExport_UserSettings, 'UserDataExport_UserSettings', 'Yes', 'User Settings' ) }"></label>
						</td></tr>

						<tr><td align="left" valign="top" nowrap>
							<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.UserDataExport_UserPreferences, 'UserDataExport_UserPreferences', 'Yes', 'User Preferences' ) }"></label>
						</td><td width="100%" valign="top">
							<table cellpadding="0" cellspacing="0">
								<tr><td>
									<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'UserDataExport_DeleteUserPreferences', 0, g.UserDataExport_DeleteUserPreferences, 'Append mode' ) }"></label>
								</td></tr>

								<tr><td>
									<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'UserDataExport_DeleteUserPreferences', 1, g.UserDataExport_DeleteUserPreferences, 'Replace all mode' ) }"></label>
								</td></tr>
							</table>
						</td></tr>
					</table>
				</td></tr>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Close() }">
		</td></tr>
	</table>

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_HTML() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].EndContent() }">
	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Buttons( 'Export', 'EXPT' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].EndScreen() }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "UserDataExport_Export_Line" PARAMETERS = "output, line" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvEXPORT FILE = "{ l.output }" DELIMITER = "" FIELDS = "l.line">
</MvFUNCTION>

<MvFUNCTION NAME = "Boolean_Encode" PARAMETERS = "value" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.value }">
		<MvFUNCTIONRETURN VALUE = "Yes">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "No">
</MvFUNCTION>

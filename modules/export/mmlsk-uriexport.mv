<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-EXP-URI-
| Next Error Code: 19
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "mmlsk-uriexport">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Export URIs to Flat File">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Miva Merchant">
	<MvASSIGN NAME = "l.module:version"		VALUE = "10.0803">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "5.70">
	<MvASSIGN NAME = "l.module:features"	VALUE = "export">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature export
|
</MvCOMMENT>

<MvFUNCTION NAME = "ExportModule_Export" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.directory"								VALUE = "{ g.MerchantPath $ 's' $ padl( g.Store:id, 2, '0' ) $ '/export/' }">
	<MvASSIGN NAME = "l.output"									VALUE = "{ l.directory $ g.URIsExport_File }">
	<MvASSIGN NAME = "g.URIsExport_Export_Time_Start"			VALUE = "{ s.time_t }">

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Start( 'URIs Export Progress', 'EXPT' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Show() }">

	<MvIF EXPR = "{ NOT URIsExport_Initialize_Export( l.directory ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	<MvELSEIF EXPR = "{ URIsExport_Finalize_Export( l.directory, l.output ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ fexists( l.output ) }">
		<MvIF EXPR = "{ g.URIsExport_Working_Append }">
			<MvASSIGN NAME = "l.header"							VALUE = 0>
		<MvELSE>
			<MvASSIGN NAME = "l.null"							VALUE = "{ fdelete( l.output ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_Format EQ 'uriimport' AND g.URIsExport_Working_Header }">
		<MvIF EXPR = "{ NOT URIsExport_Export_Header( l.output ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_Product }">
		<MvIF EXPR = "{ NOT URIsExport_Export_Products( l.output, g.URIsExport_Export_LastProduct ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_Category AND NOT g.URIsExport_Export_NeedRefresh }">
		<MvIF EXPR = "{ NOT URIsExport_Export_Categories( l.output, g.URIsExport_Export_LastCategory ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_Page AND NOT g.URIsExport_Export_NeedRefresh }">
		<MvIF EXPR = "{ NOT URIsExport_Export_Pages( l.output, g.URIsExport_Export_LastPage ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_Feed AND NOT g.URIsExport_Export_NeedRefresh }">
		<MvIF EXPR = "{ NOT URIsExport_Export_Feeds( l.output, g.URIsExport_Export_LastFeed ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.Export_Count EQ g.Export_Total_Count }">
		<MvASSIGN NAME = "g.URIsExport_Export_NeedRefresh" 	VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ExportModule_Screen" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.URIsExport_Export_NeedRefresh }">
		<MvASSIGN NAME = "g.URIsExport_Working_Append" VALUE = 1>

		<MvHIDE FIELDS = "g.Module_Code, URIsExport_Export_Initialized, g.Reset, g.URIsExport_Working_Append, g.Export_Count, g.Export_Total_Count,
						  g.URIsExport_Export_LastProduct, g.URIsExport_Export_LastCategory, g.URIsExport_Export_LastPage, g.URIsExport_Export_LastFeed,
						  g.URIsExport_File, g.URIsExport_Append, g.URIsExport_Email, g.URIsExport_SendEmail,
						  g.URIsExport_Format, g.URIsExport_Delimiter_Tab, g.URIsExport_Delimiter_Other, g.URIsExport_Header,
						  g.URIsExport_Page, g.URIsExport_Category, g.URIsExport_Feed, g.URIsExport_Product, g.URIsExport_ProductCategory_Mode, g.URIsExport_ProductCategory_Code">

		<script type="text/javascript">
			function exp_continue()
			{
				document.forms[ Screen ].submit();
			}
			window.onload = exp_continue;
		</script>

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_End() }">

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ g.Load_Fields }">
		<MvIF EXPR = "{ g.Reset NE 'No' }">
			<MvASSIGN NAME = "g.URIsExport_File"					VALUE = "">
			<MvASSIGN NAME = "g.URIsExport_Append"					VALUE = "">
			<MvASSIGN NAME = "g.URIsExport_Format"					VALUE = "uriimport">
			<MvASSIGN NAME = "g.URIsExport_Delimiter_Tab"			VALUE = 0>
			<MvASSIGN NAME = "g.URIsExport_Delimiter_Other"			VALUE = ",">
			<MvASSIGN NAME = "g.URIsExport_Header"					VALUE = 1>
			<MvASSIGN NAME = "g.URIsExport_SendEmail"				VALUE = 0>

			<MvASSIGN NAME = "g.URIsExport_Page"					VALUE = 1>
			<MvASSIGN NAME = "g.URIsExport_Category"				VALUE = 1>
			<MvASSIGN NAME = "g.URIsExport_Feed"					VALUE = 1>
			<MvASSIGN NAME = "g.URIsExport_Product"					VALUE = 1>
			<MvASSIGN NAME = "g.URIsExport_ProductCategory_Mode"	VALUE = "all">
			<MvASSIGN NAME = "g.URIsExport_ProductCategory_Code"	VALUE = "">
		</MvIF>

		<MvIF EXPR = "{ ISNULL g.URIsExport_File }">					<MvASSIGN NAME = "g.URIsExport_File"					VALUE = "uris.csv">		</MvIF>
		<MvIF EXPR = "{ ISNULL g.URIsExport_Append }">					<MvASSIGN NAME = "g.URIsExport_Append"					VALUE = 0>				</MvIF>
		<MvIF EXPR = "{ ISNULL g.URIsExport_Format }">					<MvASSIGN NAME = "g.URIsExport_Format"					VALUE = "uriimport">	</MvIF>
		<MvIF EXPR = "{ ISNULL g.URIsExport_Delimiter_Tab }">			<MvASSIGN NAME = "g.URIsExport_Delimiter_Tab"			VALUE = 0>				</MvIF>
		<MvIF EXPR = "{ ISNULL g.URIsExport_Delimiter_Other }">			<MvASSIGN NAME = "g.URIsExport_Delimiter_Other"			VALUE = ",">			</MvIF>
		<MvIF EXPR = "{ ISNULL g.URIsExport_Header }">					<MvASSIGN NAME = "g.URIsExport_Header"					VALUE = 1>				</MvIF>
		<MvIF EXPR = "{ ISNULL g.URIsExport_SendEmail }">				<MvASSIGN NAME = "g.URIsExport_SendEmail"				VALUE = 0>				</MvIF>
		<MvIF EXPR = "{ ISNULL g.URIsExport_Page }">					<MvASSIGN NAME = "g.URIsExport_Page"					VALUE = 1>				</MvIF>
		<MvIF EXPR = "{ ISNULL g.URIsExport_Category }">				<MvASSIGN NAME = "g.URIsExport_Category"				VALUE = 1>				</MvIF>
		<MvIF EXPR = "{ ISNULL g.URIsExport_Feed }">					<MvASSIGN NAME = "g.URIsExport_Feed"					VALUE = 1>				</MvIF>
		<MvIF EXPR = "{ ISNULL g.URIsExport_Product }">					<MvASSIGN NAME = "g.URIsExport_Product"					VALUE = 1>				</MvIF>
		<MvIF EXPR = "{ ISNULL g.URIsExport_ProductCategory_Mode }">	<MvASSIGN NAME = "g.URIsExport_ProductCategory_Mode"	VALUE = "all">			</MvIF>
		<MvIF EXPR = "{ ISNULL g.URIsExport_ProductCategory_Code }">	<MvASSIGN NAME = "g.URIsExport_ProductCategory_Code"	VALUE = "">				</MvIF>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_Start( 'Export URIs to Flat File', 'EXPT', '' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_UI_JavaScript() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_CategoryLookup_Dialog_CSS() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_CategoryLookup_Dialog_JavaScript() }">

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_JavaScript( 'Exporting URIs to Flat File', 'EXPT', 'EXPT' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_CSS() }">

	<script language="JavaScript">
		HaveCustomReset = 1;
		var HaveLoadFinishedHandler = 1;
		var product_checkbox, product_settings;

		function LoadFinishedHandler()
		{
			product_checkbox 	= document.getElementById( 'urisexport_product_checkbox' );
			product_settings 	= document.getElementById( 'urisexport_productsettings' );

			if ( product_checkbox ) product_checkbox.onchange = function() { HideProductSettings(); }
		}

		function HideProductSettings()
		{
			product_checkbox 	= document.getElementById( 'urisexport_product_checkbox' );
			product_settings 	= document.getElementById( 'urisexport_productsettings' );

			if ( product_checkbox.checked )	product_settings.style.visibility = 'visible';
			else 							product_settings.style.visibility = 'hidden';
		}

		function CustomReset()
		{
			document.EXPT.Reset.value = 'Yes';
		}

		function HideOptions( optval )
		{
			if ( optval == 'provisioning' )
			{
				document.getElementById( 'uriexpt_options' ).style.visibility 	= 'hidden';
				document.getElementById( 'uriexpt_options1' ).style.visibility 	= 'hidden';
				document.getElementById( 'uriexpt_options2' ).style.visibility 	= 'hidden';
				document.getElementById( 'uriexpt_options3' ).style.visibility 	= 'hidden';
				document.getElementById( 'uriexpt_options4' ).style.visibility 	= 'hidden';
			}
			else
			{
				document.getElementById( 'uriexpt_options' ).style.visibility 	= 'visible';
				document.getElementById( 'uriexpt_options1' ).style.visibility 	= 'visible';
				document.getElementById( 'uriexpt_options2' ).style.visibility 	= 'visible';
				document.getElementById( 'uriexpt_options3' ).style.visibility 	= 'visible';
				document.getElementById( 'uriexpt_options4' ).style.visibility 	= 'visible';
			}
		}
	</script>

	<MvIF EXPR = "{ g.URIsExport_Format EQ 'provisioning' }">
		<style type="text/css">
			#uriexpt_options,
			#uriexpt_options1,
			#uriexpt_options2,
			#uriexpt_options3,
			#uriexpt_options4
			{
				visibility: hidden;
			}
		</style>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_End( 'Export URIs to Flat File', 'EXPT', '' ) }">

	<input type="hidden" name="Reset" value="No">
	<MvHIDE FIELDS = "g.Module_Code">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginContent() }">
	<table width="100%">
		<tr>
			<td nowrap>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Open( 'Destination', 110 ) }">
				<tr><td nowrap>
					<font face="Arial, Helvetica" size="-1"><b>
						Export URIs to File:
					</font></b>
				</td><td width="100%">
					<input type="text" name="URIsExport_File" size="40" value="{ encodeentities( g.URIsExport_File ) }">
				</td></tr>

				<tr><td valign="top" nowrap>
					<b>If File Exists:</b>
				</td><td width="100%">
					<MvIF EXPR = "{ g.URIsExport_Append }">
						<label><input type="radio" name="URIsExport_Append" value="Yes" checked>Append To File<br></label>
						<label><input type="radio" name="URIsExport_Append" value="">Replace File</label>
					<MvELSE>
						<label><input type="radio" name="URIsExport_Append" value="Yes">Append To File</label>

						<br>

						<label><input type="radio" name="URIsExport_Append" value="" checked>Replace File</label>
					</MvIF>
				</td></tr>

				<tr><td valign="top" nowrap>
					Email File To:
				</td><td width="100%" nowrap>
					<MvIF EXPR = "{ NOT g.URIsExport_SendEmail }">
						<label><input type="radio" name="URIsExport_SendEmail" value="0" checked>Do Not Email<br></label>
						<input type="radio" name="URIsExport_SendEmail" value="1">
					<MvELSE>
						<label><input type="radio" name="URIsExport_SendEmail" value="0">Do Not Email<br></label>
						<input type="radio" name="URIsExport_SendEmail" value="1" checked>
					</MvIF>

					<input type="text" size="40" name="URIsExport_Email" value="{ encodeentities( g.Store:email ) }">
				</td></tr>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Close() }">
			</td>
			<td width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Open( 'Export Format', 110 ) }">
				<tr><td valign="top" nowrap>
					<b>File Format:</b>
				</td><td width="100%">
					<select name="URIsExport_Format" onchange="HideOptions( this.value );">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'uriimport', g.URIsExport_Format, 'URI Import' ) }">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'provisioning',  g.URIsExport_Format, 'Miva Merchant XML Provisioning' ) }">
					</select>
				</td></tr>

				<tr><td valign="top" nowrap>
					<div id="uriexpt_options">
						<b>File Delimiter:</b>
					</div>
				</td><td width="100%">
					<table border="0" cellpadding="0" cellspacing="0">
					<tr><td valign="top" nowrap>
						<div id="uriexpt_options1">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'URIsExport_Delimiter_Tab', 'Yes', g.URIsExport_Delimiter_Tab, 'Tab' ) }">
						</div>
					</td><td align="left" valign="middle">
						&nbsp;
					</td></tr>

					<tr><td valign="top" nowrap>
						<div id="uriexpt_options2">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'URIsExport_Delimiter_Tab', '', g.URIsExport_Delimiter_Tab, 'Other:' ) }">
						</div>
					</td><td align="left" valign="middle">
						<div id="uriexpt_options3">
							<input type="text" name="URIsExport_Delimiter_Other" value="{ encodeentities( g.URIsExport_Delimiter_Other ) }" size=10>
						</div>
					</td></tr>
					</table>
				</td></tr>

				<tr><td valign="top" nowrap>
					&nbsp;
				</td><td width="100%">
					<div id="uriexpt_options4">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.URIsExport_Header, 'URIsExport_Header', 'Yes', 'Export Field Names as Header' ) }">
					</div>
				</td></tr>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Close() }">
		</td>
		</tr>
		<tr>
			<td>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Open( 'URIs to Export', 110 ) }">
				<tr>
					<td valign="top" nowrap><b>URIs:</b></td>
					<td align="left" valign="middle" colspan="3">
						<table>
							<tr>
								<td align="left" valign="top" nowrap>
									<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.URIsExport_Page, 'URIsExport_Page', 'Yes', 'Page' ) }"></label>
								</td>
							</tr>
							<tr>
								<td align="left" valign="top" nowrap>
									<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.URIsExport_Category, 'URIsExport_Category', 'Yes', 'Category' ) }"></label>
								</td>
							</tr>
							<tr>
								<td align="left" valign="top" nowrap>
									<label>
									<MvIF EXPR = "{ g.URIsExport_Product }">
										<input type="checkbox" id="urisexport_product_checkbox" name="URIsExport_Product" value="Yes" checked> Product
									<MvELSE>
										<input type="checkbox" id="urisexport_product_checkbox" name="URIsExport_Product" value="Yes"> Product
									</MvIF>
									</label>
								</td>
							</tr>
							<tr>
								<td align="left" valign="top" nowrap>
									<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.URIsExport_Feed, 'URIsExport_Feed', 'Yes', 'Feed' ) }"></label>
								</td>
							</tr>
						</table>
					</td>
				</tr>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Close() }">
			</td>
			<td width = "100%" id="urisexport_productsettings">
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Open( 'Product Settings', 110 ) }">
				<tr><td nowrap>
				<MvIF EXPR = "{ g.URIsExport_ProductCategory_Mode EQ 'all' }">
					<input type="radio" name="URIsExport_ProductCategory_Mode" value="all" checked>Export All Products<br>
					<input type="radio" name="URIsExport_ProductCategory_Mode" value="from">Export Only Products In Category:
					<input type="text"  name="URIsExport_ProductCategory_Code" id="URIsExport_ProductCategory_Code" value="{ encodeentities( g.URIsExport_ProductCategory_Code ) }" size="20"><a href="JavaScript:CategoryLookupDialog( 'URIsExport_ProductCategory_Code' );"><MvEVAL EXPR = "{ [ g.Module_Admin ].DrawImgButton_Lookup( 0 ) }"></a>
				</MvIF>

				<MvIF EXPR = "{ g.URIsExport_ProductCategory_Mode EQ 'from' }">
					<input type="radio" name="URIsExport_ProductCategory_Mode" value="all">Export All Products<br>
					<input type="radio" name="URIsExport_ProductCategory_Mode" value="from" checked>Export Only Products In Category:
					<input type="text"  name="URIsExport_ProductCategory_Code" id="URIsExport_ProductCategory_Code" value="{ encodeentities( g.URIsExport_ProductCategory_Code ) }" size="20"><a href="JavaScript:CategoryLookupDialog( 'URIsExport_ProductCategory_Code' );"><MvEVAL EXPR = "{ [ g.Module_Admin ].DrawImgButton_Lookup( 0 ) }"></a>
				</MvIF>
				</td></tr>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Close() }">
			</td>
		</tr>
	</table>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_CategoryLookup_Dialog_HTML() }">
	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_HTML() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].EndContent() }">
	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Buttons( 'Export', 'EXPT' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].EndScreen() }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ExportModule_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.URIsExport_File"	VALUE = "{ trim( g.URIsExport_File ) }">
	<MvASSIGN NAME = "g.URIsExport_Header"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.URIsExport_Header ) }">

	<MvIF EXPR = "{ len( g.URIsExport_File ) EQ 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'URIsExport_File', 'Please specify a file' ) }">
	<MvELSE>
		<MvIF EXPR = "{ '/' IN g.URIsExport_File }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'URIsExport_File', 'Please do not include a path in the file name' ) }">
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "g.URIsExport_Email" 	VALUE = "{ trim( g.URIsExport_Email ) }">

	<MvIF EXPR = "{ g.URIsExport_SendEmail }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.URIsExport_Email ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'URIsExport_Email', 'Please specify a valid email address' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_ProductCategory_Mode EQ 'from' }">
		<MvIF EXPR = "{ len( g.URIsExport_ProductCategory_Code ) EQ 0 }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'URIsExport_ProductCategory_Code', 'You must enter a valid category code' ) }">
		<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Category_Load_Code( g.URIsExport_ProductCategory_Code, l.category ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'URIsExport_ProductCategory_Code', 'Category code \'' $ g.URIsExport_ProductCategory_Code $ '\' does not exist' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_Format EQ 'uriimport' }">
		<MvIF EXPR = "{ ( len( g.URIsExport_Delimiter_Other ) EQ 0 ) AND ( NOT g.URIsExport_Delimiter_Tab ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'URIsExport_Delimiter_Other', 'Please specify a delimiter' ) }">
		</MvIF>

		<MvIF EXPR = "{ g.URIsExport_Delimiter_Tab }">
			<MvASSIGN NAME = "g.URIsExport_Delimiter" VALUE = "{ asciichar( 9 ) }">
		<MvELSE>
			<MvASSIGN NAME = "g.URIsExport_Delimiter" VALUE = "{ g.URIsExport_Delimiter_Other }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "URIsExport_Initialize_Export" PARAMETERS = "directory" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.URIsExport_Export_Initialized }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "g.URIsExport_Page" 					VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.URIsExport_Page ) }">
	<MvASSIGN NAME = "g.URIsExport_Category" 				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.URIsExport_Category ) }">
	<MvASSIGN NAME = "g.URIsExport_Feed" 					VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.URIsExport_Feed ) }">
	<MvASSIGN NAME = "g.URIsExport_Product" 				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.URIsExport_Product ) }">
	<MvASSIGN NAME = "g.URIsExport_ProductCategory_Code" 	VALUE = "{ trim( g.URIsExport_ProductCategory_Code ) }">
	<MvASSIGN NAME = "g.URIsExport_ProductCategory_Mode" 	VALUE = "{ trim( g.URIsExport_ProductCategory_Mode ) }">
	<MvASSIGN NAME = "g.URIsExport_Append" 					VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.URIsExport_Append ) }">

	<MvASSIGN NAME = "g.URIsExport_Export_LastProduct" 		VALUE = 0>
	<MvASSIGN NAME = "g.URIsExport_Export_LastCategory"		VALUE = 0>
	<MvASSIGN NAME = "g.URIsExport_Export_LastPage" 		VALUE = 0>
	<MvASSIGN NAME = "g.URIsExport_Export_LastFeed" 		VALUE = 0>

	<MvASSIGN NAME = "g.Export_Count"						VALUE = 0>
	<MvASSIGN NAME = "g.Export_Total_Count"					VALUE = 0>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].EnsurePathExists( 'data', l.directory ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.query" 			VALUE = "SELECT COUNT( * ) as uri_count FROM URIs u">
	<MvASSIGN NAME = "l.where" 			VALUE = "">
	<MvASSIGN NAME = "l.fields" 		VALUE = "">

	<MvIF EXPR = "{ g.URIsExport_Product }">
		<MvIF EXPR = "{ g.URIsExport_ProductCategory_Mode EQ 'all' }">
			<MvASSIGN NAME = "l.where" 	VALUE = " WHERE u.store_id = ? AND u.product_id <> 0">
			<MvASSIGN NAME = "l.fields" VALUE = "g.Store:id">
		<MvELSE>
			<MvASSIGN NAME = "l.query" 	VALUE = "{ l.query $ ', ' $ g.Store_Table_Prefix $ 'Categories c
									  						 		LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'CategoryXProduct cxp ON cxp.cat_id = c.id' }">
			<MvASSIGN NAME = "l.where" 	VALUE = " WHERE u.store_id 	= ? AND
					  			 					    c.code 		= ? AND 
					  			 					    u.product_id = cxp.product_id">
			<MvASSIGN NAME = "l.fields"	VALUE = "g.Store:id,g.URIsExport_ProductCategory_Code">
		</MvIF>

		<MvOPENVIEW NAME 	= "Merchant"
					VIEW 	= "ProductURIs"
					QUERY	= "{ l.query $ l.where }"
					FIELDS	= "{ l.fields }">
		<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-EXP-URI-00004', g.MvOPENVIEW_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "g.Export_Total_Count" VALUE = "{ g.Export_Total_Count + ProductURIs.d.uri_count }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "ProductURIs">
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_Category }">
		<MvASSIGN NAME = "l.where" 	VALUE = " WHERE u.store_id = ? AND u.cat_id <> 0">
		<MvASSIGN NAME = "l.fields" VALUE = "g.Store:id">

		<MvOPENVIEW NAME 	= "Merchant"
					VIEW 	= "CategoryURIs"
					QUERY	= "{ l.query $ l.where }"
					FIELDS	= "{ l.fields }">
		<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-EXP-URI-00005', g.MvOPENVIEW_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "g.Export_Total_Count" VALUE = "{ g.Export_Total_Count + CategoryURIs.d.uri_count }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryURIs">
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_Page }">
		<MvASSIGN NAME = "l.where" 	VALUE = " WHERE u.store_id = ? AND u.page_code <> ''">
		<MvASSIGN NAME = "l.fields" VALUE = "g.Store:id">

		<MvOPENVIEW NAME 	= "Merchant"
					VIEW 	= "PageURIs"
					QUERY	= "{ l.query $ l.where }"
					FIELDS	= "{ l.fields }">
		<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-EXP-URI-00006', g.MvOPENVIEW_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "g.Export_Total_Count" VALUE = "{ g.Export_Total_Count + PageURIs.d.uri_count }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "PageURIs">
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_Feed }">
		<MvASSIGN NAME = "l.where"	VALUE = " WHERE u.store_id = ? AND u.feed_id <> 0">
		<MvASSIGN NAME = "l.fields" VALUE = "g.Store:id">

		<MvOPENVIEW NAME 	= "Merchant"
					VIEW 	= "FeedURIs"
					QUERY	= "{ l.query $ l.where }"
					FIELDS	= "{ l.fields }">
		<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-EXP-URI-00014', g.MvOPENVIEW_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "g.Export_Total_Count" VALUE = "{ g.Export_Total_Count + FeedURIs.d.uri_count }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "FeedURIs">
	</MvIF>

	<MvASSIGN NAME = "g.URIsExport_Working_Append" 		VALUE = "{ g.URIsExport_Append }">
	<MvASSIGN NAME = "g.URIsExport_Working_Header" 		VALUE = "{ g.URIsExport_Header }">

	<MvASSIGN NAME = "g.URIsExport_Export_Initialized"	VALUE = 1>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "URIsExport_Finalize_Export" PARAMETERS = "directory, output" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Export_Count EQ g.Export_Total_Count }">
		<MvIF EXPR = "{ g.Export_Total_Count EQ 1 }">	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_ProgressComplete( '1 URI exported to \'' $ l.output $ '\'' ) }">
		<MvELSE>										<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_ProgressComplete( g.Export_Count $ ' URIs exported to \'' $ l.output $ '\'' ) }">
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-EXP-URI-00018', 'URIs exported' ) }">

		<MvIF EXPR = "{ g.URIsExport_SendEmail }">
			<MvDO FILE = "{ g.Module_Library_Utilities }" NAME = "l.null" VALUE = "{ Send_Email_Attachment( g.Store:email, g.URIsExport_Email, '', 'Miva Merchant URI Export', g.URIsExport_File, l.directory, 'data' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-EXP-URI-00007', 'URIs Export Email Sent' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "URIsExport_Export_Products" PARAMETERS = "output, last_id var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.last_id" VALUE = "{ int( l.last_id ) }">

	<MvIF EXPR = "{ g.Export_Count GE g.Export_Total_Count }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_ProductCategory_Mode EQ 'all' }">
		<MvASSIGN NAME = "l.query" 	VALUE = "{ 'SELECT 
													u.*,
													p.code as product_code
											    FROM
													URIs u 
													LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'Products p ON p.id = u.product_id
											    WHERE
													u.store_id 		= ? AND
													u.product_id 	<> 0 AND
													u.id			> ?
				  			 					ORDER BY
				  			 						u.id' }">
		<MvASSIGN NAME = "l.fields"	VALUE = "g.Store:id, l.last_id">
	<MvELSE>
		<MvASSIGN NAME = "l.query" 	VALUE = "{ 'SELECT 
													u.*,
													p.code as product_code
											    FROM
													URIs u
								  					LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'Products p ON p.id = u.product_id,'
													$ g.Store_Table_Prefix $ 'Categories c
								  					LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'CategoryXProduct cxp ON cxp.cat_id = c.id
								  			    WHERE
								  			   		u.store_id 		= ? 				AND
								  			   		u.id			> ? 				AND
				  			 					    u.product_id 	= cxp.product_id 	AND
				  			 					    ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'c.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) $ '
				  			 					ORDER BY
				  			 						u.id' }">
		<MvASSIGN NAME = "l.fields"	VALUE = "g.Store:id, l.last_id, g.URIsExport_ProductCategory_Code">
	</MvIF>

	<MvOPENVIEW NAME 	= "Merchant"
				VIEW 	= "ProductURIs"
				QUERY	= "{ l.query }"
				FIELDS	= "{ l.fields }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-EXP-URI-00001', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_Format EQ 'uriimport' }">
		<MvWHILE EXPR = "{ NOT ProductURIs.d.EOF AND NOT g.URIsExport_Export_NeedRefresh }">
			<MvASSIGN NAME = "g.Export_Count"	VALUE = "{ g.Export_Count + 1 }">
			<MvASSIGN NAME = "l.record" 		VALUE = "">

			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Update( g.Export_Count, g.Export_Total_Count ) }">

			<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, ProductURIs.d.uri ) }">
			<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, [ g.Module_Feature_PRV_AD ].PRV_Boolean_Encode( ProductURIs.d.canonical ) ) }">
			<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, ProductURIs.d.status ) }">

			<MvIF EXPR = "{ g.URIsExport_Page }">
				<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, '' ) }">
			</MvIF>

			<MvIF EXPR = "{ g.URIsExport_Category }">
				<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, '' ) }">
			</MvIF>

			<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, ProductURIs.d.product_code ) }">

			<MvIF EXPR = "{ g.URIsExport_Feed }">
				<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, '' ) }">
			</MvIF>

			<MvIF EXPR = "{ NOT Miva_Fwriteln( l.output, l.record ) }">
				<MvCLOSEVIEW NAME = "Merchant" VIEW = "ProductURIs">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvIF EXPR = "{ ( s.globaltimeout EQ 0 ) OR ( s.globaltimeout GT 20 ) }">
				<MvASSIGN NAME = "l.end_time" VALUE = "20">
			<MvELSE>
				<MvASSIGN NAME = "l.end_time" VALUE = "{ s.globaltimeout / 3 }">
			</MvIF>

			<MvIF EXPR = "{ ( s.dyn_time_t - g.URIsExport_Export_Time_Start ) GT l.end_time }">
				<MvASSIGN NAME = "l.last_id" 						VALUE = "{ ProductURIs.d.id }">
				<MvASSIGN NAME = "g.URIsExport_Export_NeedRefresh" 	VALUE = 1>
			</MvIF>

			<MvSKIP NAME = "Merchant" VIEW = "ProductURIs" ROWS = 1>
		</MvWHILE>
	<MvELSEIF EXPR = "{ g.URIsExport_Format EQ 'provisioning' }">
		<MvWHILE EXPR = "{ NOT ProductURIs.d.EOF AND NOT g.URIsExport_Export_NeedRefresh }">
			<MvASSIGN NAME = "g.Export_Count"	VALUE = "{ g.Export_Count + 1 }">

			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Update( g.Export_Count, g.Export_Total_Count ) }">

			<MvEVAL EXPR = "{ URIsExport_Export_Line( l.output, '<URI_Add product_code=\"' $ ProductURIs.d.product_code $ '\" canonical=\"' $ [ g.Module_Feature_PRV_AD ].PRV_Boolean_Encode( ProductURIs.d.canonical ) $ '\" uri=\"' $ ProductURIs.d.uri $ '\" />' ) }">

			<MvIF EXPR = "{ ( s.globaltimeout EQ 0 ) OR ( s.globaltimeout GT 20 ) }">
				<MvASSIGN NAME = "l.end_time" VALUE = "20">
			<MvELSE>
				<MvASSIGN NAME = "l.end_time" VALUE = "{ s.globaltimeout / 3 }">
			</MvIF>

			<MvIF EXPR = "{ ( s.dyn_time_t - g.URIsExport_Export_Time_Start ) GT l.end_time }">
				<MvASSIGN NAME = "l.last_id" 						VALUE = "{ ProductURIs.d.id }">
				<MvASSIGN NAME = "g.URIsExport_Export_NeedRefresh" 	VALUE = 1>
			</MvIF>

			<MvSKIP NAME = "Merchant" VIEW = "ProductURIs" ROWS = 1>
		</MvWHILE>
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_Export_NeedRefresh AND ProductURIs.d.EOF }">
		<MvASSIGN NAME = "g.URIsExport_Export_NeedRefresh" VALUE = 0>
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ProductURIs">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "URIsExport_Export_Categories" PARAMETERS = "output, last_id var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.last_id" VALUE = "{ int( l.last_id ) }">

	<MvIF EXPR = "{ g.Export_Count GE g.Export_Total_Count }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvOPENVIEW NAME 	= "Merchant"
				VIEW 	= "CategoryURIs"
				QUERY	= "{ 'SELECT 
								u.*,
								c.code AS category_code
							  FROM
								URIs u
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'Categories c ON c.id = u.cat_id
							  WHERE
								u.store_id 	= ? AND
								u.cat_id 	<> 0 AND
								u.id 		> ?
							  ORDER BY
							  	u.id' }"
				FIELDS	= "g.Store:id, l.last_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-EXP-URI-00002', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_Format EQ 'uriimport' }">
		<MvWHILE EXPR = "{ NOT CategoryURIs.d.EOF AND NOT g.URIsExport_Export_NeedRefresh }">
			<MvASSIGN NAME = "g.Export_Count"	VALUE = "{ g.Export_Count + 1 }">
			<MvASSIGN NAME = "l.record" 		VALUE = "">

			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Update( g.Export_Count, g.Export_Total_Count ) }">

			<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, CategoryURIs.d.uri ) }">
			<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, [ g.Module_Feature_PRV_AD ].PRV_Boolean_Encode( CategoryURIs.d.canonical ) ) }">
			<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, CategoryURIs.d.status ) }">
			
			<MvIF EXPR = "{ g.URIsExport_Page }">
				<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, '' ) }">
			</MvIF>

			<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, CategoryURIs.d.category_code ) }">

			<MvIF EXPR = "{ g.URIsExport_Product }">
				<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, '' ) }">
			</MvIF>

			<MvIF EXPR = "{ g.URIsExport_Feed }">
				<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, '' ) }">
			</MvIF>

			<MvIF EXPR = "{ NOT Miva_Fwriteln( l.output, l.record ) }">
				<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryURIs">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvIF EXPR = "{ ( s.globaltimeout EQ 0 ) OR ( s.globaltimeout GT 20 ) }">
				<MvASSIGN NAME = "l.end_time" VALUE = "20">
			<MvELSE>
				<MvASSIGN NAME = "l.end_time" VALUE = "{ s.globaltimeout / 3 }">
			</MvIF>

			<MvIF EXPR = "{ ( s.dyn_time_t - g.URIsExport_Export_Time_Start ) GT l.end_time }">
				<MvASSIGN NAME = "l.last_id" 						VALUE = "{ CategoryURIs.d.id }">
				<MvASSIGN NAME = "g.URIsExport_Export_NeedRefresh" 	VALUE = 1>
			</MvIF>

			<MvSKIP NAME = "Merchant" VIEW = "CategoryURIs" ROWS = 1>
		</MvWHILE>
	<MvELSEIF EXPR = "{ g.URIsExport_Format EQ 'provisioning' }">
		<MvWHILE EXPR = "{ NOT CategoryURIs.d.EOF AND NOT g.URIsExport_Export_NeedRefresh }">
			<MvASSIGN NAME = "g.Export_Count"	VALUE = "{ g.Export_Count + 1 }">

			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Update( g.Export_Count, g.Export_Total_Count ) }">

			<MvEVAL EXPR = "{ URIsExport_Export_Line( l.output, '<URI_Add category_code=\"' $ CategoryURIs.d.category_code $ '\" canonical=\"' $ [ g.Module_Feature_PRV_AD ].PRV_Boolean_Encode( CategoryURIs.d.canonical ) $ '\" uri=\"' $ CategoryURIs.d.uri $ '\" />' ) }">

			<MvIF EXPR = "{ ( s.globaltimeout EQ 0 ) OR ( s.globaltimeout GT 20 ) }">
				<MvASSIGN NAME = "l.end_time" VALUE = "20">
			<MvELSE>
				<MvASSIGN NAME = "l.end_time" VALUE = "{ s.globaltimeout / 3 }">
			</MvIF>

			<MvIF EXPR = "{ ( s.dyn_time_t - g.URIsExport_Export_Time_Start ) GT l.end_time }">
				<MvASSIGN NAME = "l.last_id" 						VALUE = "{ CategoryURIs.d.id }">
				<MvASSIGN NAME = "g.URIsExport_Export_NeedRefresh" 	VALUE = 1>
			</MvIF>

			<MvSKIP NAME = "Merchant" VIEW = "CategoryURIs" ROWS = 1>
		</MvWHILE>
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_Export_NeedRefresh AND CategoryURIs.d.EOF }">
		<MvASSIGN NAME = "g.URIsExport_Export_NeedRefresh" VALUE = 0>
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryURIs">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "URIsExport_Export_Pages" PARAMETERS = "output, last_id var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.last_id" VALUE = "{ int( l.last_id ) }">

	<MvIF EXPR = "{ g.Export_Count GE g.Export_Total_Count }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvOPENVIEW NAME 	= "Merchant"
				VIEW 	= "PageURIs"
				QUERY	= "SELECT 
							   u.*
						   FROM
							   URIs u
						   WHERE
							   u.store_id 	= ?		AND
							   u.page_code	<> ''	AND
							   u.id			> ?
						   ORDER BY
							   u.id"
				FIELDS	= "g.Store:id, l.last_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-EXP-URI-00003', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_Format EQ 'uriimport' }">
		<MvWHILE EXPR = "{ NOT PageURIs.d.EOF AND NOT g.URIsExport_Export_NeedRefresh }">
			<MvASSIGN NAME = "g.Export_Count"	VALUE = "{ g.Export_Count + 1 }">
			<MvASSIGN NAME = "l.record" 		VALUE = "">

			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Update( g.Export_Count, g.Export_Total_Count ) }">

			<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, PageURIs.d.uri ) }">
			<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, [ g.Module_Feature_PRV_AD ].PRV_Boolean_Encode( PageURIs.d.canonical ) ) }">
			<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, PageURIs.d.status ) }">
			<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, PageURIs.d.page_code ) }">

			<MvIF EXPR = "{ g.URIsExport_Category }">
				<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, '' ) }">
			</MvIF>

			<MvIF EXPR = "{ g.URIsExport_Product }">
				<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, '' ) }">
			</MvIF>

			<MvIF EXPR = "{ g.URIsExport_Feed }">
				<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, '' ) }">
			</MvIF>

			<MvIF EXPR = "{ NOT Miva_Fwriteln( l.output, l.record ) }">
				<MvCLOSEVIEW NAME = "Merchant" VIEW = "PageURIs">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvIF EXPR = "{ ( s.globaltimeout EQ 0 ) OR ( s.globaltimeout GT 20 ) }">
				<MvASSIGN NAME = "l.end_time" VALUE = "20">
			<MvELSE>
				<MvASSIGN NAME = "l.end_time" VALUE = "{ s.globaltimeout / 3 }">
			</MvIF>

			<MvIF EXPR = "{ ( s.dyn_time_t - g.URIsExport_Export_Time_Start ) GT l.end_time }">
				<MvASSIGN NAME = "l.last_id" 						VALUE = "{ PageURIs.d.id }">
				<MvASSIGN NAME = "g.URIsExport_Export_NeedRefresh" 	VALUE = 1>
			</MvIF>

			<MvSKIP NAME = "Merchant" VIEW = "PageURIs" ROWS = 1>
		</MvWHILE>
	<MvELSEIF EXPR = "{ g.URIsExport_Format EQ 'provisioning' }">
		<MvWHILE EXPR = "{ NOT PageURIs.d.EOF AND NOT g.URIsExport_Export_NeedRefresh }">
			<MvASSIGN NAME = "g.Export_Count"	VALUE = "{ g.Export_Count + 1 }">

			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Update( g.Export_Count, g.Export_Total_Count ) }">

			<MvEVAL EXPR = "{ URIsExport_Export_Line( l.output, '<URI_Add page_code=\"' $ PageURIs.d.page_code $ '\" canonical=\"' $ [ g.Module_Feature_PRV_AD ].PRV_Boolean_Encode( PageURIs.d.canonical ) $ '\" uri=\"' $ PageURIs.d.uri $ '\" />' ) }">

			<MvIF EXPR = "{ ( s.globaltimeout EQ 0 ) OR ( s.globaltimeout GT 20 ) }">
				<MvASSIGN NAME = "l.end_time" VALUE = "20">
			<MvELSE>
				<MvASSIGN NAME = "l.end_time" VALUE = "{ s.globaltimeout / 3 }">
			</MvIF>

			<MvIF EXPR = "{ ( s.dyn_time_t - g.URIsExport_Export_Time_Start ) GT l.end_time }">
				<MvASSIGN NAME = "l.last_id" 						VALUE = "{ PageURIs.d.id }">
				<MvASSIGN NAME = "g.URIsExport_Export_NeedRefresh" 	VALUE = 1>
			</MvIF>

			<MvSKIP NAME = "Merchant" VIEW = "PageURIs" ROWS = 1>
		</MvWHILE>
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_Export_NeedRefresh AND PageURIs.d.EOF }">
		<MvASSIGN NAME = "g.URIsExport_Export_NeedRefresh" VALUE = 0>
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "PageURIs">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "URIsExport_Export_Feeds" PARAMETERS = "output, last_id var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.last_id" VALUE = "{ int( l.last_id ) }">

	<MvIF EXPR = "{ g.Export_Count GE g.Export_Total_Count }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvOPENVIEW NAME 	= "Merchant"
				VIEW 	= "FeedURIs"
				QUERY	= "{ 'SELECT 
								u.*,
								f.code AS feed_code
							  FROM
								URIs u
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'Feeds f ON f.id = u.feed_id
							  WHERE
								u.store_id 	= ? AND
								u.feed_id 	<> 0 AND
								u.id 		> ?
							  ORDER BY
							  	u.id' }"
				FIELDS	= "g.Store:id, l.last_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-EXP-URI-00015', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_Format EQ 'uriimport' }">
		<MvWHILE EXPR = "{ NOT FeedURIs.d.EOF AND NOT g.URIsExport_Export_NeedRefresh }">
			<MvASSIGN NAME = "g.Export_Count"	VALUE = "{ g.Export_Count + 1 }">
			<MvASSIGN NAME = "l.record" 		VALUE = "">

			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Update( g.Export_Count, g.Export_Total_Count ) }">

			<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, FeedURIs.d.uri ) }">
			<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, [ g.Module_Feature_PRV_AD ].PRV_Boolean_Encode( FeedURIs.d.canonical ) ) }">
			<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, FeedURIs.d.status ) }">
			
			<MvIF EXPR = "{ g.URIsExport_Page }">
				<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, '' ) }">
			</MvIF>

			<MvIF EXPR = "{ g.URIsExport_Category }">
				<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, '' ) }">
			</MvIF>

			<MvIF EXPR = "{ g.URIsExport_Product }">
				<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, '' ) }">
			</MvIF>

			<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.record, g.URIsExport_Delimiter, FeedURIs.d.feed_code ) }">

			<MvIF EXPR = "{ NOT Miva_Fwriteln( l.output, l.record ) }">
				<MvCLOSEVIEW NAME = "Merchant" VIEW = "FeedURIs">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvIF EXPR = "{ ( s.globaltimeout EQ 0 ) OR ( s.globaltimeout GT 20 ) }">
				<MvASSIGN NAME = "l.end_time" VALUE = "20">
			<MvELSE>
				<MvASSIGN NAME = "l.end_time" VALUE = "{ s.globaltimeout / 3 }">
			</MvIF>

			<MvIF EXPR = "{ ( s.dyn_time_t - g.URIsExport_Export_Time_Start ) GT l.end_time }">
				<MvASSIGN NAME = "l.last_id" 						VALUE = "{ FeedURIs.d.id }">
				<MvASSIGN NAME = "g.URIsExport_Export_NeedRefresh" 	VALUE = 1>
			</MvIF>

			<MvSKIP NAME = "Merchant" VIEW = "FeedURIs" ROWS = 1>
		</MvWHILE>
	<MvELSEIF EXPR = "{ g.URIsExport_Format EQ 'provisioning' }">
		<MvWHILE EXPR = "{ NOT FeedURIs.d.EOF AND NOT g.URIsExport_Export_NeedRefresh }">
			<MvASSIGN NAME = "g.Export_Count"	VALUE = "{ g.Export_Count + 1 }">

			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Update( g.Export_Count, g.Export_Total_Count ) }">

			<MvEVAL EXPR = "{ URIsExport_Export_Line( l.output, '<URI_Add feed_code=\"' $ FeedURIs.d.feed_code $ '\" canonical=\"' $ [ g.Module_Feature_PRV_AD ].PRV_Boolean_Encode( FeedURIs.d.canonical ) $ '\" uri=\"' $ FeedURIs.d.uri $ '\" />' ) }">

			<MvIF EXPR = "{ ( s.globaltimeout EQ 0 ) OR ( s.globaltimeout GT 20 ) }">
				<MvASSIGN NAME = "l.end_time" VALUE = "20">
			<MvELSE>
				<MvASSIGN NAME = "l.end_time" VALUE = "{ s.globaltimeout / 3 }">
			</MvIF>

			<MvIF EXPR = "{ ( s.dyn_time_t - g.URIsExport_Export_Time_Start ) GT l.end_time }">
				<MvASSIGN NAME = "l.last_id" 						VALUE = "{ FeedURIs.d.id }">
				<MvASSIGN NAME = "g.URIsExport_Export_NeedRefresh" 	VALUE = 1>
			</MvIF>

			<MvSKIP NAME = "Merchant" VIEW = "FeedURIs" ROWS = 1>
		</MvWHILE>
	</MvIF>

	<MvIF EXPR = "{ g.URIsExport_Export_NeedRefresh AND FeedURIs.d.EOF }">
		<MvASSIGN NAME = "g.URIsExport_Export_NeedRefresh" VALUE = 0>
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "FeedURIs">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "URIsExport_Export_Header" PARAMETERS = "output" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.header" VALUE = "">

	<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.header, g.URIsExport_Delimiter, 'URI' ) }">
	<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.header, g.URIsExport_Delimiter, 'CANONICAL' ) }">
	<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.header, g.URIsExport_Delimiter, 'STATUS' ) }">

	<MvIF EXPR = "{ g.URIsExport_Page }">		<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.header, g.URIsExport_Delimiter, 'PAGE_CODE' ) }"> 		</MvIF>
	<MvIF EXPR = "{ g.URIsExport_Category }">	<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.header, g.URIsExport_Delimiter, 'CATEGORY_CODE' ) }">	</MvIF>
	<MvIF EXPR = "{ g.URIsExport_Product }">	<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.header, g.URIsExport_Delimiter, 'PRODUCT_CODE' ) }"> 	</MvIF>
	<MvIF EXPR = "{ g.URIsExport_Feed }">		<MvEVAL EXPR = "{ Buffer_Add_Encoded( l.header, g.URIsExport_Delimiter, 'FEED_CODE' ) }">		</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ Miva_Fwriteln( l.output, l.header ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "URIsExport_Export_Line" PARAMETERS = "output, line" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvEXPORT FILE = "{ l.output }" DELIMITER = "" FIELDS = "l.line">
</MvFUNCTION>

<MvINCLUDE FILE = "modules/export/export_include.mv">

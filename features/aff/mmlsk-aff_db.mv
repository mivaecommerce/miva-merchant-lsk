<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2023 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-AFF-DTB-
| Next Error Code: 96
|
</MvCOMMENT>

<MvCOMMENT>
|
| Privileges
|
</MvCOMMENT>

<MvFUNCTION NAME = "AFF_Create_Data_Files" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].DomainPrivilege_Insert( 'AFLT', 'Affiliates' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].DomainPrivilege_Insert( 'MONY', 'Affiliate Money' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Store_Create" PARAMETERS = "email" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'Affiliates
								(
									id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
									password	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									lpasseml	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									site_name	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									site_url	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									con_name	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									email		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									company		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
									phone		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
									fax			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
									addr		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 100 )		$ ',
									city		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
									state		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
									zip			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
									cntry		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
									hits		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 4 )	$ ',
									order_pct	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
									order_flat	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
									lstpay_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									lstpay_amt	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
									lstpay_dt	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									lstpay_by	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									balance		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 4 )	$ ',
									status		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 10 )		$ ',
									status_dt	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									status_by	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									session_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 40 )		$ '
								) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'Affiliates_1 ON ' $ g.Store_Table_Prefix $ 'Affiliates ( id ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'Affiliates_2 ON ' $ g.Store_Table_Prefix $ 'Affiliates ( code ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'Affiliates_3 ON ' $ g.Store_Table_Prefix $ 'Affiliates ( session_id ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'AffiliateEarnings
								(
									id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									aff_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									ern_date	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									ern_type	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 10 )		$ ',
									order_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									order_amt	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
									order_pct	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
									amount		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 4 )	$ ',
									void		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
									void_reasn	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 250 )		$ ',
									void_by		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									adjst_reas	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 250 )		$ ',
									adjst_by	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									payout_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									ip_address	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 40 )		$ '
								) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'AffiliateEarnings_1 ON ' $ g.Store_Table_Prefix $ 'AffiliateEarnings ( id ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'AffiliateEarnings_2 ON ' $ g.Store_Table_Prefix $ 'AffiliateEarnings ( aff_id, ern_date ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'AffiliateEarnings_3 ON ' $ g.Store_Table_Prefix $ 'AffiliateEarnings ( payout_id, aff_id ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'AffiliatePayouts
								(
									id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									pay_date	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									amount		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
									pay_count	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									pay_by		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									processed	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
									proc_by		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									proc_date	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									void		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
									void_reasn	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 250 )		$ ',
									void_date	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									void_by		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ '
								) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00009', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'AffiliatePayouts_1 ON ' $ g.Store_Table_Prefix $ 'AffiliatePayouts ( id ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00010', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'AffiliatePayouts_2 ON ' $ g.Store_Table_Prefix $ 'AffiliatePayouts ( pay_date ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00011', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'AffiliateEmail
								(
									email_from	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 ) $ ',
									email_cc	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 ) $ ',
									subject		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 ) $ ',
									header		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO() $ '
								) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00012', g.MvQUERY_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.affiliate_email"	MEMBER = "email_from" 	VALUE = "{ l.email }">
	<MvASSIGN NAME = "l.affiliate_email"	MEMBER = "email_cc" 	VALUE = "">
	<MvASSIGN NAME = "l.affiliate_email"	MEMBER = "subject" 		VALUE = "Your affiliate password">
	<MvASSIGN NAME = "l.affiliate_email"	MEMBER = "header" 		VALUE = "Click on the following link to reset your affiliate password.">

	<MvIF EXPR = "{ NOT AffiliateEmail_Insert( l.affiliate_email ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'AffiliateManage
								(
									active		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL() $ ',
									useto		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL() $ ',
									usefrom		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL() $ ',
									usecc		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL() $ ',
									email_from	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 ) $ ',
									email_to	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 ) $ ',
									email_cc	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 ) $ ',
									subject		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 ) $ ',
									header		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO() $ '
								) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00013', g.MvQUERY_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.affiliate_manage"	MEMBER = "active" 		VALUE = 0>
	<MvASSIGN NAME = "l.affiliate_manage"	MEMBER = "usefrom" 		VALUE = 1>
	<MvASSIGN NAME = "l.affiliate_manage"	MEMBER = "useto" 		VALUE = 1>
	<MvASSIGN NAME = "l.affiliate_manage"	MEMBER = "usecc" 		VALUE = 1>
	<MvASSIGN NAME = "l.affiliate_manage"	MEMBER = "email_from" 	VALUE = "{ l.email }">
	<MvASSIGN NAME = "l.affiliate_manage"	MEMBER = "email_to" 	VALUE = "{ l.email }">
	<MvASSIGN NAME = "l.affiliate_manage"	MEMBER = "email_cc" 	VALUE = "">
	<MvASSIGN NAME = "l.affiliate_manage"	MEMBER = "subject" 		VALUE = "New Affiliate Created">
	<MvASSIGN NAME = "l.affiliate_manage"	MEMBER = "header" 		VALUE = "The following affiliate has been created:">

	<MvIF EXPR = "{ NOT AffiliateManage_Insert( l.affiliate_manage ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'AffiliateOptions
							(
								active		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								signup		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								hits		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 4 )	$ ',
								order_pct	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
								pct_slt		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 10 )		$ ',
								order_flat	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
								terms		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ ',
								pay_thresh	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
								link_image	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								link_text	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								pw_min_len	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
								pw_complex	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
								pw_chgauth	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								resettype	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 10 )		$ ',
								reset_exp	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00014', g.MvQUERY_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.affiliate_options"	MEMBER = "active" 		VALUE = 0>
	<MvASSIGN NAME = "l.affiliate_options"	MEMBER = "signup" 		VALUE = "A">
	<MvASSIGN NAME = "l.affiliate_options"	MEMBER = "hits" 		VALUE = "0.00">
	<MvASSIGN NAME = "l.affiliate_options"	MEMBER = "order_pct" 	VALUE = "0.00">
	<MvASSIGN NAME = "l.affiliate_options"	MEMBER = "pct_slt" 		VALUE = "O">
	<MvASSIGN NAME = "l.affiliate_options"	MEMBER = "order_flat" 	VALUE = "0.00">
	<MvASSIGN NAME = "l.affiliate_options"	MEMBER = "terms" 		VALUE = "">
	<MvASSIGN NAME = "l.affiliate_options"	MEMBER = "pay_thresh" 	VALUE = "0.00">
	<MvASSIGN NAME = "l.affiliate_options"	MEMBER = "link_image" 	VALUE = "">
	<MvASSIGN NAME = "l.affiliate_options"	MEMBER = "link_text" 	VALUE = "">
	<MvASSIGN NAME = "l.affiliate_options"	MEMBER = "pw_min_len"	VALUE = 6>
	<MvASSIGN NAME = "l.affiliate_options"	MEMBER = "pw_complex"	VALUE = 1>
	<MvASSIGN NAME = "l.affiliate_options"	MEMBER = "pw_chgauth"	VALUE = 1>
	<MvASSIGN NAME = "l.affiliate_options"	MEMBER = "resettype"	VALUE = "change">
	<MvASSIGN NAME = "l.affiliate_options"	MEMBER = "reset_exp" 	VALUE = 1440>

	<MvIF EXPR = "{ NOT AffiliateOptions_Insert( l.affiliate_options ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'AffiliateSession
								(
									session_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 40 ) $ ',
									affil_code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 ) $ '
								) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00015', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'AffiliateSession_1 ON ' $ g.Store_Table_Prefix $ 'AffiliateSession ( affil_code ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00016', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'AffiliateSession_2 ON ' $ g.Store_Table_Prefix $ 'AffiliateSession ( session_id ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00017', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'Affiliates', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'AffiliateEarnings', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'AffiliatePayouts', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_AffiliatePasswordReset
	|
	</MvCOMMENT>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'AffiliatePasswordReset
						  (
							aff_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							token		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 32 )	$ ',
							dt_created	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00086', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'AffiliatePasswordReset_1 ON ' $ g.Store_Table_Prefix $ 'AffiliatePasswordReset ( aff_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00087', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'AffiliatePasswordReset_2 ON ' $ g.Store_Table_Prefix $ 'AffiliatePasswordReset ( token )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00088', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'AffiliatePasswordReset_3 ON ' $ g.Store_Table_Prefix $ 'AffiliatePasswordReset ( dt_created )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00089', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AFF_Store_Delete" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'Affiliates' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'AffiliateEarnings' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'AffiliatePayouts' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'AffiliateEmail' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'AffiliateManage' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'AffiliateOptions' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'AffiliateSession' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'AffiliatePasswordReset' }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Affiliate Options
|
</MvCOMMENT>

<MvFUNCTION NAME = "AffiliateOptions_Read" PARAMETERS = "affiliateoptions var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.affiliateoptions:active"		VALUE = "{ AffiliateOptions.d.active }">
	<MvASSIGN NAME = "l.affiliateoptions:signup"		VALUE = "{ AffiliateOptions.d.signup }">
	<MvASSIGN NAME = "l.affiliateoptions:hits"			VALUE = "{ AffiliateOptions.d.hits }">
	<MvASSIGN NAME = "l.affiliateoptions:order_pct"		VALUE = "{ AffiliateOptions.d.order_pct }">
	<MvASSIGN NAME = "l.affiliateoptions:pct_slt"		VALUE = "{ AffiliateOptions.d.pct_slt }">
	<MvASSIGN NAME = "l.affiliateoptions:order_flat"	VALUE = "{ AffiliateOptions.d.order_flat }">
	<MvASSIGN NAME = "l.affiliateoptions:terms"			VALUE = "{ AffiliateOptions.d.terms }">
	<MvASSIGN NAME = "l.affiliateoptions:pay_thresh"	VALUE = "{ AffiliateOptions.d.pay_thresh }">
	<MvASSIGN NAME = "l.affiliateoptions:link_image"	VALUE = "{ AffiliateOptions.d.link_image }">
	<MvASSIGN NAME = "l.affiliateoptions:link_text"		VALUE = "{ AffiliateOptions.d.link_text }">
	<MvASSIGN NAME = "l.affiliateoptions:pw_min_len"	VALUE = "{ AffiliateOptions.d.pw_min_len }">
	<MvASSIGN NAME = "l.affiliateoptions:pw_complex"	VALUE = "{ AffiliateOptions.d.pw_complex }">
	<MvASSIGN NAME = "l.affiliateoptions:pw_chgauth"	VALUE = "{ AffiliateOptions.d.pw_chgauth }">
	<MvASSIGN NAME = "l.affiliateoptions:resettype"		VALUE = "{ AffiliateOptions.d.resettype }">
	<MvASSIGN NAME = "l.affiliateoptions:reset_exp"		VALUE = "{ AffiliateOptions.d.reset_exp }">
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateOptions_Insert" PARAMETERS = "affiliateoptions var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'AffiliateOptions
					      ( active, signup, hits, order_pct, pct_slt, order_flat, terms, pay_thresh, link_image, link_text, pw_min_len, pw_complex, pw_chgauth, resettype, reset_exp )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.affiliateoptions:active, l.affiliateoptions:signup, l.affiliateoptions:hits,
					   l.affiliateoptions:order_pct, l.affiliateoptions:pct_slt,
					   l.affiliateoptions:order_flat, l.affiliateoptions:terms,
					   l.affiliateoptions:pay_thresh, l.affiliateoptions:link_image,
					   l.affiliateoptions:link_text, l.affiliateoptions:pw_min_len, l.affiliateoptions:pw_complex,
					   l.affiliateoptions:pw_chgauth, l.affiliateoptions:resettype, l.affiliateoptions:reset_exp">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00018', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateOptions_Update" PARAMETERS = "affiliateoptions var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'AffiliateOptions
					      SET
							active		= ?,
							signup		= ?,
							hits		= ?,
							order_pct	= ?,
							pct_slt		= ?,
							order_flat	= ?,
							terms		= ?,
							pay_thresh	= ?,
							link_image	= ?,
							link_text	= ?,
							pw_min_len	= ?,
							pw_complex	= ?,
							pw_chgauth	= ?,
							resettype	= ?,
							reset_exp	= ?' }"
			 FIELDS	= "l.affiliateoptions:active, l.affiliateoptions:signup, l.affiliateoptions:hits,
					   l.affiliateoptions:order_pct, l.affiliateoptions:pct_slt,
					   l.affiliateoptions:order_flat, l.affiliateoptions:terms,
					   l.affiliateoptions:pay_thresh, l.affiliateoptions:link_image,
					   l.affiliateoptions:link_text, l.affiliateoptions:pw_min_len, l.affiliateoptions:pw_complex,
					   l.affiliateoptions:pw_chgauth, l.affiliateoptions:resettype, l.affiliateoptions:reset_exp">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00019', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateOptions_Load" PARAMETERS = "affiliateoptions var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AffiliateOptions"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AffiliateOptions' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00020', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvEVAL EXPR = "{ AffiliateOptions_Read( l.affiliateoptions ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AffiliateOptions">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Affiliate Email
|
</MvCOMMENT>

<MvFUNCTION NAME = "AffiliateEmail_Read" PARAMETERS = "affiliateemail var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.affiliateemail:email_from"	VALUE = "{ AffiliateEmail.d.email_from }">
	<MvASSIGN NAME = "l.affiliateemail:email_cc"	VALUE = "{ AffiliateEmail.d.email_cc }">
	<MvASSIGN NAME = "l.affiliateemail:subject"		VALUE = "{ AffiliateEmail.d.subject }">
	<MvASSIGN NAME = "l.affiliateemail:header"		VALUE = "{ AffiliateEmail.d.header }">
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateEmail_Insert" PARAMETERS = "affiliateemail var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'AffiliateEmail
					      ( email_from, email_cc, subject, header )
						  VALUES
						  ( ?, ?, ?, ? )' }"
			 FIELDS	= "l.affiliateemail:email_from, l.affiliateemail:email_cc, l.affiliateemail:subject,
					   l.affiliateemail:header">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00021', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateEmail_Update" PARAMETERS = "affiliateemail var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'AffiliateEmail
					      SET
							email_from	= ?,
							email_cc	= ?,
							subject		= ?,
							header		= ?' }"
			 FIELDS	= "l.affiliateemail:email_from, l.affiliateemail:email_cc, l.affiliateemail:subject,
					   l.affiliateemail:header">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00022', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateEmail_Load" PARAMETERS = "affiliateemail var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AffiliateEmail"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AffiliateEmail' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00023', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvEVAL EXPR = "{ AffiliateEmail_Read( l.affiliateemail ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AffiliateEmail">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Affiliate Manage
|
</MvCOMMENT>

<MvFUNCTION NAME = "AffiliateManage_Read" PARAMETERS = "affiliatemanage var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.affiliatemanage:active"		VALUE = "{ AffiliateManage.d.active }">
	<MvASSIGN NAME = "l.affiliatemanage:useto"		VALUE = "{ AffiliateManage.d.useto }">
	<MvASSIGN NAME = "l.affiliatemanage:usefrom"	VALUE = "{ AffiliateManage.d.usefrom }">
	<MvASSIGN NAME = "l.affiliatemanage:usecc"		VALUE = "{ AffiliateManage.d.usecc }">
	<MvASSIGN NAME = "l.affiliatemanage:email_from"	VALUE = "{ AffiliateManage.d.email_from }">
	<MvASSIGN NAME = "l.affiliatemanage:email_to"	VALUE = "{ AffiliateManage.d.email_to }">
	<MvASSIGN NAME = "l.affiliatemanage:email_cc"	VALUE = "{ AffiliateManage.d.email_cc }">
	<MvASSIGN NAME = "l.affiliatemanage:subject"	VALUE = "{ AffiliateManage.d.subject }">
	<MvASSIGN NAME = "l.affiliatemanage:header"		VALUE = "{ AffiliateManage.d.header }">
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateManage_Insert" PARAMETERS = "affiliatemanage var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'AffiliateManage
					      ( active, useto, usefrom, usecc, email_from, email_to, email_cc, subject, header )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.affiliatemanage:active, l.affiliatemanage:useto, l.affiliatemanage:usefrom,
					   l.affiliatemanage:usecc, l.affiliatemanage:email_from, l.affiliatemanage:email_to,
					   l.affiliatemanage:email_cc, l.affiliatemanage:subject, l.affiliatemanage:header">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00024', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateManage_Update" PARAMETERS = "affiliatemanage var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'AffiliateManage
					      SET
							active		= ?,
							useto		= ?,
							usefrom		= ?,
							usecc		= ?,
							email_from	= ?,
							email_to	= ?,
							email_cc	= ?,
							subject		= ?,
							header		= ?' }"
			 FIELDS	= "l.affiliatemanage:active, l.affiliatemanage:useto, l.affiliatemanage:usefrom,
					   l.affiliatemanage:usecc, l.affiliatemanage:email_from, l.affiliatemanage:email_to,
					   l.affiliatemanage:email_cc, l.affiliatemanage:subject, l.affiliatemanage:header">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00025', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateManage_Load" PARAMETERS = "affiliatemanage var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AffiliateManage"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AffiliateManage' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00026', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvEVAL EXPR = "{ AffiliateManage_Read( l.affiliatemanage ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AffiliateManage">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Affiliates
|
</MvCOMMENT>

<MvFUNCTION NAME = "Affiliate_Read" PARAMETERS = "affiliate var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.affiliate:id"			VALUE = "{ Affiliates.d.id }">
	<MvASSIGN NAME = "l.affiliate:code"			VALUE = "{ Affiliates.d.code }">
	<MvASSIGN NAME = "l.affiliate:password"		VALUE = "{ Affiliates.d.password }">
	<MvASSIGN NAME = "l.affiliate:lpasseml"		VALUE = "{ Affiliates.d.lpasseml }">
	<MvASSIGN NAME = "l.affiliate:site_name"	VALUE = "{ Affiliates.d.site_name }">
	<MvASSIGN NAME = "l.affiliate:site_url"		VALUE = "{ Affiliates.d.site_url }">
	<MvASSIGN NAME = "l.affiliate:con_name"		VALUE = "{ Affiliates.d.con_name }">
	<MvASSIGN NAME = "l.affiliate:email"		VALUE = "{ Affiliates.d.email }">
	<MvASSIGN NAME = "l.affiliate:company"		VALUE = "{ Affiliates.d.company }">
	<MvASSIGN NAME = "l.affiliate:phone"		VALUE = "{ Affiliates.d.phone }">
	<MvASSIGN NAME = "l.affiliate:fax"			VALUE = "{ Affiliates.d.fax }">
	<MvASSIGN NAME = "l.affiliate:addr"			VALUE = "{ Affiliates.d.addr }">
	<MvASSIGN NAME = "l.affiliate:city"			VALUE = "{ Affiliates.d.city }">
	<MvASSIGN NAME = "l.affiliate:state"		VALUE = "{ Affiliates.d.state }">
	<MvASSIGN NAME = "l.affiliate:zip"			VALUE = "{ Affiliates.d.zip }">
	<MvASSIGN NAME = "l.affiliate:cntry"		VALUE = "{ Affiliates.d.cntry }">
	<MvASSIGN NAME = "l.affiliate:hits"			VALUE = "{ Affiliates.d.hits }">
	<MvASSIGN NAME = "l.affiliate:order_pct"	VALUE = "{ Affiliates.d.order_pct }">
	<MvASSIGN NAME = "l.affiliate:order_flat"	VALUE = "{ Affiliates.d.order_flat }">
	<MvASSIGN NAME = "l.affiliate:lstpay_id"	VALUE = "{ Affiliates.d.lstpay_id }">
	<MvASSIGN NAME = "l.affiliate:lstpay_amt"	VALUE = "{ Affiliates.d.lstpay_amt }">
	<MvASSIGN NAME = "l.affiliate:lstpay_dt"	VALUE = "{ Affiliates.d.lstpay_dt }">
	<MvASSIGN NAME = "l.affiliate:lstpay_by"	VALUE = "{ Affiliates.d.lstpay_by }">
	<MvASSIGN NAME = "l.affiliate:balance"		VALUE = "{ Affiliates.d.balance }">
	<MvASSIGN NAME = "l.affiliate:status"		VALUE = "{ Affiliates.d.status }">
	<MvASSIGN NAME = "l.affiliate:status_dt"	VALUE = "{ Affiliates.d.status_dt }">
	<MvASSIGN NAME = "l.affiliate:status_by"	VALUE = "{ Affiliates.d.status_by }">
	<MvASSIGN NAME = "l.affiliate:session_id"	VALUE = "{ Affiliates.d.session_id }">
</MvFUNCTION>

<MvFUNCTION NAME = "Affiliate_Insert" PARAMETERS = "affiliate var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Library_Crypto ].Runtime_Password_Encrypted( l.affiliate:password ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_Crypto ].Runtime_Password_Encrypt( l.affiliate:password, l.affiliate:password ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ Affiliate_Insert_LowLevel( l.affiliate ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Affiliate_Insert_LowLevel" PARAMETERS = "affiliate var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.affiliate:id"	VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'Affiliates' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'Affiliates
					      ( id, code, password, lpasseml, site_name, site_url, con_name, email, company, phone, fax, addr, city, state,
						    zip, cntry, hits, order_pct, order_flat, lstpay_id, lstpay_amt, lstpay_dt, lstpay_by, balance,
						    status, status_dt, status_by, session_id )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.affiliate:id, l.affiliate:code, l.affiliate:password, l.affiliate:lpasseml,
					   l.affiliate:site_name, l.affiliate:site_url, l.affiliate:con_name, l.affiliate:email,
					   l.affiliate:company, l.affiliate:phone, l.affiliate:fax, l.affiliate:addr,
					   l.affiliate:city, l.affiliate:state, l.affiliate:zip, l.affiliate:cntry,
					   l.affiliate:hits, l.affiliate:order_pct, l.affiliate:order_flat, l.affiliate:lstpay_id,
					   l.affiliate:lstpay_amt, l.affiliate:lstpay_dt, l.affiliate:lstpay_by,
					   l.affiliate:balance, l.affiliate:status, l.affiliate:status_dt, l.affiliate:status_by,
					   l.affiliate:session_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00027', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Affiliate_Update" PARAMETERS = "affiliate var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Affiliates
					      SET
							code		= ?,
							password	= ?,
							lpasseml	= ?,
							site_name	= ?,
							site_url	= ?,
							con_name	= ?,
							email		= ?,
							company		= ?,
							phone		= ?,
							fax			= ?,
							addr		= ?,
							city		= ?,
							state		= ?,
							zip			= ?,
							cntry		= ?,
							hits		= ?,
							order_pct	= ?,
							order_flat	= ?,
							lstpay_id	= ?,
							lstpay_amt	= ?,
							lstpay_dt	= ?,
							lstpay_by	= ?,
							balance		= ?,
							status		= ?,
							status_dt	= ?,
							status_by	= ?,
							session_id	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.affiliate:code, l.affiliate:password, l.affiliate:lpasseml, l.affiliate:site_name,
					   l.affiliate:site_url, l.affiliate:con_name, l.affiliate:email, l.affiliate:company,
					   l.affiliate:phone, l.affiliate:fax, l.affiliate:addr, l.affiliate:city,
					   l.affiliate:state, l.affiliate:zip, l.affiliate:cntry, l.affiliate:hits,
					   l.affiliate:order_pct, l.affiliate:order_flat, l.affiliate:lstpay_id,
					   l.affiliate:lstpay_amt, l.affiliate:lstpay_dt, l.affiliate:lstpay_by,
					   l.affiliate:balance, l.affiliate:status, l.affiliate:status_dt, l.affiliate:status_by,
					   l.affiliate:session_id,
					   l.affiliate:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00028', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Affiliate_Delete" PARAMETERS = "affiliate var" STANDARDOUTPUTLEVEL = "">
	<MvTRANSACT NAME = "Merchant">

	<MvIF EXPR = "{ NOT AffiliateEarning_Delete_Aff( l.affiliate:id ) OR
					NOT AffiliateSession_Delete_Affil_code( l.affiliate:code ) }">
		<MvROLLBACK NAME = "Merchant">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT Affiliate_Delete_ID( l.affiliate:id ) }">
		<MvROLLBACK NAME = "Merchant">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMIT NAME = "Merchant">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Affiliate_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'Affiliates WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00029', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Affiliate_Load_ID" PARAMETERS = "id, affiliate var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Affiliates"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Affiliates WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00030', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Affiliates.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Affiliates">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-AFF-DTB-00063' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Affiliate_Read( l.affiliate ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Affiliates">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Affiliate_Load_Code" PARAMETERS = "code, affiliate var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Affiliates"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Affiliates WHERE ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00031', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Affiliates.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Affiliates">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-AFF-DTB-00064' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Affiliate_Read( l.affiliate ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Affiliates">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Affiliate_Clear_Session" PARAMETERS = "session_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Affiliates SET session_id = \'\' WHERE session_id = ?' }"
			 FIELDS	= "l.session_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00085', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Affiliate_Load_Session" PARAMETERS = "session_id, affiliate var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Affiliates"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Affiliates WHERE session_id = ?' }"
				FIELDS	= "l.session_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00032', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Affiliates.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Affiliates">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-AFF-DTB-00065' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Affiliate_Read( l.affiliate ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Affiliates">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateList_Load_All" PARAMETERS = "affiliates var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Affiliates"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Affiliates' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00033', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.affiliate_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Affiliates.d.EOF }">
		<MvASSIGN NAME = "l.affiliate_count" VALUE = "{ l.affiliate_count + 1 }">
		<MvEVAL EXPR = "{ Affiliate_Read( l.affiliates[ l.affiliate_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Affiliates" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Affiliates">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-AFF-DTB-00066', l.affiliate_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Affiliate_Update_Balance" PARAMETERS = "code, type, amt, order_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.AffiliateOptions:active }">
		<MvIF EXPR = "{ NOT Affiliate_Load_Code( l.code, l.affiliate ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ l.affiliate:status EQ 'Approved' }">
			<MvIF EXPR = "{ ( l.type EQ 'hit' ) }">
				<MvIF EXPR = "{ l.affiliate:hits NE 0 }">
					<MvASSIGN NAME = "l.earnings:aff_id" 		VALUE = "{ l.affiliate:id }">
					<MvASSIGN NAME = "l.earnings:ern_date" 		VALUE = "{ s.time_t }">
					<MvASSIGN NAME = "l.earnings:ern_type" 		VALUE = "hit">
					<MvASSIGN NAME = "l.earnings:order_id" 		VALUE = 0>
					<MvASSIGN NAME = "l.earnings:order_amt" 	VALUE = 0>
					<MvASSIGN NAME = "l.earnings:order_pct" 	VALUE = 0>
					<MvASSIGN NAME = "l.earnings:amount" 		VALUE = "{ l.affiliate:hits }">
					<MvASSIGN NAME = "l.earnings:payout_id" 	VALUE = 0>
					<MvASSIGN NAME = "l.earnings:void" 			VALUE = 0>
					<MvASSIGN NAME = "l.earnings:void_reasn" 	VALUE = "">
					<MvASSIGN NAME = "l.earnings:void_by" 		VALUE = 0>
					<MvASSIGN NAME = "l.earnings:adjst_by" 		VALUE = 0>
					<MvASSIGN NAME = "l.earnings:ip_address" 	VALUE = "{ s.remote_addr }">

					<MvIF EXPR = "{ NOT AffiliateEarning_Insert( l.earnings ) }">
						<MvFUNCTIONRETURN VALUE = 0>
					</MvIF>

					<MvASSIGN NAME = "l.affiliate:balance" VALUE = "{ ( l.affiliate:balance + l.affiliate:hits ) ROUND 4 }">
				</MvIF>
			<MvELSE>
				<MvIF EXPR = "{ l.type EQ 'order' }">
					<MvIF EXPR = "{ l.affiliate:order_pct NE 0 }">
						<MvASSIGN NAME = "l.earnings:aff_id" 		VALUE = "{ l.affiliate:id }">
						<MvASSIGN NAME = "l.earnings:ern_date" 		VALUE = "{ s.time_t }">
						<MvASSIGN NAME = "l.earnings:ern_type" 		VALUE = "order_pct">
						<MvASSIGN NAME = "l.earnings:order_id" 		VALUE = "{ l.order_id }">
						<MvASSIGN NAME = "l.earnings:order_amt" 	VALUE = "{ l.amt }">
						<MvASSIGN NAME = "l.earnings:order_pct" 	VALUE = "{ l.affiliate:order_pct }">
						<MvASSIGN NAME = "l.earnings:amount" 		VALUE = "{ l.amt * ( l.affiliate:order_pct / 100 ) }">
						<MvASSIGN NAME = "l.earnings:payout_id" 	VALUE = 0>
						<MvASSIGN NAME = "l.earnings:void" 			VALUE = 0>
						<MvASSIGN NAME = "l.earnings:void_reasn" 	VALUE = "">
						<MvASSIGN NAME = "l.earnings:void_by" 		VALUE = 0>
						<MvASSIGN NAME = "l.earnings:adjst_by" 		VALUE = 0>
						<MvASSIGN NAME = "l.earnings:ip_address" 	VALUE = "">

						<MvIF EXPR = "{ NOT AffiliateEarning_Insert( l.earnings ) }">
							<MvFUNCTIONRETURN VALUE = 0>
						</MvIF>

						<MvASSIGN NAME = "l.commission" 			VALUE = "{ l.amt * ( l.affiliate:order_pct / 100 ) }">
						<MvASSIGN NAME = "l.affiliate:balance" 		VALUE = "{ ( l.affiliate:balance + l.commission ) ROUND 4 }">
					</MvIF>

					<MvIF EXPR = "{ l.affiliate:order_flat NE 0 }">
						<MvASSIGN NAME = "l.earnings:aff_id" 		VALUE = "{ l.affiliate:id }">
						<MvASSIGN NAME = "l.earnings:ern_date" 		VALUE = "{ s.time_t }">
						<MvASSIGN NAME = "l.earnings:ern_type" 		VALUE = "order_flat">
						<MvASSIGN NAME = "l.earnings:order_id" 		VALUE = "{ l.order_id }">
						<MvASSIGN NAME = "l.earnings:order_amt" 	VALUE = 0>
						<MvASSIGN NAME = "l.earnings:order_pct" 	VALUE = 0>
						<MvASSIGN NAME = "l.earnings:amount" 		VALUE = "{ l.affiliate:order_flat }">
						<MvASSIGN NAME = "l.earnings:payout_id" 	VALUE = 0>
						<MvASSIGN NAME = "l.earnings:void" 			VALUE = 0>
						<MvASSIGN NAME = "l.earnings:void_reasn" 	VALUE = "">
						<MvASSIGN NAME = "l.earnings:void_by" 		VALUE = 0>
						<MvASSIGN NAME = "l.earnings:adjst_by" 		VALUE = 0>
						<MvASSIGN NAME = "l.earnings:ip_address" 	VALUE = "">

						<MvIF EXPR = "{ NOT AffiliateEarning_Insert( l.earnings ) }">
							<MvFUNCTIONRETURN VALUE = 0>
						</MvIF>

						<MvASSIGN NAME = "l.affiliate:balance" VALUE = "{ ( l.affiliate:balance + l.affiliate:order_flat ) ROUND 4 }">
					</MvIF>
				<MvELSE>
					<MvASSIGN NAME = "l.earnings:aff_id" 		VALUE = "{ l.affiliate:id }">
					<MvASSIGN NAME = "l.earnings:ern_date" 		VALUE = "{ s.time_t }">
					<MvASSIGN NAME = "l.earnings:ern_type" 		VALUE = "{ l.type }">
					<MvASSIGN NAME = "l.earnings:order_id" 		VALUE = 0>
					<MvASSIGN NAME = "l.earnings:order_amt" 	VALUE = 0>
					<MvASSIGN NAME = "l.earnings:order_pct" 	VALUE = 0>
					<MvASSIGN NAME = "l.earnings:amount" 		VALUE = "{ l.amt }">
					<MvASSIGN NAME = "l.earnings:payout_id" 	VALUE = 0>
					<MvASSIGN NAME = "l.earnings:void" 			VALUE = 0>
					<MvASSIGN NAME = "l.earnings:void_reasn" 	VALUE = "">
					<MvASSIGN NAME = "l.earnings:void_by" 		VALUE = 0>
					<MvASSIGN NAME = "l.earnings:adjst_by" 		VALUE = 0>
					<MvASSIGN NAME = "l.earnings:ip_address" 	VALUE = "">

					<MvIF EXPR = "{ NOT AffiliateEarning_Insert( l.earnings ) }">
						<MvFUNCTIONRETURN VALUE = 0>
					</MvIF>

					<MvASSIGN NAME = "l.affiliate:balance" VALUE = "{ ( l.affiliate:balance + l.amt ) ROUND 4 }">
				</MvIF>
			</MvIF>

			<MvIF EXPR = "{ NOT Affiliate_Update( l.affiliate ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Affiliate_Create_Order" PARAMETERS = "session_id, order_id, total" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.AffiliateOptions:active }">
		<MvIF EXPR = "{ NOT AffiliateSession_Load_Session( l.session_id, l.session ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>

		<MvIF EXPR = "{ len( l.session:affil_code ) }">
			<MvIF EXPR = "{ Affiliate_Load_Code( l.session:affil_code, l.affiliate ) }">
				<MvIF EXPR = "{ l.affiliate:status EQ 'Approved' }">
					<MvIF EXPR = "{ g.AffiliateOptions:pct_slt EQ 'S' }">
						<MvASSIGN NAME = "l.order_total" VALUE = "{ [ g.Module_Library_DB ].Basket_SubTotal( g.Basket:basket_id ) }">
					<MvELSE>
						<MvASSIGN NAME = "l.order_total" VALUE = "{ l.total }">
					</MvIF>

					<MvIF EXPR = "{ NOT Affiliate_Update_Balance( l.session:affil_code, 'order', l.order_total, l.order_id ) }">
						<MvFUNCTIONRETURN VALUE = 0>
					</MvIF>
				</MvIF>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Affiliate_Load_First" PARAMETERS = "affiliate var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'Affiliates',
																			 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Affiliates ORDER BY id ASC',
																			 '',
																			 0, 1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00034', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Affiliates.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Affiliates">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-AFF-DTB-00067' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Affiliate_Read( l.affiliate ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Affiliates">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Affiliate_Load_Next" PARAMETERS = "affiliate var, next_affiliate var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Affiliate_Load_Next_Affiliate_ID"		VALUE = "{ l.affiliate:id }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'Affiliates',
																			 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Affiliates WHERE id > ? ORDER BY id ASC',
																			 'g.Affiliate_Load_Next_Affiliate_ID',
																			 0, 1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00035', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Affiliates.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Affiliates">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-AFF-DTB-00068' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Affiliate_Read( l.next_affiliate ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Affiliates">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Affiliate_Load_Previous" PARAMETERS = "affiliate var, prev_affiliate var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Affiliate_Load_Previous_Affiliate_ID"	VALUE = "{ l.affiliate:id }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'Affiliates',
																			 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Affiliates WHERE id < ? ORDER BY id DESC',
																			 'g.Affiliate_Load_Previous_Affiliate_ID',
																			 0, 1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00036', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Affiliates.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Affiliates">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-AFF-DTB-00069' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Affiliate_Read( l.prev_affiliate ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Affiliates">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Affiliate_Load_Last" PARAMETERS = "affiliate var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'Affiliates',
																			 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Affiliates ORDER BY id DESC',
																			 '',
																			 0, 1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00037', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Affiliates.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Affiliates">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-AFF-DTB-00070' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Affiliate_Read( l.affiliate ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Affiliates">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateList_Load_Offset_All" PARAMETERS = "offset, search, searchable_fields, max, newoffset var, affiliates var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN	NAME = "l.query"	VALUE = "{ 'SELECT
													*
												FROM ' $
													g.Store_Table_Prefix $ 'Affiliates' }">

	<MvIF EXPR = "{ len( l.search ) }">
		<MvASSIGN NAME = "l.query"	VALUE = "{ l.query $ ' WHERE ' $ [ g.Module_Library_DB ].SQL_Search_Clause( l.search, g.Store_Table_Prefix $ 'Affiliates', l.searchable_fields, l.fields ) }">
	</MvIF>

	<MvASSIGN NAME = "l.query"	VALUE = "{ l.query $ ' ORDER BY id' }">

	<MvIF EXPR = "{ l.max EQ 0 }">	<MvASSIGN NAME = "l.limit" VALUE = 0>
	<MvELSE>						<MvASSIGN NAME = "l.limit" VALUE = "{ l.max + 1 }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'Affiliates',
																			 l.query, l.fields,
																			 l.offset, l.limit ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00038', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>
	<MvWHILE EXPR = "{ ( NOT Affiliates.d.EOF ) AND ( ( l.max EQ 0 ) OR ( l.count LT l.max ) ) }">
		<MvASSIGN NAME = "l.count" VALUE = "{ l.count + 1 }">
		<MvEVAL EXPR = "{ Affiliate_Read( l.affiliates[ l.count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "Affiliates">
	</MvWHILE>

	<MvIF EXPR = "{ Affiliates.d.EOF }">
		<MvASSIGN NAME = "l.newoffset"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.newoffset"	VALUE = "{ l.offset + l.count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Affiliates">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-AFF-DTB-00071', l.count ) }">
</MvFUNCTION>

<MvCOMMENT>
|
| Affiliate Earnings
|
</MvCOMMENT>

<MvFUNCTION NAME = "AffiliateEarning_Read" PARAMETERS = "affiliateearning var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.affiliateearning:id"			VALUE = "{ AffiliateEarnings.d.id }">
	<MvASSIGN NAME = "l.affiliateearning:aff_id"		VALUE = "{ AffiliateEarnings.d.aff_id }">
	<MvASSIGN NAME = "l.affiliateearning:ern_date"		VALUE = "{ AffiliateEarnings.d.ern_date }">
	<MvASSIGN NAME = "l.affiliateearning:ern_type"		VALUE = "{ AffiliateEarnings.d.ern_type }">
	<MvASSIGN NAME = "l.affiliateearning:order_id"		VALUE = "{ AffiliateEarnings.d.order_id }">
	<MvASSIGN NAME = "l.affiliateearning:order_amt"		VALUE = "{ AffiliateEarnings.d.order_amt }">
	<MvASSIGN NAME = "l.affiliateearning:order_pct"		VALUE = "{ AffiliateEarnings.d.order_pct }">
	<MvASSIGN NAME = "l.affiliateearning:amount"		VALUE = "{ AffiliateEarnings.d.amount }">
	<MvASSIGN NAME = "l.affiliateearning:void"			VALUE = "{ AffiliateEarnings.d.void }">
	<MvASSIGN NAME = "l.affiliateearning:void_reasn"	VALUE = "{ AffiliateEarnings.d.void_reasn }">
	<MvASSIGN NAME = "l.affiliateearning:void_by"		VALUE = "{ AffiliateEarnings.d.void_by }">
	<MvASSIGN NAME = "l.affiliateearning:adjst_reas"	VALUE = "{ AffiliateEarnings.d.adjst_reas }">
	<MvASSIGN NAME = "l.affiliateearning:adjst_by"		VALUE = "{ AffiliateEarnings.d.adjst_by }">
	<MvASSIGN NAME = "l.affiliateearning:payout_id"		VALUE = "{ AffiliateEarnings.d.payout_id }">
	<MvASSIGN NAME = "l.affiliateearning:ip_address"	VALUE = "{ AffiliateEarnings.d.ip_address }">
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateEarning_Read_Affiliate" PARAMETERS = "affiliate var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.affiliate:code" 		VALUE = "{ Affiliates.d.code }">
	<MvASSIGN NAME = "l.affiliate:site_name" 	VALUE = "{ Affiliates.d.site_name }">
	<MvASSIGN NAME = "l.affiliate:site_url" 	VALUE = "{ Affiliates.d.site_url }">
	<MvASSIGN NAME = "l.affiliate:con_name" 	VALUE = "{ Affiliates.d.con_name }">
	<MvASSIGN NAME = "l.affiliate:email" 		VALUE = "{ Affiliates.d.email }">
	<MvASSIGN NAME = "l.affiliate:company" 		VALUE = "{ Affiliates.d.company }">
	<MvASSIGN NAME = "l.affiliate:phone" 		VALUE = "{ Affiliates.d.phone }">
	<MvASSIGN NAME = "l.affiliate:fax" 			VALUE = "{ Affiliates.d.fax }">
	<MvASSIGN NAME = "l.affiliate:addr" 		VALUE = "{ Affiliates.d.addr }">
	<MvASSIGN NAME = "l.affiliate:city" 		VALUE = "{ Affiliates.d.city }">
	<MvASSIGN NAME = "l.affiliate:state" 		VALUE = "{ Affiliates.d.state }">
	<MvASSIGN NAME = "l.affiliate:zip" 			VALUE = "{ Affiliates.d.zip }">
	<MvASSIGN NAME = "l.affiliate:cntry" 		VALUE = "{ Affiliates.d.cntry }">
	<MvASSIGN NAME = "l.affiliate:amount" 		VALUE = "{ Affiliates.d.amount }">
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateEarning_Insert" PARAMETERS = "affiliateearning var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.affiliateearning:id"	VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'AffiliateEarnings' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'AffiliateEarnings
					      ( id, aff_id, ern_date, ern_type, order_id, order_amt, order_pct, amount, void, void_reasn, void_by,
						    adjst_reas, adjst_by, payout_id, ip_address )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.affiliateearning:id, l.affiliateearning:aff_id, l.affiliateearning:ern_date,
					   l.affiliateearning:ern_type, l.affiliateearning:order_id,
					   l.affiliateearning:order_amt, l.affiliateearning:order_pct,
					   l.affiliateearning:amount, l.affiliateearning:void, l.affiliateearning:void_reasn,
					   l.affiliateearning:void_by, l.affiliateearning:adjst_reas,
					   l.affiliateearning:adjst_by, l.affiliateearning:payout_id,
					   l.affiliateearning:ip_address">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00042', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateEarning_Update" PARAMETERS = "affiliateearning var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'AffiliateEarnings
					      SET
							aff_id		= ?,
							ern_date	= ?,
							ern_type	= ?,
							order_id	= ?,
							order_amt	= ?,
							order_pct	= ?,
							amount		= ?,
							void		= ?,
							void_reasn	= ?,
							void_by		= ?,
							adjst_reas	= ?,
							adjst_by	= ?,
							payout_id	= ?,
							ip_address	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.affiliateearning:aff_id, l.affiliateearning:ern_date, l.affiliateearning:ern_type,
					   l.affiliateearning:order_id, l.affiliateearning:order_amt,
					   l.affiliateearning:order_pct, l.affiliateearning:amount, l.affiliateearning:void,
					   l.affiliateearning:void_reasn, l.affiliateearning:void_by,
					   l.affiliateearning:adjst_reas, l.affiliateearning:adjst_by,
					   l.affiliateearning:payout_id, l.affiliateearning:ip_address,
					   l.affiliateearning:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00043', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateEarning_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AffiliateEarnings WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00044', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateEarning_Delete_Aff" PARAMETERS = "aff_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AffiliateEarnings WHERE aff_id = ?' }"
			 FIELDS	= "l.aff_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00045', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateEarning_Load_ID" PARAMETERS = "id, affiliateearning var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AffiliateEarnings"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AffiliateEarnings WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00046', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ AffiliateEarnings.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "AffiliateEarnings">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-AFF-DTB-00075' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ AffiliateEarning_Read( l.affiliateearning ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AffiliateEarnings">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateEarningList_Load_Payout" PARAMETERS = "payout_id, affiliateearnings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AffiliateEarnings"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AffiliateEarnings WHERE payout_id = ?' }"
				FIELDS	= "l.payout_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00047', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.affiliateearning_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT AffiliateEarnings.d.EOF }">
		<MvASSIGN NAME = "l.affiliateearning_count" VALUE = "{ l.affiliateearning_count + 1 }">
		<MvEVAL EXPR = "{ AffiliateEarning_Read( l.affiliateearnings[ l.affiliateearning_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "AffiliateEarnings" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AffiliateEarnings">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-AFF-DTB-00076', l.affiliateearning_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateEarningList_Load_Offset_Affiliate" PARAMETERS = "affiliate_id, offset, max_rec, newoffset var, earnings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.AffiliateEarningList_Load_Offset_Affiliate_Affiliate_ID"	VALUE = "{ l.affiliate_id }">

	<MvASSIGN NAME = "l.fields" VALUE = "g.AffiliateEarningList_Load_Offset_Affiliate_Affiliate_ID">
	<MvASSIGN	NAME = "l.query"	VALUE = "{ 'SELECT
													*
												FROM ' $
													g.Store_Table_Prefix $ 'AffiliateEarnings
												WHERE aff_id = ?' }">

	<MvASSIGN NAME = "l.query"	VALUE = "{ l.query $ ' ORDER BY id' }">

	<MvIF EXPR = "{ l.max_rec EQ 0 }">	<MvASSIGN NAME = "l.limit" VALUE = 0>
	<MvELSE>							<MvASSIGN NAME = "l.limit" VALUE = "{ l.max_rec + 1 }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'AffiliateEarnings',
																			 l.query, l.fields,
																			 l.offset, l.limit ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00048', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.earning_count" VALUE = 0>
	<MvWHILE EXPR = "{ ( NOT AffiliateEarnings.d.EOF ) AND ( ( l.max_rec EQ 0 ) OR ( l.earning_count LT l.max_rec ) ) }">
		<MvASSIGN NAME = "l.earning_count" VALUE = "{ l.earning_count + 1 }">
		<MvEVAL EXPR = "{ AffiliateEarning_Read( l.earnings[ l.earning_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "AffiliateEarnings">
	</MvWHILE>

	<MvIF EXPR = "{ AffiliateEarnings.d.EOF }">
		<MvASSIGN NAME = "l.newoffset"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.newoffset"	VALUE = "{ l.offset + l.earning_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AffiliateEarnings">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-AFF-DTB-00077', l.earning_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateEarningList_Load_Offset_Payout" PARAMETERS = "payout_id, offset, search, searchable_fields, max, newoffset var, earnings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.AffiliateEarningList_Load_Offset_Payout_Payout_ID"	VALUE = "{ l.payout_id }">

	<MvASSIGN NAME = "l.fields" VALUE = "g.AffiliateEarningList_Load_Offset_Payout_Payout_ID">
	<MvASSIGN NAME = "l.query"	VALUE = "{ 'SELECT
													*
												FROM ' $
													g.Store_Table_Prefix $ 'Affiliates, ' $
													g.Store_Table_Prefix $ 'AffiliateEarnings
												WHERE
													payout_id = ? AND
													aff_id = ' $
													g.Store_Table_Prefix $ 'Affiliates.id ' }">

	<MvIF EXPR = "{ len( l.search ) }">
		<MvASSIGN NAME = "l.query"	VALUE = "{ l.query $ ' AND ' $ [ g.Module_Library_DB ].SQL_Search_Clause( l.search, g.Store_Table_Prefix $ 'Affiliates', l.searchable_fields, l.fields ) }">
	</MvIF>

	<MvASSIGN NAME = "l.query"	VALUE = "{ l.query $ ' ORDER BY ' $	g.Store_Table_Prefix $ 'Affiliates.id' }">

	<MvIF EXPR = "{ l.max EQ 0 }">	<MvASSIGN NAME = "l.limit" VALUE = 0>
	<MvELSE>						<MvASSIGN NAME = "l.limit" VALUE = "{ l.max + 1 }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'Affiliates',
																			 l.query, l.fields,
																			 l.offset, l.limit ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00049', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.affiliate_count" VALUE = 0>
	<MvWHILE EXPR = "{ ( NOT Affiliates.d.EOF ) AND ( ( l.max EQ 0 ) OR ( l.affiliate_count LT l.max ) ) }">
		<MvASSIGN NAME = "l.affiliate_count" VALUE = "{ l.affiliate_count + 1 }">
		<MvEVAL EXPR = "{ AffiliateEarning_Read_Affiliate( l.earnings[ l.affiliate_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "Affiliates">
	</MvWHILE>

	<MvIF EXPR = "{ Affiliates.d.EOF }">
		<MvASSIGN NAME = "l.newoffset"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.newoffset"	VALUE = "{ l.offset + l.affiliate_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Affiliates">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-AFF-DTB-00078', l.affiliate_count ) }">
</MvFUNCTION>

<MvCOMMENT>
|
| Affiliate Payouts
|
</MvCOMMENT>

<MvFUNCTION NAME = "AffiliatePayout_Read" PARAMETERS = "affiliatepayout var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.affiliatepayout:id"			VALUE = "{ AffiliatePayouts.d.id }">
	<MvASSIGN NAME = "l.affiliatepayout:pay_date"	VALUE = "{ AffiliatePayouts.d.pay_date }">
	<MvASSIGN NAME = "l.affiliatepayout:amount"		VALUE = "{ AffiliatePayouts.d.amount }">
	<MvASSIGN NAME = "l.affiliatepayout:pay_count"	VALUE = "{ AffiliatePayouts.d.pay_count }">
	<MvASSIGN NAME = "l.affiliatepayout:pay_by"		VALUE = "{ AffiliatePayouts.d.pay_by }">
	<MvASSIGN NAME = "l.affiliatepayout:processed"	VALUE = "{ AffiliatePayouts.d.processed }">
	<MvASSIGN NAME = "l.affiliatepayout:proc_by"	VALUE = "{ AffiliatePayouts.d.proc_by }">
	<MvASSIGN NAME = "l.affiliatepayout:proc_date"	VALUE = "{ AffiliatePayouts.d.proc_date }">
	<MvASSIGN NAME = "l.affiliatepayout:void"		VALUE = "{ AffiliatePayouts.d.void }">
	<MvASSIGN NAME = "l.affiliatepayout:void_reasn"	VALUE = "{ AffiliatePayouts.d.void_reasn }">
	<MvASSIGN NAME = "l.affiliatepayout:void_date"	VALUE = "{ AffiliatePayouts.d.void_date }">
	<MvASSIGN NAME = "l.affiliatepayout:void_by"	VALUE = "{ AffiliatePayouts.d.void_by }">
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliatePayout_Insert" PARAMETERS = "affiliatepayout var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.affiliatepayout:id"	VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'AffiliatePayouts' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'AffiliatePayouts
					      ( id, pay_date, amount, pay_count, pay_by, processed, proc_by, proc_date, void, void_reasn, void_date,
						    void_by )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.affiliatepayout:id, l.affiliatepayout:pay_date, l.affiliatepayout:amount,
					   l.affiliatepayout:pay_count, l.affiliatepayout:pay_by, l.affiliatepayout:processed,
					   l.affiliatepayout:proc_by, l.affiliatepayout:proc_date, l.affiliatepayout:void,
					   l.affiliatepayout:void_reasn, l.affiliatepayout:void_date, l.affiliatepayout:void_by">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00050', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliatePayout_Update" PARAMETERS = "affiliatepayout var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'AffiliatePayouts
					      SET
							pay_date	= ?,
							amount		= ?,
							pay_count	= ?,
							pay_by		= ?,
							processed	= ?,
							proc_by		= ?,
							proc_date	= ?,
							void		= ?,
							void_reasn	= ?,
							void_date	= ?,
							void_by		= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.affiliatepayout:pay_date, l.affiliatepayout:amount, l.affiliatepayout:pay_count,
					   l.affiliatepayout:pay_by, l.affiliatepayout:processed, l.affiliatepayout:proc_by,
					   l.affiliatepayout:proc_date, l.affiliatepayout:void, l.affiliatepayout:void_reasn,
					   l.affiliatepayout:void_date, l.affiliatepayout:void_by,
					   l.affiliatepayout:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00051', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliatePayout_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AffiliatePayouts WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00052', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliatePayout_Load_ID" PARAMETERS = "id, affiliatepayout var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AffiliatePayouts"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AffiliatePayouts WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00053', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ AffiliatePayouts.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "AffiliatePayouts">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-AFF-DTB-00079' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ AffiliatePayout_Read( l.affiliatepayout ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AffiliatePayouts">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliatePayoutList_Load_Offset_Unprocessed" PARAMETERS = "offset, search, searchable_fields, max, newoffset var, payout var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN	NAME = "l.query"	VALUE = "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AffiliatePayouts' }">

	<MvIF EXPR = "{ len( l.search ) }">
		<MvASSIGN NAME = "l.query"	VALUE = "{ l.query $ ' AND ' $ [ g.Module_Library_DB ].SQL_Search_Clause( l.search, g.Store_Table_Prefix $ 'AffiliatePayouts', l.searchable_fields, l.fields ) }">
	</MvIF>

	<MvASSIGN NAME = "l.query"	VALUE = "{ l.query $ ' ORDER BY id' }">

	<MvIF EXPR = "{ l.max EQ 0 }">	<MvASSIGN NAME = "l.limit" VALUE = 0>
	<MvELSE>						<MvASSIGN NAME = "l.limit" VALUE = "{ l.max + 1 }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'AffiliatePayouts',
																			 l.query, l.fields,
																			 l.offset, l.limit ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00054', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.payout_count" VALUE = 0>
	<MvWHILE EXPR = "{ ( NOT AffiliatePayouts.d.EOF ) AND ( ( l.max EQ 0 ) OR ( l.payout_count LT l.max ) ) }">
		<MvIF EXPR = "{ NOT AffiliatePayouts.d.processed }">
			<MvASSIGN NAME = "l.payout_count" VALUE = "{ l.payout_count + 1 }">
			<MvEVAL EXPR = "{ AffiliatePayout_Read( l.payout[ l.payout_count ] ) }">
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "AffiliatePayouts">
	</MvWHILE>

	<MvIF EXPR = "{ AffiliatePayouts.d.EOF }">
		<MvASSIGN NAME = "l.newoffset"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.newoffset"	VALUE = "{ l.offset + l.payout_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AffiliatePayouts">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-AFF-DTB-00080', l.payout_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliatePayoutList_Load_Offset_Processed" PARAMETERS = "offset, search, searchable_fields, max, newoffset var, payout var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN	NAME = "l.query"	VALUE = "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AffiliatePayouts' }">

	<MvIF EXPR = "{ len( l.search ) }">
		<MvASSIGN NAME = "l.query"	VALUE = "{ l.query $ ' WHERE ' $ [ g.Module_Library_DB ].SQL_Search_Clause( l.search, g.Store_Table_Prefix $ 'AffiliatePayouts', l.searchable_fields, l.fields ) }">
	</MvIF>

	<MvASSIGN NAME = "l.query"	VALUE = "{ l.query $ ' ORDER BY id' }">

	<MvIF EXPR = "{ l.max EQ 0 }">	<MvASSIGN NAME = "l.limit" VALUE = 0>
	<MvELSE>						<MvASSIGN NAME = "l.limit" VALUE = "{ l.max + 1 }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'AffiliatePayouts',
																			 l.query, l.fields,
																			 l.offset, l.limit ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00055', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.payout_count" VALUE = 0>
	<MvWHILE EXPR = "{ ( NOT AffiliatePayouts.d.EOF ) AND ( ( l.max EQ 0 ) OR ( l.payout_count LT l.max ) ) }">
		<MvIF EXPR = "{ AffiliatePayouts.d.processed }">
			<MvASSIGN NAME = "l.payout_count" VALUE = "{ l.payout_count + 1 }">
			<MvEVAL EXPR = "{ AffiliatePayout_Read( l.payout[ l.payout_count ] ) }">
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "AffiliatePayouts">
	</MvWHILE>

	<MvIF EXPR = "{ AffiliatePayouts.d.EOF }">
		<MvASSIGN NAME = "l.newoffset"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.newoffset"	VALUE = "{ l.offset + l.payout_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AffiliatePayouts">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-AFF-DTB-00081', l.payout_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliatePayoutList_Load_Offset_All" PARAMETERS = "offset, search, searchable_fields, max, newoffset var, payout var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN	NAME = "l.query"	VALUE = "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AffiliatePayouts' }">

	<MvIF EXPR = "{ len( l.search ) }">
		<MvASSIGN NAME = "l.query"	VALUE = "{ l.query $ ' WHERE ' $ [ g.Module_Library_DB ].SQL_Search_Clause( l.search, g.Store_Table_Prefix $ 'AffiliatePayouts', l.searchable_fields, l.fields ) }">
	</MvIF>

	<MvASSIGN NAME = "l.query"	VALUE = "{ l.query $ ' ORDER BY id' }">

	<MvIF EXPR = "{ l.max EQ 0 }">	<MvASSIGN NAME = "l.limit" VALUE = 0>
	<MvELSE>						<MvASSIGN NAME = "l.limit" VALUE = "{ l.max + 1 }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'AffiliatePayouts',
																			 l.query, l.fields,
																			 l.offset, l.limit ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00056', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.payout_count" VALUE = 0>
	<MvWHILE EXPR = "{ ( NOT AffiliatePayouts.d.EOF ) AND ( ( l.max EQ 0 ) OR ( l.payout_count LT l.max ) ) }">
		<MvASSIGN NAME = "l.payout_count" VALUE = "{ l.payout_count + 1 }">
		<MvEVAL EXPR = "{ AffiliatePayout_Read( l.payout[ l.payout_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "AffiliatePayouts">
	</MvWHILE>

	<MvIF EXPR = "{ AffiliatePayouts.d.EOF }">
		<MvASSIGN NAME = "l.newoffset"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.newoffset"	VALUE = "{ l.offset + l.payout_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AffiliatePayouts">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-AFF-DTB-00082', l.payout_count ) }">
</MvFUNCTION>

<MvCOMMENT>
|
| Affiliate Sessions
|
</MvCOMMENT>

<MvFUNCTION NAME = "AffiliateSession_Read" PARAMETERS = "affiliatesession var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.affiliatesession:session_id"	VALUE = "{ AffiliateSession.d.session_id }">
	<MvASSIGN NAME = "l.affiliatesession:affil_code"	VALUE = "{ AffiliateSession.d.affil_code }">
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateSession_Insert" PARAMETERS = "affiliatesession var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'AffiliateSession
					      ( session_id, affil_code )
						  VALUES
						  ( ?, ? )' }"
			 FIELDS	= "l.affiliatesession:session_id, l.affiliatesession:affil_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00057', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateSession_Update_Session" PARAMETERS = "affiliatesession var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'AffiliateSession
					      SET
							affil_code	= ?
					      WHERE
						    session_id	= ?' }"
			 FIELDS	= "l.affiliatesession:affil_code,
					   l.affiliatesession:session_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00058', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateSession_Delete_Affil_code" PARAMETERS = "affil_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AffiliateSession WHERE affil_code = ?' }"
			 FIELDS	= "l.affil_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00059', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateSessionList_Load_Affil_code" PARAMETERS = "affil_code, affiliatesession var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AffiliateSession"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AffiliateSession WHERE affil_code = ?' }"
				FIELDS	= "l.affil_code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00060', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.affiliatesession_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT AffiliateSession.d.EOF }">
		<MvASSIGN NAME = "l.affiliatesession_count" VALUE = "{ l.affiliatesession_count + 1 }">
		<MvEVAL EXPR = "{ AffiliateSession_Read( l.affiliatesession[ l.affiliatesession_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "AffiliateSession" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AffiliateSession">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-AFF-DTB-00083', l.affiliatesession_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateSession_Load_Session" PARAMETERS = "session_id, affiliatesession var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AffiliateSession"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AffiliateSession WHERE session_id = ?' }"
				FIELDS	= "l.session_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00061', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ AffiliateSession.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "AffiliateSession">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-AFF-DTB-00084' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ AffiliateSession_Read( l.affiliatesession ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AffiliateSession">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Affiliate_Update_SessionID" PARAMETERS = "session_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.AffiliateOptions:active AND NOT ISNULL g.Affiliate }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Basket_Register( g.Basket ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT AffiliateSession_Load_Session( l.session_id, l.session ) }">
			<MvASSIGN NAME = "l.session:session_id" VALUE = "{ l.session_id }">
			<MvASSIGN NAME = "l.session:affil_code" VALUE = "{ g.Affiliate }">

			<MvIF EXPR = "{ NOT AffiliateSession_Insert( l.session ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvIF EXPR = "{ len( g.Affiliate ) }">
				<MvASSIGN NAME = "l.foo" VALUE = "{ Affiliate_Update_Balance( g.Affiliate, 'hit', '', '' ) }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ len( g.Affiliate ) }">
				<MvASSIGN NAME = "l.session:session_id" VALUE = "{ l.session_id }">
				<MvASSIGN NAME = "l.session:affil_code" VALUE = "{ g.Affiliate }">

				<MvIF EXPR = "{ NOT AffiliateSession_Update_Session( l.session ) }">
					<MvFUNCTIONRETURN VALUE = 0>
				</MvIF>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Basket_Delete" PARAMETERS = "session_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.AffiliateOptions:active }">
		<MvQUERY NAME = "Merchant"
				QUERY = "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AffiliateSession WHERE session_id = ?' }"
				FIELDS = "l.session_id">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00062', g.MvQUERY_Error ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateSession_Delete_All" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AffiliateSession' }">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliateSession_Delete_All_OlderThan" PARAMETERS = "time_t" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Native_DBAPI EQ 'postgresql' }">
		<MvQUERY NAME 	= "Merchant"
				 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AffiliateSession afs
				 			  USING ' $ g.Store_Table_Prefix $ 'Baskets b WHERE b.lastupdate < ? AND afs.session_id = b.session_id' }"
				 FIELDS = "l.time_t">
	<MvELSE>
		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'DELETE afs.* FROM ' $ g.Store_Table_Prefix $ 'AffiliateSession afs, ' $ g.Store_Table_Prefix $ 'Baskets b WHERE b.lastupdate < ? AND afs.session_id = b.session_id' }"
				 FIELDS	= "l.time_t">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_AffiliatePasswordReset
|
</MvCOMMENT>

<MvFUNCTION NAME = "AffiliatePasswordReset_Generate" PARAMETERS = "aff_id, token var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT AffiliatePasswordReset_Delete_Expired() }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.reset"				VALUE = "">
	<MvASSIGN NAME = "l.reset:aff_id"		VALUE = "{ l.aff_id }">
	<MvASSIGN NAME = "l.reset:token"		VALUE = "{ MakeSessionID() }">
	<MvASSIGN NAME = "l.reset:dt_created"	VALUE = "{ s.dyn_time_t }">

	<MvIF EXPR = "{ NOT AffiliatePasswordReset_Insert( l.reset ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_Error_IsIndexDuplicate( g.Error_Message ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT AffiliatePasswordReset_Update( l.reset ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.token"				VALUE = "{ l.reset:token }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliatePasswordReset_Insert" PARAMETERS = "reset var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'AffiliatePasswordReset
						  ( aff_id, token, dt_created )
						  VALUES
						  ( ?, ?, ? )' }"
			 FIELDS	= "l.reset:aff_id, l.reset:token, l.reset:dt_created">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00090', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliatePasswordReset_Update" PARAMETERS = "reset var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'AffiliatePasswordReset
						  SET
							token		= ?,
							dt_created	= ?
						  WHERE
							aff_id		= ?' }"
			 FIELDS	= "l.reset:token, l.reset:dt_created,
					   l.reset:aff_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00091', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliatePasswordReset_Delete_Expired" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT AffiliateOptions_Load( l.affiliate_options ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.expiry_cutoff"	VALUE = "{ s.dyn_time_t - ( l.affiliate_options:reset_exp * 60 ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AffiliatePasswordReset WHERE dt_created < ?' }"
			 FIELDS	= "l.expiry_cutoff">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00092', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliatePasswordReset_Delete_Affiliate" PARAMETERS = "aff_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AffiliatePasswordReset WHERE aff_id = ?' }"
			 FIELDS	= "l.aff_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00093', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AffiliatePasswordReset_Load_Token" PARAMETERS = "token, reset var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT AffiliatePasswordReset_Delete_Expired() }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AffiliatePasswordReset"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AffiliatePasswordReset WHERE token = ?' }"
				FIELDS	= "l.token">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AFF-DTB-00094', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ AffiliatePasswordReset.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "AffiliatePasswordReset">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-AFF-DTB-00095' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.reset"				VALUE = "">
	<MvASSIGN NAME = "l.reset:aff_id"		VALUE = "{ AffiliatePasswordReset.d.aff_id }">
	<MvASSIGN NAME = "l.reset:token"		VALUE = "{ AffiliatePasswordReset.d.token }">
	<MvASSIGN NAME = "l.reset:dt_created"	VALUE = "{ AffiliatePasswordReset.d.dt_created }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AffiliatePasswordReset">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-GFT-DTB-
| Next Error Code: 28
|
</MvCOMMENT>

<MvFUNCTION NAME = "GFT_Store_Create" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| sNN_GiftCertificates
	|
	</MvCOMMENT>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'GiftCertificates
						  (
							id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
							order_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							line_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							cust_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							create_dt	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							redeem_dt	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 10 )		$ ',
							credit_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							active		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							amount		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'GiftCertificates_1 ON ' $ g.Store_Table_Prefix $ 'GiftCertificates ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'GiftCertificates_2 ON ' $ g.Store_Table_Prefix $ 'GiftCertificates ( code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'GiftCertificates_3 ON ' $ g.Store_Table_Prefix $ 'GiftCertificates ( order_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'GiftCertificates_4 ON ' $ g.Store_Table_Prefix $ 'GiftCertificates ( line_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'GiftCertificates', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_GiftCertificateSales
	|
	</MvCOMMENT>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'GiftCertificateSales
						  (
							product_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							enabled		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							type		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 10 )		$ ',
							amount		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							email_ship	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							email_bill	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							email_attr	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							emlattcode	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
							dscattcode	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'GiftCertificateSales_1 ON ' $ g.Store_Table_Prefix $ 'GiftCertificateSales ( product_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GFT_Create_Data_Files" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].DomainPrivilege_Insert( 'GFTC', 'Gift Certificates' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GFT_Store_Delete" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'GiftCertificates' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'GiftCertificateSales' }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_GiftCertificates
|
</MvCOMMENT>

<MvFUNCTION NAME = "GiftCertificate_Read" PARAMETERS = "giftcertificate var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.giftcertificate:id"					VALUE = "{ GiftCertificates.d.id }">
	<MvASSIGN NAME = "l.giftcertificate:code"				VALUE = "{ GiftCertificates.d.code }">
	<MvASSIGN NAME = "l.giftcertificate:order_id"			VALUE = "{ GiftCertificates.d.order_id }">
	<MvASSIGN NAME = "l.giftcertificate:line_id"			VALUE = "{ GiftCertificates.d.line_id }">
	<MvASSIGN NAME = "l.giftcertificate:cust_id"			VALUE = "{ GiftCertificates.d.cust_id }">
	<MvASSIGN NAME = "l.giftcertificate:create_dt"			VALUE = "{ GiftCertificates.d.create_dt }">
	<MvASSIGN NAME = "l.giftcertificate:redeem_dt"			VALUE = "{ GiftCertificates.d.redeem_dt }">
	<MvASSIGN NAME = "l.giftcertificate:credit_id"			VALUE = "{ GiftCertificates.d.credit_id }">
	<MvASSIGN NAME = "l.giftcertificate:active"				VALUE = "{ GiftCertificates.d.active }">
	<MvASSIGN NAME = "l.giftcertificate:amount"				VALUE = "{ GiftCertificates.d.amount }">
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificate_Insert" PARAMETERS = "giftcertificate var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT GiftCertificate_Insert_LowLevel( l.giftcertificate ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_FEL_DB ].FeatureEngagement_Increment_Counter( 'giftcertificate_created', 1 ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificate_Insert_LowLevel" PARAMETERS = "giftcertificate var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.giftcertificate:id"			VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'GiftCertificates' ) }">
	<MvASSIGN NAME = "l.giftcertificate:cust_id"	VALUE = 0>
	<MvASSIGN NAME = "l.giftcertificate:create_dt"	VALUE = "{ s.dyn_time_t }">
	<MvASSIGN NAME = "l.giftcertificate:redeem_dt"	VALUE = "">
	<MvASSIGN NAME = "l.giftcertificate:credit_id"	VALUE = 0>

	<MvIF EXPR = "{ ISNULL l.giftcertificate:order_id }">	<MvASSIGN NAME = "l.giftcertificate:order_id"	VALUE = 0>		</MvIF>
	<MvIF EXPR = "{ ISNULL l.giftcertificate:line_id }">	<MvASSIGN NAME = "l.giftcertificate:line_id"	VALUE = 0>		</MvIF>
	<MvIF EXPR = "{ ISNULL l.giftcertificate:active }">		<MvASSIGN NAME = "l.giftcertificate:active"		VALUE = 0>		</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'GiftCertificates
					      ( id, code, order_id, line_id, cust_id, create_dt, redeem_dt, credit_id, active, amount )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.giftcertificate:id, l.giftcertificate:code, l.giftcertificate:order_id, l.giftcertificate:line_id,
					   l.giftcertificate:cust_id, l.giftcertificate:create_dt, l.giftcertificate:redeem_dt,
					   l.giftcertificate:credit_id, l.giftcertificate:active, l.giftcertificate:amount">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00010', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificate_Update" PARAMETERS = "giftcertificate var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT GiftCertificate_Update_LowLevel( l.giftcertificate ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ [ g.Module_Library_DB ].StoreModuleList_Load_Features_Cached( 'not_giftcert', l.modules ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Notify_GiftCertificate_Updated( l.module, l.giftcertificate ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificate_Update_LowLevel" PARAMETERS = "giftcertificate var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'GiftCertificates
						  SET
							code		= ?,
							order_id	= ?,
							line_id		= ?,
							cust_id		= ?,
							redeem_dt	= ?,
							credit_id	= ?,
							active		= ?,
							amount		= ?
						  WHERE
							id			= ?' }"
			 FIELDS	= "l.giftcertificate:code, l.giftcertificate:order_id, l.giftcertificate:line_id, l.giftcertificate:cust_id,
					   l.giftcertificate:redeem_dt, l.giftcertificate:credit_id, l.giftcertificate:active, l.giftcertificate:amount,
					   l.giftcertificate:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00011', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificate_Delete" PARAMETERS = "giftcertificate var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT GiftCertificate_Delete_ID( l.giftcertificate:id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].OrderOption_Update_AttributeCode( l.giftcertificate:order_id, l.giftcertificate:line_id, 'giftcert', 'giftcert_deleted' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ [ g.Module_Library_DB ].StoreModuleList_Load_Features_Cached( 'not_giftcert', l.modules ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Notify_GiftCertificate_Deleted( l.module, l.giftcertificate ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificate_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'GiftCertificates WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00012', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificateList_Load_All" PARAMETERS = "giftcertificates var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "GiftCertificates"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'GiftCertificates' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00013', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.giftcertificate_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT GiftCertificates.d.EOF }">
		<MvASSIGN NAME = "l.giftcertificate_count" VALUE = "{ l.giftcertificate_count + 1 }">
		<MvEVAL EXPR = "{ GiftCertificate_Read( l.giftcertificates[ l.giftcertificate_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "GiftCertificates" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "GiftCertificates">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-GFT-DTB-00014', l.giftcertificate_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificate_Load_ID" PARAMETERS = "id, giftcertificate var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "GiftCertificates"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'GiftCertificates WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00015', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ GiftCertificates.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "GiftCertificates">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-GFT-DTB-00016' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ GiftCertificate_Read( l.giftcertificate ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "GiftCertificates">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificate_Load_Code" PARAMETERS = "code, giftcertificate var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "GiftCertificates"
				QUERY	= "{ 'SELECT
								*
							  FROM
								' $ g.Store_Table_Prefix $ 'GiftCertificates
							  WHERE
							  	' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00017', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ GiftCertificates.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "GiftCertificates">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-GFT-DTB-00018' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ GiftCertificate_Read( l.giftcertificate ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "GiftCertificates">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificateList_Load_Order" PARAMETERS = "order_id, giftcertificates var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "GiftCertificates"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'GiftCertificates WHERE order_id = ?' }"
				FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00019', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT GiftCertificates.d.EOF }">
		<MvEVAL EXPR = "{ GiftCertificate_Read( l.giftcertificates[ ++l.count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "GiftCertificates" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "GiftCertificates">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-GFT-DTB-00020', l.count ) }">
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_GiftCertificateSales
|
</MvCOMMENT>

<MvFUNCTION NAME = "GiftCertificateSales_Read" PARAMETERS = "giftcertificatesales var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.giftcertificatesales:product_id"	VALUE = "{ GiftCertificateSales.d.product_id }">
	<MvASSIGN NAME = "l.giftcertificatesales:enabled"		VALUE = "{ GiftCertificateSales.d.enabled }">
	<MvASSIGN NAME = "l.giftcertificatesales:type"			VALUE = "{ GiftCertificateSales.d.type }">
	<MvASSIGN NAME = "l.giftcertificatesales:amount"		VALUE = "{ GiftCertificateSales.d.amount }">
	<MvASSIGN NAME = "l.giftcertificatesales:email_ship"	VALUE = "{ GiftCertificateSales.d.email_ship }">
	<MvASSIGN NAME = "l.giftcertificatesales:email_bill"	VALUE = "{ GiftCertificateSales.d.email_bill }">
	<MvASSIGN NAME = "l.giftcertificatesales:email_attr"	VALUE = "{ GiftCertificateSales.d.email_attr }">
	<MvASSIGN NAME = "l.giftcertificatesales:emlattcode"	VALUE = "{ GiftCertificateSales.d.emlattcode }">
	<MvASSIGN NAME = "l.giftcertificatesales:dscattcode"	VALUE = "{ GiftCertificateSales.d.dscattcode }">
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificateSales_Default" PARAMETERS = "product_id, giftcertificatesales var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.giftcertificatesales:product_id"	VALUE = "{ l.product_id }">
	<MvASSIGN NAME = "l.giftcertificatesales:enabled"		VALUE = 0>
	<MvASSIGN NAME = "l.giftcertificatesales:type"			VALUE = "price">
	<MvASSIGN NAME = "l.giftcertificatesales:amount"		VALUE = 0.00>
	<MvASSIGN NAME = "l.giftcertificatesales:email_ship"	VALUE = 1>
	<MvASSIGN NAME = "l.giftcertificatesales:email_bill"	VALUE = 0>
	<MvASSIGN NAME = "l.giftcertificatesales:email_attr"	VALUE = 0>
	<MvASSIGN NAME = "l.giftcertificatesales:emlattcode"	VALUE = "">
	<MvASSIGN NAME = "l.giftcertificatesales:dscattcode"	VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificateSales_Equal_Default" PARAMETERS = "giftcertificatesales var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.giftcertificatesales:enabled }">					<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ l.giftcertificatesales:type NE 'price' }">			<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ l.giftcertificatesales:amount NE 0 }">				<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ NOT l.giftcertificatesales:email_ship }">			<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ l.giftcertificatesales:email_bill }">				<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ l.giftcertificatesales:email_attr }">				<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ NOT ISNULL l.giftcertificatesales:emlattcode }">	<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ NOT ISNULL l.giftcertificatesales:dscattcode }">	<MvFUNCTIONRETURN VALUE = 0>	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificateSales_Insert" PARAMETERS = "giftcertificatesales var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'GiftCertificateSales
						  ( product_id, enabled, type, amount, email_ship, email_bill, email_attr, emlattcode, dscattcode )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.giftcertificatesales:product_id, l.giftcertificatesales:enabled, l.giftcertificatesales:type, l.giftcertificatesales:amount,
					   l.giftcertificatesales:email_ship, l.giftcertificatesales:email_bill, l.giftcertificatesales:email_attr,
					   l.giftcertificatesales:emlattcode, l.giftcertificatesales:dscattcode">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00021', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificateSales_Update" PARAMETERS = "giftcertificatesales var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'GiftCertificateSales
						  SET
							enabled			= ?,
							 type			= ?,
							 amount			= ?,
							 email_ship		= ?,
							 email_bill		= ?,
							 email_attr		= ?,
							 emlattcode		= ?,
							 dscattcode		= ?
						  WHERE
							product_id		= ?' }"
			 FIELDS	= "l.giftcertificatesales:enabled, l.giftcertificatesales:type, l.giftcertificatesales:amount, l.giftcertificatesales:email_ship,
					   l.giftcertificatesales:email_bill, l.giftcertificatesales:email_attr, l.giftcertificatesales:emlattcode, l.giftcertificatesales:dscattcode,
					   l.giftcertificatesales:product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00022', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificateSales_Delete_Product" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'GiftCertificateSales
						  WHERE
							product_id = ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00023', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificateSales_Load_Product" PARAMETERS = "product_id, giftcertificatesales var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "GiftCertificateSales"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'GiftCertificateSales WHERE product_id = ?' }"
				FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00024', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ GiftCertificateSales.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "GiftCertificateSales">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-GFT-DTB-00025' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ GiftCertificateSales_Read( l.giftcertificatesales ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "GiftCertificateSales">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificateSales_Load_ProductOrDefault" PARAMETERS = "product_id, giftcertificatesales var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ GiftCertificateSales_Load_Product( l.product_id, l.giftcertificatesales ) }">	<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">								<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvEVAL EXPR = "{ GiftCertificateSales_Default( l.product_id, l.giftcertificatesales ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GiftCertificateSalesAndOrderItemList_Load_Order" PARAMETERS = "order_id, giftcertificatesales var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "GiftCertificateSales"
				QUERY	= "{ 'SELECT
								gcs.*,
								oi.order_id		AS orderitem_order_id,
								oi.line_id		AS orderitem_line_id,
								oi.group_id		AS orderitem_group_id,
								oi.parent_id	AS orderitem_parent_id,
								oi.status		AS orderitem_status,
								oi.subscrp_id	AS orderitem_subscrp_id,
								oi.subterm_id	AS orderitem_subterm_id,
								oi.shpmnt_id	AS orderitem_shpmnt_id,
								oi.rma_id		AS orderitem_rma_id,
								oi.dt_instock	AS orderitem_dt_instock,
								oi.product_id	AS orderitem_product_id,
								oi.code			AS orderitem_code,
								oi.name			AS orderitem_name,
								oi.sku			AS orderitem_sku,
								oi.type			AS orderitem_type,
								oi.retail		AS orderitem_retail,
								oi.base_price	AS orderitem_base_price,
								oi.price		AS orderitem_price,
								oi.total		AS orderitem_total,
								oi.weight		AS orderitem_weight,
								oi.taxable		AS orderitem_taxable,
								oi.upsold		AS orderitem_upsold,
								oi.quantity		AS orderitem_quantity,
								oi.tax			AS orderitem_tax
							  FROM ' $
								g.Store_Table_Prefix $ 'GiftCertificateSales gcs, ' $
								g.Store_Table_Prefix $ 'OrderItems oi
							  WHERE
								oi.order_id = ? AND
								oi.product_id = gcs.product_id AND
								gcs.enabled = 1
							  ORDER BY oi.line_id' }"
				FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-GFT-DTB-00026', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.giftcertificatesales_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT GiftCertificateSales.d.EOF }">
		<MvASSIGN NAME = "l.giftcertificatesales_count" VALUE = "{ l.giftcertificatesales_count + 1 }">
		<MvEVAL EXPR = "{ GiftCertificateSales_Read( l.giftcertificatesales[ l.giftcertificatesales_count ] ) }">

		<MvREFERENCEARRAY NAME = "l.orderitem" VARIABLE = "l.giftcertificatesales">
			<MvDIMENSION INDEX = "{ l.giftcertificatesales_count }">
			<MvMEMBER NAME = "orderitem">
		</MvREFERENCEARRAY>

		<MvASSIGN NAME = "l.orderitem:order_id"		VALUE = "{ GiftCertificateSales.d.orderitem_order_id }">
		<MvASSIGN NAME = "l.orderitem:line_id"		VALUE = "{ GiftCertificateSales.d.orderitem_line_id }">
		<MvASSIGN NAME = "l.orderitem:group_id"		VALUE = "{ GiftCertificateSales.d.orderitem_group_id }">
		<MvASSIGN NAME = "l.orderitem:parent_id"	VALUE = "{ GiftCertificateSales.d.orderitem_parent_id }">
		<MvASSIGN NAME = "l.orderitem:status"		VALUE = "{ GiftCertificateSales.d.orderitem_status }">
		<MvASSIGN NAME = "l.orderitem:subscrp_id"	VALUE = "{ GiftCertificateSales.d.orderitem_subscrp_id }">
		<MvASSIGN NAME = "l.orderitem:subterm_id"	VALUE = "{ GiftCertificateSales.d.orderitem_subterm_id }">
		<MvASSIGN NAME = "l.orderitem:shpmnt_id"	VALUE = "{ GiftCertificateSales.d.orderitem_shpmnt_id }">
		<MvASSIGN NAME = "l.orderitem:rma_id"		VALUE = "{ GiftCertificateSales.d.orderitem_rma_id }">
		<MvASSIGN NAME = "l.orderitem:dt_instock"	VALUE = "{ GiftCertificateSales.d.orderitem_dt_instock }">
		<MvASSIGN NAME = "l.orderitem:product_id"	VALUE = "{ GiftCertificateSales.d.orderitem_product_id }">
		<MvASSIGN NAME = "l.orderitem:code"			VALUE = "{ GiftCertificateSales.d.orderitem_code }">
		<MvASSIGN NAME = "l.orderitem:name"			VALUE = "{ GiftCertificateSales.d.orderitem_name }">
		<MvASSIGN NAME = "l.orderitem:sku"			VALUE = "{ GiftCertificateSales.d.orderitem_sku }">
		<MvASSIGN NAME = "l.orderitem:type"			VALUE = "{ GiftCertificateSales.d.orderitem_type }">
		<MvASSIGN NAME = "l.orderitem:retail"		VALUE = "{ GiftCertificateSales.d.orderitem_retail }">
		<MvASSIGN NAME = "l.orderitem:base_price"	VALUE = "{ GiftCertificateSales.d.orderitem_base_price }">
		<MvASSIGN NAME = "l.orderitem:price"		VALUE = "{ GiftCertificateSales.d.orderitem_price }">
		<MvASSIGN NAME = "l.orderitem:total"		VALUE = "{ GiftCertificateSales.d.orderitem_total }">
		<MvASSIGN NAME = "l.orderitem:weight"		VALUE = "{ GiftCertificateSales.d.orderitem_weight }">
		<MvASSIGN NAME = "l.orderitem:taxable"		VALUE = "{ GiftCertificateSales.d.orderitem_taxable }">
		<MvASSIGN NAME = "l.orderitem:upsold"		VALUE = "{ GiftCertificateSales.d.orderitem_upsold }">
		<MvASSIGN NAME = "l.orderitem:quantity"		VALUE = "{ GiftCertificateSales.d.orderitem_quantity }">
		<MvASSIGN NAME = "l.orderitem:tax"			VALUE = "{ GiftCertificateSales.d.orderitem_tax }">

		<MvSKIP NAME = "Merchant" VIEW = "GiftCertificateSales" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "GiftCertificateSales">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-GFT-DTB-00027', l.giftcertificatesales_count ) }">
</MvFUNCTION>

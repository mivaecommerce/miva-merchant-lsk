<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2025 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-AI-JSON-
| Next Error Code: 30   
|
</MvCOMMENT>

<MvFUNCTION NAME = "JSON_AISearchIndex" PARAMETERS = "aisearchindex var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ JSON_AISearchIndex_OnDemandColumns( l.aisearchindex, l.null ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndex_OnDemandColumns" PARAMETERS = "aisearchindex var, ondemandcolumns var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	"id":			<MvEVAL EXPR = "{ encodejavascriptnumber( l.aisearchindex:id ) }">,
	"active":		<MvEVAL EXPR = "{ encodejavascriptboolean( l.aisearchindex:active ) }">,
	"code":			"<MvEVAL EXPR = "{ encodejavascriptstring( l.aisearchindex:code ) }">",
	"source":		"<MvEVAL EXPR = "{ encodejavascriptstring( l.aisearchindex:source ) }">",
	"max_res":		<MvEVAL EXPR = "{ encodejavascriptnumber( l.aisearchindex:max_res ) }">,
	"specific_t":	<MvEVAL EXPR = "{ encodejavascriptnumber( l.aisearchindex:specific_t ) }">,
	"specific_e":	<MvEVAL EXPR = "{ encodejavascriptnumber( l.aisearchindex:specific_e ) }">,
	"broad_e":		<MvEVAL EXPR = "{ encodejavascriptnumber( l.aisearchindex:broad_e ) }">,
	"sparse_n":		<MvEVAL EXPR = "{ encodejavascriptnumber( l.aisearchindex:sparse_n ) }">,
	"blend_rel":	<MvEVAL EXPR = "{ encodejavascriptboolean( l.aisearchindex:blend_rel ) }">,
	"idx_templ":	"<MvEVAL EXPR = "{ encodejavascriptstring( l.aisearchindex:idx_templ ) }">"

	<MvIF EXPR = "{ l.ondemandcolumns:count_indexed }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_AI_DB ].AISearchIndexXProduct_Count_Indexed( l.aisearchindex:id, l.aisearchindex:count_indexed ) }">

		, "count_indexed": <MvEVAL EXPR = "{ encodejavascriptnumber( l.aisearchindex:count_indexed ) }">
	</MvIF>

	<MvIF EXPR = "{ l.ondemandcolumns:count_queued }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_AI_DB ].AISearchIndexXProduct_Count_Queued( l.aisearchindex:id, l.aisearchindex:count_queued ) }">

		, "count_queued": <MvEVAL EXPR = "{ encodejavascriptnumber( l.aisearchindex:count_queued ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndex_Load" PARAMETERS = "index var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_JSON ].JSON_Input_Retrieve_Integer( 'AISearchIndex_ID', l.index_id ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_DB ].AISearchIndex_Load_ID( l.index_id, l.index ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AI-JSON-00005', 'AI Search Index not found' ) }">
		</MvIF>
	<MvELSEIF EXPR = "{ [ g.Module_JSON ].JSON_Input_Retrieve( 'Edit_AISearchIndex', l.edit_code ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_DB ].AISearchIndex_Load_Code( l.edit_code, l.index ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AI-JSON-00006', 'AI Search Index not found' ) }">
		</MvIF>
	<MvELSEIF EXPR = "{ [ g.Module_JSON ].JSON_Input_Retrieve( 'AISearchIndex_Code', l.index_code ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_DB ].AISearchIndex_Load_Code( l.index_code, l.index ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AI-JSON-00007', 'AI Search Index not found' ) }">
		</MvIF>
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AI-JSON-00008', 'Unable to load AI Search Index: One of AISearchIndex_ID, Edit_AISearchIndex, or AISearchIndex_Code must be specified' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AIGenerateText" PARAMETERS = "" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }"> <MvFUNCTIONRETURN> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'Prompt',			l.prompt )	OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Content',			l.content )	OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 'GenerateAsHTML',	l.generate_as_html ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Feature_Filename_AI_DB ].AISettings_Load( l.aisettings ) }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT l.aisettings:enabled }">										<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-AI-JSON-00001', 'AI generation is disabled' ) }">
	</MvIF>

	<MvCAPTURE VARIABLE = "l.data">
	{
		"model": "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.aisettings:engine ) }">",
		"messages":
		[
			{
				"role":		"user",
				"content":	"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.prompt ) }">"
			},
			{
				"role":		"user",
				"content":	"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.content ) }">"
			}

			<MvIF EXPR = "{ l.generate_as_html }">
				,
				{
					"role":		"user",
					"content":	"The generated result should be returned in HTML format"
				}
			</MvIF>
		]
	}
	</MvCAPTURE>

	<MIVA MvCALL_Error = "nonfatal, nodisplay">
	<MvCALL METHOD			= "RAW"
			CONTENT-TYPE	= "application/json"
			ACTION			= "https://api.openai.com/v1/chat/completions"
			HEADERS			= "{ 'Authorization: Bearer ' $ [ g.Module_Library_Crypto ].Decrypt_Payment_Password( l.aisettings:token ) $ asciichar( 13 ) $ asciichar( 10 ) }"
			FIELDS			= "l.data"
			FLAGS			= "force_verify, noparse">
		<MvASSIGN NAME = "l.http_response_status"	VALUE = "{ gettoken( s.callreturnheader[ 1 ], ' ', 2 ) }">
		<MvASSIGN NAME = "l.response_json"			VALUE = "{ s.callvalue }">
	</MvCALL>
	<MIVA MvCALL_Error = "fatal, display">

	<MvIF EXPR = "{ g.MvCALL_ERROR }">											<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-AI-JSON-00002', g.MvCALL_ERROR ) }">
	<MvELSEIF EXPR = "{ NOT miva_json_decode( l.response_json, l.response ) }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-AI-JSON-00003', miva_json_decode_last_error() ) }">
	<MvELSEIF EXPR = "{ NOT ISNULL l.response:error:message }">					<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-AI-JSON-00004', l.response:error:message ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		"content": "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.response:choices[ 1 ]:message:content ) }">"
	}
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndexList_Load_Query" PARAMETERS = "" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN>																</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'STOR', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN VALUE ="{ [ g.Module_JSON ].JSON_Response_Access_Denied() }"> </MvIF>

	<MvASSIGN NAME = "l.search_query"		VALUE = "">
	<MvASSIGN NAME = "l.available_filters"	VALUE = "">
	<MvASSIGN NAME = "l.data"				VALUE = "">
	<MvASSIGN NAME = "l.filter"				VALUE = "">
	<MvASSIGN NAME = "l.sort"				VALUE = "">
	<MvASSIGN NAME = "l.offset"				VALUE = 0>
	<MvASSIGN NAME = "l.count"				VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_INTEGER( l.available_filters,	'id',			'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(	  l.available_filters, 	'code',			'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_CHAR(	  l.available_filters, 	'source',		'' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_BOOL(	  l.available_filters,	'active',		'' ) }">

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Filter(	'o', 'Filter',	l.filter )	OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Sort',	l.sort )	OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'o', 'Offset',	l.offset )	OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'o', 'Count',	l.count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, '*' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'AISearchIndexes', '' ) }">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter_Callback( l.search_query, l.filter, l.available_filters, g.Module_Feature_AI_JSON, 'JSON_AISearchIndexList_Load_Query_Filter', '', l.data ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_OrderBy( l.search_query, l.sort, 'id,code,source,active', 'code' ) }">

	<MvASSIGN NAME = "l.search_sql" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Count( l.search_query, l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'AISearchIndexes', l.search_sql, l.search_fields, l.offset, l.count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-AI-JSON-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.index_count" VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		"total_count": <MvEVAL EXPR = "{ encodejavascriptnumber( l.total_count ) }">,
		"start_offset": <MvEVAL EXPR = "{ encodejavascriptnumber( l.offset ) }">,
		"data":
		[
		<MvWHILE EXPR = "{ ( NOT AISearchIndexes.d.EOF ) AND ( ( l.count EQ 0 ) OR ( l.index_count LT l.count ) ) }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_AI_DB ].AISearchIndex_Read( l.index ) }">
			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.index_count ) }">
				<MvEVAL EXPR = "{ JSON_AISearchIndex_OnDemandColumns( l.index, l.data:ondemandcolumns ) }">
			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">

			<MvSKIP NAME = "Merchant" VIEW = "AISearchIndexes" ROWS = 1>
		</MvWHILE>
		]
	}
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AISearchIndexes">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndexList_Load_Query_Filter" PARAMETERS = "query var, field_count var, filter_name, filter_value, data var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.filter_name EQ 'ondemandcolumns' }">
		<MvFOREACH ITERATOR = "l.column_code" ARRAY = "l.filter_value">
			<MvIF EXPR = "{ ISNULL l.column_code }">
				<MvFOREACHCONTINUE>
			</MvIF>

			<MvASSIGN NAME = "l.data:ondemandcolumns" MEMBER = "{ l.column_code }" VALUE = 1>
		</MvFOREACH>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndex_Validate" PARAMETERS = "index var, embedding_source var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Feature_AI_DB ].AISearchIndex_Load_Code( l.index:code, l.existing_index ) AND
					l.index:id NE l.existing_index:id }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Code', 'An AI Search Index with the code \'' $ l.existing_index:code $ '\' already exists' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Range( l.index:specific_t, 0, 100 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'SpecificThreshold', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Range( l.index:specific_e, 0, 100 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'SpecificEndpoint', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Range( l.index:broad_e, 0, 100 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'BroadEndpoint', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Validate_Compilation( l.index:idx_templ ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Template', g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_UT ].MARS_Call_Embeddings( l.embedding_sources ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT miva_array_search( l.embedding_sources, 1, l.found_embedding_source, 'strcasecmp( l.found_embedding_source:code, l.index:source ) EQ 0' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Source', 'Invalid source' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.embedding_source" VALUE = "{ l.found_embedding_source }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndex_Insert" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN>																</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'STOR', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN VALUE ="{ [ g.Module_JSON ].JSON_Response_Access_Denied() }"> </MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Feature_AI_DB ].AISearchIndex_Defaults( l.index ) }">

	<MvASSIGN NAME = "l.embedding_source"	VALUE = "">

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Code(		'R',		'Code',						l.index:code )				OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R:50',		'Source',					l.index:source )			OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o',		'Template',					l.index:idx_templ )			OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'O',		'Active',					l.index:active )			OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'O:nonneg',	'MaximumResults',			l.index:max_res )			OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'O',		'SpecificThreshold',		l.index:specific_t,	7, 4 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'O',		'SpecificEndpoint',			l.index:specific_e,	7, 4 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'O',		'BroadEndpoint',			l.index:broad_e,	7, 4 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'O:nonneg',	'SparseResults',			l.index:sparse_n )			OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'O',		'BlendRelationalResults',	l.index:blend_rel ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT JSON_AISearchIndex_Validate( l.index, l.embedding_source ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_UT ].AISearchIndex_Template_Compile( l.index ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_DB ].AISearchIndex_Insert( l.index ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.relationalfields"			VALUE = "">
	<MvASSIGN NAME = "l.relationalfields:index_id"	VALUE = "{ l.index:id }">

	<MvEVAL EXPR = "{ [ g.Module_Feature_AI_DB ].AISearchIndexRelationalFields_Default( l.relationalfields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_DB ].AISearchIndexRelationalFields_Insert( l.relationalfields ) }">
		<MvASSIGN NAME = "l.error_code"		VALUE = "{ g.Error_Code }">
		<MvASSIGN NAME = "l.error_message"	VALUE = "{ g.Error_Message }">
		<MvASSIGN NAME = "l.null"			VALUE = "{ [ g.Module_Feature_AI_DB ].AISearchIndex_Delete( l.index ) }">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( l.error_code, l.error_message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_UT ].AISearchIndex_Redis_Create_Index( l.index, l.embedding_source ) }">
		<MvASSIGN NAME = "l.error_code"		VALUE = "{ g.Error_Code }">
		<MvASSIGN NAME = "l.error_message"	VALUE = "{ g.Error_Message }">
		<MvASSIGN NAME = "l.null"			VALUE = "{ [ g.Module_Feature_AI_DB ].AISearchIndex_Delete( l.index ) }">

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( l.error_code, l.error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_SCH_UT ].ScheduledTask_Trigger( 'aisearchindex' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-AI-JSON-00011', 'AI Search Index \'' $ l.index:code $ '\' created' ) }">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		<MvEVAL EXPR = "{ JSON_AISearchIndex( l.index ) }">
	}
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndex_Update" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN>																</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'STOR', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN VALUE ="{ [ g.Module_JSON ].JSON_Response_Access_Denied() }"> </MvIF>

	<MvIF EXPR = "{ NOT JSON_AISearchIndex_Load( l.index ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.original_index"		VALUE = "{ l.index }">
	<MvASSIGN NAME = "l.embedding_source"	VALUE = "">

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Code(		'O',		'Code',						l.index:code )				OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'O:50',		'Source',					l.index:source )			OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o',		'Template',					l.index:idx_templ )			OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'O',		'Active',					l.index:active )			OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'O:nonneg',	'MaximumResults',			l.index:max_res )			OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'O',		'SpecificThreshold',		l.index:specific_t,	7, 4 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'O',		'SpecificEndpoint',			l.index:specific_e,	7, 4 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'O',		'BroadEndpoint',			l.index:broad_e,	7, 4 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'O:nonneg',	'SparseResults',			l.index:sparse_n )			OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'O',		'BlendRelationalResults',	l.index:blend_rel ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT JSON_AISearchIndex_Validate( l.index, l.embedding_source ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_UT ].AISearchIndex_Template_Compile( l.index ) }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Feature_AI_DB ].AISearchIndex_Update( l.index ) }">			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ strcasecmp( l.original_index:source, l.index:source ) NE 0 }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_UT ].AISearchIndex_Redis_Index_Recreate( l.index, l.embedding_source ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ strcasecmp( l.original_index:code, l.index:code ) NE 0 }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_UT ].AISearchIndex_Template_Delete( l.original_index ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_AISearchIndex_Change( 'MER-AI-JSON-00013', 'AI Search Index \'' $ l.index:code $ '\' updated', l.original_index, l.index ) }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndexList_Delete" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN>																</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'STOR', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN VALUE ="{ [ g.Module_JSON ].JSON_Response_Access_Denied() }"> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_SEA_DB ].SearchSettings_Load( l.searchsettings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Integer_Array( 'R', 'AISearchIndex_IDs', l.index_ids, l.index_id_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvASSIGN NAME = "l.processed" 		VALUE = 0>
	<MvASSIGN NAME = "l.error_count" 	VALUE = 0>
	<MvASSIGN NAME = "l.errors" 		VALUE = "">
	<MvASSIGN NAME = "l.start_time" 	VALUE = "{ s.dyn_time_t }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_SEA_UT ].SearchSettings_Load_Notify( l.notify_original_searchsettings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.notify_original_searchablecustomfield_count" VALUE = "{ [ g.Module_Feature_SEA_UT ].SearchableCustomFieldList_Load_Notify( l.notify_original_searchablecustomfields ) }">

	<MvFOREACH ITERATOR = "l.index_id" ARRAY = "l.index_ids" COUNT = "{ l.index_id_count }">
		<MvASSIGN NAME = "l.processed" 	VALUE = "{ l.processed + 1 }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_DB ].AISearchIndex_Load_ID( l.index_id, l.index ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_UT ].AISearchIndex_Template_Delete( l.index ) }">	<MvASSIGN NAME = "l.error_count" VALUE = "{ [ g.Module_JSON ].JSON_Response_ListProcessed_Error( l.index_id, g.Error_Code, g.Error_Message, l.errors ) }">
			<MvELSEIF EXPR = "{ NOT [ g.Module_Feature_AI_DB ].AISearchIndex_Delete( l.index ) }">		<MvASSIGN NAME = "l.error_count" VALUE = "{ [ g.Module_JSON ].JSON_Response_ListProcessed_Error( l.index_id, g.Error_Code, g.Error_Message, l.errors ) }">
			<MvELSE>
				<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_AI_UT ].AISearchIndex_Redis_Delete_Index( l.index ) }">

				<MvIF EXPR = "{ l.index:id EQ l.searchsettings:index_id }">
					<MvASSIGN NAME = "l.searchsettings:index_id" VALUE = 0>

					<MvIF EXPR = "{ NOT [ g.Module_Feature_SEA_DB ].SearchSettings_Update( l.searchsettings ) }">
						<MvASSIGN NAME = "l.error_count" VALUE = "{ [ g.Module_JSON ].JSON_Response_ListProcessed_Error( l.index_id, g.Error_Code, g.Error_Message, l.errors ) }">
					</MvIF>
				</MvIF>

				<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-AI-JSON-00015', 'AI Search Index \'' $ l.index:code $ '\' deleted' ) }">
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ ( s.dyn_time_t - l.start_time ) GE 60 }">								<MvFOREACHSTOP>
		<MvELSEIF EXPR = "{ ( s.dyn_time_remaining GE 0 ) AND ( s.dyn_time_remaining LE 3 ) }">	<MvFOREACHSTOP>
		</MvIF>
	</MvFOREACH>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_SEA_UT ].SearchSettings_Load_Notify( l.notify_searchsettings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.notify_searchablecustomfield_count" VALUE = "{ [ g.Module_Feature_SEA_UT ].SearchableCustomFieldList_Load_Notify( l.notify_searchablecustomfields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_SEA_UT ].SearchSettings_Notify_Modules( l.notify_original_searchsettings, l.notify_original_searchablecustomfields, l.notify_original_searchablecustomfield_count, l.notify_searchsettings, l.notify_searchablecustomfields, l.notify_searchablecustomfield_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ l.error_count }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_ListProcessed_Errors( l.processed, l.errors ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_ListProcessed_Success( l.processed ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndexEmbeddingList_Load" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN>																</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'STOR', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN VALUE ="{ [ g.Module_JSON ].JSON_Response_Access_Denied() }"> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_UT ].MARS_Call_Embeddings( l.embeddings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.embedding_count" VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		"data":
		[
		<MvFOREACH ITERATOR = "l.embedding" ARRAY = "l.embeddings">				
			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.embedding_count ) }">
			"code":			"<MvEVAL EXPR = "{ encodejavascriptstring( l.embedding:code ) }">",
			"model":		"<MvEVAL EXPR = "{ encodejavascriptstring( l.embedding:model ) }">",
			"dimensions":	"<MvEVAL EXPR = "{ encodejavascriptnumber( l.embedding:dimensions ) }">",
			"max_tokens":	"<MvEVAL EXPR = "{ encodejavascriptnumber( l.embedding:max_tokens ) }">"
			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
		</MvFOREACH>
		],
		"total_count": <MvEVAL EXPR = "{ int( l.embedding_count ) }">
	}

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndex_Rebuild" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN>																</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'STOR', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN VALUE ="{ [ g.Module_JSON ].JSON_Response_Access_Denied() }"> </MvIF>

	<MvIF EXPR = "{ NOT JSON_AISearchIndex_Load( l.index ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_UT ].MARS_Call_Embeddings( l.embeddings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT miva_array_search( l.embeddings, 1, l.embedding, 'strcasecmp( l.embedding:code, l.index:source ) EQ 0' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-AI-JSON-00016', 'Invalid source' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_UT ].AISearchIndex_Redis_Index_Recreate( l.index, l.embedding ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ l.index:active }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_DB ].AISearchIndexXProduct_Queue_Active( l.index:id ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>

		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_SCH_UT ].ScheduledTask_Trigger( 'aisearchindex' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MER-AI-JSON-00017', 'AI Search Index \'' $ l.index:code $ '\' rebuilt' ) }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ProductList_Load_AISearch_Filter" PARAMETERS = "query var, field_count var, filter_name, filter_value, data var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.filter_name EQ 'ondemandcolumns' }">
		<MvFOREACH ITERATOR = "l.column_code" ARRAY = "l.filter_value">
			<MvIF EXPR = "{ ISNULL l.column_code }">
				<MvFOREACHCONTINUE>
			</MvIF>

			<MvIF EXPR = "{ ( 'imagetype:' CIN l.column_code ) NE 1 }">
				<MvASSIGN NAME = "l.data:ondemandcolumns" MEMBER = "{ l.column_code }" VALUE = 1>
			<MvELSEIF EXPR = "{ [ g.Module_Admin ].CanI( 'IMGS', 1, 0, 0, 0 ) }">
				<MvASSIGN NAME = "l.imagetype_code" VALUE = "{ substring_var( l.column_code, 11, len_var( l.column_code ) - 10 ) }">

				<MvIF EXPR = "{ NOT miva_array_search( l.data:ondemandcolumns:imagetypes, 1, l.search_imagetype, 'strcasecmp( l.search_imagetype:code, l.imagetype_code ) EQ 0' ) }">
					<MvIF EXPR = "{ [ g.Module_Library_DB ].ImageType_Load_Code( l.imagetype_code, l.imagetype ) }">
						<MvASSIGN NAME = "l.data:ondemandcolumns:imagetype_count" VALUE = "{ miva_array_insert_var( l.data:ondemandcolumns:imagetypes, l.imagetype, -1 ) }">
					</MvIF>
				</MvIF>
			</MvIF>
		</MvFOREACH>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ProductList_Load_AISearch" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">							<MvFUNCTIONRETURN>																</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'STOR', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN VALUE ="{ [ g.Module_JSON ].JSON_Response_Access_Denied() }"> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'PROD', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN VALUE ="{ [ g.Module_JSON ].JSON_Response_Access_Denied() }"> </MvIF>

	<MvIF EXPR = "{ NOT JSON_AISearchIndex_Load( l.index ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.filter"			VALUE = "">
	<MvASSIGN NAME = "l.count"			VALUE = 1000>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Filter(	'o', 'Filter',					l.filter )	OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'o', 'Count',					l.count )	OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'Search',					l.search )	OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'O', 'BlendRelationalResults',	l.index:blend_rel ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter_Callback( l.null, l.filter, '', g.Module_Feature_AI_JSON, 'JSON_ProductList_Load_AISearch_Filter', '', l.data ) }">

	<MvCOMMENT>
	|
	| Load Relational Results
	|
	</MvCOMMENT>
	
	<MvASSIGN NAME = "l.relational_product_count" VALUE = "{ JSON_ProductList_Load_AISearch_Relational( l.index, l.search, l.count, l.relational_products ) }">
	<MvIF EXPR = "{ l.relational_product_count EQ 0 }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvCOMMENT>
	|
	| Load AI Results
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.inputs" INDEX = 1 VALUE = "{ l.search }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_UT ].MARS_Call_Embeddings_Query( l.index, l.inputs, l.embeddings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.options"										VALUE = "">
	<MvASSIGN NAME = "l.options:sortby"									VALUE = "vector_score">
	<MvASSIGN NAME = "l.options:sortby_direction"						VALUE = "ASC">
	<MvASSIGN NAME = "l.options:dialect"								VALUE = 2>
	<MvASSIGN NAME = "l.options:limit_offset"							VALUE = 0>
	<MvASSIGN NAME = "l.options:limit_num"								VALUE = "{ l.count }">

	<MvASSIGN NAME = "l.options:params:query_vector"					VALUE = "{ redis_pack_f32( l.embeddings[ 1 ] ) }">

	<MvASSIGN NAME = "l.options:return"	INDEX = 1 MEMBER = "identifier"	VALUE = "vector_score">

	<MvASSIGN NAME = "l.query"											VALUE = "{ '(*) => [KNN ' $ int( l.count ) $ ' @' $ int( l.index:id ) $ ' $query_vector AS vector_score]' }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_UT ].AISearchIndex_Redis_Search_Index( l.index, l.query, l.options, l.results ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.ai_product_count"			VALUE = 0>
	<MvASSIGN NAME = "l.result_count"				VALUE = "{ miva_array_elements( l.results ) }">

	<MvFOR INDEX = "l.pos" FIRST = "{ 2 }" LAST = "{ l.result_count }" NEXT = "{ l.pos + 2 }">
		<MvASSIGN NAME = "l.product_id"				VALUE = "{ gettoken( l.results[ l.pos ], ':', 2 ) }">
		<MvASSIGN NAME = "l.distance"				VALUE = "{ l.results[ l.pos + 1 ][ 2 ] }">

		<MvIF EXPR = "{ miva_array_search( l.relational_products, 1, l.relational_product, 'l.relational_product:id EQ l.product_id' ) }">
			<MvFORCONTINUE>
		<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_ID_WithRuntimeInventory( l.product_id, l.product ) }">
			<MvFORCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.product:similarity"								VALUE = "{ ( ( 1 - l.distance ) * 100 ) ROUND 4 }">
		<MvASSIGN NAME = "l.ai_products" INDEX = "{ ++l.ai_product_count }"	VALUE = "{ l.product }">

		<MvIF EXPR = "{ l.relational_product_count + l.ai_product_count EQ l.count }">
			<MvFORSTOP>
		</MvIF>
	</MvFOR>

	<MvCOMMENT>
	|
	| Output both arrays
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">	
	{
		"relational_results":
		[
			<MvASSIGN NAME = "l.output_product_count" VALUE = 0>
			<MvFOREACH ITERATOR = "l.relational_product" ARRAY = "l.relational_products" COUNT = "{ l.relational_product_count }">
				<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.output_product_count ) }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Product_OnDemandColumns( l.relational_product, l.data:ondemandcolumns ) }">
				<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
			</MvFOREACH>
		],
		"ai_results":
		[
			<MvASSIGN NAME = "l.output_product_count" VALUE = 0>
			<MvFOREACH ITERATOR = "l.ai_product" ARRAY = "l.ai_products" COUNT = "{ l.ai_product_count }">
				<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.output_product_count ) }">
					"similarity": <MvEVAL EXPR = "{ encodejavascriptnumber( l.ai_product:similarity ) }">,
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Product_OnDemandColumns( l.ai_product, l.data:ondemandcolumns ) }">
				<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
			</MvFOREACH>
		]
	}
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ProductList_Load_AISearch_Relational" PARAMETERS = "index var, search, count, products var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| Relational search may be disabled, in which case we do nothing
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT l.index:blend_rel }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-AI-JSON-00029' ) }">
	</MvIF>
	
	<MvCOMMENT>
	|
	| Likewise, if enabled at the index level but no fields are enabled, we cannot perform
	| a relational search.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_AI_DB ].AISearchIndexRelationalFields_Load_Index_Cached( l.index:id, l.relationalfields ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.customfield_count"	VALUE = "{ [ g.Module_Feature_SEA_UT ].SearchableCustomFieldList_Load_AISearchIndex_With_Capabilities_Cached( l.index, l.customfields ) }">

	<MvIF EXPR = "{ NOT l.relationalfields:code			AND
					NOT l.relationalfields:name			AND
					NOT l.relationalfields:sku			AND
					NOT l.relationalfields:descrip		AND
					NOT l.relationalfields:atr_code		AND
					NOT l.relationalfields:atr_prompt	AND
					NOT l.relationalfields:opt_code		AND
					NOT l.relationalfields:opt_prompt }">
		<MvIF EXPR = "{ NOT miva_array_search( l.customfields, 1, l.customfield, 'l.customfield:enabled' ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-AI-JSON-00028' ) }">
		</MvIF>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query_SELECT_FROM( l.query ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query_SELECT_Inventory( l.query ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Runtime_ProductList_Build_Query_AISearchIndex_Relational( l.query, l.search, l.index ) }">

	<MvASSIGN NAME = "l.sql" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.query, l.fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'Products',
																			 l.sql, l.fields,
																			 0, l.count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AI-JSON-00026', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.products"		VALUE = "">
	<MvASSIGN NAME = "l.product_count"	VALUE = 0>

	<MvWHILE EXPR = "{ NOT Products.d.EOF }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Product_Read( l.products[ ++l.product_count ] ) }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_INV_RT ].Runtime_Product_InventoryFields_Read( l.products[ l.product_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Products" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Products">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-AI-JSON-00027', l.product_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndexRelationalField" PARAMETERS = "field var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	"field_code":	"<MvEVAL EXPR = "{ encodejavascriptstring( l.field:field_code ) }">",
	"field_name":	"<MvEVAL EXPR = "{ encodejavascriptstring( l.field:field_name ) }">",
	"enabled":		<MvEVAL EXPR = "{ encodejavascriptboolean( l.field:enabled ) }">,
	"searchtype":	"<MvEVAL EXPR = "{ encodejavascriptstring( l.field:searchtype ) }">",
	"relevance":	<MvEVAL EXPR = "{ encodejavascriptboolean( l.field:relevance ) }">,
	"weight":		<MvEVAL EXPR = "{ encodejavascriptnumber( l.field:weight ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndexRelationalCustomFieldModule_Capabilities" PARAMETERS = "capabilities var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	"search_fulltext":		<MvEVAL EXPR = "{ encodejavascriptboolean( l.capabilities:search_fulltext ) }">,
	"orderby_relevance":	<MvEVAL EXPR = "{ encodejavascriptboolean( l.capabilities:orderby_relevance ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndex_SearchableField_Load" PARAMETERS = "index var, searchablefield var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_JSON ].JSON_Input_Retrieve( 'Field_Code', l.field_code ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_SEA_UT ].SearchableField_Load_AISearchIndex_Builtin( l.index, l.field_code, l.searchablefield ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AI-JSON-00018', 'Searchable field not found' ) }">
		</MvIF>
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AI-JSON-00019', 'Unable to load searchable field: Field_Code must be specified' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndex_SearchableCustomField_Load" PARAMETERS = "index var, searchablecustomfield var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_JSON ].JSON_Input_Retrieve_Integer(	'Module_ID',	l.module_id ) AND
					[ g.Module_JSON ].JSON_Input_Retrieve(			'Field_Code',	l.field_code ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_SEA_UT ].SearchableField_Load_AISearchIndex_CustomField( l.index, l.module_id, l.field_code, l.searchablecustomfield ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AI-JSON-00020', 'Searchable custom field not found' ) }">
		</MvIF>
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-AI-JSON-00021', 'Unable to load searchable custom field: Module_ID and Field_Code must be specified' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndexRelationalFieldList_Load_Query" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'STOR', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">	</MvIF>

	<MvIF EXPR = "{ NOT JSON_AISearchIndex_Load( l.index ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.available_filters"				VALUE = "">
	<MvASSIGN NAME = "l.filter"							VALUE = "">
	<MvASSIGN NAME = "l.sort"							VALUE = "">
	<MvASSIGN NAME = "l.offset"							VALUE = 0>
	<MvASSIGN NAME = "l.count"							VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_MivaScript_CHAR(	l.available_filters, 'field_code',	'l.filtered_field:field_code' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_MivaScript_CHAR(	l.available_filters, 'field_name',	'l.filtered_field:field_name' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_MivaScript_BOOL(	l.available_filters, 'enabled',		'l.filtered_field:enabled' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_MivaScript_CHAR(	l.available_filters, 'searchtype',	'l.filtered_field:searchtype' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_MivaScript_BOOL(	l.available_filters, 'relevance',	'l.filtered_field:relevance' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_MivaScript_NUMBER(	l.available_filters, 'weight',		'l.filtered_field:weight' ) }">
	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_AvailableFilter_MivaScript_CHAR(	l.available_filters, 'module',		'l.filtered_field:module:code' ) }">

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Filter(	'o',		'Filter',	l.filter )	OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o',		'Sort',		l.sort )	OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'o:nonneg', 'Offset',	l.offset )	OR
					NOT [ g.Module_JSON ].JSON_Input_Integer(	'o', 		'Count',	l.count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvASSIGN NAME = "l.filtered_fields"				VALUE = "">
	<MvASSIGN NAME = "l.filtered_field_count"			VALUE = 0>

	<MvASSIGN NAME = "l.searchablefield_count"			VALUE = "{ [ g.Module_Feature_SEA_UT ].SearchableFieldList_Load_All_AISearchIndex_Builtins( l.index, l.searchablefields ) }">
	<MvASSIGN NAME = "l.searchablecustomfield_count"	VALUE = "{ [ g.Module_Feature_SEA_UT ].SearchableFieldList_Load_All_AISearchIndex_CustomFields( l.index, l.searchablecustomfields ) }">

	<MvASSIGN NAME = "l.filtered_field_count"			VALUE = "{ miva_array_merge_ref( l.searchablefields, 1, l.searchablefield_count, l.filtered_fields, -1 ) }">
	<MvASSIGN NAME = "l.filtered_field_count"			VALUE = "{ miva_array_merge_ref( l.searchablecustomfields, 1, l.searchablecustomfield_count, l.filtered_fields, -1 ) }">

	<MvFOREACH ITERATOR = "l.filtered_field" ARRAY = "l.filtered_fields" COUNT = "{ l.filtered_field_count }">
		<MvCOMMENT>
		|
		| Round decimal columns to their final length so that LIKE/NOTLIKE operations in JSON_Filter_MivaScript operate on the
		| same data the user sees.
		|
		</MvCOMMENT>

		<MvASSIGN NAME = "l.filtered_field:weight"		VALUE = "{ l.filtered_field:weight ROUND 2 }">
	</MvFOREACH>

	<MvASSIGN NAME = "l.search_expression"				VALUE = "1 EQ 1">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter_MivaScript( l.search_expression, l.filter, l.available_filters ) }">

	<MvASSIGN NAME = "l.total_count"					VALUE = "{ miva_array_filter_ref( l.filtered_fields, 1, l.filtered_field, l.search_expression, l.filtered_array ) }">
	<MvASSIGN NAME = "l.null"							VALUE = "{ miva_array_sort( l.filtered_array, 'JSON_AISearchIndexRelationalFieldList_Sort_Callback', l.sort ) }">

	<MvASSIGN NAME = "l.output_count" 					VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		"data":
		[
			<MvFOREACH ITERATOR = "l.filtered_field" ARRAY = "l.filtered_array" FIRST = "{ l.offset + 1 }" LAST = "{ l.total_count }">
				<MvIF EXPR = "{ ( l.output_count LT l.count ) OR ( l.count EQ 0 ) }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.output_count ) }">
						<MvEVAL EXPR = "{ JSON_AISearchIndexRelationalField( l.filtered_field ) }">

						<MvIF EXPR = "{ l.filtered_field:module:id }">
							, "module":
							{
								<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Module( l.filtered_field:module ) }">
							},
							"capabilities":
							{
								<MvEVAL EXPR = "{ JSON_AISearchIndexRelationalCustomFieldModule_Capabilities( l.filtered_field:capabilities ) }">
							}
						</MvIF>
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
				</MvIF>
			</MvFOREACH>
		],
		"total_count": 	<MvEVAL EXPR = "{ encodejavascriptnumber( l.total_count ) }">,
		"start_offset": <MvEVAL EXPR = "{ encodejavascriptnumber( l.offset ) }">
	}
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndexRelationalFieldList_Sort_Callback" PARAMETERS = "left var, right var, sort" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.result" VALUE = 0>

	<MvIF EXPR = "{ l.sort EQ 'field_code' }">		<MvASSIGN NAME = "l.result"	VALUE = "{ strcasecmp( l.left:field_code, l.right:field_code ) }">
	<MvELSEIF EXPR = "{ l.sort EQ '-field_code' }">	<MvASSIGN NAME = "l.result"	VALUE = "{ strcasecmp( l.right:field_code, l.left:field_code ) }">
	<MvELSEIF EXPR = "{ l.sort EQ 'field_name' }">	<MvASSIGN NAME = "l.result"	VALUE = "{ strcasecmp( l.left:field_name, l.right:field_name ) }">
	<MvELSEIF EXPR = "{ l.sort EQ '-field_name' }">	<MvASSIGN NAME = "l.result"	VALUE = "{ strcasecmp( l.right:field_name, l.left:field_name ) }">
	<MvELSEIF EXPR = "{ l.sort EQ 'searchtype' }">	<MvASSIGN NAME = "l.result"	VALUE = "{ strcasecmp( l.left:searchtype, l.right:searchtype ) }">
	<MvELSEIF EXPR = "{ l.sort EQ '-searchtype' }">	<MvASSIGN NAME = "l.result"	VALUE = "{ strcasecmp( l.right:searchtype, l.left:searchtype ) }">
	<MvELSEIF EXPR = "{ l.sort EQ 'module' }">		<MvASSIGN NAME = "l.result"	VALUE = "{ strcasecmp( l.left:module:code, l.right:module:code ) }">
	<MvELSEIF EXPR = "{ l.sort EQ '-module' }">		<MvASSIGN NAME = "l.result"	VALUE = "{ strcasecmp( l.right:module:code, l.left:module:code ) }">
	<MvELSE>
		<MvIF EXPR = "{ l.sort EQ 'enabled' }">			<MvASSIGN NAME = "l.left_data"	VALUE = "{ l.left:enabled }">		<MvASSIGN NAME = "l.right_data"	VALUE = "{ l.right:enabled }">
		<MvELSEIF EXPR = "{ l.sort EQ '-enabled' }">	<MvASSIGN NAME = "l.left_data"	VALUE = "{ l.right:enabled }">		<MvASSIGN NAME = "l.right_data"	VALUE = "{ l.left:enabled }">
		<MvELSEIF EXPR = "{ l.sort EQ 'relevance' }">	<MvASSIGN NAME = "l.left_data"	VALUE = "{ l.left:relevance }">		<MvASSIGN NAME = "l.right_data"	VALUE = "{ l.right:relevance }">
		<MvELSEIF EXPR = "{ l.sort EQ '-relevance' }">	<MvASSIGN NAME = "l.left_data"	VALUE = "{ l.right:relevance }">	<MvASSIGN NAME = "l.right_data"	VALUE = "{ l.left:relevance }">
		<MvELSEIF EXPR = "{ l.sort EQ 'weight' }">		<MvASSIGN NAME = "l.left_data"	VALUE = "{ l.left:weight }">		<MvASSIGN NAME = "l.right_data"	VALUE = "{ l.right:weight }">
		<MvELSEIF EXPR = "{ l.sort EQ '-weight' }">		<MvASSIGN NAME = "l.left_data"	VALUE = "{ l.right:weight }">		<MvASSIGN NAME = "l.right_data"	VALUE = "{ l.left:weight }">
		</MvIF>

		<MvIF EXPR = "{ l.left_data LT l.right_data }">		<MvASSIGN NAME = "l.result" VALUE = -1>
		<MvELSEIF EXPR = "{ l.left_data GT l.right_data }">	<MvASSIGN NAME = "l.result" VALUE = 1>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.result }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndexRelationalField_Update" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'STOR', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">	</MvIF>

	<MvIF EXPR = "{ NOT JSON_AISearchIndex_Load( l.index ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT JSON_AISearchIndex_SearchableField_Load( l.index, l.searchablefield ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Boolean(	'O', 'Enabled',			l.searchablefield:enabled )			OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'O', 'Relevance',		l.searchablefield:relevance )		OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'O', 'Weight',			l.searchablefield:weight,	10, 2 )	OR
					NOT [ g.Module_JSON ].JSON_Input_List(		'O', 'SearchType',		l.searchablefield:searchtype,	'exact,contains,contains_term,fulltext',
																														'exact,contains,contains_term,fulltext' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ l.searchablefield:relevance OR ( l.searchablefield:searchtype EQ 'fulltext' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_FULLTEXT_Supported() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-AI-JSON-00022', 'The current database does not support fulltext searching or relevance' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_SEA_UT ].SearchableField_Update_AISearchIndex_Builtin( l.index, l.searchablefield ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AISearchIndexRelationalCustomField_Update" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'STOR', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">	</MvIF>

	<MvIF EXPR = "{ NOT JSON_AISearchIndex_Load( l.index ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT JSON_AISearchIndex_SearchableCustomField_Load( l.index, l.searchablecustomfield ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Boolean(	'O', 'Enabled',			l.searchablecustomfield:enabled )		OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'O', 'Relevance',		l.searchablecustomfield:relevance )		OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'O', 'Weight',			l.searchablecustomfield:weight,	10, 2 )	OR
					NOT [ g.Module_JSON ].JSON_Input_List(		'O', 'SearchType',		l.searchablecustomfield:searchtype,	'exact,contains,contains_term,fulltext',
																															'exact,contains,contains_term,fulltext' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ l.searchablecustomfield:relevance OR ( l.searchablecustomfield:searchtype EQ 'fulltext' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_FULLTEXT_Supported() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-AI-JSON-00023', 'The current database does not support fulltext searching or relevance' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ( l.searchablecustomfield:searchtype EQ 'fulltext' ) AND ( NOT l.searchablecustomfield:capabilities:search_fulltext ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-AI-JSON-00024', 'Field \'' $ l.searchablecustomfield:field_name $ '\' does not support full text searching' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.searchablecustomfield:relevance AND ( NOT l.searchablecustomfield:capabilities:orderby_relevance ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MER-AI-JSON-00025', 'Field \'' $ l.searchablecustomfield:field_name $ '\' does not support relevance sorting' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_SEA_UT ].SearchableField_Update_AISearchIndex_CustomField( l.index, l.searchablecustomfield ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvINCLUDE FILE = "json/mivascript.mv">

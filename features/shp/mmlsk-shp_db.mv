<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2022 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-SHP-DTB-
| Next Error Code: 77   
|
</MvCOMMENT>

<MvFUNCTION NAME = "SHP_Store_Create" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| sNN_ShippingRules
	|
	</MvCOMMENT>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'ShippingRules
						  (
							req_page	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
							fb_excldpo	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL() 			$ ',
							fb_descrip	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 100 )		$ ',
							fb_type		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )		$ ',
							fb_amount	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							aval_ordr	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							aval_cust	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							subignruls	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							subfb		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 10 )		$ ',
							subfbmod	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
							subfbmeth	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 100 )		$ ',
							subfbupd	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.shippingrules:req_page"		VALUE = "OCST">
	<MvASSIGN NAME = "l.shippingrules:fb_excldpo" 	VALUE = 0>
	<MvASSIGN NAME = "l.shippingrules:fb_descrip"	VALUE = "Estimated Shipping">
	<MvASSIGN NAME = "l.shippingrules:fb_type"		VALUE = "fixed">
	<MvASSIGN NAME = "l.shippingrules:fb_amount"	VALUE = 0.00>
	<MvASSIGN NAME = "l.shippingrules:aval_ordr"	VALUE = 1>
	<MvASSIGN NAME = "l.shippingrules:aval_cust"	VALUE = 1>
	<MvASSIGN NAME = "l.shippingrules:subignruls"	VALUE = 1>
	<MvASSIGN NAME = "l.shippingrules:subfb"		VALUE = "error">
	<MvASSIGN NAME = "l.shippingrules:subfbmod"		VALUE = "">
	<MvASSIGN NAME = "l.shippingrules:subfbmeth"	VALUE = "">
	<MvASSIGN NAME = "l.shippingrules:subfbupd"		VALUE = 0>

	<MvIF EXPR = "{ NOT ShippingRules_Insert( l.shippingrules ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_HandlingCharge
	|
	</MvCOMMENT>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'HandlingCharge
						  (
							type		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )		$ ',
							min_value	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							max_value	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							amt_type	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )		$ ',
							amount		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							separate	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							descrip		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 100 )		$ ',
							tax_exempt	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.handlingcharge:type"			VALUE = "disabled">
	<MvASSIGN NAME = "l.handlingcharge:min_value"		VALUE = 0.00>
	<MvASSIGN NAME = "l.handlingcharge:max_value"		VALUE = 0.00>
	<MvASSIGN NAME = "l.handlingcharge:amt_type"		VALUE = "fixed">
	<MvASSIGN NAME = "l.handlingcharge:amount"			VALUE = 0.00>
	<MvASSIGN NAME = "l.handlingcharge:separate"		VALUE = 1>
	<MvASSIGN NAME = "l.handlingcharge:descrip"			VALUE = "Handling">
	<MvASSIGN NAME = "l.handlingcharge:tax_exempt"		VALUE = 1>

	<MvIF EXPR = "{ NOT HandlingCharge_Insert( l.handlingcharge ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_ShippingMethodRules
	|
	</MvCOMMENT>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'ShippingMethodRules
						  (
							mod_code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
							meth_code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 100 )		$ ',
							agrpcount	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							priority	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							descrip		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 100 )		$ ',
							adjusttype	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )		$ ',
							adjustamnt	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							min_subtot	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							max_subtot	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							min_quan	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							max_quan	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							min_weight	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							max_weight	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							states		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ ',
							zips		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ ',
							countries	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ ',
							exclude_po	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL() 			$ ',
							exclude_df	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'ShippingMethodRules_1 ON ' $ g.Store_Table_Prefix $ 'ShippingMethodRules ( mod_code, meth_code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'ShippingMethodRules_2 ON ' $ g.Store_Table_Prefix $ 'ShippingMethodRules ( priority DESC )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_ShippingMethodExclusions
	|
	</MvCOMMENT>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'ShippingMethodExclusions
						  (
							excludermo	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
							excluderme	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 100 )		$ ',
							excludedmo	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
							excludedme	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 100 )		$ ',
							immutable	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'ShippingMethodExclusions_1 ON ' $ g.Store_Table_Prefix $ 'ShippingMethodExclusions ( excludermo, excluderme, excludedmo, excludedme )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'ShippingMethodExclusions_2 ON ' $ g.Store_Table_Prefix $ 'ShippingMethodExclusions ( excludedmo, excludedme )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00018', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_ProductShippingRules
	|
	</MvCOMMENT>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'ProductShippingRules
						  (
							product_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							ownpackage	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							width		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							length		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							height		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							limitmeths	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00019', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'ProductShippingRules_1 ON ' $ g.Store_Table_Prefix $ 'ProductShippingRules ( product_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00020', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_ProductShippingMethods
	|
	</MvCOMMENT>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'ProductShippingMethods
						  (
							product_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							mod_code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )	$ ',
							meth_code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 100 )	$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00021', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'ProductShippingMethods_1 ON ' $ g.Store_Table_Prefix $ 'ProductShippingMethods ( product_id, mod_code, meth_code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00022', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_PackagingRules
	|
	</MvCOMMENT>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'PackagingRules
						  (
							fb_width	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							fb_length	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							fb_height	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							pack_max	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							vp_mode		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )		$ ',
							vp_part_up	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							vp_part_dn	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							vp_part_ng	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00023', g.MvQUERY_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.packagingrules:fb_width"		VALUE = 0.00>
	<MvASSIGN NAME = "l.packagingrules:fb_length"		VALUE = 0.00>
	<MvASSIGN NAME = "l.packagingrules:fb_height"		VALUE = 0.00>
	<MvASSIGN NAME = "l.packagingrules:pack_max"		VALUE = 1000>
	<MvASSIGN NAME = "l.packagingrules:vp_mode"			VALUE = "parts_proportional">
	<MvASSIGN NAME = "l.packagingrules:vp_part_up"		VALUE = 1>
	<MvASSIGN NAME = "l.packagingrules:vp_part_dn"		VALUE = 0>
	<MvASSIGN NAME = "l.packagingrules:vp_part_ng"		VALUE = 0>

	<MvIF EXPR = "{ NOT PackagingRules_Insert( l.packagingrules ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_Boxes
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'Boxes', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'Boxes
						  (
							id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							descrip		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 100 )		$ ',
							enabled		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							width		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							length		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							height		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							weight		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00024', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'Boxes_1 ON ' $ g.Store_Table_Prefix $ 'Boxes ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00025', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'Boxes_2 ON ' $ g.Store_Table_Prefix $ 'Boxes ( enabled )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00026', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SHP_Store_Delete" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME = "Merchant"	QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'ShippingRules' }"> 
	<MvQUERY NAME = "Merchant"	QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'HandlingCharge' }"> 
	<MvQUERY NAME = "Merchant"	QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'ShippingMethodRules' }"> 
	<MvQUERY NAME = "Merchant"	QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'ShippingMethodExclusions' }"> 
	<MvQUERY NAME = "Merchant"	QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'ProductShippingRules' }"> 
	<MvQUERY NAME = "Merchant"	QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'ProductShippingMethods' }"> 
	<MvQUERY NAME = "Merchant"	QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'PackagingRules' }"> 
	<MvQUERY NAME = "Merchant"	QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'Boxes' }"> 

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_ShippingRules
|
</MvCOMMENT>

<MvFUNCTION NAME = "ShippingRules_Read" PARAMETERS = "shippingrules var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.shippingrules:req_page"		VALUE = "{ ShippingRules.d.req_page }">
	<MvASSIGN NAME = "l.shippingrules:fb_excldpo"	VALUE = "{ ShippingRules.d.fb_excldpo }">
	<MvASSIGN NAME = "l.shippingrules:fb_descrip"	VALUE = "{ ShippingRules.d.fb_descrip }">
	<MvASSIGN NAME = "l.shippingrules:fb_type"		VALUE = "{ ShippingRules.d.fb_type }">
	<MvASSIGN NAME = "l.shippingrules:fb_amount"	VALUE = "{ ShippingRules.d.fb_amount }">
	<MvASSIGN NAME = "l.shippingrules:aval_ordr"	VALUE = "{ ShippingRules.d.aval_ordr }">
	<MvASSIGN NAME = "l.shippingrules:aval_cust"	VALUE = "{ ShippingRules.d.aval_cust }">
	<MvASSIGN NAME = "l.shippingrules:subignruls"	VALUE = "{ ShippingRules.d.subignruls }">
	<MvASSIGN NAME = "l.shippingrules:subfb"		VALUE = "{ ShippingRules.d.subfb }">
	<MvASSIGN NAME = "l.shippingrules:subfbmod"		VALUE = "{ ShippingRules.d.subfbmod }">
	<MvASSIGN NAME = "l.shippingrules:subfbmeth"	VALUE = "{ ShippingRules.d.subfbmeth }">
	<MvASSIGN NAME = "l.shippingrules:subfbupd"		VALUE = "{ ShippingRules.d.subfbupd }">
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingRules_Insert" PARAMETERS = "shippingrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.shippingrules:fb_excldpo }"> <MvASSIGN NAME = "l.shippingrules:fb_excldpo" VALUE = 0> </MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'ShippingRules
						  ( req_page, fb_excldpo, fb_descrip, fb_type, fb_amount, aval_ordr, aval_cust, subignruls, subfb, subfbmod, subfbmeth, subfbupd )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.shippingrules:req_page, l.shippingrules:fb_excldpo, l.shippingrules:fb_descrip, l.shippingrules:fb_type, l.shippingrules:fb_amount,
			 		   l.shippingrules:aval_ordr, l.shippingrules:aval_cust, l.shippingrules:subignruls, l.shippingrules:subfb, l.shippingrules:subfbmod, 
			 		   l.shippingrules:subfbmeth, l.shippingrules:subfbupd">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00010', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingRules_Update" PARAMETERS = "shippingrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'ShippingRules
						  SET
							req_page	= ?,
							fb_excldpo	= ?,
							fb_descrip	= ?,
							fb_type		= ?,
							fb_amount	= ?,
							aval_ordr	= ?,
							aval_cust	= ?,
							subignruls	= ?,
							subfb		= ?,
							subfbmod	= ?,
							subfbmeth	= ?,
							subfbupd	= ?' }"
			 FIELDS	= "l.shippingrules:req_page, l.shippingrules:fb_excldpo, l.shippingrules:fb_descrip, l.shippingrules:fb_type, l.shippingrules:fb_amount, 
			 		   l.shippingrules:aval_ordr, l.shippingrules:aval_cust, l.shippingrules:subignruls, l.shippingrules:subfb, l.shippingrules:subfbmod, 
			 		   l.shippingrules:subfbmeth, l.shippingrules:subfbupd">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00011', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingRules_Load_Cached" PARAMETERS = "shippingrules var" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCE NAME = "l.cache" VARIABLE = "g.Session:cache:shippingrules_load_cached">

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ ShippingRules_Load( l.cache:shippingrules ) }">

		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code" 		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message" 	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof" 	VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ NOT l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( l.cache:error_code ) }">
	</MvIF>

	<MvASSIGN NAME = "l.shippingrules"					VALUE = "{ l.cache:shippingrules }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingRules_Load" PARAMETERS = "shippingrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ShippingRules"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'ShippingRules' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00012', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ ShippingRules.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "ShippingRules">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-SHP-DTB-00013' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ ShippingRules_Read( l.shippingrules ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ShippingRules">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_HandlingCharge
|
</MvCOMMENT>

<MvFUNCTION NAME = "HandlingCharge_Read" PARAMETERS = "handlingcharge var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.handlingcharge:type"		VALUE = "{ HandlingCharge.d.type }">
	<MvASSIGN NAME = "l.handlingcharge:min_value"	VALUE = "{ HandlingCharge.d.min_value }">
	<MvASSIGN NAME = "l.handlingcharge:max_value"	VALUE = "{ HandlingCharge.d.max_value }">
	<MvASSIGN NAME = "l.handlingcharge:amt_type"	VALUE = "{ HandlingCharge.d.amt_type }">
	<MvASSIGN NAME = "l.handlingcharge:amount"		VALUE = "{ HandlingCharge.d.amount }">
	<MvASSIGN NAME = "l.handlingcharge:separate"	VALUE = "{ HandlingCharge.d.separate }">
	<MvASSIGN NAME = "l.handlingcharge:descrip"		VALUE = "{ HandlingCharge.d.descrip }">
	<MvASSIGN NAME = "l.handlingcharge:tax_exempt"	VALUE = "{ HandlingCharge.d.tax_exempt }">
</MvFUNCTION>

<MvFUNCTION NAME = "HandlingCharge_Insert" PARAMETERS = "handlingcharge var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'HandlingCharge
						  ( type, min_value, max_value, amt_type, amount, separate, descrip, tax_exempt )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.handlingcharge:type, l.handlingcharge:min_value, l.handlingcharge:max_value, l.handlingcharge:amt_type,
					   l.handlingcharge:amount, l.handlingcharge:separate, l.handlingcharge:descrip, l.handlingcharge:tax_exempt">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00014', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "HandlingCharge_Update" PARAMETERS = "handlingcharge var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'HandlingCharge
						  SET
							type		= ?,
							min_value	= ?,
							max_value	= ?,
							amt_type	= ?,
							amount		= ?,
							separate	= ?,
							descrip		= ?,
							tax_exempt	= ?' }"
			 FIELDS	= "l.handlingcharge:type, l.handlingcharge:min_value, l.handlingcharge:max_value, l.handlingcharge:amt_type,
					   l.handlingcharge:amount, l.handlingcharge:separate, l.handlingcharge:descrip, l.handlingcharge:tax_exempt">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00015', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "HandlingCharge_Load" PARAMETERS = "handlingcharge var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "HandlingCharge"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'HandlingCharge' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00016', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ HandlingCharge.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "HandlingCharge">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-SHP-DTB-00017' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ HandlingCharge_Read( l.handlingcharge ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "HandlingCharge">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_ShippingMethodRules
|
</MvCOMMENT>

<MvFUNCTION NAME = "ShippingMethodRules_Read" PARAMETERS = "shippingmethodrules var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.shippingmethodrules:mod_code"	VALUE = "{ ShippingMethodRules.d.mod_code }">
	<MvASSIGN NAME = "l.shippingmethodrules:meth_code"	VALUE = "{ ShippingMethodRules.d.meth_code }">
	<MvASSIGN NAME = "l.shippingmethodrules:agrpcount"	VALUE = "{ ShippingMethodRules.d.agrpcount }">
	<MvASSIGN NAME = "l.shippingmethodrules:priority"	VALUE = "{ ShippingMethodRules.d.priority }">
	<MvASSIGN NAME = "l.shippingmethodrules:descrip"	VALUE = "{ ShippingMethodRules.d.descrip }">
	<MvASSIGN NAME = "l.shippingmethodrules:adjusttype"	VALUE = "{ ShippingMethodRules.d.adjusttype }">
	<MvASSIGN NAME = "l.shippingmethodrules:adjustamnt"	VALUE = "{ ShippingMethodRules.d.adjustamnt }">
	<MvASSIGN NAME = "l.shippingmethodrules:min_subtot"	VALUE = "{ ShippingMethodRules.d.min_subtot }">
	<MvASSIGN NAME = "l.shippingmethodrules:max_subtot"	VALUE = "{ ShippingMethodRules.d.max_subtot }">
	<MvASSIGN NAME = "l.shippingmethodrules:min_quan"	VALUE = "{ ShippingMethodRules.d.min_quan }">
	<MvASSIGN NAME = "l.shippingmethodrules:max_quan"	VALUE = "{ ShippingMethodRules.d.max_quan }">
	<MvASSIGN NAME = "l.shippingmethodrules:min_weight"	VALUE = "{ ShippingMethodRules.d.min_weight }">
	<MvASSIGN NAME = "l.shippingmethodrules:max_weight"	VALUE = "{ ShippingMethodRules.d.max_weight }">

	<MvIF EXPR = "{ len( ShippingMethodRules.d.states ) EQ 0 }">	<MvASSIGN NAME = "l.shippingmethodrules:states"		VALUE = "">
	<MvELSE>														<MvASSIGN NAME = "l.shippingmethodrules:states"		VALUE = "{ miva_array_deserialize( ShippingMethodRules.d.states ) }">
	</MvIF>

	<MvASSIGN NAME = "l.shippingmethodrules:zips"		VALUE = "{ ShippingMethodRules.d.zips }">

	<MvIF EXPR = "{ len( ShippingMethodRules.d.countries ) EQ 0 }">	<MvASSIGN NAME = "l.shippingmethodrules:countries"	VALUE = "">
	<MvELSE>														<MvASSIGN NAME = "l.shippingmethodrules:countries"	VALUE = "{ miva_array_deserialize( ShippingMethodRules.d.countries ) }">
	</MvIF>
	
	<MvASSIGN NAME = "l.shippingmethodrules:exclude_po"	VALUE = "{ ShippingMethodRules.d.exclude_po }">
	<MvASSIGN NAME = "l.shippingmethodrules:exclude_df"	VALUE = "{ ShippingMethodRules.d.exclude_df }">
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodRules_Default" PARAMETERS = "module_code, method_code, shippingmethodrules var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.shippingmethodrules:mod_code"	VALUE = "{ l.module_code }">
	<MvASSIGN NAME = "l.shippingmethodrules:meth_code"	VALUE = "{ l.method_code }">
	<MvASSIGN NAME = "l.shippingmethodrules:agrpcount"	VALUE = 0>
	<MvASSIGN NAME = "l.shippingmethodrules:priority"	VALUE = 0>
	<MvASSIGN NAME = "l.shippingmethodrules:descrip"	VALUE = "">
	<MvASSIGN NAME = "l.shippingmethodrules:adjusttype"	VALUE = "">
	<MvASSIGN NAME = "l.shippingmethodrules:adjustamnt"	VALUE = 0.00>
	<MvASSIGN NAME = "l.shippingmethodrules:min_subtot"	VALUE = 0> 
	<MvASSIGN NAME = "l.shippingmethodrules:max_subtot"	VALUE = 0>
	<MvASSIGN NAME = "l.shippingmethodrules:min_quan"	VALUE = 0>
	<MvASSIGN NAME = "l.shippingmethodrules:max_quan"	VALUE = 0>
	<MvASSIGN NAME = "l.shippingmethodrules:min_weight"	VALUE = 0>
	<MvASSIGN NAME = "l.shippingmethodrules:max_weight"	VALUE = 0>
	<MvASSIGN NAME = "l.shippingmethodrules:states"		VALUE = "">
	<MvASSIGN NAME = "l.shippingmethodrules:zips"		VALUE = "">
	<MvASSIGN NAME = "l.shippingmethodrules:countries"	VALUE = "">
	<MvASSIGN NAME = "l.shippingmethodrules:exclude_po" VALUE = 0>
	<MvASSIGN NAME = "l.shippingmethodrules:exclude_df" VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodRules_Insert" PARAMETERS = "shippingmethodrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.states"		VALUE = "{ miva_array_serialize( l.shippingmethodrules:states ) }">
	<MvASSIGN NAME = "l.countries"	VALUE = "{ miva_array_serialize( l.shippingmethodrules:countries ) }">

	<MvIF EXPR = "{ ISNULL l.shippingmethodrules:agrpcount }">
		<MvASSIGN NAME = "l.shippingmethodrules:agrpcount"	VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.shippingmethodrules:adjustamnt }">
		<MvASSIGN NAME = "l.shippingmethodrules:adjustamnt" VALUE = 0.00>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.shippingmethodrules:exclude_po }">
		<MvASSIGN NAME = "l.shippingmethodrules:exclude_po" VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.shippingmethodrules:exclude_df }">
		<MvASSIGN NAME = "l.shippingmethodrules:exclude_df" VALUE = 0>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'ShippingMethodRules
						  ( mod_code, meth_code, agrpcount, priority, descrip, adjusttype, adjustamnt, min_subtot,
						  	max_subtot, min_quan, max_quan, min_weight, max_weight, states, zips, countries, exclude_po, exclude_df )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.shippingmethodrules:mod_code, l.shippingmethodrules:meth_code, l.shippingmethodrules:agrpcount,
			 		   l.shippingmethodrules:priority, l.shippingmethodrules:descrip, l.shippingmethodrules:adjusttype,
			 		   l.shippingmethodrules:adjustamnt, l.shippingmethodrules:min_subtot, l.shippingmethodrules:max_subtot,
			 		   l.shippingmethodrules:min_quan, l.shippingmethodrules:max_quan, l.shippingmethodrules:min_weight,
			 		   l.shippingmethodrules:max_weight, l.states, l.shippingmethodrules:zips, l.countries,
			 		   l.shippingmethodrules:exclude_po, l.shippingmethodrules:exclude_df">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00027', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodRules_Update" PARAMETERS = "shippingmethodrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.states"		VALUE = "{ miva_array_serialize( l.shippingmethodrules:states ) }">
	<MvASSIGN NAME = "l.countries"	VALUE = "{ miva_array_serialize( l.shippingmethodrules:countries ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'ShippingMethodRules
						  SET
							priority	= ?,
							descrip		= ?,
							adjusttype	= ?,
							adjustamnt	= ?,
							min_subtot	= ?,
							max_subtot	= ?,
							min_quan	= ?,
							max_quan	= ?,
							min_weight	= ?,
							max_weight	= ?,
							states		= ?,
							zips		= ?,
							countries	= ?,
							exclude_po	= ?,
							exclude_df	= ?
						  WHERE
							mod_code	= ? AND
							meth_code	= ?' }"
			 FIELDS	= "l.shippingmethodrules:priority, l.shippingmethodrules:descrip, l.shippingmethodrules:adjusttype,
					   l.shippingmethodrules:adjustamnt, l.shippingmethodrules:min_subtot, l.shippingmethodrules:max_subtot,
					   l.shippingmethodrules:min_quan, l.shippingmethodrules:max_quan, l.shippingmethodrules:min_weight,
					   l.shippingmethodrules:max_weight, l.states, l.shippingmethodrules:zips, l.countries,
					   l.shippingmethodrules:exclude_po, l.shippingmethodrules:exclude_df,
					   l.shippingmethodrules:mod_code, l.shippingmethodrules:meth_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00028', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodRules_Update_Method" PARAMETERS = "mod_code, old_meth_code, new_meth_code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT ShippingMethodRules_Delete( l.mod_code, l.new_meth_code )				OR
					NOT ProductShippingMethod_Delete_All_Method( l.mod_code, l.new_meth_code )	OR
					NOT [ g.Module_Feature_AGR_DB ].AvailGroupXShippingMethod_Delete_All_Method( l.mod_code, l.new_meth_code ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT ShippingMethodRules_Update_Method_LowLevel( l.mod_code, l.old_meth_code, l.new_meth_code )	OR
					NOT ProductShippingMethod_Update_All_Method( l.mod_code, l.old_meth_code, l.new_meth_code )		OR
					NOT [ g.Module_Feature_AGR_DB ].AvailGroupXShippingMethod_Update_All_Method( l.mod_code, l.old_meth_code, l.new_meth_code ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodRules_Update_Method_LowLevel" PARAMETERS = "mod_code, old_meth_code, new_meth_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'ShippingMethodRules
						  SET
							meth_code	= ?
						  WHERE
							mod_code	= ? AND
							meth_code	= ?' }"
			 FIELDS	= "l.new_meth_code,
					   l.mod_code, l.old_meth_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00069', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodRules_Delete" PARAMETERS = "mod_code, meth_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ShippingMethodRules
						  WHERE
							mod_code	= ? AND
							meth_code	= ?' }"
			 FIELDS	= "l.mod_code, l.meth_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00029', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodRules_Load_Method" PARAMETERS = "module_code, method_code, shippingmethodrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ShippingMethodRules"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'ShippingMethodRules WHERE mod_code = ? AND meth_code = ?' }"
				FIELDS	= "l.module_code, l.method_code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00030', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ ShippingMethodRules.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "ShippingMethodRules">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-SHP-DTB-00031' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ ShippingMethodRules_Read( l.shippingmethodrules ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ShippingMethodRules">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodRules_Load_MethodOrDefault" PARAMETERS = "module_code, method_code, shippingmethodrules var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ShippingMethodRules_Load_Method( l.module_code, l.method_code, l.shippingmethodrules ) }">	<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">											<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvEVAL EXPR = "{ ShippingMethodRules_Default( l.module_code, l.method_code, l.shippingmethodrules ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodRules_Load_MethodOrDefault_Cached" PARAMETERS = "module_code, method_code, shippingmethodrules var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.module_code OR ISNULL l.method_code }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-SHP-DTB-00072' ) }">
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.cache" 					VARIABLE = "g.Session:cache:shippingmethodrules_load_methodordefault_cached:methods">
		<MvMEMBER NAME = "{ l.module_code }">
		<MvMEMBER NAME = "{ l.method_code }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ ShippingMethodRules_Load_MethodOrDefault( l.module_code, l.method_code, l.cache:shippingmethodrules ) }">

		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.shippingmethodrules"			VALUE = "{ l.cache:shippingmethodrules }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodRulesList_Load_All" PARAMETERS = "shippingmethodrules_list var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ShippingMethodRules"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'ShippingMethodRules ORDER BY priority DESC' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00032', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.shippingmethodrules_count"		VALUE = 0>
	<MvWHILE EXPR = "{ NOT ShippingMethodRules.d.EOF }">
		<MvASSIGN NAME = "l.shippingmethodrules_count"	VALUE = "{ l.shippingmethodrules_count + 1 }">
		<MvEVAL EXPR = "{ ShippingMethodRules_Read( l.shippingmethodrules_list[ l.shippingmethodrules_count ] ) }">
		
		<MvSKIP NAME = "Merchant" VIEW = "ShippingMethodRules" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ShippingMethodRules">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-SHP-DTB-00033', l.shippingmethodrules_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodRules_Increment_AvailabilityGroupCount" PARAMETERS = "mod_code, meth_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'ShippingMethodRules
			 			  SET
			 			  	agrpcount	= agrpcount + 1
			 			  WHERE
			 			  	mod_code	= ? AND
			 			  	meth_code	= ?' }"
			 FIELDS = "l.mod_code, l.meth_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00067', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodRules_Decrement_AvailabilityGroupCount" PARAMETERS = "mod_code, meth_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'ShippingMethodRules
			 			  SET
			 			  	agrpcount	= agrpcount - 1
			 			  WHERE
			 			  	mod_code	= ? AND
			 			  	meth_code	= ?' }"
			 FIELDS = "l.mod_code, l.meth_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00068', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodRules_Decrement_AvailabilityGroupCount_All" PARAMETERS = "availabilitygroup_id" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.availgroupxshippingmethod" ARRAY = "l.availgroupxshippingmethods" COUNT = "{ [ g.Module_Feature_AGR_DB ].AvailGroupXShippingMethodList_Load( l.availabilitygroup_id, l.availgroupxshippingmethods ) }">
		<MvIF EXPR = "{ NOT ShippingMethodRules_Decrement_AvailabilityGroupCount( l.availgroupxshippingmethod:mod_code, l.availgroupxshippingmethod:meth_code ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_ShippingMethodExclusions
|
</MvCOMMENT>

<MvFUNCTION NAME = "ShippingMethodExclusion_Read" PARAMETERS = "shippingmethodexclusion var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.shippingmethodexclusion:excludermo"	VALUE = "{ ShippingMethodExclusions.d.excludermo }">
	<MvASSIGN NAME = "l.shippingmethodexclusion:excluderme"	VALUE = "{ ShippingMethodExclusions.d.excluderme }">
	<MvASSIGN NAME = "l.shippingmethodexclusion:excludedmo"	VALUE = "{ ShippingMethodExclusions.d.excludedmo }">
	<MvASSIGN NAME = "l.shippingmethodexclusion:excludedme"	VALUE = "{ ShippingMethodExclusions.d.excludedme }">
	<MvASSIGN NAME = "l.shippingmethodexclusion:immutable"	VALUE = "{ ShippingMethodExclusions.d.immutable }">
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodExclusion_Insert" PARAMETERS = "shippingmethodexclusion var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'ShippingMethodExclusions
						  ( excludermo, excluderme, excludedmo, excludedme, immutable )
						  VALUES
						  ( ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.shippingmethodexclusion:excludermo, l.shippingmethodexclusion:excluderme,
					   l.shippingmethodexclusion:excludedmo, l.shippingmethodexclusion:excludedme,
					   l.shippingmethodexclusion:immutable">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00034', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodExclusion_Delete" PARAMETERS = "shippingmethodexclusion var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ShippingMethodExclusions
						  WHERE
							excludermo	= ? AND
							excluderme	= ? AND
							excludedmo	= ? AND
							excludedme	= ?' }"
			 FIELDS	= "l.shippingmethodexclusion:excludermo, l.shippingmethodexclusion:excluderme,
					   l.shippingmethodexclusion:excludedmo, l.shippingmethodexclusion:excludedme">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00035', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodExclusion_Delete_All_Module" PARAMETERS = "module_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ShippingMethodExclusions
						  WHERE
							excludermo	= ? OR
							excludedmo	= ?' }"
			 FIELDS	= "l.module_code, l.module_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00036', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodExclusion_Delete_NonImmutable_Method" PARAMETERS = "module_code, method_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ShippingMethodExclusions
						  WHERE
							( excludermo = ? AND excluderme = ? ) OR
							( excludedmo = ? AND excludedme = ? )' }"
			 FIELDS	= "l.module_code, l.method_code,
					   l.module_code, l.method_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00066', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodExclusionList_Load_Method" PARAMETERS = "module_code, method_code, shippingmethodexclusions var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ShippingMethodExclusions"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'ShippingMethodExclusions
							  WHERE
								( excludermo = ? AND excluderme = ? ) OR
								( excludedmo = ? AND excludedme = ? )' }"
				FIELDS	= "l.module_code, l.method_code, l.module_code, l.method_code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00037', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.shippingmethodexclusion_count"		VALUE = 0>
	<MvWHILE EXPR = "{ NOT ShippingMethodExclusions.d.EOF }">
		<MvASSIGN NAME = "l.shippingmethodexclusion_count"	VALUE = "{ l.shippingmethodexclusion_count + 1 }">
		<MvEVAL EXPR = "{ ShippingMethodExclusion_Read( l.shippingmethodexclusions[ l.shippingmethodexclusion_count ] ) }">
		
		<MvSKIP NAME = "Merchant" VIEW = "ShippingMethodExclusions" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ShippingMethodExclusions">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-SHP-DTB-00038', l.shippingmethodexclusion_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodExclusion_Load" PARAMETERS = "excludermo, excluderme, excludedmo, excludedme, shippingmethodexclusion var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ShippingMethodExclusions"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'ShippingMethodExclusions
							  WHERE
								excludermo	= ? AND
								excluderme	= ? AND
								excludedmo	= ? AND
								excludedme	= ?' }"
				FIELDS	= "l.excludermo, l.excluderme, l.excludedmo, l.excludedme">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00039', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ ShippingMethodExclusions.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "ShippingMethodExclusions">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-SHP-DTB-00040' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ ShippingMethodExclusion_Read( l.shippingmethodexclusion ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ShippingMethodExclusions">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodExclusionList_Load_Excluder" PARAMETERS = "module_code, method_code, shippingmethodexclusions var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ShippingMethodExclusions"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'ShippingMethodExclusions WHERE excludermo = ? AND excluderme = ?' }"
				FIELDS	= "l.module_code, l.method_code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00041', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.shippingmethodexclusion_count"		VALUE = 0>
	<MvWHILE EXPR = "{ NOT ShippingMethodExclusions.d.EOF }">
		<MvASSIGN NAME = "l.shippingmethodexclusion_count"	VALUE = "{ l.shippingmethodexclusion_count + 1 }">
		<MvEVAL EXPR = "{ ShippingMethodExclusion_Read( l.shippingmethodexclusions[ l.shippingmethodexclusion_count ] ) }">
		
		<MvSKIP NAME = "Merchant" VIEW = "ShippingMethodExclusions" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ShippingMethodExclusions">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-SHP-DTB-00042', l.shippingmethodexclusion_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ShippingMethodExclusionList_Load_Excluded" PARAMETERS = "module_code, method_code, shippingmethodexclusions var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ShippingMethodExclusions"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'ShippingMethodExclusions WHERE excludedmo = ? AND excludedme = ?' }"
				FIELDS	= "l.module_code, l.method_code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00043', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.shippingmethodexclusion_count"		VALUE = 0>
	<MvWHILE EXPR = "{ NOT ShippingMethodExclusions.d.EOF }">
		<MvASSIGN NAME = "l.shippingmethodexclusion_count"	VALUE = "{ l.shippingmethodexclusion_count + 1 }">
		<MvEVAL EXPR = "{ ShippingMethodExclusion_Read( l.shippingmethodexclusions[ l.shippingmethodexclusion_count ] ) }">
		
		<MvSKIP NAME = "Merchant" VIEW = "ShippingMethodExclusions" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ShippingMethodExclusions">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-SHP-DTB-00044', l.shippingmethodexclusion_count ) }">
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_ProductShippingRules
|
</MvCOMMENT>

<MvFUNCTION NAME = "ProductShippingRules_Read" PARAMETERS = "productshippingrules var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.productshippingrules:product_id"	VALUE = "{ ProductShippingRules.d.product_id }">
	<MvASSIGN NAME = "l.productshippingrules:ownpackage"	VALUE = "{ ProductShippingRules.d.ownpackage }">
	<MvASSIGN NAME = "l.productshippingrules:width"			VALUE = "{ ProductShippingRules.d.width }">
	<MvASSIGN NAME = "l.productshippingrules:length"		VALUE = "{ ProductShippingRules.d.length }">
	<MvASSIGN NAME = "l.productshippingrules:height"		VALUE = "{ ProductShippingRules.d.height }">
	<MvASSIGN NAME = "l.productshippingrules:limitmeths"	VALUE = "{ ProductShippingRules.d.limitmeths }">
</MvFUNCTION>

<MvFUNCTION NAME = "ProductShippingRules_Default" PARAMETERS = "product_id, productshippingrules var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.productshippingrules:product_id"	VALUE = "{ l.product_id }">
	<MvASSIGN NAME = "l.productshippingrules:ownpackage"	VALUE = 0>
	<MvASSIGN NAME = "l.productshippingrules:width"			VALUE = 0>
	<MvASSIGN NAME = "l.productshippingrules:length"		VALUE = 0>
	<MvASSIGN NAME = "l.productshippingrules:height"		VALUE = 0>
	<MvASSIGN NAME = "l.productshippingrules:limitmeths"	VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductShippingRules_Equal_Default" PARAMETERS = "productshippingrules var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.productshippingrules:ownpackage NE 0 }">	<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ l.productshippingrules:width NE 0 }">		<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ l.productshippingrules:length NE 0 }">		<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ l.productshippingrules:height NE 0 }">		<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ l.productshippingrules:limitmeths NE 0 }">	<MvFUNCTIONRETURN VALUE = 0>	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductShippingRules_Insert" PARAMETERS = "productshippingrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'ProductShippingRules
						  ( product_id, ownpackage, width, length, height, limitmeths )
						  VALUES
						  ( ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.productshippingrules:product_id, l.productshippingrules:ownpackage, l.productshippingrules:width,
					   l.productshippingrules:length, l.productshippingrules:height, l.productshippingrules:limitmeths">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00045', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductShippingRules_Update" PARAMETERS = "productshippingrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'ProductShippingRules
						  SET
							ownpackage	= ?,
							width		= ?,
							length		= ?,
							height		= ?,
							limitmeths	= ?
						  WHERE
							product_id = ?' }"
			 FIELDS	= "l.productshippingrules:ownpackage, l.productshippingrules:width, l.productshippingrules:length,
					   l.productshippingrules:height, l.productshippingrules:limitmeths,
					   l.productshippingrules:product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00046', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductShippingRules_Delete_Product" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ProductShippingRules
						  WHERE
							product_id = ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00047', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductShippingRules_Load_Product" PARAMETERS = "product_id, productshippingrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ProductShippingRules"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'ProductShippingRules WHERE product_id = ?' }"
				FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00048', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ ProductShippingRules.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "ProductShippingRules">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-SHP-DTB-00049' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ ProductShippingRules_Read( l.productshippingrules ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ProductShippingRules">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductShippingRules_Load_ProductOrDefault" PARAMETERS = "product_id, productshippingrules var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ProductShippingRules_Load_Product( l.product_id, l.productshippingrules ) }">	<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">								<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvEVAL EXPR = "{ ProductShippingRules_Default( l.product_id, l.productshippingrules ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_ProductShippingMethods
|
</MvCOMMENT>

<MvFUNCTION NAME = "ProductShippingMethod_Read" PARAMETERS = "productshippingmethod var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.productshippingmethod:product_id"	VALUE = "{ ProductShippingMethods.d.product_id }">
	<MvASSIGN NAME = "l.productshippingmethod:mod_code"		VALUE = "{ ProductShippingMethods.d.mod_code }">
	<MvASSIGN NAME = "l.productshippingmethod:meth_code"	VALUE = "{ ProductShippingMethods.d.meth_code }">
</MvFUNCTION>

<MvFUNCTION NAME = "ProductShippingMethod_Insert" PARAMETERS = "productshippingmethod var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'ProductShippingMethods
						  ( product_id, mod_code, meth_code )
						  VALUES
						  ( ?, ?, ? )' }"
			 FIELDS	= "l.productshippingmethod:product_id, l.productshippingmethod:mod_code, l.productshippingmethod:meth_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00050', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductShippingMethod_Update_All_Method" PARAMETERS = "mod_code, old_meth_code, new_meth_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'ProductShippingMethods
						  SET
							meth_code	= ?
						  WHERE
							mod_code	= ? AND
							meth_code	= ?' }"
			 FIELDS	= "l.new_meth_code,
					   l.mod_code, l.old_meth_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00075', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductShippingMethod_Delete" PARAMETERS = "productshippingmethod var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ProductShippingMethods
						  WHERE
							product_id	= ? AND
							mod_code	= ? AND
							meth_code	= ?' }"
			 FIELDS	= "l.productshippingmethod:product_id, l.productshippingmethod:mod_code, l.productshippingmethod:meth_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00051', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductShippingMethod_Delete_All_Product" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ProductShippingMethods
						  WHERE
							product_id = ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00052', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductShippingMethod_Delete_All_Method" PARAMETERS = "mod_code, meth_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'ProductShippingMethods
						  WHERE
							mod_code	= ? AND
							meth_code	= ?' }"
			 FIELDS = "l.mod_code, l.meth_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00076', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ProductShippingMethodList_Load_Product_Cached" PARAMETERS = "product_id, productshippingrules var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.product_id" VALUE = "{ int( l.product_id ) }">

	<MvIF EXPR = "{ l.product_id LE 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00070', 'Invalid product ID' ) }">
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.cache" VARIABLE = "g.Session:cache:product_shipping_methods">
		<MvDIMENSION INDEX = "{ l.product_id }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result" VALUE = "{ ProductShippingMethodList_Load_Product( l.product_id, l.cache:productshippingrules ) }">

		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code" 		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message" 	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof" 	VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ NOT l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-SHP-DTB-00071', l.cache:result ) }">
	</MvIF>

	<MvASSIGN NAME = "l.productshippingrules" VALUE = "{ l.cache:productshippingrules }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "ProductShippingMethodList_Load_Product" PARAMETERS = "product_id, productshippingmethods var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "ProductShippingMethods"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'ProductShippingMethods WHERE product_id = ?' }"
				FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00053', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.productshippingmethod_count"		VALUE = 0>
	<MvWHILE EXPR = "{ NOT ProductShippingMethods.d.EOF }">
		<MvASSIGN NAME = "l.productshippingmethod_count"	VALUE = "{ l.productshippingmethod_count + 1 }">
		<MvEVAL EXPR = "{ ProductShippingMethod_Read( l.productshippingmethods[ l.productshippingmethod_count ] ) }">
		
		<MvSKIP NAME = "Merchant" VIEW = "ProductShippingMethods" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "ProductShippingMethods">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-SHP-DTB-00054', l.productshippingmethod_count ) }">
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_PackagingRules
|
</MvCOMMENT>

<MvFUNCTION NAME = "PackagingRules_Read" PARAMETERS = "packagingrules var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.packagingrules:fb_width"	VALUE = "{ PackagingRules.d.fb_width }">
	<MvASSIGN NAME = "l.packagingrules:fb_length"	VALUE = "{ PackagingRules.d.fb_length }">
	<MvASSIGN NAME = "l.packagingrules:fb_height"	VALUE = "{ PackagingRules.d.fb_height }">
	<MvASSIGN NAME = "l.packagingrules:pack_max"	VALUE = "{ PackagingRules.d.pack_max }">
	<MvASSIGN NAME = "l.packagingrules:vp_mode"		VALUE = "{ PackagingRules.d.vp_mode }">
	<MvASSIGN NAME = "l.packagingrules:vp_part_up"	VALUE = "{ PackagingRules.d.vp_part_up }">
	<MvASSIGN NAME = "l.packagingrules:vp_part_dn"	VALUE = "{ PackagingRules.d.vp_part_dn }">
	<MvASSIGN NAME = "l.packagingrules:vp_part_ng"	VALUE = "{ PackagingRules.d.vp_part_ng }">
</MvFUNCTION>

<MvFUNCTION NAME = "PackagingRules_Insert" PARAMETERS = "packagingrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.packagingrules:vp_part_up }">	<MvASSIGN NAME = "l.packagingrules:vp_part_up"	VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ ISNULL l.packagingrules:vp_part_dn }">	<MvASSIGN NAME = "l.packagingrules:vp_part_dn"	VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ ISNULL l.packagingrules:vp_part_ng }">	<MvASSIGN NAME = "l.packagingrules:vp_part_ng"	VALUE = 0>	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'PackagingRules
						  ( fb_width, fb_length, fb_height, pack_max, vp_mode, vp_part_up, vp_part_dn, vp_part_ng )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.packagingrules:fb_width, l.packagingrules:fb_length, l.packagingrules:fb_height,
					   l.packagingrules:pack_max, l.packagingrules:vp_mode, l.packagingrules:vp_part_up,
					   l.packagingrules:vp_part_dn, l.packagingrules:vp_part_ng">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00055', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "PackagingRules_Update" PARAMETERS = "packagingrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'PackagingRules
						  SET
							fb_width	= ?,
							fb_length	= ?,
							fb_height	= ?,
							pack_max	= ?,
							vp_mode		= ?,
							vp_part_up	= ?,
							vp_part_dn	= ?,
							vp_part_ng	= ?' }"
			 FIELDS	= "l.packagingrules:fb_width, l.packagingrules:fb_length, l.packagingrules:fb_height, l.packagingrules:pack_max,
					   l.packagingrules:vp_mode, l.packagingrules:vp_part_up, l.packagingrules:vp_part_dn, l.packagingrules:vp_part_ng">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00056', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "PackagingRules_Load" PARAMETERS = "packagingrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "PackagingRules"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'PackagingRules' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00057', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ PackagingRules.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "PackagingRules">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-SHP-DTB-00058' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ PackagingRules_Read( l.packagingrules ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "PackagingRules">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_Boxes
|
</MvCOMMENT>

<MvFUNCTION NAME = "Box_Read" PARAMETERS = "box var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.box:id"			VALUE = "{ Boxes.d.id }">
	<MvASSIGN NAME = "l.box:descrip"	VALUE = "{ Boxes.d.descrip }">
	<MvASSIGN NAME = "l.box:enabled"	VALUE = "{ Boxes.d.enabled }">
	<MvASSIGN NAME = "l.box:width"		VALUE = "{ Boxes.d.width }">
	<MvASSIGN NAME = "l.box:length"		VALUE = "{ Boxes.d.length }">
	<MvASSIGN NAME = "l.box:height"		VALUE = "{ Boxes.d.height }">
	<MvASSIGN NAME = "l.box:weight"		VALUE = "{ Boxes.d.weight }">
</MvFUNCTION>

<MvFUNCTION NAME = "Box_Insert" PARAMETERS = "box var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.box:id"	VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'Boxes' ) }">

	<MvIF EXPR = "{ ISNULL l.box:weight }">
		<MvASSIGN NAME = "l.box:weight" VALUE = 0>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'Boxes
						  ( id, descrip, enabled, width, length, height, weight )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.box:id, l.box:descrip, l.box:enabled,
					   l.box:width, l.box:length, l.box:height, l.box:weight">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00059', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Box_Update" PARAMETERS = "box var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Boxes
						  SET
							descrip		= ?,
							enabled		= ?,
							width		= ?,
							length		= ?,
							height		= ?,
							weight		= ?
						  WHERE
							id			= ?' }"
			 FIELDS	= "l.box:descrip, l.box:enabled, l.box:width, l.box:length, l.box:height, l.box:weight,
					   l.box:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00060', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Box_Delete_ID" PARAMETERS = "box_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'Boxes WHERE id = ?' }"
			 FIELDS	= "l.box_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00061', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Box_Load_ID" PARAMETERS = "box_id, box var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Boxes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Boxes WHERE id = ?' }"
				FIELDS	= "l.box_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00062', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Boxes.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Boxes">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-SHP-DTB-00063' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Box_Read( l.box ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Boxes">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Box_Load_Description" PARAMETERS = "descrip, box var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Boxes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Boxes WHERE ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'descrip' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.descrip">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00073', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Boxes.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Boxes">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-SHP-DTB-00074' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Box_Read( l.box ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Boxes">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "BoxList_Load_Enabled" PARAMETERS = "boxes var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Boxes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Boxes WHERE enabled = 1' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SHP-DTB-00064', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.box_count"		VALUE = 0>
	<MvWHILE EXPR = "{ NOT Boxes.d.EOF }">
		<MvASSIGN NAME = "l.box_count"	VALUE = "{ l.box_count + 1 }">
		<MvEVAL EXPR = "{ Box_Read( l.boxes[ l.box_count ] ) }">
		
		<MvSKIP NAME = "Merchant" VIEW = "Boxes" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Boxes">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-SHP-DTB-00065', l.box_count ) }">
</MvFUNCTION>

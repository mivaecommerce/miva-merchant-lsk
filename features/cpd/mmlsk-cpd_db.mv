<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-CPD-DTB-
| Next Error Code: 30
|
</MvCOMMENT>

<MvFUNCTION NAME = "CPD_Store_Create" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| sNN_CopyProductRules
	|
	</MvCOMMENT>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'CopyProductRules
						  (
							id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 100 )	$ ',
							product		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ ',
							attributes	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ ',
							categories	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ ',
							invset		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ ',
							invlevel	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ ',
							images		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ ',
							relprod		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ ',
							upsale		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ ',
							availgroup	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ ',
							pricegroup	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ ',
							ddownload	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ ',
							giftcert	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ ',
							subscrip	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ ',
							payment		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ ',
							shipping	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ ',
							kit			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ ',
							variants	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'CopyProductRules_1 ON ' $ g.Store_Table_Prefix $ 'CopyProductRules ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'CopyProductRules_2 ON ' $ g.Store_Table_Prefix $ 'CopyProductRules ( name )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'CopyProductRules', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_CopyProductRulesXModule
	|
	</MvCOMMENT>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'CopyProductRulesXModule
						  (
							rules_id ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()	$ ',
							mod_code ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )	$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'CopyProductRulesXModule_1 ON ' $ g.Store_Table_Prefix $ 'CopyProductRulesXModule ( rules_id, mod_code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_CopyProductRulesXCustomField
	|
	</MvCOMMENT>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'CopyProductRulesXCustomField
						  (
							rules_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							mod_code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )	$ ',
							field_code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )	$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'CopyProductRulesXCustomField_1 ON ' $ g.Store_Table_Prefix $ 'CopyProductRulesXCustomField ( rules_id, mod_code, field_code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CPD_Store_Delete" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'CopyProductRules' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'CopyProductRulesXModule' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'CopyProductRulesXCustomField' }">

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'CopyProductRules' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CPD_Create_Data_Files" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].DomainPrivilege_Insert( 'CPDR', 'Copy Product Rules' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CPD_Delete_Data_Files" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_CopyProductRules
|
</MvCOMMENT>

<MvFUNCTION NAME = "CopyProductRules_Default" PARAMETERS = "copyproductrules var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.copyproductrules"				VALUE = "">
	<MvASSIGN NAME = "l.copyproductrules:id"			VALUE = 0>
	<MvASSIGN NAME = "l.copyproductrules:name"			VALUE = "<Copy All Product Data>">
	<MvASSIGN NAME = "l.copyproductrules:product"		VALUE = 1>
	<MvASSIGN NAME = "l.copyproductrules:attributes"	VALUE = 1>
	<MvASSIGN NAME = "l.copyproductrules:categories"	VALUE = 1>
	<MvASSIGN NAME = "l.copyproductrules:invset"		VALUE = 1>
	<MvASSIGN NAME = "l.copyproductrules:invlevel"		VALUE = 1>
	<MvASSIGN NAME = "l.copyproductrules:images"		VALUE = 1>
	<MvASSIGN NAME = "l.copyproductrules:relprod"		VALUE = 1>
	<MvASSIGN NAME = "l.copyproductrules:upsale"		VALUE = 1>
	<MvASSIGN NAME = "l.copyproductrules:availgroup"	VALUE = 1>
	<MvASSIGN NAME = "l.copyproductrules:pricegroup"	VALUE = 1>
	<MvASSIGN NAME = "l.copyproductrules:ddownload"		VALUE = 1>
	<MvASSIGN NAME = "l.copyproductrules:giftcert"		VALUE = 1>
	<MvASSIGN NAME = "l.copyproductrules:subscrip"		VALUE = 1>
	<MvASSIGN NAME = "l.copyproductrules:payment"		VALUE = 1>
	<MvASSIGN NAME = "l.copyproductrules:shipping"		VALUE = 1>
	<MvASSIGN NAME = "l.copyproductrules:kit"			VALUE = 1>
	<MvASSIGN NAME = "l.copyproductrules:variants"		VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CopyProductRules_Read" PARAMETERS = "copyproductrules var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.copyproductrules"				VALUE = "">
	<MvASSIGN NAME = "l.copyproductrules:id"			VALUE = "{ CopyProductRules.d.id }">
	<MvASSIGN NAME = "l.copyproductrules:name"			VALUE = "{ CopyProductRules.d.name }">
	<MvASSIGN NAME = "l.copyproductrules:product"		VALUE = "{ CopyProductRules.d.product }">
	<MvASSIGN NAME = "l.copyproductrules:attributes"	VALUE = "{ CopyProductRules.d.attributes }">
	<MvASSIGN NAME = "l.copyproductrules:categories"	VALUE = "{ CopyProductRules.d.categories }">
	<MvASSIGN NAME = "l.copyproductrules:invset"		VALUE = "{ CopyProductRules.d.invset }">
	<MvASSIGN NAME = "l.copyproductrules:invlevel"		VALUE = "{ CopyProductRules.d.invlevel }">
	<MvASSIGN NAME = "l.copyproductrules:images"		VALUE = "{ CopyProductRules.d.images }">
	<MvASSIGN NAME = "l.copyproductrules:relprod"		VALUE = "{ CopyProductRules.d.relprod }">
	<MvASSIGN NAME = "l.copyproductrules:upsale"		VALUE = "{ CopyProductRules.d.upsale }">
	<MvASSIGN NAME = "l.copyproductrules:availgroup"	VALUE = "{ CopyProductRules.d.availgroup }">
	<MvASSIGN NAME = "l.copyproductrules:pricegroup"	VALUE = "{ CopyProductRules.d.pricegroup }">
	<MvASSIGN NAME = "l.copyproductrules:ddownload"		VALUE = "{ CopyProductRules.d.ddownload }">
	<MvASSIGN NAME = "l.copyproductrules:giftcert"		VALUE = "{ CopyProductRules.d.giftcert }">
	<MvASSIGN NAME = "l.copyproductrules:subscrip"		VALUE = "{ CopyProductRules.d.subscrip }">
	<MvASSIGN NAME = "l.copyproductrules:payment"		VALUE = "{ CopyProductRules.d.payment }">
	<MvASSIGN NAME = "l.copyproductrules:shipping"		VALUE = "{ CopyProductRules.d.shipping }">
	<MvASSIGN NAME = "l.copyproductrules:kit"			VALUE = "{ CopyProductRules.d.kit }">
	<MvASSIGN NAME = "l.copyproductrules:variants"		VALUE = "{ CopyProductRules.d.variants }">
</MvFUNCTION>

<MvFUNCTION NAME = "CopyProductRule_Insert" PARAMETERS = "copyproductrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.copyproductrules:id" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'CopyProductRules' ) }">

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'CopyProductRules
			 			  ( id, name, product, attributes, categories, invset, invlevel, images, relprod, upsale, availgroup, pricegroup, ddownload, giftcert, subscrip, payment, shipping, kit, variants )
			 			  VALUES
			 			  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS = "l.copyproductrules:id, l.copyproductrules:name, l.copyproductrules:product, l.copyproductrules:attributes, l.copyproductrules:categories,
			 		   l.copyproductrules:invset, l.copyproductrules:invlevel, l.copyproductrules:images, l.copyproductrules:relprod, l.copyproductrules:upsale, l.copyproductrules:availgroup,
			 		   l.copyproductrules:pricegroup, l.copyproductrules:ddownload, l.copyproductrules:giftcert, l.copyproductrules:subscrip, l.copyproductrules:payment, l.copyproductrules:shipping,
			 		   l.copyproductrules:kit, l.copyproductrules:variants">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CopyProductRule_Update" PARAMETERS = "copyproductrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'CopyProductRules
			 			  SET
			 			  	name		= ?,
			 			  	product		= ?,
			 			  	attributes	= ?,
			 			  	categories	= ?,
			 			  	invset		= ?,
			 			  	invlevel	= ?,
			 			  	images		= ?,
			 			  	relprod		= ?,
			 			  	upsale		= ?,
			 			  	availgroup	= ?,
			 			  	pricegroup	= ?,
			 			  	ddownload	= ?,
			 			  	giftcert	= ?,
			 			  	subscrip	= ?,
			 			  	payment		= ?,
			 			  	shipping	= ?,
			 			  	kit			= ?,
			 			  	variants	= ?
			 			  WHERE
			 			  	id			= ?' }"
			 FIELDS = "l.copyproductrules:name, l.copyproductrules:product, l.copyproductrules:attributes, l.copyproductrules:categories, l.copyproductrules:invset, l.copyproductrules:invlevel,
			 		   l.copyproductrules:images, l.copyproductrules:relprod, l.copyproductrules:upsale, l.copyproductrules:availgroup, l.copyproductrules:pricegroup, l.copyproductrules:ddownload, l.copyproductrules:giftcert,
			 		   l.copyproductrules:subscrip, l.copyproductrules:payment, l.copyproductrules:shipping, l.copyproductrules:kit, l.copyproductrules:variants,
			 		   l.copyproductrules:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00009', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CopyProductRules_Load_ID" PARAMETERS = "id, copyproductrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "CopyProductRules"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CopyProductRules WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00011', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ CopyProductRules.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "CopyProductRules">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-CPD-DTB-00012' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ CopyProductRules_Read( l.copyproductrules ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CopyProductRules">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CopyProductRules_Load_Name" PARAMETERS = "name, copyproductrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "CopyProductRules"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CopyProductRules WHERE ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'name' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.name">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00013', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ CopyProductRules.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "CopyProductRules">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-CPD-DTB-00014' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ CopyProductRules_Read( l.copyproductrules ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CopyProductRules">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CopyProductRules_Delete" PARAMETERS = "copyproductrules var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT CopyProductRulesXModule_Delete_All_RulesID( l.copyproductrules:id )			OR
					NOT CopyProductRulesXCustomField_Delete_All_RulesID( l.copyproductrules:id )	OR
					NOT CopyProductRules_Delete_ID( l.copyproductrules:id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CopyProductRules_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CopyProductRules WHERE id = ?' }"
			 FIELDS = "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00015', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_CopyProductRulesXModule
|
</MvCOMMENT>

<MvFUNCTION NAME = "CopyProductRulesXModule_Read" PARAMETERS = "copyproductrulesxmodule var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.copyproductrulesxmodule"			VALUE = "">
	<MvASSIGN NAME = "l.copyproductrulesxmodule:rules_id"	VALUE = "{ CopyProductRulesXModule.d.rules_id }">
	<MvASSIGN NAME = "l.copyproductrulesxmodule:mod_code"	VALUE = "{ CopyProductRulesXModule.d.mod_code }">
</MvFUNCTION>

<MvFUNCTION NAME = "CopyProductRulesXModule_Insert" PARAMETERS = "rules_id, mod_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'CopyProductRulesXModule
			 			  ( rules_id, mod_code )
			 			  VALUES
			 			  ( ?, ? )' }"
			 FIELDS = "l.rules_id, l.mod_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00016', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CopyProductRulesXModule_Load" PARAMETERS = "rules_id, mod_code, copyproductrulesxmodule var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "CopyProductRulesXModule"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CopyProductRulesXModule WHERE rules_id = ? AND ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'mod_code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.rules_id, l.mod_code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00017', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ CopyProductRulesXModule.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "CopyProductRulesXModule">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-CPD-DTB-00018' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ CopyProductRulesXModule_Read( l.copyproductrulesxmodule ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CopyProductRulesXModule">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CopyProductRulesXModuleList_Load_RulesID" PARAMETERS = "rules_id, copyproductrulesxmodules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "CopyProductRulesXModule"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CopyProductRulesXModule WHERE rules_id = ?' }"
				FIELDS	= "l.rules_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00019', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT CopyProductRulesXModule.d.EOF }">
		<MvEVAL EXPR = "{ CopyProductRulesXModule_Read( l.copyproductrulesxmodules[ ++l.count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "CopyProductRulesXModule" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CopyProductRulesXModule">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-CPD-DTB-00020', l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "CopyProductRulesXModule_Delete" PARAMETERS = "rules_id, mod_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CopyProductRulesXModule WHERE rules_id = ? AND ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'mod_code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
			 FIELDS = "l.rules_id, l.mod_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00021', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CopyProductRulesXModule_Delete_All_RulesID" PARAMETERS = "rules_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CopyProductRulesXModule WHERE rules_id = ?' }"
			 FIELDS = "l.rules_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00022', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_CopyProductRulesXCustomField
|
</MvCOMMENT>

<MvFUNCTION NAME = "CopyProductRulesXCustomField_Read" PARAMETERS = "copyproductrulesxcustomfield var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.copyproductrulesxcustomfield"				VALUE = "">
	<MvASSIGN NAME = "l.copyproductrulesxcustomfield:rules_id"		VALUE = "{ CopyProductRulesXCustomField.d.rules_id }">
	<MvASSIGN NAME = "l.copyproductrulesxcustomfield:mod_code"		VALUE = "{ CopyProductRulesXCustomField.d.mod_code }">
	<MvASSIGN NAME = "l.copyproductrulesxcustomfield:field_code"	VALUE = "{ CopyProductRulesXCustomField.d.field_code }">
</MvFUNCTION>

<MvFUNCTION NAME = "CopyProductRulesXCustomField_Insert" PARAMETERS = "rules_id, mod_code, field_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'CopyProductRulesXCustomField
			 			  ( rules_id, mod_code, field_code )
			 			  VALUES
			 			  ( ?, ?, ? )' }"
			 FIELDS = "l.rules_id, l.mod_code, l.field_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00023', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CopyProductRulesXCustomField_Load" PARAMETERS = "rules_id, mod_code, field_code, copyproductrulesxcustomfield var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "CopyProductRulesXCustomField"
				QUERY 	= "{ 'SELECT
								*
							  FROM ' $
								g.Store_Table_Prefix $ 'CopyProductRulesXCustomField
							  WHERE
								rules_id = ? AND ' $
								[ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'mod_code' )	$ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) $ ' AND ' $
								[ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'field_code' )	$ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.rules_id, l.mod_code, l.field_code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00024', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ CopyProductRulesXCustomField.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "CopyProductRulesXCustomField">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-CPD-DTB-00025' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ CopyProductRulesXCustomField_Read( l.copyproductrulesxcustomfield ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CopyProductRulesXCustomField">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CopyProductRulesXCustomFieldList_Load_RulesID" PARAMETERS = "rules_id, copyproductrulesxcustomfields var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "CopyProductRulesXCustomField"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CopyProductRulesXCustomField WHERE rules_id = ? ORDER BY mod_code, field_code' }"
				FIELDS	= "l.rules_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00026', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT CopyProductRulesXCustomField.d.EOF }">
		<MvEVAL EXPR = "{ CopyProductRulesXCustomField_Read( l.copyproductrulesxcustomfields[ ++l.count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "CopyProductRulesXCustomField" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CopyProductRulesXCustomField">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-CPD-DTB-00027', l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "CopyProductRulesXCustomField_Delete" PARAMETERS = "rules_id, mod_code, field_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CopyProductRulesXCustomField
						  WHERE
							rules_id = ? AND ' $
							[ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'mod_code' )	$ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) $ ' AND ' $
							[ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'field_code' )	$ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
			 FIELDS = "l.rules_id, l.mod_code, l.field_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00028', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CopyProductRulesXCustomField_Delete_All_RulesID" PARAMETERS = "rules_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CopyProductRulesXCustomField WHERE rules_id = ?' }"
			 FIELDS = "l.rules_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-CPD-DTB-00029', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

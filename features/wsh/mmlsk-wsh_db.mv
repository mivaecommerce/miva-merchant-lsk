<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2025 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-WSH-DTB-
| Next Error Code: 67
|
</MvCOMMENT>

<MvFUNCTION NAME = "WSH_Store_Create" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| sNN_WishLists
	|
	</MvCOMMENT>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'WishLists
						  (
							id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							cust_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							title		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )	$ ',
							notes		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()		$ ',
							shared		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'WishLists_1 ON ' $ g.Store_Table_Prefix $ 'WishLists ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'WishLists_2 ON ' $ g.Store_Table_Prefix $ 'WishLists ( cust_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'WishLists', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_WishListItems
	|
	</MvCOMMENT>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'WishListItems
						  (
							id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()	$ ',
							wshlst_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()	$ ',
							parent_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()	$ ',
							product_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()	$ ',
							dtadded		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()	$ ',
							quantity	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()	$ ',
							notes		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()	$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'WishListItems_1 ON ' $ g.Store_Table_Prefix $ 'WishListItems ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'WishListItems_2 ON ' $ g.Store_Table_Prefix $ 'WishListItems ( wshlst_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'WishListItems_3 ON ' $ g.Store_Table_Prefix $ 'WishListItems ( wshlst_id, parent_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00060', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'WishListItems_4 ON ' $ g.Store_Table_Prefix $ 'WishListItems ( product_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00065', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'WishListItems', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_WishListOptions
	|
	</MvCOMMENT>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'WishListOptions
						  (
							wish_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							attr_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							attmpat_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							option_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							data		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )	$ ',
							data_long	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()		$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'WishListOptions_1 ON ' $ g.Store_Table_Prefix $ 'WishListOptions ( wish_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WSH_Store_Delete" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'WishLists' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'WishListItems' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'WishListOptions' }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_WishLists
|
</MvCOMMENT>

<MvFUNCTION NAME = "WishList_Read" PARAMETERS = "wishlist var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.wishlist:id"		VALUE = "{ WishLists.d.id }">
	<MvASSIGN NAME = "l.wishlist:cust_id"	VALUE = "{ WishLists.d.cust_id }">
	<MvASSIGN NAME = "l.wishlist:title"		VALUE = "{ WishLists.d.title }">
	<MvASSIGN NAME = "l.wishlist:notes"		VALUE = "{ WishLists.d.notes }">
	<MvASSIGN NAME = "l.wishlist:shared"	VALUE = "{ WishLists.d.shared }">
</MvFUNCTION>

<MvFUNCTION NAME = "WishList_Insert" PARAMETERS = "wishlist var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.wishlist:id" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'WishLists' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'WishLists
					      ( id, cust_id, title, notes, shared )
						  VALUES
						  ( ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.wishlist:id, l.wishlist:cust_id, l.wishlist:title, l.wishlist:notes, l.wishlist:shared">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00012', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishList_Update" PARAMETERS = "wishlist var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'WishLists
					      SET
							title		= ?,
							notes		= ?,
							shared	= ?
						  WHERE
						  	id			= ?' }"
			 FIELDS	= "l.wishlist:title, l.wishlist:notes, l.wishlist:shared,
			 		   l.wishlist:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00013', g.MvQUERY_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "g.Session:cache:wishlist_load_id"				INDEX = "{ l.wishlist:id }" VALUE = "">
	<MvASSIGN NAME = "g.Session:cache:wishlist_load_wishlistitem" 	INDEX = "{ l.wishlist:id }" VALUE = "">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishLists_Delete_All_Customer" PARAMETERS = "cust_id" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.wishlist" ARRAY = "l.wishlists" COUNT = "{ WishListList_Load_Customer( l.cust_id, l.wishlists ) }">
		<MvIF EXPR = "{ NOT WishList_Delete( l.wishlist ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishList_Delete" PARAMETERS = "wishlist var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT WishListItems_Delete_All_WishList( l.wishlist:id ) OR
					NOT WishList_Delete_LowLevel( l.wishlist:id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishList_Delete_LowLevel" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'WishLists WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00014', g.MvQUERY_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "g.Session:cache:wishlist_load_id"				INDEX = "{ l.id }" VALUE = "">
	<MvASSIGN NAME = "g.Session:cache:wishlist_load_wishlistitem" 	INDEX = "{ l.id }" VALUE = "">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishList_Load_ID" PARAMETERS = "id, wishlist var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "WishLists"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'WishLists WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00015', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ WishLists.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishLists">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-WSH-DTB-00016' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ WishList_Read( l.wishlist ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishLists">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishList_Load_ID_Cached" PARAMETERS = "id, wishlist var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.id"								VALUE = "{ int( l.id ) }">

	<MvIF EXPR = "{ l.id LE 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-WSH-DTB-00017' ) }">
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.cache"					VARIABLE = "g.Session:cache:wishlist_load_id">
		<MvDIMENSION INDEX = "{ l.id }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result" 				VALUE = "{ WishList_Load_ID( l.id, l.cache:wishlist ) }">
		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE =  "{ [ g.Module_Library_DB ].Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.wishlist"						VALUE = "{ l.cache:wishlist }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishList_Load_WishListItem" PARAMETERS = "wish_id, wishlist var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "WishLists"
				QUERY	= "{ 'SELECT
								wl.*
							  FROM ' $
							  	g.Store_Table_Prefix $ 'WishListItems wli, ' $
							  	g.Store_Table_Prefix $ 'WishLists wl
							  WHERE
							  	wli.id 			= ? AND
							  	wli.wshlst_id 	= wl.id' }"
				FIELDS	= "l.wish_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00020', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ WishLists.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishLists">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-WSH-DTB-00021' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ WishList_Read( l.wishlist ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishLists">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishList_Load_WishListItem_Cached" PARAMETERS = "wish_id, wishlist var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.wish_id" VALUE = "{ int( l.wish_id ) }">

	<MvIF EXPR = "{ l.wish_id LE 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-WSH-DTB-00022' ) }">
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.cache" VARIABLE = "g.Session:cache:wishlist_load_wishlistitem">
		<MvDIMENSION INDEX = "{ l.wish_id }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result" 				VALUE = "{ WishList_Load_WishListItem( l.wish_id, l.cache:wishlist ) }">

		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code" 		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message" 	VALUE = "{ g.Error_Message }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.wishlist" VALUE = "{ l.cache:wishlist }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListList_Load_Customer" PARAMETERS = "cust_id, wishlists var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "WishLists"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'WishLists WHERE cust_id = ? ORDER BY id' }"
				FIELDS	= "l.cust_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00023', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.wishlist_count"	VALUE = 0>
	<MvWHILE EXPR = "{ NOT WishLists.d.EOF }">
		<MvEVAL EXPR = "{ WishList_Read( l.wishlists[ ++l.wishlist_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "WishLists">
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishLists">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-WSH-DTB-00024', l.wishlist_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "WishListList_Load_Offset_Customer" PARAMETERS = "cust_id, offset, max, sort, nextoffset var, wishlists var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.sort EQ 'id_desc' }">				<MvASSIGN NAME = "l.sort_col" VALUE = "wish.id">			<MvASSIGN NAME = "l.sort_dir" VALUE = "DESC">
	<MvELSEIF EXPR = "{ l.sort EQ 'title_asc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "wish.title">			<MvASSIGN NAME = "l.sort_dir" VALUE = "ASC">
	<MvELSEIF EXPR = "{ l.sort EQ 'title_desc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "wish.title">			<MvASSIGN NAME = "l.sort_dir" VALUE = "DESC">
	<MvELSEIF EXPR = "{ l.sort EQ 'fname_asc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "cust.bill_fname">	<MvASSIGN NAME = "l.sort_dir" VALUE = "ASC">
	<MvELSEIF EXPR = "{ l.sort EQ 'fname_desc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "cust.bill_fname">	<MvASSIGN NAME = "l.sort_dir" VALUE = "DESC">
	<MvELSEIF EXPR = "{ l.sort EQ 'lname_asc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "cust.bill_lname">	<MvASSIGN NAME = "l.sort_dir" VALUE = "ASC">
	<MvELSEIF EXPR = "{ l.sort EQ 'lname_desc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "cust.bill_lname">	<MvASSIGN NAME = "l.sort_dir" VALUE = "DESC">
	<MvELSE>											<MvASSIGN NAME = "l.sort_col" VALUE = "wish.id">			<MvASSIGN NAME = "l.sort_dir" VALUE = "ASC">
	</MvIF>

	<MvIF EXPR = "{ l.max EQ 0 }">	<MvASSIGN NAME = "l.limit" VALUE = 0>
	<MvELSE>						<MvASSIGN NAME = "l.limit" VALUE = "{ l.max + 1 }">
	</MvIF>

	<MvASSIGN NAME = "g.Session:cache:wishlistlist_load_offset_customer:cust_id"	VALUE = "{ l.cust_id }">
	<MvASSIGN NAME = "l.fields"														VALUE = "g.Session:cache:wishlistlist_load_offset_customer:cust_id">

	<MvASSIGN NAME = "l.query"	VALUE = "{ 'SELECT
												wish.*,
												cust.bill_fname,
												cust.bill_lname
											FROM ' $ 
												g.Store_Table_Prefix $ 'WishLists wish
												LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'Customers cust ON wish.cust_id = cust.id
											WHERE
												wish.cust_id = ? 
											ORDER BY ' $
												l.sort_col $ ' ' $ l.sort_dir }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'WishLists',
																			 l.query, l.fields,
																			 l.offset, l.limit ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00025', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.wishlist_count" VALUE = 0>
	<MvWHILE EXPR = "{ ( NOT WishLists.d.EOF ) AND ( ( l.max EQ 0 ) OR ( l.wishlist_count LT l.max ) ) }">
		<MvEVAL EXPR = "{ WishList_Read( l.wishlists[ ++l.wishlist_count ] ) }">

		<MvASSIGN NAME = "l.wishlists" INDEX = "{ l.wishlist_count }" MEMBER = "bill_fname" VALUE = "{ WishLists.d.bill_fname }">
		<MvASSIGN NAME = "l.wishlists" INDEX = "{ l.wishlist_count }" MEMBER = "bill_lname" VALUE = "{ WishLists.d.bill_lname }">

		<MvSKIP NAME = "Merchant" VIEW = "WishLists" ROWS = 1>
	</MvWHILE>
	
	<MvIF EXPR = "{ WishLists.d.EOF }">
		<MvASSIGN NAME = "l.nextoffset"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.nextoffset"	VALUE = "{ l.offset + l.wishlist_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishLists">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-WSH-DTB-00026', l.wishlist_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "WishList_Count_Customer" PARAMETERS = "cust_id, count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "WishLists"
				QUERY	= "{ 'SELECT
								COUNT( * ) AS total_count
							  FROM ' $
								g.Store_Table_Prefix $ 'WishLists
							  WHERE
								cust_id = ?' }"
				FIELDS	= "l.cust_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ WishLists.d.EOF }">	<MvASSIGN NAME = "l.count" VALUE = 0>
	<MvELSE>							<MvASSIGN NAME = "l.count" VALUE = "{ WishLists.d.total_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishLists">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListList_Load_Offset_Customer_Search" PARAMETERS = "search, cust_id, offset, max, sort, nextoffset var, wishlists var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.search"				VALUE = "{ trim( l.search ) }">

	<MvASSIGN NAME = "l.search_clause"		VALUE = "{ [ g.Module_Library_DB ].SQL_Search_Clause( l.search, '', 'wish.title,cust.bill_fname,cust.bill_lname', l.fields ) }">
	<MvIF EXPR = "{ NOT ISNULL l.search_clause }">
		<MvASSIGN NAME = "l.search_clause"	VALUE = "{ 'AND ' $ l.search_clause }">	
	</MvIF>

	<MvIF EXPR = "{ l.sort EQ 'id_desc' }">				<MvASSIGN NAME = "l.sort_col" VALUE = "wish.id">			<MvASSIGN NAME = "l.sort_dir" VALUE = "DESC">
	<MvELSEIF EXPR = "{ l.sort EQ 'title_asc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "wish.title">			<MvASSIGN NAME = "l.sort_dir" VALUE = "ASC">
	<MvELSEIF EXPR = "{ l.sort EQ 'title_desc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "wish.title">			<MvASSIGN NAME = "l.sort_dir" VALUE = "DESC">
	<MvELSEIF EXPR = "{ l.sort EQ 'fname_asc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "cust.bill_fname">	<MvASSIGN NAME = "l.sort_dir" VALUE = "ASC">
	<MvELSEIF EXPR = "{ l.sort EQ 'fname_desc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "cust.bill_fname">	<MvASSIGN NAME = "l.sort_dir" VALUE = "DESC">
	<MvELSEIF EXPR = "{ l.sort EQ 'lname_asc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "cust.bill_lname">	<MvASSIGN NAME = "l.sort_dir" VALUE = "ASC">
	<MvELSEIF EXPR = "{ l.sort EQ 'lname_desc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "cust.bill_lname">	<MvASSIGN NAME = "l.sort_dir" VALUE = "DESC">
	<MvELSE>											<MvASSIGN NAME = "l.sort_col" VALUE = "wish.id">			<MvASSIGN NAME = "l.sort_dir" VALUE = "ASC">
	</MvIF>

	<MvIF EXPR = "{ l.max EQ 0 }">	<MvASSIGN NAME = "l.limit" VALUE = 0>
	<MvELSE>						<MvASSIGN NAME = "l.limit" VALUE = "{ l.max + 1 }">
	</MvIF>

	<MvASSIGN NAME = "g.Session:cache:wishlistlist_load_offset_customer_search:cust_id" VALUE = "{ l.cust_id }">

	<MvIF EXPR = "{ NOT ISNULL l.fields }">		<MvASSIGN NAME = "l.fields" VALUE = "{ 'g.Session:cache:wishlistlist_load_offset_customer_search:cust_id,' $ l.fields }">
	<MvELSE>									<MvASSIGN NAME = "l.fields" VALUE = "g.Session:cache:wishlistlist_load_offset_customer_search:cust_id">
	</MvIF>

	<MvASSIGN NAME = "l.query"	VALUE = "{ 'SELECT
												wish.*,
												cust.bill_fname,
												cust.bill_lname
											FROM ' $ 
												g.Store_Table_Prefix $ 'WishLists wish
												LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'Customers cust ON wish.cust_id = cust.id
											WHERE
												(
													wish.shared 	= 1 OR
													wish.cust_id 	= ?
												) ' $
												l.search_clause $ '
											ORDER BY ' $
												l.sort_col $ ' ' $ l.sort_dir }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'WishLists',
																			 l.query, l.fields,
																			 l.offset, l.limit ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00027', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.wishlist_count" VALUE = 0>
	<MvWHILE EXPR = "{ ( NOT WishLists.d.EOF ) AND ( ( l.max EQ 0 ) OR ( l.wishlist_count LT l.max ) ) }">
		<MvEVAL EXPR = "{ WishList_Read( l.wishlists[ ++l.wishlist_count ] ) }">

		<MvASSIGN NAME = "l.wishlists" INDEX = "{ l.wishlist_count }" MEMBER = "bill_fname" VALUE = "{ WishLists.d.bill_fname }">
		<MvASSIGN NAME = "l.wishlists" INDEX = "{ l.wishlist_count }" MEMBER = "bill_lname" VALUE = "{ WishLists.d.bill_lname }">

		<MvSKIP NAME = "Merchant" VIEW = "WishLists" ROWS = 1>
	</MvWHILE>
	
	<MvIF EXPR = "{ WishLists.d.EOF }">
		<MvASSIGN NAME = "l.nextoffset"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.nextoffset"	VALUE = "{ l.offset + l.wishlist_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishLists">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-WSH-DTB-00028', l.wishlist_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "WishList_Count_Customer_Search" PARAMETERS = "search, cust_id, count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.search"				VALUE = "{ trim( l.search ) }">

	<MvASSIGN NAME = "l.search_clause"		VALUE = "{ [ g.Module_Library_DB ].SQL_Search_Clause( l.search, '', 'wish.title,cust.bill_fname,cust.bill_lname', l.fields ) }">
	<MvIF EXPR = "{ NOT ISNULL l.search_clause }">
		<MvASSIGN NAME = "l.search_clause"	VALUE = "{ 'AND ' $ l.search_clause }">	
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.fields }">		<MvASSIGN NAME = "l.fields" VALUE = "{ 'l.cust_id,' $ l.fields }">
	<MvELSE>									<MvASSIGN NAME = "l.fields" VALUE = "l.cust_id">
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "WishLists"
				QUERY	= "{ 'SELECT
								COUNT( * ) AS total_count
							  FROM ' $
								g.Store_Table_Prefix $ 'WishLists wish 
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'Customers cust ON wish.cust_id = cust.id
							  WHERE
								(
									wish.shared 	= 1 OR
									wish.cust_id 	= ?
								) ' $
								l.search_clause }"
				FIELDS	= "{ l.fields }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ WishLists.d.EOF }">	<MvASSIGN NAME = "l.count" VALUE = 0>
	<MvELSE>							<MvASSIGN NAME = "l.count" VALUE = "{ WishLists.d.total_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishLists">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_WishListItems
|
</MvCOMMENT>

<MvFUNCTION NAME = "WishListItem_Read" PARAMETERS = "wishlistitem var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.wishlistitem:id"			VALUE = "{ WishListItems.d.id }">
	<MvASSIGN NAME = "l.wishlistitem:wshlst_id"		VALUE = "{ WishListItems.d.wshlst_id }">
	<MvASSIGN NAME = "l.wishlistitem:parent_id"		VALUE = "{ WishListItems.d.parent_id }">
	<MvASSIGN NAME = "l.wishlistitem:product_id"	VALUE = "{ WishListItems.d.product_id }">
	<MvASSIGN NAME = "l.wishlistitem:dtadded"		VALUE = "{ WishListItems.d.dtadded }">
	<MvASSIGN NAME = "l.wishlistitem:quantity"		VALUE = "{ WishListItems.d.quantity }">
	<MvASSIGN NAME = "l.wishlistitem:notes"			VALUE = "{ WishListItems.d.notes }">
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItem_Insert" PARAMETERS = "wishlistitem var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.wishlistitem:id" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'WishListItems' ) }">

	<MvIF EXPR = "{ ISNULL l.wishlistitem:parent_id }">
		<MvASSIGN NAME = "l.wishlistitem:parent_id" VALUE = 0>
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'WishListItems
					      ( id, wshlst_id, parent_id, product_id, dtadded, quantity, notes )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.wishlistitem:id, l.wishlistitem:wshlst_id, l.wishlistitem:parent_id, l.wishlistitem:product_id, l.wishlistitem:dtadded, l.wishlistitem:quantity, l.wishlistitem:notes">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00029', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItem_Update" PARAMETERS = "wishlistitem var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'WishListItems
					      SET
							quantity	= ?,
							notes		= ?
						  WHERE
						  	id			= ?' }"
			 FIELDS	= "l.wishlistitem:quantity, l.wishlistitem:notes,
			 		   l.wishlistitem:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00030', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItem_Update_Child_Quantity" PARAMETERS = "parent_wishlistitem var, original_quantity, quantity" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.quantity EQ l.original_quantity }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFOREACH ITERATOR = "l.wishlistitem" ARRAY = "l.wishlistitems" COUNT = "{ WishListItemList_Load_Parent( l.parent_wishlistitem:wshlst_id, l.parent_wishlistitem:id, l.wishlistitems ) }">
		<MvIF EXPR = "{ ( l.wishlistitem:quantity LT l.original_quantity ) OR ( fmod( l.wishlistitem:quantity, l.original_quantity ) NE 0 ) }">
			<MvCOMMENT>
			|
			| Skip processing. Child-parent quantity ratio is less than 1:1, or the ratio is not a whole number.
			|
			</MvCOMMENT>

			<MvFOREACHCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.wishlistitem:quantity" VALUE = "{ l.quantity * ( l.wishlistitem:quantity / l.original_quantity ) }">

		<MvIF EXPR = "{ l.wishlistitem:quantity GT 0 }">
			<MvIF EXPR = "{ NOT WishListItem_Update( l.wishlistitem ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ NOT WishListItem_Delete( l.wishlistitem ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItem_Update_WishList" PARAMETERS = "wish_id, wshlst_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'WishListItems
					      SET
							wshlst_id	= ?
						  WHERE
						  	id			= ?' }"
			 FIELDS	= "l.wshlst_id, l.wish_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00031', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItem_Increment_Quantity" PARAMETERS = "id, quantity" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'WishListItems
					      SET
							quantity	= quantity + ?
						  WHERE
						  	id			= ?' }"
			 FIELDS	= "l.quantity,
			 		   l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00032', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItems_Delete_All_WishList" PARAMETERS = "wshlst_id" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.wishlistitem" ARRAY = "l.wishlistitems" COUNT = "{ WishListItemList_Load_WishList( l.wshlst_id, l.wishlistitems ) }">
		<MvIF EXPR = "{ NOT WishListItem_Delete( l.wishlistitem ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItems_Delete_All_Product" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.wishlistitem" ARRAY = "l.wishlistitems" COUNT = "{ WishListItemList_Load_Product( l.product_id, l.wishlistitems ) }">
		<MvIF EXPR = "{ NOT WishListItem_Delete( l.wishlistitem ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItems_Delete_All_Attribute" PARAMETERS = "attr_id" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.wishlistitem" ARRAY = "l.wishlistitems" COUNT = "{ WishListItemList_Load_Attribute( l.attr_id, l.wishlistitems ) }">
		<MvIF EXPR = "{ NOT WishListItem_Delete( l.wishlistitem ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItems_Delete_All_Option" PARAMETERS = "attr_id, option_id" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.wishlistitem" ARRAY = "l.wishlistitems" COUNT = "{ WishListItemList_Load_Option( l.attr_id, l.option_id, l.wishlistitems ) }">
		<MvIF EXPR = "{ NOT WishListItem_Delete( l.wishlistitem ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItems_Delete_All_AttributeTemplateAttribute" PARAMETERS = "attmpat_id" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.wishlistitem" ARRAY = "l.wishlistitems" COUNT = "{ WishListItemList_Load_AttributeTemplateAttribute( l.attmpat_id, l.wishlistitems ) }">
		<MvIF EXPR = "{ NOT WishListItem_Delete( l.wishlistitem ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItems_Delete_All_AttributeTemplateOption" PARAMETERS = "attmpat_id, option_id" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.wishlistitem" ARRAY = "l.wishlistitems" COUNT = "{ WishListItemList_Load_AttributeTemplateOption( l.attmpat_id, l.option_id, l.wishlistitems ) }">
		<MvIF EXPR = "{ NOT WishListItem_Delete( l.wishlistitem ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItem_Delete" PARAMETERS = "wishlistitem var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT WishListItem_Delete_LowLevel( l.wishlistitem:id )			OR
					NOT WishListOption_Delete_All_WishListItem( l.wishlistitem:id )	OR
					NOT WishListItem_Delete_Children( l.wishlistitem ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItem_Delete_Children" PARAMETERS = "wishlistitem var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.child_wishlistitem" ARRAY = "l.child_wishlistitems" COUNT = "{ WishListItemList_Load_Parent( l.wishlistitem:wshlst_id, l.wishlistitem:id, l.child_wishlistitems ) }">
		<MvIF EXPR = "{ NOT WishListItem_Delete( l.child_wishlistitem ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItem_Delete_LowLevel" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'WishListItems WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00033', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItem_Load_ID" PARAMETERS = "id, wishlistitem var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "WishListItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'WishListItems WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00034', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ WishListItems.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishListItems">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-WSH-DTB-00035' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ WishListItem_Read( l.wishlistitem ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishListItems">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItemList_Load_WishList" PARAMETERS = "wshlst_id, wishlistitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "WishListItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'WishListItems WHERE wshlst_id = ? ORDER BY parent_id, id' }"
				FIELDS	= "l.wshlst_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00036', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.wishlistitem_count"	VALUE = 0>
	<MvASSIGN NAME = "l.parent_indexes"		VALUE = "">

	<MvCOMMENT>
	|
	| Process top-level WishListItems
	|
	</MvCOMMENT>

	<MvWHILE EXPR = "{ NOT WishListItems.d.EOF }">
		<MvIF EXPR = "{ WishListItems.d.parent_id }">
			<MvWHILESTOP>
		</MvIF>

		<MvREFERENCEARRAY NAME = "l.wishlistitem" VARIABLE = "l.wishlistitems">
			<MvDIMENSION INDEX = "{ ++l.wishlistitem_count }">
		</MvREFERENCEARRAY>

		<MvEVAL EXPR = "{ WishListItem_Read( l.wishlistitem ) }">

		<MvASSIGN NAME = "l.wishlistitem:child_count"						VALUE = 0>
		<MvASSIGN NAME = "l.parent_indexes"	INDEX = "{ l.wishlistitem:id }"	VALUE = "{ l.wishlistitem_count }">

		<MvSKIP NAME = "Merchant" VIEW = "WishListItems" ROWS = 1>
	</MvWHILE>

	<MvCOMMENT>
	|
	| Process children, end result is that the child ends up in both:
	|
	|	parent:children[]
	|	wishlistitems[ following parent ]
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.last_parent_id"							VALUE = 0>
	<MvASSIGN NAME = "l.last_parent_index_correction"			VALUE = 0>
	<MvASSIGN NAME = "l.parent_index_correction"				VALUE = 0>

	<MvWHILE EXPR = "{ NOT WishListItems.d.EOF }">
		<MvIF EXPR = "{ WishListItems.d.parent_id NE l.last_parent_id }">
			<MvASSIGN NAME = "l.last_parent_index_correction"   VALUE = "{ l.last_parent_index_correction + l.parent_index_correction }">
			<MvASSIGN NAME = "l.last_parent_id"                 VALUE = "{ WishListItems.d.parent_id }">
			<MvASSIGN NAME = "l.parent_index_correction"		VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT miva_element_exists( l.parent_indexes, WishListItems.d.parent_id ) }">
			<MvEVAL EXPR = "{ WishListItem_Read( l.wishlistitems[ ++l.wishlistitem_count ] ) }">
		<MvELSE>
			<MvREFERENCEARRAY NAME = "l.parent_wishlistitem" VARIABLE = "l.wishlistitems">
				<MvDIMENSION INDEX = "{ l.parent_indexes[ WishListItems.d.parent_id ] + l.last_parent_index_correction }">
			</MvREFERENCEARRAY>
	
			<MvEVAL EXPR = "{ WishListItem_Read( l.parent_wishlistitem:children[ ++l.parent_wishlistitem:child_count ] ) }">

			<MvASSIGN NAME = "l.wishlistitem_count"				VALUE = "{ miva_array_insert_ref( l.wishlistitems, l.parent_wishlistitem:children[ l.parent_wishlistitem:child_count ], l.parent_indexes[ WishListItems.d.parent_id ] + l.last_parent_index_correction + l.parent_index_correction + 1 ) }">
			<MvASSIGN NAME = "l.parent_index_correction"		VALUE = "{ l.parent_index_correction + 1 }">
		</MvIF> 

		<MvSKIP NAME = "Merchant" VIEW = "WishListItems" ROWS = 1>
	 </MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishListItems">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-WSH-DTB-00037', l.wishlistitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItemList_Load_Parent" PARAMETERS = "wshlst_id, parent_id, wishlistitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "WishListItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'WishListItems WHERE wshlst_id = ? AND parent_id = ? ORDER BY id' }"
				FIELDS	= "l.wshlst_id, l.parent_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00061', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.wishlistitem_count"	VALUE = 0>

	<MvWHILE EXPR = "{ NOT WishListItems.d.EOF }">
		<MvEVAL EXPR = "{ WishListItem_Read( l.wishlistitems[ ++l.wishlistitem_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "WishListItems">
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishListItems">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-WSH-DTB-00062', l.wishlistitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItemList_Load_Product" PARAMETERS = "product_id, wishlistitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "WishListItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'WishListItems WHERE product_id = ?' }"
				FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00038', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.wishlistitem_count"	VALUE = 0>
	<MvWHILE EXPR = "{ NOT WishListItems.d.EOF }">
		<MvEVAL EXPR = "{ WishListItem_Read( l.wishlistitems[ ++l.wishlistitem_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "WishListItems">
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishListItems">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-WSH-DTB-00039', l.wishlistitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItemList_Load_Attribute" PARAMETERS = "attr_id, wishlistitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "WishListItems"
				QUERY	= "{ 'SELECT
								wli.*
							  FROM ' $ 
							  	g.Store_Table_Prefix $ 'WishListItems wli, ' $
							  	g.Store_Table_Prefix $ 'WishListOptions wlo
							  WHERE
							  	wli.id 		= wlo.wish_id AND
							  	wlo.attr_id	= ?' }"
				FIELDS	= "l.attr_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00040', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.wishlistitem_count"	VALUE = 0>
	<MvWHILE EXPR = "{ NOT WishListItems.d.EOF }">
		<MvEVAL EXPR = "{ WishListItem_Read( l.wishlistitems[ ++l.wishlistitem_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "WishListItems">
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishListItems">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-WSH-DTB-00041', l.wishlistitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItemList_Load_Option" PARAMETERS = "attr_id, option_id, wishlistitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "WishListItems"
				QUERY	= "{ 'SELECT
								wli.*
							  FROM ' $ 
							  	g.Store_Table_Prefix $ 'WishListItems wli, ' $
							  	g.Store_Table_Prefix $ 'WishListOptions wlo
							  WHERE
							  	wli.id 			= wlo.wish_id 	AND
							  	wlo.attr_id 	= ? 			AND
							  	wlo.option_id	= ?' }"
				FIELDS	= "l.attr_id, l.option_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00042', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.wishlistitem_count"	VALUE = 0>
	<MvWHILE EXPR = "{ NOT WishListItems.d.EOF }">
		<MvEVAL EXPR = "{ WishListItem_Read( l.wishlistitems[ ++l.wishlistitem_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "WishListItems">
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishListItems">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-WSH-DTB-00043', l.wishlistitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItemList_Load_AttributeTemplateAttribute" PARAMETERS = "attmpat_id, wishlistitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "WishListItems"
				QUERY	= "{ 'SELECT
								wli.*
							  FROM ' $ 
							  	g.Store_Table_Prefix $ 'WishListItems wli, ' $
							  	g.Store_Table_Prefix $ 'WishListOptions wlo
							  WHERE
							  	wli.id = wlo.wish_id AND
							  	wlo.attmpat_id = ?' }"
				FIELDS	= "l.attmpat_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00044', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.wishlistitem_count"	VALUE = 0>
	<MvWHILE EXPR = "{ NOT WishListItems.d.EOF }">
		<MvEVAL EXPR = "{ WishListItem_Read( l.wishlistitems[ ++l.wishlistitem_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "WishListItems">
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishListItems">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-WSH-DTB-00045', l.wishlistitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItemList_Load_AttributeTemplateOption" PARAMETERS = "attmpat_id, option_id, wishlistitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "WishListItems"
				QUERY	= "{ 'SELECT
								wli.*
							  FROM ' $ 
							  	g.Store_Table_Prefix $ 'WishListItems wli, ' $
							  	g.Store_Table_Prefix $ 'WishListOptions wlo
							  WHERE
							  	wli.id 			= wlo.wish_id 	AND
							  	wlo.attmpat_id 	= ? 			AND
							  	wlo.option_id	= ?' }"
				FIELDS	= "l.attmpat_id, l.option_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00046', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.wishlistitem_count"	VALUE = 0>
	<MvWHILE EXPR = "{ NOT WishListItems.d.EOF }">
		<MvEVAL EXPR = "{ WishListItem_Read( l.wishlistitems[ ++l.wishlistitem_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "WishListItems">
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishListItems">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-WSH-DTB-00047', l.wishlistitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItemList_Load_WishList_Product" PARAMETERS = "wshlst_id, product_id, wishlistitems var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "WishListItems"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'WishListItems WHERE wshlst_id = ? AND product_id = ? ORDER BY id DESC' }"
				FIELDS	= "l.wshlst_id, l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00048', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.wishlistitem_count"	VALUE = 0>
	<MvWHILE EXPR = "{ NOT WishListItems.d.EOF }">
		<MvEVAL EXPR = "{ WishListItem_Read( l.wishlistitems[ ++l.wishlistitem_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "WishListItems">
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishListItems">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-WSH-DTB-00049', l.wishlistitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Runtime_WishListProductList_Load_Offset_WishList" PARAMETERS = "wshlst_id, offset, max, sort, nextoffset var, wishlistitems var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.sort EQ 'id_desc' }">				<MvASSIGN NAME = "l.sort_col" VALUE = "prod.id">		<MvASSIGN NAME = "l.sort_dir" VALUE = "DESC">
	<MvELSEIF EXPR = "{ l.sort EQ 'code_asc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "prod.code">		<MvASSIGN NAME = "l.sort_dir" VALUE = "ASC">
	<MvELSEIF EXPR = "{ l.sort EQ 'code_desc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "prod.code">		<MvASSIGN NAME = "l.sort_dir" VALUE = "DESC">
	<MvELSEIF EXPR = "{ l.sort EQ 'name_asc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "prod.name">		<MvASSIGN NAME = "l.sort_dir" VALUE = "ASC">
	<MvELSEIF EXPR = "{ l.sort EQ 'name_desc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "prod.name">		<MvASSIGN NAME = "l.sort_dir" VALUE = "DESC">
	<MvELSEIF EXPR = "{ l.sort EQ 'price_asc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "prod.price">		<MvASSIGN NAME = "l.sort_dir" VALUE = "ASC">
	<MvELSEIF EXPR = "{ l.sort EQ 'price_desc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "prod.price">		<MvASSIGN NAME = "l.sort_dir" VALUE = "DESC">
	<MvELSEIF EXPR = "{ l.sort EQ 'dtadded_asc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "wli.dtadded">	<MvASSIGN NAME = "l.sort_dir" VALUE = "ASC">
	<MvELSEIF EXPR = "{ l.sort EQ 'dtadded_desc' }">	<MvASSIGN NAME = "l.sort_col" VALUE = "wli.dtadded">	<MvASSIGN NAME = "l.sort_dir" VALUE = "DESC">
	<MvELSE>											<MvASSIGN NAME = "l.sort_col" VALUE = "prod.id">		<MvASSIGN NAME = "l.sort_dir" VALUE = "ASC">
	</MvIF>

	<MvIF EXPR = "{ l.max EQ 0 }">	<MvASSIGN NAME = "l.limit" VALUE = 0>
	<MvELSE>						<MvASSIGN NAME = "l.limit" VALUE = "{ l.max + 1 }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.query,	'wli.*,
																			prod.catcount 		AS product_catcount,
																			prod.cancat_id 		AS product_cancat_id,
																			prod.agrpcount 		AS product_agrpcount,
																			prod.pgrpcount 		AS product_pgrpcount,
																			prod.disp_order 	AS product_disp_order,
																			pbv.page_id 		AS product_page_id,
																			prod.page_code 		AS product_page_code,
																			prod.code 			AS product_code,
																			prod.sku 			AS product_sku,
																			prod.name 			AS product_name,
																			prod.page_title 	AS product_page_title,
																			prod.thumbnail 		AS product_thumbnail,
																			prod.image 			AS product_image,
																			prod.price 			AS product_price,
																			prod.cost 			AS product_cost,
																			prod.descrip 		AS product_descrip,
																			prod.weight 		AS product_weight,
																			prod.taxable 		AS product_taxable,
																			prod.active 		AS product_active,
																			prod.dt_created		AS product_dt_created,
																			prod.dt_updated		AS product_dt_updated,
																			' $ l.sort_col $ ' 	AS sort_order,
																			ipc.inventory		AS ipc_inventory,
																			ips.active			AS ips_active,
																			ips.in_long			AS ips_in_long,
																			ips.in_short		AS ips_in_short,
																			ips.low_level		AS ips_low_level,
																			ips.low_long		AS ips_low_long,
																			ips.low_lvl_d		AS ips_low_lvl_d,
																			ips.low_short		AS ips_low_short,
																			ips.low_track		AS ips_low_track,
																			ips.out_level		AS ips_out_level,
																			ips.out_long		AS ips_out_long,
																			ips.out_lvl_d		AS ips_out_lvl_d,
																			ips.out_short		AS ips_out_short,
																			ips.out_track		AS ips_out_track,
																			iset.active			AS iset_active,
																			iset.in_long		AS iset_in_long,
																			iset.in_short		AS iset_in_short,
																			iset.low_level		AS iset_low_level,
																			iset.low_long		AS iset_low_long,
																			iset.low_short		AS iset_low_short,
																			iset.low_track		AS iset_low_track,
																			iset.out_level		AS iset_out_level,
																			iset.out_long		AS iset_out_long,
																			iset.out_short		AS iset_out_short,
																			iset.out_track		AS iset_out_track' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query, g.Store_Table_Prefix $ 'WishListItems', 'wli' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query, g.Store_Table_Prefix $ 'Products', 'prod' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query, g.Store_Table_Prefix $ 'InventorySettings', 'iset' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, 'prod', g.Store_Table_Prefix $ 'PageBranchVersions', 'pbv', 'prod.page_code <> \'\' AND pbv.branch_id = ? AND pbv.head = 1 AND ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'pbv.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'prod.page_code' ), [ g.Module_Library_DB ].SQL_Query_Field( [ g.Module_Feature_TUI_MGR ].TemplateManager_Working_Branch_ID() ) ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, 'prod', g.Store_Table_Prefix $ 'InventoryProductSettings', 'ips', 'ips.product_id = prod.id', '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, 'prod', g.Store_Table_Prefix $ 'InventoryProductCounts', 'ipc', 'ipc.product_id = prod.id', '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'wli.wshlst_id 	= ?			AND 
																		 wli.product_id = prod.id	AND 
																		 prod.active = 1', [ g.Module_Library_DB ].SQL_Query_Field( l.wshlst_id ) ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, '( ' $ [ g.Module_Library_DB ].Runtime_Product_AvailabilityGroup_Where( l.agrp_fields ) $ ' )', l.agrp_fields ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY( l.query, l.sort_col, l.sort_dir ) }">

	<MvASSIGN NAME = "l.sql" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.query, l.fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'Products', l.sql, l.fields, l.offset, l.limit ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00050', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.wishlistitem_count" VALUE = 0>
	<MvWHILE EXPR = "{ ( NOT Products.d.EOF ) AND ( ( l.max EQ 0 ) OR ( l.wishlistitem_count LT l.max ) ) }">
		<MvREFERENCEARRAY NAME = "l.wishlistitem" VARIABLE = "l.wishlistitems">
			<MvDIMENSION INDEX = "{ ++l.wishlistitem_count }">
		</MvREFERENCEARRAY>

		<MvASSIGN NAME = "l.wishlistitem:id"					VALUE = "{ Products.d.id }">
		<MvASSIGN NAME = "l.wishlistitem:wshlst_id"				VALUE = "{ Products.d.wshlst_id }">
		<MvASSIGN NAME = "l.wishlistitem:parent_id"				VALUE = "{ Products.d.parent_id }">
		<MvASSIGN NAME = "l.wishlistitem:product_id"			VALUE = "{ Products.d.product_id }">
		<MvASSIGN NAME = "l.wishlistitem:dtadded"				VALUE = "{ Products.d.dtadded }">
		<MvASSIGN NAME = "l.wishlistitem:quantity"				VALUE = "{ Products.d.quantity }">
		<MvASSIGN NAME = "l.wishlistitem:notes"					VALUE = "{ Products.d.notes }">

		<MvASSIGN NAME = "l.wishlistitem:product:id"			VALUE = "{ Products.d.product_id }">
		<MvASSIGN NAME = "l.wishlistitem:product:catcount"		VALUE = "{ Products.d.product_catcount }">
		<MvASSIGN NAME = "l.wishlistitem:product:cancat_id"		VALUE = "{ Products.d.product_cancat_id }">
		<MvASSIGN NAME = "l.wishlistitem:product:agrpcount"		VALUE = "{ Products.d.product_agrpcount }">
		<MvASSIGN NAME = "l.wishlistitem:product:pgrpcount"		VALUE = "{ Products.d.product_pgrpcount }">
		<MvASSIGN NAME = "l.wishlistitem:product:disp_order"	VALUE = "{ Products.d.product_disp_order }">
		<MvASSIGN NAME = "l.wishlistitem:product:page_id"		VALUE = "{ int( Products.d.product_page_id ) }">
		<MvASSIGN NAME = "l.wishlistitem:product:page_code"		VALUE = "{ Products.d.product_page_code }">
		<MvASSIGN NAME = "l.wishlistitem:product:code"			VALUE = "{ Products.d.product_code }">
		<MvASSIGN NAME = "l.wishlistitem:product:sku"			VALUE = "{ Products.d.product_sku }">
		<MvASSIGN NAME = "l.wishlistitem:product:name"			VALUE = "{ Products.d.product_name }">
		<MvASSIGN NAME = "l.wishlistitem:product:page_title"	VALUE = "{ Products.d.product_page_title }">
		<MvASSIGN NAME = "l.wishlistitem:product:thumbnail"		VALUE = "{ Products.d.product_thumbnail }">
		<MvASSIGN NAME = "l.wishlistitem:product:image"			VALUE = "{ Products.d.product_image }">
		<MvASSIGN NAME = "l.wishlistitem:product:price"			VALUE = "{ Products.d.product_price }">
		<MvASSIGN NAME = "l.wishlistitem:product:cost"			VALUE = "{ Products.d.product_cost }">
		<MvASSIGN NAME = "l.wishlistitem:product:descrip"		VALUE = "{ Products.d.product_descrip }">
		<MvASSIGN NAME = "l.wishlistitem:product:weight"		VALUE = "{ Products.d.product_weight }">
		<MvASSIGN NAME = "l.wishlistitem:product:taxable"		VALUE = "{ Products.d.product_taxable }">
		<MvASSIGN NAME = "l.wishlistitem:product:active"		VALUE = "{ Products.d.product_active }">
		<MvASSIGN NAME = "l.wishlistitem:product:dt_created"	VALUE = "{ Products.d.product_dt_created }">
		<MvASSIGN NAME = "l.wishlistitem:product:dt_updated"	VALUE = "{ Products.d.product_dt_updated }">

		<MvEVAL EXPR = "{ [ g.Module_Feature_INV_RT ].Runtime_Product_InventoryFields_Read( l.wishlistitem:product ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Products" ROWS = 1>
	</MvWHILE>
	
	<MvIF EXPR = "{ Products.d.EOF }">
		<MvASSIGN NAME = "l.nextoffset"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.nextoffset"	VALUE = "{ l.offset + l.wishlistitem_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Products">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-WSH-DTB-00051', l.wishlistitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Runtime_WishListProductList_Load_Parent" PARAMETERS = "wshlst_id, parent_id, wishlistitems var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.query,	'wli.*,
																			 prod.catcount		AS product_catcount,
																			 prod.cancat_id		AS product_cancat_id,
																			 prod.agrpcount		AS product_agrpcount,
																			 prod.pgrpcount		AS product_pgrpcount,
																			 prod.disp_order	AS product_disp_order,
																			 pbv.page_id		AS product_page_id,
																			 prod.page_code		AS product_page_code,
																			 prod.code			AS product_code,
																			 prod.sku			AS product_sku,
																			 prod.name			AS product_name,
																			 prod.page_title	AS product_page_title,
																			 prod.thumbnail		AS product_thumbnail,
																			 prod.image			AS product_image,
																			 prod.price			AS product_price,
																			 prod.cost			AS product_cost,
																			 prod.descrip		AS product_descrip,
																			 prod.weight		AS product_weight,
																			 prod.taxable		AS product_taxable,
																			 prod.active		AS product_active,
																			 prod.dt_created	AS product_dt_created,
																			 prod.dt_updated	AS product_dt_updated,
																			 ipc.inventory		AS ipc_inventory,
																			 ips.active			AS ips_active,
																			 ips.in_long		AS ips_in_long,
																			 ips.in_short		AS ips_in_short,
																			 ips.low_level		AS ips_low_level,
																			 ips.low_long		AS ips_low_long,
																			 ips.low_lvl_d		AS ips_low_lvl_d,
																			 ips.low_short		AS ips_low_short,
																			 ips.low_track		AS ips_low_track,
																			 ips.out_level		AS ips_out_level,
																			 ips.out_long		AS ips_out_long,
																			 ips.out_lvl_d		AS ips_out_lvl_d,
																			 ips.out_short		AS ips_out_short,
																			 ips.out_track		AS ips_out_track,
																			 iset.active		AS iset_active,
																			 iset.in_long		AS iset_in_long,
																			 iset.in_short		AS iset_in_short,
																			 iset.low_level		AS iset_low_level,
																			 iset.low_long		AS iset_low_long,
																			 iset.low_short		AS iset_low_short,
																			 iset.low_track		AS iset_low_track,
																			 iset.out_level		AS iset_out_level,
																			 iset.out_long		AS iset_out_long,
																			 iset.out_short		AS iset_out_short,
																			 iset.out_track		AS iset_out_track' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query, g.Store_Table_Prefix $ 'WishListItems', 'wli' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query, g.Store_Table_Prefix $ 'Products', 'prod' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query, g.Store_Table_Prefix $ 'InventorySettings', 'iset' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, 'prod', g.Store_Table_Prefix $ 'PageBranchVersions', 'pbv', 'prod.page_code <> \'\' AND pbv.branch_id = ? AND pbv.head = 1 AND ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'pbv.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'prod.page_code' ), [ g.Module_Library_DB ].SQL_Query_Field( [ g.Module_Feature_TUI_MGR ].TemplateManager_Working_Branch_ID() ) ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, 'prod', g.Store_Table_Prefix $ 'InventoryProductSettings', 'ips', 'ips.product_id = prod.id', '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, 'prod', g.Store_Table_Prefix $ 'InventoryProductCounts', 'ipc', 'ipc.product_id = prod.id', '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'wli.wshlst_id 	= ?			AND 
																		 wli.parent_id 	= ?			AND 
																		 wli.product_id = prod.id	AND 
																		 prod.active = 1', [ g.Module_Library_DB ].SQL_Query_Field( l.wshlst_id ) $ ', ' $ [ g.Module_Library_DB ].SQL_Query_Field( l.parent_id ) ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, '( ' $ [ g.Module_Library_DB ].Runtime_Product_AvailabilityGroup_Where( l.agrp_fields ) $ ' )', l.agrp_fields ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY( l.query, 'prod.id', 'ASC' ) }">

	<MvASSIGN NAME = "l.sql" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.query, l.fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'Products', l.sql, l.fields, 0, 0 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00063', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.wishlistitem_count"						VALUE = 0>

	<MvWHILE EXPR = "{ NOT Products.d.EOF }">
		<MvREFERENCEARRAY NAME = "l.wishlistitem" VARIABLE = "l.wishlistitems">
			<MvDIMENSION INDEX = "{ ++l.wishlistitem_count }">
		</MvREFERENCEARRAY>

		<MvASSIGN NAME = "l.wishlistitem:id"					VALUE = "{ Products.d.id }">
		<MvASSIGN NAME = "l.wishlistitem:wshlst_id"				VALUE = "{ Products.d.wshlst_id }">
		<MvASSIGN NAME = "l.wishlistitem:parent_id"				VALUE = "{ Products.d.parent_id }">
		<MvASSIGN NAME = "l.wishlistitem:product_id"			VALUE = "{ Products.d.product_id }">
		<MvASSIGN NAME = "l.wishlistitem:dtadded"				VALUE = "{ Products.d.dtadded }">
		<MvASSIGN NAME = "l.wishlistitem:quantity"				VALUE = "{ Products.d.quantity }">
		<MvASSIGN NAME = "l.wishlistitem:notes"					VALUE = "{ Products.d.notes }">

		<MvASSIGN NAME = "l.wishlistitem:product:id"			VALUE = "{ Products.d.product_id }">
		<MvASSIGN NAME = "l.wishlistitem:product:catcount"		VALUE = "{ Products.d.product_catcount }">
		<MvASSIGN NAME = "l.wishlistitem:product:cancat_id"		VALUE = "{ Products.d.product_cancat_id }">
		<MvASSIGN NAME = "l.wishlistitem:product:agrpcount"		VALUE = "{ Products.d.product_agrpcount }">
		<MvASSIGN NAME = "l.wishlistitem:product:pgrpcount"		VALUE = "{ Products.d.product_pgrpcount }">
		<MvASSIGN NAME = "l.wishlistitem:product:disp_order"	VALUE = "{ Products.d.product_disp_order }">
		<MvASSIGN NAME = "l.wishlistitem:product:page_id"		VALUE = "{ int( Products.d.product_page_id ) }">
		<MvASSIGN NAME = "l.wishlistitem:product:page_code"		VALUE = "{ Products.d.product_page_code }">
		<MvASSIGN NAME = "l.wishlistitem:product:code"			VALUE = "{ Products.d.product_code }">
		<MvASSIGN NAME = "l.wishlistitem:product:sku"			VALUE = "{ Products.d.product_sku }">
		<MvASSIGN NAME = "l.wishlistitem:product:name"			VALUE = "{ Products.d.product_name }">
		<MvASSIGN NAME = "l.wishlistitem:product:page_title"	VALUE = "{ Products.d.product_page_title }">
		<MvASSIGN NAME = "l.wishlistitem:product:thumbnail"		VALUE = "{ Products.d.product_thumbnail }">
		<MvASSIGN NAME = "l.wishlistitem:product:image"			VALUE = "{ Products.d.product_image }">
		<MvASSIGN NAME = "l.wishlistitem:product:price"			VALUE = "{ Products.d.product_price }">
		<MvASSIGN NAME = "l.wishlistitem:product:cost"			VALUE = "{ Products.d.product_cost }">
		<MvASSIGN NAME = "l.wishlistitem:product:descrip"		VALUE = "{ Products.d.product_descrip }">
		<MvASSIGN NAME = "l.wishlistitem:product:weight"		VALUE = "{ Products.d.product_weight }">
		<MvASSIGN NAME = "l.wishlistitem:product:taxable"		VALUE = "{ Products.d.product_taxable }">
		<MvASSIGN NAME = "l.wishlistitem:product:active"		VALUE = "{ Products.d.product_active }">
		<MvASSIGN NAME = "l.wishlistitem:product:dt_created"	VALUE = "{ Products.d.product_dt_created }">
		<MvASSIGN NAME = "l.wishlistitem:product:dt_updated"	VALUE = "{ Products.d.product_dt_updated }">

		<MvEVAL EXPR = "{ [ g.Module_Feature_INV_RT ].Runtime_Product_InventoryFields_Read( l.wishlistitem:product ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Products" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Products">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-WSH-DTB-00064', l.wishlistitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Runtime_WishListProduct_Count_WishList" PARAMETERS = "wshlst_id, count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.query, 'COUNT( * ) AS total_count' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query, g.Store_Table_Prefix $ 'WishListItems', 'wli' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query, g.Store_Table_Prefix $ 'Products', 'prod' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query, g.Store_Table_Prefix $ 'InventorySettings', 'iset' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, 'prod', g.Store_Table_Prefix $ 'InventoryProductSettings', 'ips', 'ips.product_id = prod.id', '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, 'prod', g.Store_Table_Prefix $ 'InventoryProductCounts', 'ipc', 'ipc.product_id = prod.id', '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'wli.wshlst_id 	= ?			AND 
																		 wli.product_id = prod.id	AND 
																		 prod.active = 1', [ g.Module_Library_DB ].SQL_Query_Field( l.wshlst_id ) ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, '( ' $ [ g.Module_Library_DB ].Runtime_Product_AvailabilityGroup_Where( l.agrp_fields ) $ ' )', l.agrp_fields ) }">

	<MvASSIGN NAME = "l.sql" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.query, l.fields ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "WishListItems"
				QUERY	= "{ l.sql }"
				FIELDS	= "{ l.fields }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00059', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ WishListItems.d.EOF }">	<MvASSIGN NAME = "l.count" VALUE = 0>
	<MvELSE>								<MvASSIGN NAME = "l.count" VALUE = "{ WishListItems.d.total_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishListItems">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListItemQuantity_Load_Customer_And_Product" PARAMETERS = "cust_id, product_id, quantity var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "WishListItems"
				QUERY	= "{ 'SELECT
								SUM( wli.quantity ) AS total_quantity
							  FROM ' $
								g.Store_Table_Prefix $ 'WishListItems wli,' $
								g.Store_Table_Prefix $ 'WishLists wl
							  WHERE
								wli.wshlst_id	= wl.id AND
								wli.product_id	= ?		AND
								wl.cust_id		= ?' }"
				FIELDS	= "l.product_id, l.cust_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00066', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ WishListItems.d.EOF }">	<MvASSIGN NAME = "l.quantity" VALUE = 0>
	<MvELSE>								<MvASSIGN NAME = "l.quantity" VALUE = "{ WishListItems.d.total_quantity }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishListItems">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_WishListOptions
|
</MvCOMMENT>

<MvFUNCTION NAME = "WishListOption_Read" PARAMETERS = "wishlistoption var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.wishlistoption:wish_id"				VALUE = "{ WishListOptions.d.wish_id }">
	<MvASSIGN NAME = "l.wishlistoption:attr_id"				VALUE = "{ WishListOptions.d.attr_id }">
	<MvASSIGN NAME = "l.wishlistoption:attmpat_id"			VALUE = "{ WishListOptions.d.attmpat_id }">
	<MvASSIGN NAME = "l.wishlistoption:option_id"			VALUE = "{ WishListOptions.d.option_id }">
	<MvASSIGN NAME = "l.wishlistoption:data"				VALUE = "{ WishListOptions.d.data }">
	<MvASSIGN NAME = "l.wishlistoption:data_long"			VALUE = "{ WishListOptions.d.data_long }">

	<MvIF EXPR = "{ WishListOptions.d.joined_attmpat_id }">
		<MvASSIGN NAME = "l.wishlistoption:attr_code"		VALUE = "{ WishListOptions.d.attmpat_code }">
		<MvASSIGN NAME = "l.wishlistoption:attr_type"		VALUE = "{ WishListOptions.d.attmpat_type }">
		<MvASSIGN NAME = "l.wishlistoption:attr_prompt"		VALUE = "{ WishListOptions.d.attmpat_prompt }">
		<MvASSIGN NAME = "l.wishlistoption:attr_price"		VALUE = "{ WishListOptions.d.attmpat_price }">
		<MvASSIGN NAME = "l.wishlistoption:attr_cost"		VALUE = "{ WishListOptions.d.attmpat_cost }">
		<MvASSIGN NAME = "l.wishlistoption:attr_weight"		VALUE = "{ WishListOptions.d.attmpat_weight }">
		<MvASSIGN NAME = "l.wishlistoption:attr_required"	VALUE = "{ WishListOptions.d.attmpat_required }">
		<MvASSIGN NAME = "l.wishlistoption:attr_inventory"	VALUE = "{ WishListOptions.d.attmpat_inventory }">
		<MvASSIGN NAME = "l.wishlistoption:attr_image"		VALUE = "{ WishListOptions.d.attmpat_image }">

		<MvIF EXPR = "{ WishListOptions.d.joined_attmpopt_id }">
			<MvASSIGN NAME = "l.wishlistoption:opt_code"	VALUE = "{ WishListOptions.d.attmpopt_code }">
			<MvASSIGN NAME = "l.wishlistoption:opt_prompt"	VALUE = "{ WishListOptions.d.attmpopt_prompt }">
			<MvASSIGN NAME = "l.wishlistoption:opt_price"	VALUE = "{ WishListOptions.d.attmpopt_price }">
			<MvASSIGN NAME = "l.wishlistoption:opt_cost"	VALUE = "{ WishListOptions.d.attmpopt_cost }">
			<MvASSIGN NAME = "l.wishlistoption:opt_weight"	VALUE = "{ WishListOptions.d.attmpopt_weight }">
			<MvASSIGN NAME = "l.wishlistoption:opt_image"	VALUE = "{ WishListOptions.d.attmpopt_image }">
		<MvELSE>
			<MvASSIGN NAME = "l.wishlistoption:opt_code"	VALUE = "">
			<MvASSIGN NAME = "l.wishlistoption:opt_prompt"	VALUE = "">
			<MvASSIGN NAME = "l.wishlistoption:opt_price"	VALUE = 0>
			<MvASSIGN NAME = "l.wishlistoption:opt_cost"	VALUE = 0>
			<MvASSIGN NAME = "l.wishlistoption:opt_weight"	VALUE = 0>
			<MvASSIGN NAME = "l.wishlistoption:opt_image"	VALUE = "">
		</MvIF>

		<MvIF EXPR = "{ ( l.wishlistoption:attr_type EQ 'radio' ) OR
						( l.wishlistoption:attr_type EQ 'select' ) OR
						( l.wishlistoption:attr_type EQ 'swatch-select' ) }">
			<MvASSIGN NAME = "l.wishlistoption:retail"		VALUE = "{ WishListOptions.d.attmpopt_price }">
			<MvASSIGN NAME = "l.wishlistoption:base_price"	VALUE = "{ WishListOptions.d.attmpopt_price }">
			<MvASSIGN NAME = "l.wishlistoption:price"		VALUE = "{ WishListOptions.d.attmpopt_price }">
			<MvASSIGN NAME = "l.wishlistoption:weight"		VALUE = "{ WishListOptions.d.attmpopt_weight }">
		<MvELSE>
			<MvASSIGN NAME = "l.wishlistoption:retail"		VALUE = "{ WishListOptions.d.attmpat_price }">
			<MvASSIGN NAME = "l.wishlistoption:base_price"	VALUE = "{ WishListOptions.d.attmpat_price }">
			<MvASSIGN NAME = "l.wishlistoption:price"		VALUE = "{ WishListOptions.d.attmpat_price }">
			<MvASSIGN NAME = "l.wishlistoption:weight"		VALUE = "{ WishListOptions.d.attmpat_weight }">
		</MvIF>
	<MvELSEIF EXPR = "{ WishListOptions.d.joined_attr_id }">
		<MvASSIGN NAME = "l.wishlistoption:attr_code"		VALUE = "{ WishListOptions.d.attr_code }">
		<MvASSIGN NAME = "l.wishlistoption:attr_type"		VALUE = "{ WishListOptions.d.attr_type }">
		<MvASSIGN NAME = "l.wishlistoption:attr_prompt"		VALUE = "{ WishListOptions.d.attr_prompt }">
		<MvASSIGN NAME = "l.wishlistoption:attr_price"		VALUE = "{ WishListOptions.d.attr_price }">
		<MvASSIGN NAME = "l.wishlistoption:attr_cost"		VALUE = "{ WishListOptions.d.attr_cost }">
		<MvASSIGN NAME = "l.wishlistoption:attr_weight"		VALUE = "{ WishListOptions.d.attr_weight }">
		<MvASSIGN NAME = "l.wishlistoption:attr_required"	VALUE = "{ WishListOptions.d.attr_required }">
		<MvASSIGN NAME = "l.wishlistoption:attr_inventory"	VALUE = "{ WishListOptions.d.attr_inventory }">
		<MvASSIGN NAME = "l.wishlistoption:attr_image"		VALUE = "{ WishListOptions.d.attr_image }">

		<MvIF EXPR = "{ WishListOptions.d.joined_opt_id }">
			<MvASSIGN NAME = "l.wishlistoption:opt_code"	VALUE = "{ WishListOptions.d.opt_code }">
			<MvASSIGN NAME = "l.wishlistoption:opt_prompt"	VALUE = "{ WishListOptions.d.opt_prompt }">
			<MvASSIGN NAME = "l.wishlistoption:opt_price"	VALUE = "{ WishListOptions.d.opt_price }">
			<MvASSIGN NAME = "l.wishlistoption:opt_cost"	VALUE = "{ WishListOptions.d.opt_cost }">
			<MvASSIGN NAME = "l.wishlistoption:opt_weight"	VALUE = "{ WishListOptions.d.opt_weight }">
			<MvASSIGN NAME = "l.wishlistoption:opt_image"	VALUE = "{ WishListOptions.d.opt_image }">
		<MvELSE>
			<MvASSIGN NAME = "l.wishlistoption:opt_code"	VALUE = "">
			<MvASSIGN NAME = "l.wishlistoption:opt_prompt"	VALUE = "">
			<MvASSIGN NAME = "l.wishlistoption:opt_price"	VALUE = 0>
			<MvASSIGN NAME = "l.wishlistoption:opt_cost"	VALUE = 0>
			<MvASSIGN NAME = "l.wishlistoption:opt_weight"	VALUE = 0>
			<MvASSIGN NAME = "l.wishlistoption:opt_image"	VALUE = "">
		</MvIF>

		<MvIF EXPR = "{ ( l.wishlistoption:attr_type EQ 'radio' ) OR
						( l.wishlistoption:attr_type EQ 'select' ) OR
						( l.wishlistoption:attr_type EQ 'swatch-select' ) }">
			<MvASSIGN NAME = "l.wishlistoption:retail"		VALUE = "{ WishListOptions.d.opt_price }">
			<MvASSIGN NAME = "l.wishlistoption:base_price"	VALUE = "{ WishListOptions.d.opt_price }">
			<MvASSIGN NAME = "l.wishlistoption:price"		VALUE = "{ WishListOptions.d.opt_price }">
			<MvASSIGN NAME = "l.wishlistoption:weight"		VALUE = "{ WishListOptions.d.opt_weight }">
		<MvELSE>
			<MvASSIGN NAME = "l.wishlistoption:retail"		VALUE = "{ WishListOptions.d.attr_price }">
			<MvASSIGN NAME = "l.wishlistoption:base_price"	VALUE = "{ WishListOptions.d.attr_price }">
			<MvASSIGN NAME = "l.wishlistoption:price"		VALUE = "{ WishListOptions.d.attr_price }">
			<MvASSIGN NAME = "l.wishlistoption:weight"		VALUE = "{ WishListOptions.d.attr_weight }">
		</MvIF>
	<MvELSE>
		<MvASSIGN NAME = "l.wishlistoption:attr_code"		VALUE = "">
		<MvASSIGN NAME = "l.wishlistoption:attr_type"		VALUE = "">
		<MvASSIGN NAME = "l.wishlistoption:attr_prompt"		VALUE = "">
		<MvASSIGN NAME = "l.wishlistoption:attr_price"		VALUE = 0>
		<MvASSIGN NAME = "l.wishlistoption:attr_cost"		VALUE = 0>
		<MvASSIGN NAME = "l.wishlistoption:attr_weight"		VALUE = 0>
		<MvASSIGN NAME = "l.wishlistoption:attr_required"	VALUE = "">
		<MvASSIGN NAME = "l.wishlistoption:attr_inventory"	VALUE = "">
		<MvASSIGN NAME = "l.wishlistoption:attr_image"		VALUE = "">
		<MvASSIGN NAME = "l.wishlistoption:opt_code"		VALUE = "">
		<MvASSIGN NAME = "l.wishlistoption:opt_prompt"		VALUE = "">
		<MvASSIGN NAME = "l.wishlistoption:opt_price"		VALUE = 0>
		<MvASSIGN NAME = "l.wishlistoption:opt_cost"		VALUE = 0>
		<MvASSIGN NAME = "l.wishlistoption:opt_weight"		VALUE = 0>
		<MvASSIGN NAME = "l.wishlistoption:opt_image"		VALUE = "">
		<MvASSIGN NAME = "l.wishlistoption:retail"			VALUE = 0>
		<MvASSIGN NAME = "l.wishlistoption:base_price"		VALUE = 0>
		<MvASSIGN NAME = "l.wishlistoption:price"			VALUE = 0>
		<MvASSIGN NAME = "l.wishlistoption:weight"			VALUE = 0>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListOption_Insert" PARAMETERS = "wishlistoption var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'WishListOptions
					      ( wish_id, attr_id, attmpat_id, option_id, data, data_long )
						  VALUES
						  ( ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.wishlistoption:wish_id, l.wishlistoption:attr_id, l.wishlistoption:attmpat_id, l.wishlistoption:option_id, l.wishlistoption:data, l.wishlistoption:data_long">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00052', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListOption_Delete_All_WishListItem" PARAMETERS = "wish_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'WishListOptions WHERE wish_id = ?' }"
			 FIELDS	= "l.wish_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00053', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "WishListOptionList_Load_WishListItem" PARAMETERS = "wish_id, wishlistoptions var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "WishListOptions"
				QUERY	= "{ 'SELECT
								wlo.*,
								ats.id			AS joined_attr_id,
								ats.code		AS attr_code,
								ats.type		AS attr_type,
								ats.prompt		AS attr_prompt,
								ats.price		AS attr_price,
								ats.cost		AS attr_cost,
								ats.weight		AS attr_weight,
								ats.required	AS attr_required,
								ats.inventory	AS attr_inventory,
								ats.image		AS attr_image,
								ata.id			AS joined_attmpat_id,
								ata.code		AS attmpat_code,
								ata.type		AS attmpat_type,
								ata.prompt		AS attmpat_prompt,
								ata.price		AS attmpat_price,
								ata.cost		AS attmpat_cost,
								ata.weight		AS attmpat_weight,
								ata.required	AS attmpat_required,
								ata.inventory	AS attmpat_inventory,
								ata.image		AS attmpat_image,
								opt.id			AS joined_opt_id,
								opt.code		AS opt_code,
								opt.prompt		AS opt_prompt,
								opt.price		AS opt_price,
								opt.cost		AS opt_cost,
								opt.weight		AS opt_weight,
								opt.image		AS opt_image,
								ato.id			AS joined_attmpopt_id,
								ato.code		AS attmpopt_code,
								ato.prompt		AS attmpopt_prompt,
								ato.price		AS attmpopt_price,
								ato.cost		AS attmpopt_cost,
								ato.weight		AS attmpopt_weight,
								ato.image		AS attmpopt_image
							  FROM ' $
							  	g.Store_Table_Prefix $ 'WishListOptions wlo
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'Attributes ats ON ats.id = wlo.attr_id
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs ata ON ata.id = wlo.attmpat_id
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'Options opt ON opt.id = wlo.option_id
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions ato ON ato.id = wlo.option_id
							  WHERE
							  	wlo.wish_id = ?
							  ORDER BY
							  	ats.disp_order, ata.disporder' }"
				FIELDS	= "l.wish_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-WSH-DTB-00057', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.wishlistoption_count"	VALUE = 0>
	<MvWHILE EXPR = "{ NOT WishListOptions.d.EOF }">
		<MvEVAL EXPR = "{ WishListOption_Read( l.wishlistoptions[ ++l.wishlistoption_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "WishListOptions">
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "WishListOptions">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-WSH-DTB-00058', l.wishlistoption_count ) }">
</MvFUNCTION>

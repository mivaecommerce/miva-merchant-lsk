<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-ATT-DTB-
| Next Error Code: 63   
|
</MvCOMMENT>

<MvCOMMENT>
|
| Attribute Templates
|
</MvCOMMENT>

<MvFUNCTION NAME = "ATT_Create_Data_Files" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].DomainPrivilege_Insert( 'ATMP', 'Attribute Templates' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ATT_Store_Create" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'AttributeTemplates
								(
									id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
									code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )	$ ',
									prompt		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )	$ ',
									refcount	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ '
								) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'AttributeTemplates_1 ON ' $ g.Store_Table_Prefix $ 'AttributeTemplates ( id ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'AttributeTemplates_2 ON ' $ g.Store_Table_Prefix $ 'AttributeTemplates ( code ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'AttributeTemplateAttrs
								(
									id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									attemp_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									default_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									disporder	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
									type		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )		$ ',
									prompt		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ ',
									price		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 16, 8 )	$ ',
									cost		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 16, 8 )	$ ',
									weight		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 16, 8 )	$ ',
									required	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
									inventory	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
									image		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ '
								) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs_1 ON ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs ( id ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs_2 ON ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs ( attemp_id, code ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs_3 ON ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs ( attemp_id, disporder ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'AttributeTemplateOptions
								(
									id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									attemp_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									attmpat_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									disporder	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
									code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
									prompt		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ ',
									price		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 16, 8 )	$ ',
									cost		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 16, 8 )	$ ',
									weight		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 16, 8 )	$ ',
									image		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ '
								) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions_1 ON ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions ( id ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00009', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions_2 ON ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions ( attmpat_id, code ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00010', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions_3 ON ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions ( attmpat_id, disporder ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00011', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'AttributeTemplates', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'AttributeTemplateOptions', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'AttributeTemplateAttrs', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'AttributeTemplateAttributeDisplayOrder', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'AttributeTemplateAttributeOptionDisplayOrder', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ATT_Store_Delete" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'AttributeTemplates' }">

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'AttributeTemplateAttrs' }">

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'AttributeTemplateOptions' }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplate_Read" PARAMETERS = "attributetemplate var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.attributetemplate:id"		VALUE = "{ AttributeTemplates.d.id }">
	<MvASSIGN NAME = "l.attributetemplate:code"		VALUE = "{ AttributeTemplates.d.code }">
	<MvASSIGN NAME = "l.attributetemplate:prompt"	VALUE = "{ AttributeTemplates.d.prompt }">
	<MvASSIGN NAME = "l.attributetemplate:refcount"	VALUE = "{ AttributeTemplates.d.refcount }">
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplate_Insert" PARAMETERS = "attributetemplate var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT AttributeTemplate_Insert_LowLevel( l.attributetemplate ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ [ g.Module_Library_DB ].StoreModuleList_Load_Features_Cached( 'not_attemp', l.modules ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Notify_AttributeTemplate_Insert( l.module, l.attributetemplate ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplate_Insert_LowLevel" PARAMETERS = "attributetemplate var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.attributetemplate:id" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'AttributeTemplates' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'AttributeTemplates
					      ( id, code, prompt, refcount )
						  VALUES
						  ( ?, ?, ?, ? )' }"
			 FIELDS	= "l.attributetemplate:id, l.attributetemplate:code, l.attributetemplate:prompt,
					   l.attributetemplate:refcount">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00012', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplate_Update" PARAMETERS = "attributetemplate var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT AttributeTemplate_Update_LowLevel( l.attributetemplate ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ [ g.Module_Library_DB ].StoreModuleList_Load_Features_Cached( 'not_attemp', l.modules ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Notify_AttributeTemplate_Update( l.module, l.attributetemplate ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplate_Update_LowLevel" PARAMETERS = "attributetemplate var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'AttributeTemplates
					      SET
							code		= ?,
							prompt		= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.attributetemplate:code, l.attributetemplate:prompt,
					   l.attributetemplate:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00013', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplate_Delete" PARAMETERS = "template var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ [ g.Module_Library_DB ].StoreModuleList_Load_Features_Cached( 'not_attemp', l.modules ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Notify_AttributeTemplate_Delete( l.module, l.template ) }">
	</MvFOREACH>

	<MvTRANSACT NAME = "Merchant">

	<MvFOREACH ITERATOR = "l.attribute" ARRAY = "l.attributes" COUNT = "{ AttributeTemplateAttrList_Load_Template( l.template:id, l.attributes ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_WSH_DB ].WishListItems_Delete_All_AttributeTemplateAttribute( l.attribute:id ) }">
			<MvROLLBACK NAME = "Merchant">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvIF EXPR = "{ NOT AttributeTemplateOption_Delete_Template( l.template:id ) OR
					NOT AttributeTemplateAttr_Delete_Template( l.template:id ) OR
					NOT AttributeTemplate_Delete_ID( l.template:id ) }">
		<MvROLLBACK NAME = "Merchant">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMIT NAME = "Merchant">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplate_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplates WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00014', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplate_Load_ID" PARAMETERS = "id, attributetemplate var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AttributeTemplates"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplates WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00015', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ AttributeTemplates.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplates">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-ATT-DTB-00038' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ AttributeTemplate_Read( l.attributetemplate ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplates">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplate_Load_Code" PARAMETERS = "code, attributetemplate var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AttributeTemplates"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplates WHERE ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00016', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ AttributeTemplates.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplates">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-ATT-DTB-00039' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ AttributeTemplate_Read( l.attributetemplate ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplates">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateList_Load_All" PARAMETERS = "attributetemplates var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AttributeTemplates"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplates' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00017', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.attributetemplate_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT AttributeTemplates.d.EOF }">
		<MvASSIGN NAME = "l.attributetemplate_count" VALUE = "{ l.attributetemplate_count + 1 }">
		<MvEVAL EXPR = "{ AttributeTemplate_Read( l.attributetemplates[ l.attributetemplate_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "AttributeTemplates" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplates">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-ATT-DTB-00040', l.attributetemplate_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplate_Increment_ReferenceCount" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'AttributeTemplates
					      SET
							refcount	= refcount + 1
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00018', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplate_Decrement_ReferenceCount" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'AttributeTemplates
					      SET
							refcount	= refcount - 1
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00019', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplate_Decrement_ReferenceCount_Product" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Attributes"
				QUERY	= "{ 'SELECT attemp_id, COUNT( * ) AS attemp_count FROM ' $ g.Store_Table_Prefix $ 'Attributes WHERE product_id = ? AND type = \'template\' GROUP BY attemp_id' }"
				FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00062', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT Attributes.d.EOF }">
		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'AttributeTemplates
				 			  SET
				 			  	refcount = refcount - ?
				 			  WHERE
				 			  	id = ?' }"
				 FIELDS = "Attributes.d.attemp_count, Attributes.d.attemp_id">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "Attributes">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00020', g.MvQUERY_Error ) }">
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "Attributes" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Attributes">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateList_Load_Offset" PARAMETERS = "offset, search, max, newoffset var, template var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.query"			VALUE = "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplates ' }">

	<MvIF EXPR = "{ len( l.search ) }">
		<MvASSIGN NAME = "l.query"	VALUE = "{ l.query $ ' WHERE ' $ [ g.Module_Library_DB ].SQL_Search_Clause( l.search, g.Store_Table_Prefix $ 'AttributeTemplates', 'code,prompt', l.fields ) }">
	</MvIF>

	<MvASSIGN NAME = "l.query" VALUE = "{ l.query $ ' ORDER BY id ASC' }">

	<MvIF EXPR = "{ l.max EQ 0 }">	<MvASSIGN NAME = "l.limit" VALUE = 0>
	<MvELSE>						<MvASSIGN NAME = "l.limit" VALUE = "{ l.max + 1 }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'AttributeTemplates',
																			 l.query, l.fields,
																			 l.offset, l.limit ) }">
		<MvFUNCTIONRETURN VALUE = "{  [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00021', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.template_count"	VALUE = 0>
	<MvWHILE EXPR = "{ ( NOT AttributeTemplates.d.EOF ) AND ( ( l.max EQ 0 ) OR ( l.template_count LT l.max ) ) }">
		<MvASSIGN NAME = "l.template_count"	VALUE = "{ l.template_count + 1 }">
		<MvEVAL EXPR = "{ AttributeTemplate_Read( l.template[ l.template_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "AttributeTemplates">
	</MvWHILE>

	<MvIF EXPR = "{ AttributeTemplates.d.EOF }">
		<MvASSIGN NAME = "l.newoffset"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.newoffset"	VALUE = "{ l.offset + l.template_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplates">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-ATT-DTB-00041', l.template_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplate_Copy" PARAMETERS = "template_id, product_id, nodups" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.attribute_count" VALUE = "{ AttributeTemplateAttrList_Load_Template( l.template_id, l.attribute ) }">

	<MvASSIGN NAME = "l.atr_pos" VALUE = 1>
	<MvWHILE EXPR = "{ l.atr_pos LE l.attribute_count }">
		<MvASSIGN NAME = "l.code_suffix" VALUE = "">
		<MvASSIGN NAME = "l.attribute_code" VALUE = "{ l.attribute[ l.atr_pos ]:code $ l.code_suffix }">

		<MvIF EXPR = "{ NOT l.nodups }">
			<MvASSIGN NAME = "l.code_found" VALUE = "{ [ g.Module_Library_DB ].Attribute_Load_Code( l.product_id, l.attribute_code, l.null ) }">
			<MvWHILE EXPR = "{ l.code_found }">
				<MvASSIGN NAME = "l.code_suffix" VALUE = "{ l.code_suffix + 1 }">
				<MvASSIGN NAME = "l.attribute_code" VALUE = "{ l.attribute[ l.atr_pos ]:code $ l.code_suffix }">
				<MvASSIGN NAME = "l.code_found" VALUE = "{ [ g.Module_Library_DB ].Attribute_Load_Code( l.product_id, l.attribute_code, l.null ) }">
			</MvWHILE>
		</MvIF>

		<MvIF EXPR = "{ NOT l.code_found }">
			<MvASSIGN NAME = "l.update_attribute"			VALUE = 0>

			<MvASSIGN NAME = "l.new_attribute:product_id" 	VALUE = "{ l.product_id }">
			<MvASSIGN NAME = "l.new_attribute:attemp_id" 	VALUE = 0>
			<MvASSIGN NAME = "l.new_attribute:default_id" 	VALUE = 0>
			<MvASSIGN NAME = "l.new_attribute:code"			VALUE = "{ l.attribute_code }">
			<MvASSIGN NAME = "l.new_attribute:type"			VALUE = "{ l.attribute[ l.atr_pos ]:type }">
			<MvASSIGN NAME = "l.new_attribute:prompt"		VALUE = "{ l.attribute[ l.atr_pos ]:prompt }">
			<MvASSIGN NAME = "l.new_attribute:price"		VALUE = "{ l.attribute[ l.atr_pos ]:price }">
			<MvASSIGN NAME = "l.new_attribute:cost"			VALUE = "{ l.attribute[ l.atr_pos ]:cost }">
			<MvASSIGN NAME = "l.new_attribute:weight"		VALUE = "{ l.attribute[ l.atr_pos ]:weight }">
			<MvASSIGN NAME = "l.new_attribute:required" 	VALUE = "{ l.attribute[ l.atr_pos ]:required }">
			<MvASSIGN NAME = "l.new_attribute:inventory" 	VALUE = "{ l.attribute[ l.atr_pos ]:inventory }">
			<MvASSIGN NAME = "l.new_attribute:image"		VALUE = "{ l.attribute[ l.atr_pos ]:image }">

			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Attribute_Insert( l.new_attribute ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.opt_pos" VALUE = 1>
			<MvASSIGN NAME = "l.option_count" VALUE = "{ AttributeTemplateOptionList_Load_Attribute( l.attribute[ l.atr_pos ]:id, l.option ) }">
			<MvWHILE EXPR = "{ l.opt_pos LE l.option_count }">
				<MvASSIGN NAME = "l.new_option:product_id" 	VALUE = "{ l.product_id }">
				<MvASSIGN NAME = "l.new_option:attr_id" 	VALUE = "{ l.new_attribute:id }">
				<MvASSIGN NAME = "l.new_option:code" 		VALUE = "{ l.option[ l.opt_pos ]:code }">
				<MvASSIGN NAME = "l.new_option:prompt" 		VALUE = "{ l.option[ l.opt_pos ]:prompt }">
				<MvASSIGN NAME = "l.new_option:price" 		VALUE = "{ l.option[ l.opt_pos ]:price }">
				<MvASSIGN NAME = "l.new_option:cost" 		VALUE = "{ l.option[ l.opt_pos ]:cost }">
				<MvASSIGN NAME = "l.new_option:weight" 		VALUE = "{ l.option[ l.opt_pos ]:weight }">
				<MvASSIGN NAME = "l.new_option:image" 		VALUE = "{ l.option[ l.opt_pos ]:image }">

				<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Option_Insert( l.new_option ) }">
					<MvFUNCTIONRETURN VALUE = 0>
				</MvIF>

				<MvIF EXPR = "{ l.attribute[ l.atr_pos ]:default_id EQ l.option[ l.opt_pos ]:id }">
					<MvASSIGN NAME = "l.update_attribute" 		VALUE = 1>
					<MvASSIGN NAME = "l.new_attribute:default_id" 	VALUE = "{ l.new_option:id }">
				</MvIF>

				<MvASSIGN NAME = "l.opt_pos" VALUE = "{ l.opt_pos + 1 }">
			</MvWHILE>

			<MvIF EXPR = "{ l.update_attribute }">
				<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Attribute_Update_Default( l.new_attribute ) }">
					<MvFUNCTIONRETURN VALUE = 0>
				</MvIF>
			</MvIF>
		</MvIF>

		<MvASSIGN NAME = "l.atr_pos" VALUE = "{ l.atr_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	Attribute Template Attributes
|
</MvCOMMENT>

<MvFUNCTION NAME = "AttributeTemplateAttr_Read" PARAMETERS = "attributetemplateattr var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.attributetemplateattr:id"			VALUE = "{ AttributeTemplateAttrs.d.id }">
	<MvASSIGN NAME = "l.attributetemplateattr:attemp_id"	VALUE = "{ AttributeTemplateAttrs.d.attemp_id }">
	<MvASSIGN NAME = "l.attributetemplateattr:default_id"	VALUE = "{ AttributeTemplateAttrs.d.default_id }">
	<MvASSIGN NAME = "l.attributetemplateattr:disporder"	VALUE = "{ AttributeTemplateAttrs.d.disporder }">
	<MvASSIGN NAME = "l.attributetemplateattr:code"			VALUE = "{ AttributeTemplateAttrs.d.code }">
	<MvASSIGN NAME = "l.attributetemplateattr:type"			VALUE = "{ AttributeTemplateAttrs.d.type }">
	<MvASSIGN NAME = "l.attributetemplateattr:prompt"		VALUE = "{ AttributeTemplateAttrs.d.prompt }">
	<MvASSIGN NAME = "l.attributetemplateattr:price"		VALUE = "{ AttributeTemplateAttrs.d.price }">
	<MvASSIGN NAME = "l.attributetemplateattr:cost"			VALUE = "{ AttributeTemplateAttrs.d.cost }">
	<MvASSIGN NAME = "l.attributetemplateattr:weight"		VALUE = "{ AttributeTemplateAttrs.d.weight }">
	<MvASSIGN NAME = "l.attributetemplateattr:required"		VALUE = "{ AttributeTemplateAttrs.d.required }">
	<MvASSIGN NAME = "l.attributetemplateattr:inventory"	VALUE = "{ AttributeTemplateAttrs.d.inventory }">
	<MvASSIGN NAME = "l.attributetemplateattr:image"		VALUE = "{ AttributeTemplateAttrs.d.image }">
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttr_Insert" PARAMETERS = "attributetemplateattr var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT AttributeTemplateAttr_Insert_LowLevel( l.attributetemplateattr ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ [ g.Module_Library_DB ].StoreModuleList_Load_Features_Cached( 'not_attemp', l.modules ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Notify_AttributeTemplateAttr_Insert( l.module, l.attributetemplateattr ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttr_Insert_LowLevel" PARAMETERS = "attributetemplateattr var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.attributetemplateattr:id"			VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'AttributeTemplateAttrs' ) }">
	<MvASSIGN NAME = "l.attributetemplateattr:disporder" 	VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'AttributeTemplateAttributeDisplayOrder' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs
					      ( id, attemp_id, default_id, disporder, code, type, prompt, price, cost, weight, required, inventory, image )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.attributetemplateattr:id, l.attributetemplateattr:attemp_id,
					   l.attributetemplateattr:default_id, l.attributetemplateattr:disporder,
					   l.attributetemplateattr:code, l.attributetemplateattr:type,
					   l.attributetemplateattr:prompt, l.attributetemplateattr:price,
					   l.attributetemplateattr:cost, l.attributetemplateattr:weight,
					   l.attributetemplateattr:required, l.attributetemplateattr:inventory,
					   l.attributetemplateattr:image">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00022', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttr_Update" PARAMETERS = "attributetemplateattr var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT AttributeTemplateAttr_Update_LowLevel( l.attributetemplateattr ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ [ g.Module_Library_DB ].StoreModuleList_Load_Features_Cached( 'not_attemp', l.modules ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Notify_AttributeTemplateAttr_Update( l.module, l.attributetemplateattr ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttr_Update_LowLevel" PARAMETERS = "attributetemplateattr var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs
					      SET
							attemp_id	= ?,
							default_id	= ?,
							disporder	= ?,
							code		= ?,
							type		= ?,
							prompt		= ?,
							price		= ?,
							cost		= ?,
							weight		= ?,
							required	= ?,
							image		= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.attributetemplateattr:attemp_id, l.attributetemplateattr:default_id,
					   l.attributetemplateattr:disporder, l.attributetemplateattr:code,
					   l.attributetemplateattr:type, l.attributetemplateattr:prompt,
					   l.attributetemplateattr:price, l.attributetemplateattr:cost,
					   l.attributetemplateattr:weight, l.attributetemplateattr:required,
					   l.attributetemplateattr:image,
					   l.attributetemplateattr:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00023', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttr_Update_Inventory" PARAMETERS = "attributetemplateattr var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT l.attributetemplateattr:inventory }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ProductKit_Delete_All_AttributeTemplateAttribute( l.attributetemplateattr:id ) OR
						NOT [ g.Module_Library_DB ].ProductVariant_Delete_All_AttributeTemplateAttribute( l.attributetemplateattr:id ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT AttributeTemplateAttr_Update_Inventory_LowLevel( l.attributetemplateattr ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ [ g.Module_Library_DB ].StoreModuleList_Load_Features_Cached( 'not_attemp', l.modules ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Notify_AttributeTemplateAttr_Update( l.module, l.attributetemplateattr ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttr_Update_Inventory_LowLevel" PARAMETERS = "attributetemplateattr var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs
					      SET
							inventory	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.attributetemplateattr:inventory,
					   l.attributetemplateattr:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00049', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttr_Delete_Attr" PARAMETERS = "attribute var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ [ g.Module_Library_DB ].StoreModuleList_Load_Features_Cached( 'not_attemp', l.modules ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Notify_AttributeTemplateAttr_Delete( l.module, l.attribute ) }">
	</MvFOREACH>

	<MvIF EXPR = "{ l.attribute:inventory }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ProductKit_Delete_All_AttributeTemplateAttribute( l.attribute:id ) OR
						NOT [ g.Module_Library_DB ].ProductVariant_Delete_All_AttributeTemplateAttribute( l.attribute:id ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_WSH_DB ].WishListItems_Delete_All_AttributeTemplateAttribute( l.attribute:id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT AttributeTemplateAttr_Delete_ID_LowLevel( l.attribute:id ) OR
					NOT AttributeTemplateOption_Delete_All_Attribute_LowLevel( l.attribute:id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttr_Delete" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "">
	<MvTRANSACT NAME = "Merchant">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_WSH_DB ].WishListItems_Delete_All_AttributeTemplateAttribute( l.id ) }">
		<MvROLLBACK NAME = "Merchant">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT AttributeTemplateAttr_Delete_ID( l.id ) OR
					NOT AttributeTemplateOption_Delete_All_Attribute_LowLevel( l.id ) }">
		<MvROLLBACK NAME = "Merchant">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMIT NAME = "Merchant">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttr_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module_count"			VALUE = "{ [ g.Module_Library_DB ].StoreModuleList_Load_Features_Cached( 'not_attemp', l.modules ) }">
	<MvASSIGN NAME = "l.attribute_loaded"		VALUE = 0>

	<MvIF EXPR = "{ l.module_count }">
		<MvASSIGN NAME = "l.attribute_loaded"	VALUE = "{ AttributeTemplateAttr_Load_ID( l.id, l.attributetemplateattr ) }">
	</MvIF>

	<MvIF EXPR = "{ l.module_count AND l.attribute_loaded }">
		<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ l.module_count }">
			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Notify_AttributeTemplateAttr_Delete( l.module, l.attributetemplateattr ) }">
		</MvFOREACH>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_WSH_DB ].WishListItems_Delete_All_AttributeTemplateAttribute( l.id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ProductKit_Delete_All_AttributeTemplateAttribute( l.id )		OR
					NOT [ g.Module_Library_DB ].ProductVariant_Delete_All_AttributeTemplateAttribute( l.id )	OR
					NOT AttributeTemplateAttr_Delete_ID_LowLevel( l.id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttr_Delete_ID_LowLevel" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs WHERE id = ?' }"
			FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{  [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00024', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttr_Delete_Template" PARAMETERS = "attemp_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs WHERE attemp_id = ?' }"
			FIELDS	= "l.attemp_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{  [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00025', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttr_Load_Code" PARAMETERS = "template_id, code, attributetemplateattr var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AttributeTemplateAttrs"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs
							  WHERE ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) $ ' AND attemp_id = ?' }"
				FIELDS	= "l.code, l.template_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00026', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ AttributeTemplateAttrs.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateAttrs">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-ATT-DTB-00042' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ AttributeTemplateAttr_Read( l.attributetemplateattr ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateAttrs">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttr_Load_ID" PARAMETERS = "id, attributetemplateattr var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AttributeTemplateAttrs"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00060', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ AttributeTemplateAttrs.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateAttrs">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-ATT-DTB-00061' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ AttributeTemplateAttr_Read( l.attributetemplateattr ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateAttrs">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttr_Load_Product_Code" PARAMETERS = "product_id, code, attributetemplateattr var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AttributeTemplateAttrs"
				QUERY	= "{ 'SELECT
								ata.*,
								a.id	AS attr_id
							  FROM
								' $ g.Store_Table_Prefix $ 'Attributes a,
								' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs ata
							  WHERE
								a.product_id	= ?				AND
								a.attemp_id		<> 0			AND
								ata.attemp_id	= a.attemp_id	AND
								' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( 'ata.code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) $ '
							  ORDER BY
								a.disp_order, ata.disporder' }"
				FIELDS	= "l.product_id, l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00051', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ AttributeTemplateAttrs.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateAttrs">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-ATT-DTB-00052' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ AttributeTemplateAttr_Read( l.attributetemplateattr ) }">
	<MvASSIGN NAME = "l.attributetemplateattr:attr_id"	VALUE = "{ AttributeTemplateAttrs.d.attr_id }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateAttrs">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttrList_Load_All" PARAMETERS = "attributetemplateattrs var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AttributeTemplateAttrs"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00027', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.attributetemplateattr_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT AttributeTemplateAttrs.d.EOF }">
		<MvASSIGN NAME = "l.attributetemplateattr_count" VALUE = "{ l.attributetemplateattr_count + 1 }">
		<MvEVAL EXPR = "{ AttributeTemplateAttr_Read( l.attributetemplateattrs[ l.attributetemplateattr_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "AttributeTemplateAttrs" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateAttrs">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-ATT-DTB-00043', l.attributetemplateattr_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttrList_Load_Template" PARAMETERS = "attemp_id, attributetemplateattrs var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AttributeTemplateAttrs"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs WHERE attemp_id = ? ORDER BY disporder' }"
				FIELDS	= "l.attemp_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00028', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.attributetemplateattr_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT AttributeTemplateAttrs.d.EOF }">
		<MvASSIGN NAME = "l.attributetemplateattr_count" VALUE = "{ l.attributetemplateattr_count + 1 }">
		<MvEVAL EXPR = "{ AttributeTemplateAttr_Read( l.attributetemplateattrs[ l.attributetemplateattr_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "AttributeTemplateAttrs" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateAttrs">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-ATT-DTB-00044', l.attributetemplateattr_count ) }">
</MvFUNCTION>

<MvCOMMENT>
|
|	AttributeTemplateOptions
|
</MvCOMMENT>

<MvFUNCTION NAME = "AttributeTemplateOption_Read" PARAMETERS = "attributetemplateoption var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.attributetemplateoption:id"			VALUE = "{ AttributeTemplateOptions.d.id }">
	<MvASSIGN NAME = "l.attributetemplateoption:attemp_id"	VALUE = "{ AttributeTemplateOptions.d.attemp_id }">
	<MvASSIGN NAME = "l.attributetemplateoption:attmpat_id"	VALUE = "{ AttributeTemplateOptions.d.attmpat_id }">
	<MvASSIGN NAME = "l.attributetemplateoption:disporder"	VALUE = "{ AttributeTemplateOptions.d.disporder }">
	<MvASSIGN NAME = "l.attributetemplateoption:code"		VALUE = "{ AttributeTemplateOptions.d.code }">
	<MvASSIGN NAME = "l.attributetemplateoption:prompt"		VALUE = "{ AttributeTemplateOptions.d.prompt }">
	<MvASSIGN NAME = "l.attributetemplateoption:price"		VALUE = "{ AttributeTemplateOptions.d.price }">
	<MvASSIGN NAME = "l.attributetemplateoption:cost"		VALUE = "{ AttributeTemplateOptions.d.cost }">
	<MvASSIGN NAME = "l.attributetemplateoption:weight"		VALUE = "{ AttributeTemplateOptions.d.weight }">
	<MvASSIGN NAME = "l.attributetemplateoption:image"		VALUE = "{ AttributeTemplateOptions.d.image }">
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateOption_Insert" PARAMETERS = "attributetemplateoption var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT AttributeTemplateOption_Insert_LowLevel( l.attributetemplateoption ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ [ g.Module_Library_DB ].StoreModuleList_Load_Features_Cached( 'not_attemp', l.modules ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Notify_AttributeTemplateOption_Insert( l.module, l.attributetemplateoption ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateOption_Insert_LowLevel" PARAMETERS = "attributetemplateoption var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.attributetemplateoption:id"			VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'AttributeTemplateOptions' ) }">
	<MvASSIGN NAME = "l.attributetemplateoption:disporder" 	VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'AttributeTemplateAttributeOptionDisplayOrder' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions
					      ( id, attemp_id, attmpat_id, disporder, code, prompt, price, cost, weight, image )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.attributetemplateoption:id, l.attributetemplateoption:attemp_id,
			 		   l.attributetemplateoption:attmpat_id, l.attributetemplateoption:disporder,
			 		   l.attributetemplateoption:code, l.attributetemplateoption:prompt,
			 		   l.attributetemplateoption:price, l.attributetemplateoption:cost,
			 		   l.attributetemplateoption:weight, l.attributetemplateoption:image">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00029', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateOption_Update" PARAMETERS = "attributetemplateoption var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT AttributeTemplateOption_Update_LowLevel( l.attributetemplateoption ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ [ g.Module_Library_DB ].StoreModuleList_Load_Features_Cached( 'not_attemp', l.modules ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Notify_AttributeTemplateOption_Update( l.module, l.attributetemplateoption ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateOption_Update_LowLevel" PARAMETERS = "attributetemplateoption var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions
					      SET
							attmpat_id	= ?,
							attemp_id	= ?,
							disporder	= ?,
							code		= ?,
							prompt		= ?,
							price		= ?,
							cost		= ?,
							weight		= ?,
							image		= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.attributetemplateoption:attmpat_id, l.attributetemplateoption:attemp_id,
			 		   l.attributetemplateoption:disporder, l.attributetemplateoption:code,
			 		   l.attributetemplateoption:prompt, l.attributetemplateoption:price,
			 		   l.attributetemplateoption:cost, l.attributetemplateoption:weight, l.attributetemplateoption:image,
					   l.attributetemplateoption:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00030', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateOption_Delete" PARAMETERS = "attribute var, option var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ [ g.Module_Library_DB ].StoreModuleList_Load_Features_Cached( 'not_attemp', l.modules ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Notify_AttributeTemplateOption_Delete( l.module, l.option ) }">
	</MvFOREACH>

	<MvIF EXPR = "{ l.attribute:inventory }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ProductKit_Delete_All_AttributeTemplateOption( l.attribute:id, l.option:id ) OR
						NOT [ g.Module_Library_DB ].ProductVariant_Delete_All_AttributeTemplateOption( l.attribute:id, l.option:id ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_WSH_DB ].WishListItems_Delete_All_AttributeTemplateOption( l.attribute:id, l.option:id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT AttributeTemplateOption_Delete_ID_LowLevel( l.option:id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateOption_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT AttributeTemplateOption_Load_ID( l.id, l.option ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ [ g.Module_Library_DB ].StoreModuleList_Load_Features_Cached( 'not_attemp', l.modules ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Notify_AttributeTemplateOption_Delete( l.module, l.option ) }">
	</MvFOREACH>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_WSH_DB ].WishListItems_Delete_All_AttributeTemplateOption( l.option:attmpat_id, l.id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ProductKit_Delete_All_AttributeTemplateOption( l.option:attmpat_id, l.id )		OR
					NOT [ g.Module_Library_DB ].ProductVariant_Delete_All_AttributeTemplateOption( l.option:attmpat_id, l.id )	OR
					NOT AttributeTemplateOption_Delete_ID_LowLevel( l.id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateOption_Delete_ID_LowLevel" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00031', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateOption_Delete_All_Attribute" PARAMETERS = "attribute_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_WSH_DB ].WishListItems_Delete_All_AttributeTemplateAttribute( l.attribute_id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ProductKit_Delete_All_AttributeTemplateAttribute( l.attribute_id ) AND
								 [ g.Module_Library_DB ].ProductVariant_Delete_All_AttributeTemplateAttribute( l.attribute_id ) AND
							     AttributeTemplateOption_Delete_All_Attribute_LowLevel( l.attribute_id ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateOption_Delete_All_Attribute_LowLevel" PARAMETERS = "attribute_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions WHERE attmpat_id = ?' }"
			FIELDS	= "l.attribute_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{  [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00032', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateOption_Delete_Template" PARAMETERS = "template_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions WHERE attemp_id = ?' }"
			FIELDS	= "l.template_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{  [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00033', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateOption_Load_ID" PARAMETERS = "id, attributetemplateoption var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AttributeTemplateOptions"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00034', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ AttributeTemplateOptions.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateOptions">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-ATT-DTB-00045' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ AttributeTemplateOption_Read( l.attributetemplateoption ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateOptions">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateOption_Load_Code" PARAMETERS = "attr_id, code, attributetemplateoption var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AttributeTemplateOptions"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions
							  WHERE ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) $ ' AND attmpat_id = ?' }"
				FIELDS	= "l.code, l.attr_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00035', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ AttributeTemplateOptions.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateOptions">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-ATT-DTB-00046' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ AttributeTemplateOption_Read( l.attributetemplateoption ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateOptions">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateOptionList_Load_Attribute" PARAMETERS = "attmpat_id, attributetemplateoptions var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "AttributeTemplateOptions"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions WHERE attmpat_id = ? ORDER BY disporder' }"
				FIELDS	= "l.attmpat_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00036', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.attributetemplateoption_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT AttributeTemplateOptions.d.EOF }">
		<MvASSIGN NAME = "l.attributetemplateoption_count" VALUE = "{ l.attributetemplateoption_count + 1 }">
		<MvEVAL EXPR = "{ AttributeTemplateOption_Read( l.attributetemplateoptions[ l.attributetemplateoption_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "AttributeTemplateOptions" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateOptions">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-ATT-DTB-00047', l.attributetemplateoption_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeList_Load_Template" PARAMETERS = "template_id, attributes var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Attributes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Attributes WHERE type = \'template\' AND attemp_id = ?' }"
				FIELDS	= "l.template_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00037', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.attribute_count" VALUE = 0>
	<MvWHILE EXPR = "{ NOT Attributes.d.EOF }">
		<MvASSIGN NAME = "l.attribute_count" VALUE = "{ l.attribute_count + 1 }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].Attribute_Read( l.attributes[ l.attribute_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "Attributes" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Attributes">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-ATT-DTB-00048', l.attribute_count ) }">
</MvFUNCTION>

<MvCOMMENT>
|
| Sorting:  Items to be sorted are processed in three groups.  The input array to the sort function just contains the
|		    items to be sorted, their desired offsets, and their current offsets.  We initially sort this data into
|			two groups: items moving up and items moving down.  During processing, items moving down are segregated
|			into two groups: items moving past the highest current offset, and items moving within the current
|			data set.
|
|			Ties are broken using the items original offset.
|
|			The end result is that we process items in the following order:
|
|				+-----------------------+  offset  original_offset
|				|                       |    |            ^
|				|    Items moving up    |    |            |
|				|                       |    v            |
|				+-----------------------+
|				+-----------------------+
|				|                       |    |            |
|				| Items moving past end |    |            |
|				|                       |    v            v
|				+-----------------------+
|				+-----------------------+
|				|                       |    ^            |
|				|   Items moving down   |    |            |
|				|                       |    |            v
|				+-----------------------+
|
</MvCOMMENT>

<MvFUNCTION NAME = "AttributeTemplateAttributeList_Update_Offsets_PastEnd" PARAMETERS = "template_id, pastend var, pastend_count" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| Process records moving past the end.  These records must be sorted in ascending order.
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].SortOffsetArray_PastEnd( l.pastend, l.pastend_count ) }">

	<MvASSIGN NAME = "l.pastend_pos"		VALUE = 1>
	<MvWHILE EXPR = "{ l.pastend_pos LE l.pastend_count }">
		<MvASSIGN NAME = "g.Attributes_Order_AttributeID" 	VALUE = "{ l.pastend[ l.pastend_pos ]:id }">
		<MvASSIGN NAME = "l.disporder"						VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'AttributeTemplateAttributeDisplayOrder' ) }">

		<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
													$ g.Store_Table_Prefix $ 'AttributeTemplateAttrs 
												SET 
													disporder = ? 
												WHERE 
													id = ? AND attemp_id = ?' }" 
												FIELDS = "l.disporder, g.Attributes_Order_AttributeID, l.template_id">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00050', g.MvQUERY_Error ) }">
		</MvIF>
		
		<MvASSIGN NAME = "l.pastend_pos"	VALUE = "{ l.pastend_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttributeList_Update_Offsets" PARAMETERS = "template_id, attributes var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pastend_count"				VALUE = 0>

	<MvASSIGN NAME = "l.attribute_pos"				VALUE = 1>
	<MvASSIGN NAME = "l.attribute_count"			VALUE = "{ miva_array_elements( l.attributes ) }">
	<MvASSIGN NAME = "g.Attributes_Order_AttributeTemplateID" VALUE = "{ l.template_id }">

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].SortOffsetArray( l.attributes, l.attribute_count ) }">

	<MvWHILE EXPR = "{ l.attribute_pos LE l.attribute_count }">
		<MvASSIGN NAME = "g.Attributes_Order_AttributeID" 	VALUE = "{ l.attributes[ l.attribute_pos ]:id }">

		<MvCOMMENT>
		|
		| Determine disporder of current attribute at this offset.
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant',
																				 'AttributeTemplateAttrs',
																				 'SELECT id, disporder FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplateAttrs WHERE id <> ? AND attemp_id = ? ORDER BY disporder',
																				 'g.Attributes_Order_AttributeID, g.Attributes_Order_AttributeTemplateID',
																				 l.attributes[ l.attribute_pos ]:offset - 1,
																				 1 ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00053', g.MvOPENVIEW_Error ) }">
		</MvIF>

		<MvIF EXPR = "{ AttributeTemplateAttrs.d.EOF }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateAttrs">

			<MvCOMMENT>
			|
			| Offset was higher than the last record.  This case requires specialized processing, so save this record for later.
			|
			</MvCOMMENT>

			<MvASSIGN NAME = "l.pastend_count"							VALUE = "{ l.pastend_count + 1 }">
			<MvASSIGN NAME = "l.pastend" INDEX = "{ l.pastend_count }"	VALUE = "{ l.attributes[ l.attribute_pos ] }">

			<MvASSIGN NAME = "l.attribute_pos"	VALUE = "{ l.attribute_pos + 1 }">
		<MvELSEIF EXPR = "{ l.pastend_count }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateAttrs">

			<MvIF EXPR = "{ NOT AttributeTemplateAttributeList_Update_Offsets_PastEnd( l.template_id, l.pastend, l.pastend_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.pastend_count" VALUE = 0>
		<MvELSE>
			<MvASSIGN NAME = "l.disporder" VALUE = "{ AttributeTemplateAttrs.d.disporder }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateAttrs">

			<MvCOMMENT>
			|
			| Make a hole by shifting attributes after this disporder down
			|
			</MvCOMMENT>

			<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
														$ g.Store_Table_Prefix $ 'AttributeTemplateAttrs 
													SET 
														disporder = disporder + 1 
													WHERE 
														disporder >= ? AND attemp_id = ?' }" 
													FIELDS = "l.disporder, l.template_id">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00054', g.MvQUERY_Error ) }">
			</MvIF>

			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'AttributeTemplateAttributeDisplayOrder' ) }">

			<MvCOMMENT>
			|
			| Put the attribute in the newly created hole
			|
			</MvCOMMENT>

			<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
														$ g.Store_Table_Prefix $ 'AttributeTemplateAttrs 
													SET 
														disporder = ? 
													WHERE 
														id = ? AND attemp_id = ?' }" 
													FIELDS = "l.disporder, g.Attributes_Order_AttributeID, l.template_id">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00055', g.MvQUERY_Error ) }">
			</MvIF>

			<MvASSIGN NAME = "l.attribute_pos"	VALUE = "{ l.attribute_pos + 1 }">
		</MvIF>
	</MvWHILE>

	<MvCOMMENT>
	|
	| If all the downward records were past the end, they must be processed here.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.pastend_count }">
		<MvIF EXPR = "{ NOT AttributeTemplateAttributeList_Update_Offsets_PastEnd( l.template_id, l.pastend, l.pastend_count ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>		

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Sorting:  Items to be sorted are processed in three groups.  The input array to the sort function just contains the
|		    items to be sorted, their desired offsets, and their current offsets.  We initially sort this data into
|			two groups: items moving up and items moving down.  During processing, items moving down are segregated
|			into two groups: items moving past the highest current offset, and items moving within the current
|			data set.
|
|			Ties are broken using the items original offset.
|
|			The end result is that we process items in the following order:
|
|				+-----------------------+  offset  original_offset
|				|                       |    |            ^
|				|    Items moving up    |    |            |
|				|                       |    v            |
|				+-----------------------+
|				+-----------------------+
|				|                       |    |            |
|				| Items moving past end |    |            |
|				|                       |    v            v
|				+-----------------------+
|				+-----------------------+
|				|                       |    ^            |
|				|   Items moving down   |    |            |
|				|                       |    |            v
|				+-----------------------+
|
</MvCOMMENT>

<MvFUNCTION NAME = "AttributeTemplateAttributeOptionList_Update_Offsets_PastEnd" PARAMETERS = "template_id, attr_id, pastend var, pastend_count" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| Process records moving past the end.  These records must be sorted in ascending order.
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].SortOffsetArray_PastEnd( l.pastend, l.pastend_count ) }">

	<MvASSIGN NAME = "l.pastend_pos"		VALUE = 1>
	<MvWHILE EXPR = "{ l.pastend_pos LE l.pastend_count }">
		<MvASSIGN NAME = "g.Options_Order_OptionID" 	VALUE = "{ l.pastend[ l.pastend_pos ]:id }">
		<MvASSIGN NAME = "l.disporder"					VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'AttributeTemplateAttributeOptionDisplayOrder' ) }">

		<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
													$ g.Store_Table_Prefix $ 'AttributeTemplateOptions 
												SET 
													disporder = ? 
												WHERE 
													id = ? AND 
													attemp_id = ? AND
													attmpat_id = ?' }" 
												FIELDS = "l.disporder, g.Options_Order_OptionID, l.template_id, l.attr_id">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00056', g.MvQUERY_Error ) }">
		</MvIF>
		
		<MvASSIGN NAME = "l.pastend_pos"	VALUE = "{ l.pastend_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "AttributeTemplateAttributeOptionList_Update_Offsets" PARAMETERS = "template_id, attr_id, options var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pastend_count"						VALUE = 0>

	<MvASSIGN NAME = "l.option_pos"							VALUE = 1>
	<MvASSIGN NAME = "l.option_count"						VALUE = "{ miva_array_elements( l.options ) }">
	<MvASSIGN NAME = "g.Options_Order_AttributeTemplateID" 	VALUE = "{ l.template_id }">
	<MvASSIGN NAME = "g.Options_Order_AttributeID" 			VALUE = "{ l.attr_id }">

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].SortOffsetArray( l.options, l.option_count ) }">

	<MvWHILE EXPR = "{ l.option_pos LE l.option_count }">
		<MvASSIGN NAME = "g.Options_Order_OptionID" 	VALUE = "{ l.options[ l.option_pos ]:id }">

		<MvCOMMENT>
		|
		| Determine disporder of current option at this offset.
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant',
																				 'AttributeTemplateOptions',
																				 'SELECT id, disporder FROM ' $ g.Store_Table_Prefix $ 'AttributeTemplateOptions WHERE id <> ? AND attemp_id = ? AND attmpat_id = ? ORDER BY disporder',
																				 'g.Options_Order_OptionID, g.Options_Order_AttributeTemplateID, g.Options_Order_AttributeID',
																				 l.options[ l.option_pos ]:offset - 1,
																				 1 ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00057', g.MvOPENVIEW_Error ) }">
		</MvIF>

		<MvIF EXPR = "{ AttributeTemplateOptions.d.EOF }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateOptions">

			<MvCOMMENT>
			|
			| Offset was higher than the last record.  This case requires specialized processing, so save this record for later.
			|
			</MvCOMMENT>

			<MvASSIGN NAME = "l.pastend_count"							VALUE = "{ l.pastend_count + 1 }">
			<MvASSIGN NAME = "l.pastend" INDEX = "{ l.pastend_count }"	VALUE = "{ l.options[ l.option_pos ] }">

			<MvASSIGN NAME = "l.option_pos"	VALUE = "{ l.option_pos + 1 }">
		<MvELSEIF EXPR = "{ l.pastend_count }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateOptions">

			<MvIF EXPR = "{ NOT AttributeTemplateAttributeOptionList_Update_Offsets_PastEnd( l.template_id, l.attr_id, l.pastend, l.pastend_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.pastend_count" VALUE = 0>
		<MvELSE>
			<MvASSIGN NAME = "l.disporder" VALUE = "{ AttributeTemplateOptions.d.disporder }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "AttributeTemplateOptions">

			<MvCOMMENT>
			|
			| Make a hole by shifting options after this disporder down
			|
			</MvCOMMENT>

			<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
														$ g.Store_Table_Prefix $ 'AttributeTemplateOptions 
													SET 
														disporder = disporder + 1 
													WHERE 
														disporder >= ? AND 
														attemp_id = ? AND
														attmpat_id = ?' }" 
													FIELDS = "l.disporder, l.template_id, g.Options_Order_AttributeID">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00058', g.MvQUERY_Error ) }">
			</MvIF>

			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'AttributeTemplateAttributeOptionDisplayOrder' ) }">

			<MvCOMMENT>
			|
			| Put the option in the newly created hole
			|
			</MvCOMMENT>

			<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
														$ g.Store_Table_Prefix $ 'AttributeTemplateOptions 
													SET 
														disporder = ? 
													WHERE 
														id = ? AND 
														attemp_id = ? AND
														attmpat_id = ?' }" 
													FIELDS = "l.disporder, g.Options_Order_OptionID, l.template_id, g.Options_Order_AttributeID">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-ATT-DTB-00059', g.MvQUERY_Error ) }">
			</MvIF>

			<MvASSIGN NAME = "l.option_pos"	VALUE = "{ l.option_pos + 1 }">
		</MvIF>
	</MvWHILE>

	<MvCOMMENT>
	|
	| If all the downward records were past the end, they must be processed here.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.pastend_count }">
		<MvIF EXPR = "{ NOT AttributeTemplateAttributeOptionList_Update_Offsets_PastEnd( l.template_id, l.attr_id, l.pastend, l.pastend_count ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>		

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>


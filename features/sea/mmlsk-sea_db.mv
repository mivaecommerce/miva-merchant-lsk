<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-SEA-DB-
| Next Error Code: 31
|
</MvCOMMENT>

<MvFUNCTION NAME = "SEA_Store_Create" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| sNN_SearchSettings
	|
	</MvCOMMENT>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'SearchSettings
						  (
							code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							code_st		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )		$ ',
							code_rel	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							code_wt		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							name_st		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )		$ ',
							name_rel	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							name_wt		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							sku			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							sku_st		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )		$ ',
							sku_rel		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							sku_wt		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							descrip		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							desc_st		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )		$ ',
							desc_rel	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							desc_wt		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							atr_code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							acode_st	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )		$ ',
							acode_rel	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							acode_wt	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							atr_prompt	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							aprom_st	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )		$ ',
							aprom_rel	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							aprom_wt	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							opt_code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							ocode_st	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )		$ ',
							ocode_rel	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							ocode_wt	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							opt_prompt	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							oprom_st	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )		$ ',
							oprom_rel	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							oprom_wt	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
							max_terms	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							max_res		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							logrtanon	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							logrtcust	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							logrtorder	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							logexua		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvEVAL EXPR = "{ SearchSettings_Default( l.searchsettings ) }">

	<MvIF EXPR = "{ NOT SearchSettings_Insert( l.searchsettings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_SearchableCustomFields
	|
	</MvCOMMENT>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'SearchableCustomFields
						  (
							module_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()			$ ',
							field_code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
							enabled		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							searchtype	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )		$ ',
							relevance	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
							weight		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'SearchableCustomFields_1 ON ' $ g.Store_Table_Prefix $ 'SearchableCustomFields ( module_id, field_code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_SearchLogEntries
	|
	</MvCOMMENT>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'SearchLogEntries
						  (
							id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							srchses_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 40 )	$ ',
							cust_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							order_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							dtstamp		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							ip			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )	$ ',
							useragent	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )	$ ',
							search		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )	$ ',
							prodcount	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							origin		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 100 )	$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00016', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'SearchLogEntries', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'SearchLogEntries_1 ON ' $ g.Store_Table_Prefix $ 'SearchLogEntries ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00017', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'SearchLogEntries_2 ON ' $ g.Store_Table_Prefix $ 'SearchLogEntries ( srchses_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00018', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'SearchLogEntries_3 ON ' $ g.Store_Table_Prefix $ 'SearchLogEntries ( cust_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00019', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'SearchLogEntries_4 ON ' $ g.Store_Table_Prefix $ 'SearchLogEntries ( order_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00020', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'SearchLogEntries_5 ON ' $ g.Store_Table_Prefix $ 'SearchLogEntries ( dtstamp )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00030', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SEA_Store_Delete" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'SearchSettings' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'SearchableCustomFields' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'SearchLogEntries' }">

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'SearchLogEntries' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_SearchSettings
|
</MvCOMMENT>

<MvFUNCTION NAME = "SearchSettings_Read" PARAMETERS = "searchsettings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.searchsettings:code"		VALUE = "{ SearchSettings.d.code }">
	<MvASSIGN NAME = "l.searchsettings:code_st"		VALUE = "{ SearchSettings.d.code_st }">
	<MvASSIGN NAME = "l.searchsettings:code_rel"	VALUE = "{ SearchSettings.d.code_rel }">
	<MvASSIGN NAME = "l.searchsettings:code_wt"		VALUE = "{ SearchSettings.d.code_wt }">
	<MvASSIGN NAME = "l.searchsettings:name"		VALUE = "{ SearchSettings.d.name }">
	<MvASSIGN NAME = "l.searchsettings:name_st"		VALUE = "{ SearchSettings.d.name_st }">
	<MvASSIGN NAME = "l.searchsettings:name_rel"	VALUE = "{ SearchSettings.d.name_rel }">
	<MvASSIGN NAME = "l.searchsettings:name_wt"		VALUE = "{ SearchSettings.d.name_wt }">
	<MvASSIGN NAME = "l.searchsettings:sku"			VALUE = "{ SearchSettings.d.sku }">
	<MvASSIGN NAME = "l.searchsettings:sku_st"		VALUE = "{ SearchSettings.d.sku_st }">
	<MvASSIGN NAME = "l.searchsettings:sku_rel"		VALUE = "{ SearchSettings.d.sku_rel }">
	<MvASSIGN NAME = "l.searchsettings:sku_wt"		VALUE = "{ SearchSettings.d.sku_wt }">
	<MvASSIGN NAME = "l.searchsettings:descrip"		VALUE = "{ SearchSettings.d.descrip }">
	<MvASSIGN NAME = "l.searchsettings:desc_st"		VALUE = "{ SearchSettings.d.desc_st }">
	<MvASSIGN NAME = "l.searchsettings:desc_rel"	VALUE = "{ SearchSettings.d.desc_rel }">
	<MvASSIGN NAME = "l.searchsettings:desc_wt"		VALUE = "{ SearchSettings.d.desc_wt }">
	<MvASSIGN NAME = "l.searchsettings:atr_code"	VALUE = "{ SearchSettings.d.atr_code }">
	<MvASSIGN NAME = "l.searchsettings:acode_st"	VALUE = "{ SearchSettings.d.acode_st }">
	<MvASSIGN NAME = "l.searchsettings:acode_rel"	VALUE = "{ SearchSettings.d.acode_rel }">
	<MvASSIGN NAME = "l.searchsettings:acode_wt"	VALUE = "{ SearchSettings.d.acode_wt }">
	<MvASSIGN NAME = "l.searchsettings:atr_prompt"	VALUE = "{ SearchSettings.d.atr_prompt }">
	<MvASSIGN NAME = "l.searchsettings:aprom_st"	VALUE = "{ SearchSettings.d.aprom_st }">
	<MvASSIGN NAME = "l.searchsettings:aprom_rel"	VALUE = "{ SearchSettings.d.aprom_rel }">
	<MvASSIGN NAME = "l.searchsettings:aprom_wt"	VALUE = "{ SearchSettings.d.aprom_wt }">
	<MvASSIGN NAME = "l.searchsettings:opt_code"	VALUE = "{ SearchSettings.d.opt_code }">
	<MvASSIGN NAME = "l.searchsettings:ocode_st"	VALUE = "{ SearchSettings.d.ocode_st }">
	<MvASSIGN NAME = "l.searchsettings:ocode_rel"	VALUE = "{ SearchSettings.d.ocode_rel }">
	<MvASSIGN NAME = "l.searchsettings:ocode_wt"	VALUE = "{ SearchSettings.d.ocode_wt }">
	<MvASSIGN NAME = "l.searchsettings:opt_prompt"	VALUE = "{ SearchSettings.d.opt_prompt }">
	<MvASSIGN NAME = "l.searchsettings:oprom_st"	VALUE = "{ SearchSettings.d.oprom_st }">
	<MvASSIGN NAME = "l.searchsettings:oprom_rel"	VALUE = "{ SearchSettings.d.oprom_rel }">
	<MvASSIGN NAME = "l.searchsettings:oprom_wt"	VALUE = "{ SearchSettings.d.oprom_wt }">
	<MvASSIGN NAME = "l.searchsettings:max_terms"	VALUE = "{ SearchSettings.d.max_terms }">
	<MvASSIGN NAME = "l.searchsettings:max_res"		VALUE = "{ SearchSettings.d.max_res }">
	<MvASSIGN NAME = "l.searchsettings:logrtanon"	VALUE = "{ SearchSettings.d.logrtanon }">
	<MvASSIGN NAME = "l.searchsettings:logrtcust"	VALUE = "{ SearchSettings.d.logrtcust }">
	<MvASSIGN NAME = "l.searchsettings:logrtorder"	VALUE = "{ SearchSettings.d.logrtorder }">
	<MvASSIGN NAME = "l.searchsettings:logexua"		VALUE = "{ SearchSettings.d.logexua }">
</MvFUNCTION>

<MvFUNCTION NAME = "SearchSettings_Default" PARAMETERS = "searchsettings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.searchsettings:code"		VALUE = 1>
	<MvASSIGN NAME = "l.searchsettings:code_st"		VALUE = "contains_term">
	<MvASSIGN NAME = "l.searchsettings:code_rel"	VALUE = 0>
	<MvASSIGN NAME = "l.searchsettings:code_wt"		VALUE = 1.00>
	<MvASSIGN NAME = "l.searchsettings:name"		VALUE = 1>
	<MvASSIGN NAME = "l.searchsettings:name_st"		VALUE = "contains_term">
	<MvASSIGN NAME = "l.searchsettings:name_rel"	VALUE = 0>
	<MvASSIGN NAME = "l.searchsettings:name_wt"		VALUE = 1.00>
	<MvASSIGN NAME = "l.searchsettings:sku"			VALUE = 1>
	<MvASSIGN NAME = "l.searchsettings:sku_st"		VALUE = "contains_term">
	<MvASSIGN NAME = "l.searchsettings:sku_rel"		VALUE = 0>
	<MvASSIGN NAME = "l.searchsettings:sku_wt"		VALUE = 1.00>
	<MvASSIGN NAME = "l.searchsettings:descrip"		VALUE = 1>
	<MvASSIGN NAME = "l.searchsettings:desc_st"		VALUE = "contains_term">
	<MvASSIGN NAME = "l.searchsettings:desc_rel"	VALUE = 0>
	<MvASSIGN NAME = "l.searchsettings:desc_wt"		VALUE = 1.00>
	<MvASSIGN NAME = "l.searchsettings:atr_code"	VALUE = 0>
	<MvASSIGN NAME = "l.searchsettings:acode_st"	VALUE = "contains_term">
	<MvASSIGN NAME = "l.searchsettings:acode_rel"	VALUE = 0>
	<MvASSIGN NAME = "l.searchsettings:acode_wt"	VALUE = 1.00>
	<MvASSIGN NAME = "l.searchsettings:atr_prompt"	VALUE = 0>
	<MvASSIGN NAME = "l.searchsettings:aprom_st"	VALUE = "contains_term">
	<MvASSIGN NAME = "l.searchsettings:aprom_rel"	VALUE = 0>
	<MvASSIGN NAME = "l.searchsettings:aprom_wt"	VALUE = 1.00>
	<MvASSIGN NAME = "l.searchsettings:opt_code"	VALUE = 0>
	<MvASSIGN NAME = "l.searchsettings:ocode_st"	VALUE = "contains_term">
	<MvASSIGN NAME = "l.searchsettings:ocode_rel"	VALUE = 0>
	<MvASSIGN NAME = "l.searchsettings:ocode_wt"	VALUE = 1.00>
	<MvASSIGN NAME = "l.searchsettings:opt_prompt"	VALUE = 0>
	<MvASSIGN NAME = "l.searchsettings:oprom_st"	VALUE = "contains_term">
	<MvASSIGN NAME = "l.searchsettings:oprom_rel"	VALUE = 0>
	<MvASSIGN NAME = "l.searchsettings:oprom_wt"	VALUE = 1.00>
	<MvASSIGN NAME = "l.searchsettings:max_terms"	VALUE = 10>
	<MvASSIGN NAME = "l.searchsettings:max_res"		VALUE = 250>
	<MvASSIGN NAME = "l.searchsettings:logrtanon"	VALUE = 7>
	<MvASSIGN NAME = "l.searchsettings:logrtcust"	VALUE = 30>
	<MvASSIGN NAME = "l.searchsettings:logrtorder"	VALUE = 30>
	<MvASSIGN NAME = "l.searchsettings:logexua"		VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "SearchSettings_Insert" PARAMETERS = "searchsettings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'SearchSettings
						  ( code, code_st, code_rel, code_wt, name, name_st, name_rel, name_wt, sku, sku_st, sku_rel, sku_wt,
						    descrip, desc_st, desc_rel, desc_wt, atr_code, acode_st, acode_rel, acode_wt, atr_prompt, aprom_st,
						    aprom_rel, aprom_wt, opt_code, ocode_st, ocode_rel, ocode_wt, opt_prompt, oprom_st, oprom_rel,
						    oprom_wt, max_terms, max_res, logrtanon, logrtcust, logrtorder, logexua )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.searchsettings:code, l.searchsettings:code_st, l.searchsettings:code_rel, l.searchsettings:code_wt,
			 		   l.searchsettings:name, l.searchsettings:name_st, l.searchsettings:name_rel, l.searchsettings:name_wt,
			 		   l.searchsettings:sku, l.searchsettings:sku_st, l.searchsettings:sku_rel, l.searchsettings:sku_wt,
			 		   l.searchsettings:descrip, l.searchsettings:desc_st, l.searchsettings:desc_rel, l.searchsettings:desc_wt,
			 		   l.searchsettings:atr_code, l.searchsettings:acode_st, l.searchsettings:acode_rel, l.searchsettings:acode_wt,
			 		   l.searchsettings:atr_prompt, l.searchsettings:aprom_st, l.searchsettings:aprom_rel, l.searchsettings:aprom_wt,
			 		   l.searchsettings:opt_code, l.searchsettings:ocode_st, l.searchsettings:ocode_rel, l.searchsettings:ocode_wt,
			 		   l.searchsettings:opt_prompt, l.searchsettings:oprom_st, l.searchsettings:oprom_rel, l.searchsettings:oprom_wt,
			 		   l.searchsettings:max_terms, l.searchsettings:max_res, l.searchsettings:logrtanon, l.searchsettings:logrtcust, 
			 		   l.searchsettings:logrtorder, l.searchsettings:logexua">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchSettings_Update" PARAMETERS = "searchsettings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT SearchSettings_Load_Cached( l.original_searchsettings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT SearchSettings_Update_LowLevel( l.searchsettings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ miva_array_serialize( l.original_searchsettings ) NE miva_array_serialize( l.searchsettings ) }">
		<MvIF EXPR = "{ NOT [ g.Feature_Filename_SEA_UT ].Search_Update_FULLTEXT_Indexes( l.original_searchsettings, l.searchsettings ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchSettings_Update_LowLevel" PARAMETERS = "searchsettings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'SearchSettings
			 			  SET
			 			  	code		= ?,
							code_st		= ?,
							code_rel	= ?,
							code_wt		= ?,
							name		= ?,
							name_st		= ?,
							name_rel	= ?,
							name_wt		= ?,
							sku			= ?,
							sku_st		= ?,
							sku_rel		= ?,
							sku_wt		= ?,
							descrip		= ?,
							desc_st		= ?,
							desc_rel	= ?,
							desc_wt		= ?,
							atr_code	= ?,
							acode_st	= ?,
							acode_rel	= ?,
							acode_wt	= ?,
							atr_prompt	= ?,
							aprom_st	= ?,
							aprom_rel	= ?,
							aprom_wt	= ?,
							opt_code	= ?,
							ocode_st	= ?,
							ocode_rel	= ?,
							ocode_wt	= ?,
							opt_prompt	= ?,
							oprom_st	= ?,
							oprom_rel	= ?,
							oprom_wt	= ?,
							max_terms	= ?,
							max_res		= ?,
							logrtanon	= ?,
							logrtcust	= ?,
							logrtorder	= ?,
							logexua		= ?' }"
			 FIELDS = "l.searchsettings:code, l.searchsettings:code_st, l.searchsettings:code_rel, l.searchsettings:code_wt,
			 		   l.searchsettings:name, l.searchsettings:name_st, l.searchsettings:name_rel, l.searchsettings:name_wt,
			 		   l.searchsettings:sku, l.searchsettings:sku_st, l.searchsettings:sku_rel, l.searchsettings:sku_wt,
			 		   l.searchsettings:descrip, l.searchsettings:desc_st, l.searchsettings:desc_rel, l.searchsettings:desc_wt,
			 		   l.searchsettings:atr_code, l.searchsettings:acode_st, l.searchsettings:acode_rel, l.searchsettings:acode_wt,
			 		   l.searchsettings:atr_prompt, l.searchsettings:aprom_st, l.searchsettings:aprom_rel, l.searchsettings:aprom_wt,
			 		   l.searchsettings:opt_code, l.searchsettings:ocode_st, l.searchsettings:ocode_rel, l.searchsettings:ocode_wt,
			 		   l.searchsettings:opt_prompt, l.searchsettings:oprom_st, l.searchsettings:oprom_rel, l.searchsettings:oprom_wt,
			 		   l.searchsettings:max_terms, l.searchsettings:max_res, l.searchsettings:logrtanon, l.searchsettings:logrtcust, 
			 		   l.searchsettings:logrtorder, l.searchsettings:logexua">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchSettings_Load_Cached" PARAMETERS = "searchsettings var" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCE NAME = "l.cache" VARIABLE = "g.Session:cache:SEA:searchsettings">

	<MvIF EXPR = "{ ISNULL l.cache }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ SearchSettings_Load( l.cache:searchsettings ) }">

		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.searchsettings"					VALUE = "{ l.cache:searchsettings }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchSettings_Load" PARAMETERS = "searchsettings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "SearchSettings"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'SearchSettings' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00006', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvEVAL EXPR = "{ SearchSettings_Read( l.searchsettings ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SearchSettings">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_SearchableCustomFields
|
</MvCOMMENT>

<MvFUNCTION NAME = "SearchableCustomField_Default" PARAMETERS = "module_id, field_code, searchablecustomfield var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.searchablecustomfield:module_id"	VALUE = "{ l.module_id }">
	<MvASSIGN NAME = "l.searchablecustomfield:field_code"	VALUE = "{ l.field_code }">
	<MvASSIGN NAME = "l.searchablecustomfield:enabled"		VALUE = 0>
	<MvASSIGN NAME = "l.searchablecustomfield:searchtype"	VALUE = "contains_term">
	<MvASSIGN NAME = "l.searchablecustomfield:relevance"	VALUE = 0>
	<MvASSIGN NAME = "l.searchablecustomfield:weight"		VALUE = 1.00>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchableCustomField_Read" PARAMETERS = "searchablecustomfield var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.searchablecustomfield:module_id"	VALUE = "{ SearchableCustomFields.d.module_id }">
	<MvASSIGN NAME = "l.searchablecustomfield:field_code"	VALUE = "{ SearchableCustomFields.d.field_code }">
	<MvASSIGN NAME = "l.searchablecustomfield:enabled"		VALUE = "{ SearchableCustomFields.d.enabled }">
	<MvASSIGN NAME = "l.searchablecustomfield:searchtype"	VALUE = "{ SearchableCustomFields.d.searchtype }">
	<MvASSIGN NAME = "l.searchablecustomfield:relevance"	VALUE = "{ SearchableCustomFields.d.relevance }">
	<MvASSIGN NAME = "l.searchablecustomfield:weight"		VALUE = "{ SearchableCustomFields.d.weight }">
</MvFUNCTION>

<MvFUNCTION NAME = "SearchableCustomFields_Equal_Default" PARAMETERS = "searchablecustomfield var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.searchablecustomfield:enabled }">							<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ l.searchablecustomfield:searchtype NE 'contains_term' }">	<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ l.searchablecustomfield:relevance }">						<MvFUNCTIONRETURN VALUE = 0>	</MvIF>
	<MvIF EXPR = "{ l.searchablecustomfield:weight NE 1.00 }">					<MvFUNCTIONRETURN VALUE = 0>	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchableCustomField_Insert" PARAMETERS = "searchablecustomfield var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'SearchableCustomFields
						  ( module_id, field_code, enabled, searchtype, relevance, weight )
						  VALUES
						  ( ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.searchablecustomfield:module_id, l.searchablecustomfield:field_code, l.searchablecustomfield:enabled,
			 		   l.searchablecustomfield:searchtype, l.searchablecustomfield:relevance, l.searchablecustomfield:weight">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchableCustomField_Update" PARAMETERS = "searchablecustomfield var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'SearchableCustomFields
			 			  SET
			 			  	enabled		= ?,
							searchtype	= ?,
							relevance	= ?,
			 			  	weight		= ?
			 			  WHERE
			 			  	module_id 	= ? AND
			 			  	field_code	= ?' }"
			 FIELDS	= "l.searchablecustomfield:enabled, l.searchablecustomfield:searchtype, l.searchablecustomfield:relevance,
					   l.searchablecustomfield:weight,
					   l.searchablecustomfield:module_id, l.searchablecustomfield:field_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00013', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchableCustomField_Delete_Field" PARAMETERS = "module_id, field_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'SearchableCustomFields WHERE module_id = ? AND ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'field_code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
			 FIELDS	= "l.module_id, l.field_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00014', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchableCustomField_Delete_All" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'SearchableCustomFields' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchableCustomField_Disable_All" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'SearchableCustomFields SET enabled = 0' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00015', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchableCustomField_Load_FieldOrDefault" PARAMETERS = "module_id, field_code, searchablecustomfield var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ SearchableCustomField_Load( l.module_id, l.field_code, l.searchablecustomfield ) }">	<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">										<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvEVAL EXPR = "{ SearchableCustomField_Default( l.module_id, l.field_code, l.searchablecustomfield ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchableCustomField_Load" PARAMETERS = "module_id, field_code, searchablecustomfield var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "SearchableCustomFields"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'SearchableCustomFields WHERE module_id = ? AND ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'field_code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.module_id, l.field_code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ SearchableCustomFields.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "SearchableCustomFields">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-SEA-DB-00010' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ SearchableCustomField_Read( l.searchablecustomfield ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SearchableCustomFields">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchableCustomFieldList_Load" PARAMETERS = "searchablecustomfields var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "SearchableCustomFields"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'SearchableCustomFields ORDER BY module_id, field_code' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00011', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT SearchableCustomFields.d.EOF }">
		<MvEVAL EXPR = "{ SearchableCustomField_Read( l.searchablecustomfields[ ++l.count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "SearchableCustomFields" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SearchableCustomFields">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-SEA-DB-00012', l.count ) }">
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_SearchLogEntries
|
</MvCOMMENT>

<MvFUNCTION NAME = "SearchLogEntry_Read" PARAMETERS = "searchlogentry var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.searchlogentry:id"			VALUE = "{ SearchLogEntries.d.id }">
	<MvASSIGN NAME = "l.searchlogentry:srchses_id"	VALUE = "{ SearchLogEntries.d.srchses_id }">
	<MvASSIGN NAME = "l.searchlogentry:cust_id"		VALUE = "{ SearchLogEntries.d.cust_id }">
	<MvASSIGN NAME = "l.searchlogentry:order_id"	VALUE = "{ SearchLogEntries.d.order_id }">
	<MvASSIGN NAME = "l.searchlogentry:dtstamp"		VALUE = "{ SearchLogEntries.d.dtstamp }">
	<MvASSIGN NAME = "l.searchlogentry:ip"			VALUE = "{ SearchLogEntries.d.ip }">
	<MvASSIGN NAME = "l.searchlogentry:useragent"	VALUE = "{ SearchLogEntries.d.useragent }">
	<MvASSIGN NAME = "l.searchlogentry:search"		VALUE = "{ SearchLogEntries.d.search }">
	<MvASSIGN NAME = "l.searchlogentry:prodcount"	VALUE = "{ SearchLogEntries.d.prodcount }">
	<MvASSIGN NAME = "l.searchlogentry:origin"		VALUE = "{ SearchLogEntries.d.origin }">
</MvFUNCTION>

<MvFUNCTION NAME = "SearchLogEntry_Load_ID" PARAMETERS = "id, searchlogentry var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "SearchLogEntries"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'SearchLogEntries WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00021', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ SearchLogEntries.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "SearchLogEntries">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-SEA-DB-00026' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ SearchLogEntry_Read( l.searchlogentry ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "SearchLogEntries">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchLogEntry_Insert" PARAMETERS = "searchlogentry var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.searchlogentry:id" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'SearchLogEntries' ) }">

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'SearchLogEntries
						  ( id, srchses_id, cust_id, order_id, dtstamp, ip, useragent, search, prodcount, origin )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.searchlogentry:id, l.searchlogentry:srchses_id, l.searchlogentry:cust_id, l.searchlogentry:order_id, l.searchlogentry:dtstamp, 
			 		   l.searchlogentry:ip, l.searchlogentry:useragent, l.searchlogentry:search, l.searchlogentry:prodcount, l.searchlogentry:origin">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00022', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchLogEntry_Update_SearchSession_Customer" PARAMETERS = "srchses_id, cust_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'SearchLogEntries
			 			  SET
			 			  	cust_id		= ?
			 			  WHERE
			 			  	srchses_id 	= ?' }"
			 FIELDS	= "l.cust_id,
					   l.srchses_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00023', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchLogEntry_Update_SearchSession_Order" PARAMETERS = "srchses_id, order_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'SearchLogEntries
			 			  SET
			 			  	order_id	= ?
			 			  WHERE
			 			  	srchses_id 	= ?' }"
			 FIELDS	= "l.order_id,
					   l.srchses_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00024', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchLogEntry_Delete" PARAMETERS = "searchlogentry var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'SearchLogEntries WHERE id = ?' }"
			 FIELDS	= "l.searchlogentry:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00025', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchLogEntry_Delete_Anonymous_OlderThan" PARAMETERS = "dtstamp" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'SearchLogEntries WHERE dtstamp < ? AND cust_id = 0 AND order_id = 0' }"
			 FIELDS = "l.dtstamp">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00027', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchLogEntry_Delete_Customer_OlderThan" PARAMETERS = "dtstamp" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'SearchLogEntries WHERE dtstamp < ? AND cust_id <> 0 AND order_id = 0' }"
			 FIELDS = "l.dtstamp">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00028', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SearchLogEntry_Delete_Order_OlderThan" PARAMETERS = "dtstamp" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'SearchLogEntries WHERE dtstamp < ? AND order_id <> 0' }"
			 FIELDS = "l.dtstamp">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-SEA-DB-00029', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

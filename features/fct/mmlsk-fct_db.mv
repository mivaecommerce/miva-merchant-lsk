<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2019 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-FCT-DTB-
| Next Error Code: 37
|
</MvCOMMENT>

<MvFUNCTION NAME = "FCT_Store_Create" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| sNN_FacetRules
	|
	</MvCOMMENT>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'FacetRules
			  		      (
			  		    	mod_code 	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )	$ ',
			  		    	facet_code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )	$ ',
			  		    	priority	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							handle		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 10 )	$ ',
							cathandle	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 10 )	$ ',
			  		    	disp_name	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )	$ ',
			  		    	type		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )	$ '
			  		      )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'FacetRules_1 ON ' $ g.Store_Table_Prefix $ 'FacetRules ( mod_code, facet_code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'FacetRules_2 ON ' $ g.Store_Table_Prefix $ 'FacetRules ( priority DESC )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00022', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_CategoryFacetRules
	|
	</MvCOMMENT>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'CategoryFacetRules
			  		      (
							cat_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
			  		    	mod_code 	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )	$ ',
			  		    	facet_code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )	$ ',
			  		    	priority	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							handle		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 10 )	$ ',
			  		    	disp_name	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )	$ ',
			  		    	type		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )	$ '
			  		      )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00023', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'CategoryFacetRules_1 ON ' $ g.Store_Table_Prefix $ 'CategoryFacetRules ( cat_id, mod_code, facet_code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00024', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'CategoryFacetRules_2 ON ' $ g.Store_Table_Prefix $ 'CategoryFacetRules ( cat_id DESC, priority DESC )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00025', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_CachedProductListFacets
	|
	</MvCOMMENT>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'CachedProductListFacets
			  		      (
							id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							cache_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							module_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							facet_code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )	$ ',
							type		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 )	$ ',
							populated	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ '
			  		      )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00010', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'CachedProductListFacets_1 ON ' $ g.Store_Table_Prefix $ 'CachedProductListFacets ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00011', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'CachedProductListFacets_2 ON ' $ g.Store_Table_Prefix $ 'CachedProductListFacets ( cache_id, module_id, facet_code, type )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00012', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'CachedProductListFacets', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_CachedProductListFacetValues
	|
	</MvCOMMENT>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'CachedProductListFacetValues
			  		      (
							cachfct_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							cache_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							prodcount	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ ',
							value		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )	$ ',
							disp_order	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()		$ '
			  		      )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00013', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'CachedProductListFacetValues_1 ON ' $ g.Store_Table_Prefix $ 'CachedProductListFacetValues ( cachfct_id, disp_order )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00014', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'CachedProductListFacetValues_2 ON ' $ g.Store_Table_Prefix $ 'CachedProductListFacetValues ( cache_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00015', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "FCT_Store_Delete" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'FacetRules' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'CategoryFacetRules' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'CachedProductListFacets' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'CachedProductListFacetValues' }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "FCT_Create_Data_Files" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].DomainPrivilege_Insert( 'FACT', 'Facet Rules' ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "FCT_Delete_Data_Files" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_FacetRules
|
</MvCOMMENT>

<MvFUNCTION NAME = "FacetRules_Default" PARAMETERS = "module_code, facet_code, facetrules var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.facetrules:mod_code" 	VALUE = "{ l.module_code }">
	<MvASSIGN NAME = "l.facetrules:facet_code" 	VALUE = "{ l.facet_code }">
	<MvASSIGN NAME = "l.facetrules:priority" 	VALUE = 0>
	<MvASSIGN NAME = "l.facetrules:handle"		VALUE = "show">
	<MvASSIGN NAME = "l.facetrules:cathandle"	VALUE = "show">
	<MvASSIGN NAME = "l.facetrules:disp_name" 	VALUE = "">
	<MvASSIGN NAME = "l.facetrules:type" 		VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "FacetRules_Read" PARAMETERS = "facetrules var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.facetrules:mod_code" 	VALUE = "{ FacetRules.d.mod_code }">
	<MvASSIGN NAME = "l.facetrules:facet_code" 	VALUE = "{ FacetRules.d.facet_code }">
	<MvASSIGN NAME = "l.facetrules:priority" 	VALUE = "{ FacetRules.d.priority }">
	<MvASSIGN NAME = "l.facetrules:handle"		VALUE = "{ FacetRules.d.handle }">
	<MvASSIGN NAME = "l.facetrules:cathandle"	VALUE = "{ FacetRules.d.cathandle }">
	<MvASSIGN NAME = "l.facetrules:disp_name" 	VALUE = "{ FacetRules.d.disp_name }">
	<MvASSIGN NAME = "l.facetrules:type" 		VALUE = "{ FacetRules.d.type }">
</MvFUNCTION>

<MvFUNCTION NAME = "FacetRules_Insert" PARAMETERS = "facetrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'FacetRules
			 			  ( mod_code, facet_code, priority, handle, cathandle, disp_name, type )
			 			  VALUES
			 			  ( ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.facetrules:mod_code, l.facetrules:facet_code, l.facetrules:priority,
					   l.facetrules:handle, l.facetrules:cathandle, l.facetrules:disp_name,
					   l.facetrules:type">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "FacetRules_Update" PARAMETERS = "facetrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'FacetRules
			 			  SET
			 			  	priority	= ?,
							handle		= ?,
							cathandle	= ?,
			 			  	disp_name	= ?,
			 			  	type		= ?
			 			  WHERE
			 			  	mod_code 	= ? AND
			 			  	facet_code	= ?' }"
			 FIELDS	= "l.facetrules:priority, l.facetrules:handle, l.facetrules:cathandle,
					   l.facetrules:disp_name, l.facetrules:type,
					   l.facetrules:mod_code, l.facetrules:facet_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "FacetRules_Delete" PARAMETERS = "mod_code, facet_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'FacetRules WHERE mod_code = ? AND facet_code = ?' }"
			 FIELDS	= "l.mod_code, l.facet_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "FacetRules_Load_FacetOrDefault" PARAMETERS = "mod_code, facet_code, facetrules var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ FacetRules_Load( l.mod_code, l.facet_code, l.facetrules ) }">	<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">				<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvEVAL EXPR = "{ FacetRules_Default( l.mod_code, l.facet_code, l.facetrules ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "FacetRules_Load" PARAMETERS = "mod_code, facet_code, facetrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "FacetRules"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'FacetRules WHERE mod_code = ? AND facet_code = ?' }"
				FIELDS	= "l.mod_code, l.facet_code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00006', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ FacetRules.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "FacetRules">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-FCT-DTB-00007' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ FacetRules_Read( l.facetrules ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "FacetRules">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "FacetRulesList_Load_All" PARAMETERS = "facetrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "FacetRules"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'FacetRules ORDER BY priority DESC' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00008', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT FacetRules.d.EOF }">
		<MvEVAL EXPR = "{ FacetRules_Read( l.facetrules[ ++l.count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "FacetRules" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "FacetRules">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-FCT-DTB-00009', l.count ) }">
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_CategoryFacetRules
|
</MvCOMMENT>

<MvFUNCTION NAME = "CategoryFacetRules_Default" PARAMETERS = "cat_id, module_code, facet_code, categoryfacetrules var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.categoryfacetrules:cat_id"		VALUE = "{ l.cat_id }">
	<MvASSIGN NAME = "l.categoryfacetrules:mod_code" 	VALUE = "{ l.module_code }">
	<MvASSIGN NAME = "l.categoryfacetrules:facet_code" 	VALUE = "{ l.facet_code }">
	<MvASSIGN NAME = "l.categoryfacetrules:priority" 	VALUE = 0>
	<MvASSIGN NAME = "l.categoryfacetrules:handle"		VALUE = "show">
	<MvASSIGN NAME = "l.categoryfacetrules:disp_name" 	VALUE = "">
	<MvASSIGN NAME = "l.categoryfacetrules:type" 		VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryFacetRules_Read" PARAMETERS = "categoryfacetrules var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.categoryfacetrules:cat_id"		VALUE = "{ CategoryFacetRules.d.cat_id }">
	<MvASSIGN NAME = "l.categoryfacetrules:mod_code" 	VALUE = "{ CategoryFacetRules.d.mod_code }">
	<MvASSIGN NAME = "l.categoryfacetrules:facet_code" 	VALUE = "{ CategoryFacetRules.d.facet_code }">
	<MvASSIGN NAME = "l.categoryfacetrules:priority" 	VALUE = "{ CategoryFacetRules.d.priority }">
	<MvASSIGN NAME = "l.categoryfacetrules:handle"		VALUE = "{ CategoryFacetRules.d.handle }">
	<MvASSIGN NAME = "l.categoryfacetrules:disp_name" 	VALUE = "{ CategoryFacetRules.d.disp_name }">
	<MvASSIGN NAME = "l.categoryfacetrules:type" 		VALUE = "{ CategoryFacetRules.d.type }">
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryFacetRules_Insert" PARAMETERS = "categoryfacetrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'CategoryFacetRules
			 			  ( cat_id, mod_code, facet_code, priority, handle, disp_name, type )
			 			  VALUES
			 			  ( ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.categoryfacetrules:cat_id, l.categoryfacetrules:mod_code, l.categoryfacetrules:facet_code,
					   l.categoryfacetrules:priority, l.categoryfacetrules:handle, l.categoryfacetrules:disp_name,
					   l.categoryfacetrules:type">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00026', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryFacetRules_Update" PARAMETERS = "categoryfacetrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'CategoryFacetRules
			 			  SET
			 			  	priority	= ?,
							handle		= ?,
			 			  	disp_name	= ?,
			 			  	type		= ?
			 			  WHERE
							cat_id		= ? AND
			 			  	mod_code 	= ? AND
			 			  	facet_code	= ?' }"
			 FIELDS	= "l.categoryfacetrules:priority, l.categoryfacetrules:handle, l.categoryfacetrules:disp_name,
					   l.categoryfacetrules:type,
			 		   l.categoryfacetrules:cat_id, l.categoryfacetrules:mod_code, l.categoryfacetrules:facet_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00027', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryFacetRules_Delete" PARAMETERS = "cat_id, mod_code, facet_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CategoryFacetRules WHERE cat_id = ? AND mod_code = ? AND facet_code = ?' }"
			 FIELDS	= "l.cat_id, l.mod_code, l.facet_code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00028', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryFacetRules_Delete_All_Category" PARAMETERS = "cat_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CategoryFacetRules WHERE cat_id = ?' }"
			 FIELDS	= "l.cat_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00029', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryFacetRules_Load_FacetOrDefault" PARAMETERS = "cat_id, mod_code, facet_code, categoryfacetrules var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ CategoryFacetRules_Load( l.cat_id, l.mod_code, l.facet_code, l.categoryfacetrules ) }">	<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">										<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvEVAL EXPR = "{ CategoryFacetRules_Default( l.cat_id, l.mod_code, l.facet_code, l.categoryfacetrules ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryFacetRules_Load" PARAMETERS = "cat_id, mod_code, facet_code, categoryfacetrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "CategoryFacetRules"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CategoryFacetRules WHERE cat_id = ? AND mod_code = ? AND facet_code = ?' }"
				FIELDS	= "l.cat_id, l.mod_code, l.facet_code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00030', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ CategoryFacetRules.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryFacetRules">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-FCT-DTB-00031' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ CategoryFacetRules_Read( l.categoryfacetrules ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryFacetRules">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CategoryFacetRulesList_Load_Category" PARAMETERS = "cat_id, categoryfacetrules var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "CategoryFacetRules"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CategoryFacetRules WHERE cat_id = ? ORDER BY priority DESC' }"
				FIELDS	= "l.cat_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00032', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT CategoryFacetRules.d.EOF }">
		<MvEVAL EXPR = "{ CategoryFacetRules_Read( l.categoryfacetrules[ ++l.count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "CategoryFacetRules" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CategoryFacetRules">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-FCT-DTB-00033', l.count ) }">
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_CachedProductListFacets
|
</MvCOMMENT>

<MvFUNCTION NAME = "CachedProductListFacet_Read" PARAMETERS = "cachedproductlistfacet var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.cachedproductlistfacet:id"			VALUE = "{ CachedProductListFacets.d.id }">
	<MvASSIGN NAME = "l.cachedproductlistfacet:cache_id" 	VALUE = "{ CachedProductListFacets.d.cache_id }">
	<MvASSIGN NAME = "l.cachedproductlistfacet:module_id" 	VALUE = "{ CachedProductListFacets.d.module_id }">
	<MvASSIGN NAME = "l.cachedproductlistfacet:facet_code" 	VALUE = "{ CachedProductListFacets.d.facet_code }">
	<MvASSIGN NAME = "l.cachedproductlistfacet:type"		VALUE = "{ CachedProductListFacets.d.type }">
	<MvASSIGN NAME = "l.cachedproductlistfacet:populated" 	VALUE = "{ CachedProductListFacets.d.populated }">
</MvFUNCTION>

<MvFUNCTION NAME = "CachedProductListFacet_Load" PARAMETERS = "cache_id, module_id, facet_code, type, cachedproductlistfacet var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "CachedProductListFacets"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CachedProductListFacets
							  WHERE
								cache_id	= ?	AND
								module_id	= ?	AND
								' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'facet_code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_Upper( '?' ) $ ' AND
								type		= ?' }"
				FIELDS	= "l.cache_id, l.module_id, l.facet_code, l.type">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00016', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ CachedProductListFacets.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "CachedProductListFacets">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-FCT-DTB-00017' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ CachedProductListFacet_Read( l.cachedproductlistfacet ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CachedProductListFacets">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CachedProductListFacet_Insert" PARAMETERS = "cachedproductlistfacet var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.cachedproductlistfacet:id"	VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'CachedProductListFacets' ) }">

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'CachedProductListFacets
			 			  ( id, cache_id, module_id, facet_code, type, populated )
			 			  VALUES
			 			  ( ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.cachedproductlistfacet:id, l.cachedproductlistfacet:cache_id, l.cachedproductlistfacet:module_id,
					   l.cachedproductlistfacet:facet_code, l.cachedproductlistfacet:type, l.cachedproductlistfacet:populated">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00018', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CachedProductListFacet_Update_Populated" PARAMETERS = "cachedproductlistfacet var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'CachedProductListFacets
						  SET
							populated	= ?
						  WHERE
							id			= ?' }"
			 FIELDS	= "l.cachedproductlistfacet:populated,
					   l.cachedproductlistfacet:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00034', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CachedProductListFacet_Delete_All_Cache" PARAMETERS = "cache_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CachedProductListFacets WHERE cache_id = ?' }"
			 FIELDS	= "l.cache_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00035', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_CachedProductListFacetValues
|
</MvCOMMENT>

<MvFUNCTION NAME = "CachedProductListFacetValue_Read_AsFacetValue" PARAMETERS = "facet_value var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.facet_value:count" 	VALUE = "{ CachedProductListFacetValues.d.prodcount }">
	<MvASSIGN NAME = "l.facet_value:value" 	VALUE = "{ CachedProductListFacetValues.d.value }">
</MvFUNCTION>

<MvFUNCTION NAME = "CachedProductListFacetValueList_Load_AsFacetValues" PARAMETERS = "cachfct_id, facet_values var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "CachedProductListFacetValues"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CachedProductListFacetValues WHERE cachfct_id = ? ORDER BY disp_order' }"
				FIELDS	= "l.cachfct_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00020', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT CachedProductListFacetValues.d.EOF }">
		<MvEVAL EXPR = "{ CachedProductListFacetValue_Read_AsFacetValue( l.facet_values[ ++l.count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "CachedProductListFacetValues" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CachedProductListFacetValues">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MER-FCT-DTB-00021', l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "CachedProductListFacetValue_Insert" PARAMETERS = "cachfct_id, cache_id, count, value var, disp_order" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'CachedProductListFacetValues
			 			  ( cachfct_id, cache_id, prodcount, value, disp_order )
			 			  VALUES
			 			  ( ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.cachfct_id, l.cache_id, l.count, l.value, l.disp_order">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00019', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CachedProductListFacetValue_Delete_All_Cache" PARAMETERS = "cache_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CachedProductListFacetValues WHERE cache_id = ?' }"
			 FIELDS	= "l.cache_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-FCT-DTB-00036', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

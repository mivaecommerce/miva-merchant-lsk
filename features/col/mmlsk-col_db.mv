<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Miva Merchant
|
| This file and the source codes contained herein are the property of
| Miva, Inc.  Use of this file is restricted to the specific terms and
| conditions in the License Agreement associated with this file.  Distribution
| of this file or portions of this file for uses not covered by the License
| Agreement is not allowed without a written agreement signed by an officer of
| Miva, Inc.
|
| Copyright 1998-2024 Miva, Inc.  All rights reserved.
| http://www.miva.com
|
| Prefix         : MER-COL-DB-
| Next Error Code: 31
|
</MvCOMMENT>

<MvFUNCTION NAME = "COL_Store_Create" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| sNN_Collections
	|
	</MvCOMMENT>

	<MvQUERY NAME   = "Merchant"
			 QUERY  = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'Collections
						  (
							id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()	$ ',
							code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )	$ ',
							name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 100 )	$ ',
							filter		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()		$ ',
							immediate	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()		$ ',
							rebuildkey	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )	$ ',
							lastrbld	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()	$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME   = "Merchant"
			 QUERY  = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'Collections_1 ON ' $ g.Store_Table_Prefix $ 'Collections ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME   = "Merchant"
			 QUERY  = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'Collections_2 ON ' $ g.Store_Table_Prefix $ 'Collections ( code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME   = "Merchant"
			 QUERY  = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'Collections_3 ON ' $ g.Store_Table_Prefix $ 'Collections ( immediate )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME   = "Merchant"
			 QUERY  = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'Collections_4 ON ' $ g.Store_Table_Prefix $ 'Collections ( rebuildkey, lastrbld )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'Collections', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_CollectionXProduct
	|
	</MvCOMMENT>

	<MvQUERY NAME   = "Merchant"
			 QUERY  = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'CollectionXProduct
						  (
							col_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()	$ ',
							product_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_INTEGER()	$ ',
							type		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 1 )	$ ',
							status		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 1 )	$ '
						  )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME   = "Merchant"
			 QUERY  = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'CollectionXProduct_1 ON ' $ g.Store_Table_Prefix $ 'CollectionXProduct ( col_id, product_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME   = "Merchant"
			 QUERY  = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'CollectionXProduct_2 ON ' $ g.Store_Table_Prefix $ 'CollectionXProduct ( col_id, status )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "COL_Store_Delete" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'Collections' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'CollectionXProduct' }">

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'Collections' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_Collections
|
</MvCOMMENT>

<MvFUNCTION NAME = "Collection_Read" PARAMETERS = "collection var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.collection:id"			VALUE = "{ Collections.d.id }">
	<MvASSIGN NAME = "l.collection:code"		VALUE = "{ Collections.d.code }">
	<MvASSIGN NAME = "l.collection:name"		VALUE = "{ Collections.d.name }">
	<MvASSIGN NAME = "l.collection:immediate"	VALUE = "{ Collections.d.immediate }">
	<MvASSIGN NAME = "l.collection:rebuildkey"	VALUE = "{ Collections.d.rebuildkey }">
	<MvASSIGN NAME = "l.collection:lastrbld"	VALUE = "{ Collections.d.lastrbld }">
	<MvASSIGN NAME = "l.null"					VALUE = "{ miva_json_decode( Collections.d.filter, l.collection:filter ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Collection_Load_ID" PARAMETERS = "id, collection var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Collections"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Collections WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00009', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Collections.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Collections">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-COL-DB-00010' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Collection_Read( l.collection ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Collections">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Collection_Load_Code" PARAMETERS = "code, collection var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "Collections"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Collections WHERE ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00011', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Collections.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Collections">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-COL-DB-00012' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Collection_Read( l.collection ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Collections">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Collection_Insert" PARAMETERS = "collection var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.collection:id"			VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'Collections' ) }">
	<MvASSIGN NAME = "l.collection:lastrbld"	VALUE = 0>
	<MvASSIGN NAME = "l.filter"					VALUE = "{ miva_json_encode( l.collection:filter, '' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'Collections
					      ( id, code, name, filter, immediate, rebuildkey, lastrbld )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.collection:id, l.collection:code, l.collection:name, l.filter,
			 		   l.collection:immediate, l.collection:rebuildkey, l.collection:lastrbld">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00015', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Collection_Update" PARAMETERS = "collection var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.filter" VALUE = "{ miva_json_encode( l.collection:filter, '' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Collections
					      SET
							code		= ?,
							name		= ?,
							filter		= ?,
							immediate	= ?,
							rebuildkey	= ?,
							lastrbld	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.collection:code, l.collection:name, l.filter, l.collection:immediate, l.collection:rebuildkey, l.collection:lastrbld,
					   l.collection:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00016', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Collection_Update_LastRebuild" PARAMETERS = "collection var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.collection:lastrbld" VALUE = "{ s.dyn_time_t }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'Collections
					      SET
							lastrbld	= ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.collection:lastrbld,
					   l.collection:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00017', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Collection_Delete_ID" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'Collections WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00018', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Collection_Delete" PARAMETERS = "collection var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Feature_Filename_COL_UT ].Collection_Remove_All_Category_Products( l.collection:id )				OR
					NOT [ g.Feature_Filename_COL_UT ].Collection_Remove_All_PriceGroup_Products( l.collection:id )				OR
					NOT [ g.Feature_Filename_COL_UT ].Collection_Remove_All_PriceGroup_ExcludedProducts( l.collection:id )		OR
					NOT [ g.Feature_Filename_COL_UT ].Collection_Remove_All_PriceGroup_QualifyingProducts( l.collection:id )	OR
					NOT [ g.Feature_Filename_COL_UT ].Collection_Remove_All_AvailGroup_Products( l.collection:id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFOREACH ITERATOR = "l.module" ARRAY = "l.modules" COUNT = "{ [ g.Module_Library_DB ].StoreModuleList_Load_Features( 'colprod', l.modules ) }">
		<MvIF EXPR = "{ l.module:api_ver GE 10.11 }">
			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Root $ l.module:module ].Module_Collection_Product_Remove_All( l.module, l.collection:id ) }">
		</MvIF>
	</MvFOREACH>

	<MvIF EXPR = "{ NOT CollectionXProduct_Delete_All_Collection( l.collection:id )												OR
					NOT [ g.Module_Library_DB ].CategoryXCollection_Delete_All_Collection( l.collection:id )					OR
					NOT [ g.Module_Feature_PGR_DB ].PriceGroupXCollection_Delete_All_Collection( l.collection:id )				OR
					NOT [ g.Module_Feature_PGR_DB ].PriceGroupXExcludedCollection_Delete_All_Collection( l.collection:id )		OR
					NOT [ g.Module_Feature_PGR_DB ].PriceGroupXQualifyingCollection_Delete_All_Collection( l.collection:id )	OR
					NOT [ g.Module_Feature_AGR_DB ].AvailGroupXCollection_Delete_All_Collection( l.collection:id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ Collection_Delete_ID( l.collection:id ) }">
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_CollectionXProduct
|
</MvCOMMENT>

<MvFUNCTION NAME = "CollectionXProduct_Read" PARAMETERS = "collectionxproduct var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.collectionxproduct:col_id"		VALUE = "{ CollectionXProduct.d.col_id }">
	<MvASSIGN NAME = "l.collectionxproduct:product_id"	VALUE = "{ CollectionXProduct.d.product_id }">
	<MvASSIGN NAME = "l.collectionxproduct:type"		VALUE = "{ CollectionXProduct.d.type }">
	<MvASSIGN NAME = "l.collectionxproduct:status"		VALUE = "{ CollectionXProduct.d.status }">
</MvFUNCTION>

<MvFUNCTION NAME = "CollectionXProduct_Load" PARAMETERS = "col_id, product_id, collectionxproduct var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CollectionXProduct"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'CollectionXProduct WHERE col_id = ? AND product_id = ?' }"
				FIELDS	= "l.col_id, l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00019', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ CollectionXProduct.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "CollectionXProduct">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MER-COL-DB-00020' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ CollectionXProduct_Read( l.collectionxproduct ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CollectionXProduct">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CollectionXProduct_Insert" PARAMETERS = "collectionxproduct var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'CollectionXProduct
					      ( col_id, product_id, type, status )
						  VALUES
						  ( ?, ?, ?, ? )' }"
			 FIELDS	= "l.collectionxproduct:col_id, l.collectionxproduct:product_id, l.collectionxproduct:type, l.collectionxproduct:status">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00023', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CollectionXProduct_Update" PARAMETERS = "collectionxproduct var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'CollectionXProduct
					      SET
							type		= ?,
							status		= ?
						  WHERE
						  	col_id		= ? AND
						  	product_id	= ?' }"
			 FIELDS	= "l.collectionxproduct:type, l.collectionxproduct:status, 
			 		   l.collectionxproduct:col_id, l.collectionxproduct:product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00024', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CollectionXProduct_Replace_Status_Collection" PARAMETERS = "col_id, old_status, new_status" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'CollectionXProduct
						  SET
							status	= ?
						  WHERE
							col_id	= ? AND
							status	= ?' }"
			 FIELDS	= "l.new_status, l.col_id, l.old_status">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00029', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CollectionXProduct_Delete" PARAMETERS = "col_id, product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CollectionXProduct
						  WHERE
							col_id		= ? AND
							product_id	= ?' }"
			 FIELDS	= "l.col_id, l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00025', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CollectionXProduct_Delete_All_Collection" PARAMETERS = "col_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CollectionXProduct WHERE col_id = ?' }"
			 FIELDS	= "l.col_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00026', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CollectionXProduct_Delete_All_Collection_Status" PARAMETERS = "col_id, status" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CollectionXProduct
						  WHERE
							col_id = ? AND
							status = ?' }"
			 FIELDS	= "l.col_id, l.status">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00028', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CollectionXProduct_Delete_All_Product" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'CollectionXProduct WHERE product_id = ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00027', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "CollectionXProduct_Count_Collection" PARAMETERS = "col_id, counts var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "CollectionXProduct"
				QUERY	= "{ 'SELECT
								type,
								COUNT( * ) AS total_count
							  FROM '
							  	$ g.Store_Table_Prefix $ 'CollectionXProduct
							  WHERE
							  	col_id = ?
							  GROUP BY
							  	type' }"
				FIELDS	= "l.col_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MER-COL-DB-00030', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.counts"				VALUE = "">
	<MvASSIGN NAME = "l.counts:automatic"	VALUE = 0>
	<MvASSIGN NAME = "l.counts:manual"		VALUE = 0>
	<MvASSIGN NAME = "l.counts:excluded"	VALUE = 0>

	<MvWHILE EXPR = "{ NOT CollectionXProduct.d.EOF }">
		<MvIF EXPR = "{ CollectionXProduct.d.type EQ 'A' }">		<MvASSIGN NAME = "l.counts:automatic"	VALUE = "{ CollectionXProduct.d.total_count }">
		<MvELSEIF EXPR = "{ CollectionXProduct.d.type EQ 'M' }">	<MvASSIGN NAME = "l.counts:manual"		VALUE = "{ CollectionXProduct.d.total_count }">
		<MvELSEIF EXPR = "{ CollectionXProduct.d.type EQ 'X' }">	<MvASSIGN NAME = "l.counts:excluded"	VALUE = "{ CollectionXProduct.d.total_count }">
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "CollectionXProduct" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "CollectionXProduct">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>
